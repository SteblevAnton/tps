#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("ПроведениеУсловияАгентскогоВознаграждения");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.УсловияАгентскогоВознагражения") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		ТекстЗапросаТаблицаУсловияАгентскогоВознаграждения(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Добавляет команду создания документа "Специальные требования".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	ТаблицаЗначений, Неопределено - сформированные команды для вывода в подменю.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.УсловияАгентскогоВознагражения) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.УсловияАгентскогоВознагражения.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.УсловияАгентскогоВознагражения);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период
	|ИЗ
	|	Документ.УсловияАгентскогоВознагражения КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаУсловияАгентскогоВознаграждения(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "УсловияАгентскогоВознаграждения";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	УсловияАгентскогоВознаграженияАгенты.Ссылка.Организация КАК Организация,
	               |	УсловияАгентскогоВознаграженияАгенты.Ссылка.Клиент КАК Клиент,
	               |	УсловияАгентскогоВознаграженияАгенты.Агент КАК Агент,
	               |	УсловияАгентскогоВознаграженияНоменклатура.Номенклатура КАК Номенклатура,
	               |	УсловияАгентскогоВознаграженияНоменклатура.Характеристика КАК Характеристика,
	               |	УсловияАгентскогоВознаграженияАгенты.БазоваяСтавка КАК БазоваяСтавкаОсновная,
	               |	УсловияАгентскогоВознаграженияАгенты.СтавкаАгента КАК СтавкаАгентаОсновная,
	               |	УсловияАгентскогоВознаграженияНоменклатура.СтавкаАгента КАК СтавкаАгента,
	               |	УсловияАгентскогоВознаграженияАгенты.Ссылка.Договор КАК ДоговорКлиента,
	               |	УсловияАгентскогоВознаграженияАгенты.ДоговорАгента КАК ДоговорАгента,
	               |	УсловияАгентскогоВознаграженияАгенты.Ссылка.Основание КАК Основание
	               |ИЗ
	               |	Документ.УсловияАгентскогоВознагражения.Агенты КАК УсловияАгентскогоВознаграженияАгенты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УсловияАгентскогоВознагражения.Номенклатура КАК УсловияАгентскогоВознаграженияНоменклатура
	               |		ПО УсловияАгентскогоВознаграженияАгенты.Агент = УсловияАгентскогоВознаграженияНоменклатура.Агент
	               |			И УсловияАгентскогоВознаграженияАгенты.Ссылка = УсловияАгентскогоВознаграженияНоменклатура.Ссылка
	               |ГДЕ
	               |	УсловияАгентскогоВознаграженияАгенты.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.ДвиженияДокумента.ДобавитьКомандуОтчетОДвиженияхДокумента(КомандыОтчетов, Параметры);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли