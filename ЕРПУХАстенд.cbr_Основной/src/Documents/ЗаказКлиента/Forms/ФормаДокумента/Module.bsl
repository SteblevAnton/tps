
#Область ОбработчикиСобытийФормы

//++АшаеваАН 02.02.26 ПРД.11
&НаКлиенте
Процедура cbr_ТранспортныйТарифПриИзменении(Элемент) 
	
	cbr_ПересчитатьСтоимостьДоставки(,Истина);
	
КонецПроцедуры //--АшаеваАН 02.02.26 ПРД.11

//++АшаеваАН 02.02.26 ПРД.11
&НаКлиенте
Процедура cbr_СтоимостьДоставкиСНДСПриИзменении(Элемент)

    Объект.cbr_СтоимостьДоставкиБезНДС = Объект.cbr_СтоимостьДоставкиСНДС / 1.22;
	cbr_ПересчитатьСтоимостьДоставки(Истина);
	
КонецПроцедуры //--АшаеваАН 02.02.26 ПРД.11

//++АшаеваАН 02.02.26 ПРД.11
&НаКлиенте
Процедура cbr_СтоимостьДоставкиБезНДСПриИзменении(Элемент)
	
	Объект.cbr_СтоимостьДоставкиСНДС = Объект.cbr_СтоимостьДоставкиБезНДС * 1.22; 
	cbr_ПересчитатьСтоимостьДоставки(Истина);
	
КонецПроцедуры //--АшаеваАН 02.02.26 ПРД.11

#КонецОбласти
#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

#Область ОбработчикиСобытийЭлементовТаблицыФормыcbr_АгентскиеВознаграждения
&НаКлиенте
Процедура cbr_АгентскоеВознаграждениеДоговорАгентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	//++ cbr-larkovgeorg 12.02.2026 ПРД12
	
	ТекСтрока = Элементы.cbr_АгентскиеВознаграждения.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекСтрока.Агент) Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Отбор, АгентскоеВознаграждение", Новый Структура("Организация, Контрагент",
			Объект.Организация, ТекСтрока.Агент), Истина);
		ОповещениеЗакрытия = Новый ОписаниеОповещения("cbr_АгентскоеВознаграждениеДоговорАгентаНачалоВыбораЗавершение",
			ЭтотОбъект);
		ОткрытьФорму("Документ.ВерсияСоглашенияКоммерческийДоговор.Форма.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , ,
			, ОповещениеЗакрытия);
	КонецЕсли;
	
КонецПроцедуры //-- cbr-larkovgeorg 12.02.2026 ПРД12

&НаКлиенте
Процедура cbr_АгентскоеВознаграждениеДоговорАгентаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	//++ cbr-larkovgeorg 12.02.2026 ПРД12
	Если ЗначениеЗаполнено(Результат) Тогда 
	
		ТекСтрока = Элементы.cbr_АгентскиеВознаграждения.ТекущиеДанные;
		ТекСтрока.ДоговорАгента = Результат;
		
	КонецЕсли
	
КонецПроцедуры //-- cbr-larkovgeorg 12.02.2026 ПРД12
#КонецОбласти
&НаКлиенте
Процедура ТоварыЦенаСоСкидкойПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	//++AlopinAS ПРД20
	Если ТекущаяСтрока.cbr_ЦенаСоСкидкой = 0 Тогда
		ТекущаяСтрока.ПроцентРучнойСкидки = 0;
		ТекущаяСтрока.СуммаРучнойСкидки = 0;
		ТоварыЦенаПриИзменении(Элемент);
	Иначе
		СуммаСкидки = ТекущаяСтрока.Цена - ТекущаяСтрока.cbr_ЦенаСоСкидкой;
		Если ТекущаяСтрока.Цена <> 0 Тогда
			ТекущаяСтрока.ПроцентРучнойСкидки = (СуммаСкидки / ТекущаяСтрока.Цена) * 100;
			Если ТекущаяСтрока.КоличествоУпаковок = 0 Тогда
				ТоварыПроцентРучнойСкидкиПриИзменении(Элемент);	
			КонецЕсли;
			
		КонецЕсли;
		//++ cbr-larkovgeorg 16.02.2026 ПРД24 
		Если ЗначениеЗаполнено(ТекущаяСтрока.cbr_ЦенаСоСкидкой) И ТекущаяСтрока.Сумма <> ТекущаяСтрока.cbr_ЦенаСоСкидкой * ТекущаяСтрока.КоличествоУпаковок Тогда

			ТекущаяСтрока.Сумма = ТекущаяСтрока.cbr_ЦенаСоСкидкой * ТекущаяСтрока.КоличествоУпаковок;
			ТоварыСуммаПриИзменении(Элемент);
		КонецЕсли;
		
		
		//-- cbr-larkovgeorg 16.02.2026 ПРД24	
	КонецЕсли;
	//--AlopinAS ПРД20
	

КонецПроцедуры

//++ cba AlopinAS ПРД.20
&НаСервере
Процедура ТоварыТЧЦенаСоСкидкойПриИзмененииНаСервере()
	
	Если Объект.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнено соглашение с клиентом'"));
		Возврат;
	КонецЕсли;
	СтрокиИзСоглашения = ПолучитьСтрокиКИзменениюИзСоглашения();

	МассивКИзменению = Новый Массив;

	Для Каждого Стр Из Объект.Товары Цикл

		Стр.cbr_ЦенаСоСкидкой = 0;
		Стр.cbr_СтрокаИзСоглашенияСКлиентом = Ложь;

		СтрокаКИзменению = СтрокиИзСоглашения.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, ДатаОтгрузки",
			Стр.Номенклатура, Стр.Характеристика, Стр.ДатаОтгрузки));

		Если СтрокаКИзменению.Количество() > 0 Тогда
			Стр.cbr_ЦенаСоСкидкой = СтрокаКИзменению[0].ЦенаСоСкидкой;
			Стр.cbr_СтрокаИзСоглашенияСКлиентом = Истина;

		КонецЕсли;
		СуммаСкидки = Стр.Цена - Стр.cbr_ЦенаСоСкидкой;

		Если Стр.Цена <> 0 Тогда
			Стр.ПроцентРучнойСкидки = (СуммаСкидки / Стр.Цена) * 100;
		КонецЕсли;

		Если ЗначениеЗаполнено(Стр.cbr_ЦенаСоСкидкой) И Стр.Сумма <> Стр.cbr_ЦенаСоСкидкой * Стр.КоличествоУпаковок Тогда

			Стр.Сумма = Стр.cbr_ЦенаСоСкидкой * Стр.КоличествоУпаковок;

		КонецЕсли;
		
		МассивКИзменению.Добавить(Стр);
		
	КонецЦикла;

	ОперацииПередачи = Новый Массив;
	ОперацииПередачи.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
	ОперацииПередачи.Добавить(Перечисления.ХозяйственныеОперации.ПередачаНаХранениеСПравомПродажи);
	ОперацииПередачи.Добавить(Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала);

	ПередачаТоваров = ОперацииПередачи.Найти(Объект.ХозяйственнаяОперация) <> Неопределено;

	СтруктураПересчетаЦены  = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруПересчетаЦеныСкидкиВПродажахВТЧ(
		Объект, ПередачаТоваров);
	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьЦенуСкидкуПоСуммеВПродажах", СтруктураПересчетаЦены);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(МассивКИзменению, СтруктураДействий, Объект.Товары);


КонецПроцедуры //-- cba AlopinAS ПРД.20

//++ cba AlopinAS ПРД.20
&НаСервере
Функция ПолучитьСтрокиКИзменениюИзСоглашения()
	
	ТчТовары = Объект.Товары.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТчТовары.Номенклатура КАК Номенклатура,
		|	ТчТовары.Характеристика КАК Характеристика,
		|	ТчТовары.ДатаОтгрузки КАК ДатаОтгрузки
		|ПОМЕСТИТЬ ВТТЧ
		|ИЗ
		|	&ТчТовары КАК ТчТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЦенаСоСкидкой КАК ЦенаСоСкидкой,
		|	ВТТЧ.ДатаОтгрузки КАК ДатаОтгрузки
		|ИЗ
		|	ВТТЧ КАК ВТТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
		|		ПО ВТТЧ.Номенклатура = СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура
		|		И ВТТЧ.Характеристика = СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика
		|		И ВТТЧ.ДатаОтгрузки
		|			МЕЖДУ СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала И СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания
		|ГДЕ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка = &СсылкаСоглашение";
	
	Запрос.УстановитьПараметр("СсылкаСоглашение", Объект.Соглашение);
	Запрос.УстановитьПараметр("ТчТовары", ТчТовары);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЧКИзменению = РезультатЗапроса.Выгрузить();
	
	Возврат ТЧКИзменению;
	
КонецФункции //-- cba AlopinAS ПРД.20


//++ cba AlopinAS 27.01.26
&НаКлиенте
&ИзменениеИКонтроль("ТоварыНоменклатураПриИзменении")
Процедура cbr_ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	#Вставка
	//++ cba AlopinAS 27.01.26
	ЭтоНеОбеспечивать = ?(ТекущаяСтрока.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"),
	Истина, Ложь);
	ЭтоПустоеЗначение = ?(ТекущаяСтрока.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ПустаяСсылка"),
	Истина, Ложь);
	//-- cba AlopinAS 27.01.26
	#КонецВставки
	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);

	ОчиститьСообщения();
	ПрименитьОплатуБонуснымиБалламиКлиент(, Истина);

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПроверитьЗаполнитьСклад", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСкладаВСтрокеТЧ(Объект, СкладГруппа));
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(Объект, Истина));
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСВозвратнойТары", Объект.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакВариантОформленияПродажи", Новый Структура("Номенклатура", "ВариантОформленияПродажи"));
	СтруктураДействий.Вставить("ЗаполнитьСодержание", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСодержанияУслугиВСтрокеТЧ(Объект, Ложь));
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());
	СтруктураДействий.Вставить("ЗаполнитьПризнакБезВозвратнойТары", Объект.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ЗаполнитьПризнакОтмененоБезВозвратнойТары", Объект.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус", Новый Структура("Склад, ПараметрыУказанияСерий", ТекущаяСтрока.Склад, ПараметрыУказанияСерий));
	ДобавитьДействияОбеспеченияНаКлиенте(СтруктураДействий,
	"ВариантОбеспечения,ДоступноВДругихСтроках,СкладОбязателен,ДатаОтгрузкиОбязательна");
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);

	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
	ЭтаФорма.ИмяФормы, "Товары"));
	Если Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПоставкаПодПринципала") Тогда
		СтруктураДействий.Вставить("ЗаполнитьПодразделениеВСтрокеТЧ", Новый Структура("Подразделение", Объект.Подразделение));
	КонецЕсли;

	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	Если ОбеспечениеВДокументахКлиент.ТребуетсяОбновитьКолонкуДоступно(СтруктураДействий) Тогда
		ОбновитьКолонкуДоступноСервер();
	КонецЕсли;

	ОбеспечениеВДокументахКлиентСервер.ЗаполнитьСлужебныеРеквизиты(Объект.Товары, ДатаОтгрузкиОбязательна, СкладОбязателен);

	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);

	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);
	#Вставка
	//++ cba AlopinAS 27.01.26
	Если ЭтоНеОбеспечивать или ЭтоПустоеЗначение Тогда
		ТекущаяСтрока.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется");
		ДоступныеОстаткиПараметрыВстраивания.СлужебныеПараметры.ВариантОбеспеченияВыбранноеЗначение = Неопределено;
		
		ОбеспечениеВДокументахКлиент.ВариантОбеспеченияПриИзменении(
		ЭтотОбъект,
		Неопределено,
		"ОбработатьВыборСкладаИСерии",
		Истина,
		"ТоварыВариантОбеспеченияПриИзмененииПослеВопроса");
	КонецЕсли;
	//-- cba AlopinAS 27.01.26
	
	//++AlopinAS 28.01.2026 ПРД.20
	ТекущаяСтрокаТаблицы = Элементы.Товары.ТекущаяСтрока;
	cbr_ОбработатьНоменклатуру(ТекущаяСтрокаТаблицы);
	
	ТоварыЦенаСоСкидкойПриИзменении(Элемент);
	//--AlopinAS 28.01.2026 ПРД.20
	#КонецВставки
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&После("ПартнерПриИзменении")
&НаКлиенте
Процедура cbr_ПартнерПриИзменении(Элемент)
	
	//++ БаженоваЕО 29.01.2026 ПРД.18
	Оповещение = Новый ОписаниеОповещения("cbr_ПослеЗакрытияВопроса", ЭтотОбъект);

	Если ЗначениеЗаполнено(Объект.Партнер) И Объект.cbr_ПересечениеМенеджеров Тогда
		ПоказатьВопрос(Оповещение, "Вкладка ""Пересечение менеджеров"" будет перезаполнена. Продолжить?",
			РежимДиалогаВопрос.ОКОтмена, );
	КонецЕсли;
	//-- БаженоваЕО 29.01.2026
	
КонецПроцедуры

//++ БаженоваЕО 29.01.2026 ПРД.18
&НаКлиенте
Процедура cbr_ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
 
    Если Результат = КодВозвратаДиалога.ОК Тогда
    	cbr_ЗаполнитьТЧПересечениеМенеджеров();
        ОбщегоНазначенияКлиент.СообщитьПользователю("Вкладка ""Пересечение менеджеров"" перезаполнена");
    КонецЕсли;	
 
КонецПроцедуры //-- БаженоваЕО 29.01.2026

//++ БаженоваЕО 29.01.2026 ПРД.18
&НаКлиенте
Процедура cbr_ПересечениеМенеджеровПриИзменении(Элемент)
	
	Элементы.ПересечениеМенеджеров.Видимость = Объект.cbr_ПересечениеМенеджеров;

КонецПроцедуры //-- БаженоваЕО 29.01.2026

//++ cba AlopinAS 27.01.26
&После("СоглашениеПриИзменении")
&НаКлиенте
Процедура cbr_СоглашениеПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
	
		СтруктураСпецТребования = Новый Структура();
		СтруктураСпецТребования.Вставить("ЕстьРазличия",Ложь);
		СтруктураСпецТребования.Вставить("ДокументыДляВодителя","");
		СтруктураСпецТребования.Вставить("ТаможенноеСопровождение","");
		СтруктураСпецТребования.Вставить("УсловияДоставки","");
		СтруктураСпецТребования.Вставить("ТребованияКУпаковке","");
		СтруктураСпецТребования.Вставить("ТребованияКЭтикетке","");
		СтруктураСпецТребования.Вставить("ТребованиеККомплектации","");
		СтруктураСпецТребования.Вставить("НеБолееДатыПроизводства",0);
		СтруктураСпецТребования.Вставить("ДопустимыйОстатокГСХНеМенееПроцент",0);
		СтруктураСпецТребования.Вставить("ДопустимыйОстатокГСХНеМенееМес",0);
		СтруктураСпецТребования.Вставить("ПрочиеТребования","");

		cbr_СверитьИнформациюПоСпециальнымТребованиям(СтруктураСпецТребования);

		Если СтруктураСпецТребования.ЕстьРазличия Тогда
			cbr_ЗадатьВопросОРаспределенииТоваровПоЗаказам(СтруктураСпецТребования);
		КонецЕсли;
	
	КонецЕсли;
	
	cbr_СоглашениеПриИзмененииНаСервере(Объект.Соглашение);
	
	//++ Тусеев Н.Ю. 04.02.2026 ПРД12
	РезультатСравненияДанных = СовпадениеАгентскогоВознаграждения();
	Если Не РезультатСравненияДанных.СовпадлаетАгентскоеВознаграждение
		Или Не РезультатСравненияДанных.СовпадаетТч Тогда
		cbr_ЗадатьВопросОбОбновленииАгентскогоВознаграждения(РезультатСравненияДанных);
	КонецЕсли;
	//-- Тусеев Н.Ю. 04.02.2026 ПРД12
	
	//++AlopinAS ПРД20
		ТоварыТЧЦенаСоСкидкойПриИзмененииНаСервере();
		РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);
		СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);
	//--AlopinAS ПРД20
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Процедура cbr_ОбработатьНоменклатуру(ТекущаяСтрока)
	
	ТекущаяСтрокаТч = Объект.Товары.НайтиПоИдентификатору(ТекущаяСтрока);
	ТекущаяСтрокаТч.cbr_СтрокаИзСоглашенияСКлиентом = Ложь;
	
	СтруктураПарамтеров = Новый Структура();
	СтруктураПарамтеров.Вставить("Соглашение", Объект.Соглашение);
	СтруктураПарамтеров.Вставить("Номенклатура", ТекущаяСтрокаТч.Номенклатура);
	СтруктураПарамтеров.Вставить("ДатаОтгрузки", ТекущаяСтрокаТЧ.ДатаОтгрузки);
	СтруктураПарамтеров.Вставить("Характеристика", ТекущаяСтрокаТч.Характеристика);
	СтруктураПарамтеров.Вставить("ЕдиницаИзмерения",ТекущаяСтрокаТч.Упаковка);
	СтрокаТчЦеныПоТоварам = ПолучитьТЧЦеныПоТоварам(СтруктураПарамтеров);
			
	
	Если СтрокаТчЦеныПоТоварам.Количество() > 0 Тогда 
		ТекущаяСтрокаТч.cbr_ЦенаСоСкидкой = СтрокаТчЦеныПоТоварам[0].ЦенаСоСкидкой;
		ТекущаяСтрокаТч.cbr_СтрокаИзСоглашенияСКлиентом = Истина;
		
	Иначе
		ТекущаяСтрокаТч.cbr_ЦенаСоСкидкой = 0;	
	КонецЕсли;	
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Процедура cbr_ОбработатьМассивНоменклатур()
	
	ТчТоварыКЗаполнению = ПолучитьТЧЦеныПоМассивуТоваров();
	ВсегоКИзменению = ТчТоварыКЗаполнению.Количество();
	Для Каждого Стр Из Объект.Товары Цикл
		Стр.cbr_СтрокаИзСоглашенияСКлиентом = Ложь;
		Если ВсегоКИзменению > 0 Тогда
			Для Каждого СтрИзСоглашения Из ТчТоварыКЗаполнению Цикл
				Если Стр.Номенклатура = СтрИзСоглашения.Номенклатура и Стр.Характеристика = СтрИзСоглашения.Характеристика Тогда
					Стр.cbr_ЦенаСоСкидкой = СтрИзСоглашения.ЦенаСоСкидкой;
					Стр.cbr_СтрокаИзСоглашенияСКлиентом = Истина;
					ВсегоКИзменению = ВсегоКИзменению - 1;
				КонецЕсли;
			КонецЦикла;	
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Функция ПолучитьТЧЦеныПоМассивуТоваров()
	
	Если Объект.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнено соглашение с клиентом'"));
		Возврат Неопределено;	
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки
		|ПОМЕСТИТЬ ВТТЧ
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ВидЦены КАК ВидЦены,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Цена КАК Цена,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ПрцентРучнаяСкидка КАК ПрцентРучнаяСкидка,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЦенаСоСкидкой КАК ЦенаСоСкидкой,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСоглашение
		|ИЗ
		|	Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
		|ГДЕ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка = &СсылкаСоглашение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТСоглашение.Номенклатура КАК Номенклатура,
		|	ВТСоглашение.Характеристика КАК Характеристика,
		|	ВТСоглашение.ВидЦены КАК ВидЦены,
		|	ВТСоглашение.Цена КАК Цена,
		|	ВТСоглашение.ПрцентРучнаяСкидка КАК ПрцентРучнаяСкидка,
		|	ВТСоглашение.ЦенаСоСкидкой КАК ЦенаСоСкидкой,
		|	ВТТЧ.ДатаОтгрузки КАК ДатаОтгрузки
		|ИЗ
		|	ВТТЧ КАК ВТТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСоглашение КАК ВТСоглашение
		|		ПО ВТСоглашение.Номенклатура = ВТТЧ.Номенклатура
		|		И ВТСоглашение.Характеристика = ВТТЧ.Характеристика
		|		И ВТТЧ.ДатаОтгрузки МЕЖДУ ВТСоглашение.ДатаНачала И ВТСоглашение.ДатаОкончания";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("СсылкаСоглашение", Объект.Соглашение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = РезультатЗапроса.Выгрузить();
	
	Возврат Результат;
	
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаСервереБезКонтекста
Функция ПолучитьТЧЦеныПоТоварам(СтруктураПарамтеров)
	
	Если СтруктураПарамтеров.Соглашение = Справочники.СоглашенияСКлиентами.ПустаяСсылка() Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не заполнено соглашение с клиентом'"));
		Возврат Неопределено;	
	КонецЕсли;
	
	Если СтруктураПарамтеров.ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка() Тогда
		ЕденицаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПарамтеров.Номенклатура, "ЕдиницаИзмерения");
	Иначе
		ЕденицаИзмерения = СтруктураПарамтеров.ЕдиницаИзмерения;
	КонецЕсли;	
	
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Количество КАК Количество,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ВидЦены КАК ВидЦены,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Цена КАК Цена,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ПрцентРучнаяСкидка КАК ПрцентРучнаяСкидка,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.СуммаРучнаяСкидка КАК СуммаРучнаяСкидка,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЦенаСоСкидкой КАК ЦенаСоСкидкой,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Сумма КАК Сумма,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания
			|ИЗ
			|	Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
			|ГДЕ
			|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка = &Ссылка
			|	И СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура = &Номенклатура
			|	И СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика = &Характеристика
			|	И СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЕдиницаИзмерения = &ЕдиницаИзмерения
			|	И &ДатаОтгрузки
			|		МЕЖДУ СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала И СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания";
		
		Запрос.УстановитьПараметр("Ссылка", СтруктураПарамтеров.Соглашение);
		Запрос.УстановитьПараметр("Номенклатура", СтруктураПарамтеров.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика", СтруктураПарамтеров.Характеристика);
		Запрос.УстановитьПараметр("ЕдиницаИзмерения", ЕденицаИзмерения);
		Запрос.УстановитьПараметр("ДатаОтгрузки", СтруктураПарамтеров.ДатаОтгрузки);
		
		РезультатЗапроса = Запрос.Выполнить();
		Результат = РезультатЗапроса.Выгрузить();	
	
	Возврат Результат;
	
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&После("ТоварыДатаОтгрузкиПриИзменении")
&НаКлиенте
Процедура cbr_ТоварыДатаОтгрузкиПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущаяСтрока;
	cbr_ОбработатьНоменклатуру(ТекущаяСтрока);
	ТоварыЦенаСоСкидкойПриИзменении(Элемент);
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&После("ДатаОтгрузкиПриИзменении")
&НаКлиенте
Процедура cbr_ДатаОтгрузкиПриИзменении(Элемент)
	
	ТоварыТЧЦенаСоСкидкойПриИзмененииНаСервере();
	
	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);

	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&После("НеОтгружатьЧастямиПриИзменении")
&НаКлиенте
Процедура cbr_НеОтгружатьЧастямиПриИзменении(Элемент)
	
	ТоварыТЧЦенаСоСкидкойПриИзмененииНаСервере();
	
	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);

	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Процедура cbr_ПересчитатьКоличество(МассивСтрок)
	
	МассивСтрокДляОбработки = Новый Массив();
	Для Каждого Стр из МассивСтрок Цикл
		ТекущаяСтрока = Объект.Товары.НайтиПоИдентификатору(Стр - 1);
		МассивСтрокДляОбработки.Добавить(ТекущаяСтрока);
	КонецЦикла;
	
	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуРучнойСкидки");
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(МассивСтрокДляОбработки, СтруктураДействий, Объект.Товары);
	
	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);
	
КонецПроцедуры

//++АшаеваАН 02.02.26 ПРД.11
&НаСервере
Процедура cbr_ПересчитатьСтоимостьДоставки(ПересчитатьТариф = Ложь, ПересчитатьСтоимость = Ложь)
	
	Если ЗначениеЗаполнено(Объект.cbr_СтоимостьДоставкиБезНДС) И ПересчитатьТариф Тогда
		Объект.cbr_ТранспортныйТариф = ?(ЭтотОбъект.ОбщийВесДокумента = 0, 0, Объект.cbr_СтоимостьДоставкиБезНДС / ЭтотОбъект.ОбщийВесДокумента);
	ИначеЕсли ЗначениеЗаполнено(Объект.cbr_ТранспортныйТариф) И ПересчитатьСтоимость Тогда 
		Объект.cbr_СтоимостьДоставкиБезНДС = Объект.cbr_ТранспортныйТариф * ЭтотОбъект.ОбщийВесДокумента;
		Объект.cbr_СтоимостьДоставкиСНДС = Объект.cbr_СтоимостьДоставкиБезНДС * 1.22;
	ИначеЕсли ЗначениеЗаполнено(Объект.cbr_СтоимостьДоставкиБезНДС) Тогда
		Объект.cbr_ТранспортныйТариф = ?(ЭтотОбъект.ОбщийВесДокумента = 0, 0, Объект.cbr_СтоимостьДоставкиБезНДС / ЭтотОбъект.ОбщийВесДокумента);
	КонецЕсли;
	
КонецПроцедуры
//--АшаеваАН 02.02.26 ПРД.11

//++ БаженоваЕО 20.02.2026 ПРД.18
&НаСервере
Процедура cbr_ЗаполнитьТЧПересечениеМенеджеров()
	
	Если ЗначениеЗаполнено(ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(
					Объект.Партнер.ОсновнойМенеджер, "ФизическоеЛицо")) Тогда
			Объект.cbr_ТЧПересечениеМенеджеров.Очистить();
			НоваяСтрока = Объект.cbr_ТЧПересечениеМенеджеров.Добавить();
			НоваяСтрока.ФизическоеЛицо = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(
						Объект.Партнер.ОсновнойМенеджер, "ФизическоеЛицо");
			НоваяСтрока.Процент = 100;
	КонецЕсли;
	
КонецПроцедуры //-- БаженоваЕО 20.02.2026

#Область Прочее

&ИзменениеИКонтроль("РассчитатьИтоговыеПоказателиЗаказа")
&НаКлиентеНаСервереБезКонтекста
Процедура cbr_РассчитатьИтоговыеПоказателиЗаказа(Форма)

	// Заполнение итогов по таблице "Товары"

	КоллекцияТовары = Форма.Объект.Товары;

	Форма.СуммаЗаказаноСЗалоговойТарой = ?(Форма.Объект.ТребуетсяЗалогЗаТару,
		КоллекцияТовары.Итог("СуммаСНДС") - КоллекцияТовары.Итог("СуммаСНДСОтменено"),
		КоллекцияТовары.Итог("СуммаСНДСБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаСНДСОтмененоБезВозвратнойТары"));
	Форма.СуммаЗаказано     = КоллекцияТовары.Итог("СуммаСНДСБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаСНДСОтмененоБезВозвратнойТары");
	Форма.СуммаЗалогаЗаТару = ?(Форма.Объект.ТребуетсяЗалогЗаТару,(КоллекцияТовары.Итог("СуммаСНДС") - КоллекцияТовары.Итог("СуммаСНДСОтменено"))
			- (КоллекцияТовары.Итог("СуммаСНДСБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаСНДСОтмененоБезВозвратнойТары")),0);
	Форма.СуммаНДСЗаказано  = ?(Форма.Объект.ТребуетсяЗалогЗаТару,
		КоллекцияТовары.Итог("СуммаНДС") - КоллекцияТовары.Итог("СуммаНДСОтменено"),
		КоллекцияТовары.Итог("СуммаНДСБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаНДСОтмененоБезВозвратнойТары"));

	СуммаАвтоСкидки   = ?(Форма.Объект.ТребуетсяЗалогЗаТару,
		КоллекцияТовары.Итог("СуммаАвтоматическойСкидки") - КоллекцияТовары.Итог("СуммаАвтоматическойСкидкиОтменено"),
		КоллекцияТовары.Итог("СуммаАвтоматическойСкидкиБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаАвтоматическойСкидкиОтмененоБезВозвратнойТары"));
	СуммаРучнойСкидки = ?(Форма.Объект.ТребуетсяЗалогЗаТару,
		КоллекцияТовары.Итог("СуммаРучнойСкидки") - КоллекцияТовары.Итог("СуммаРучнойСкидкиОтменено"),
		КоллекцияТовары.Итог("СуммаРучнойСкидкиБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаРучнойСкидкиОтмененоБезВозвратнойТары"));
	СуммаСуммаБонусныхБалловКСписаниюВВалюте = ?(Форма.Объект.ТребуетсяЗалогЗаТару,
		КоллекцияТовары.Итог("СуммаБонусныхБалловКСписаниюВВалюте") - КоллекцияТовары.Итог("СуммаБонусныхБалловКСписаниюВВалютеОтменено"),
		КоллекцияТовары.Итог("СуммаБонусныхБалловКСписаниюВВалютеБезВозвратнойТары") - КоллекцияТовары.Итог("СуммаБонусныхБалловКСписаниюВВалютеОтмененоБезВозвратнойТары"));
	Форма.СуммаСкидки       = СуммаАвтоСкидки + СуммаРучнойСкидки + СуммаСуммаБонусныхБалловКСписаниюВВалюте;
    #Вставка 
    //++ Тусеев Н.Ю. 27.01.2026 ПРД7
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ТоварыВес") Тогда
		Форма.ОбщийВесДокумента = 0;
		Для Каждого Стр Из КоллекцияТовары Цикл 
			//++ cbr-larkovgeorg 09.02.2026 ПРД7
			Если ЗначениеЗаполнено(Стр.Упаковка) Тогда
				ВесУпаковки = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Стр.Упаковка, "Числитель");
			ИначеЕсли ЗначениеЗаполнено(Стр.ЕдиницаИзмеренияВеса) Тогда
				ВесУпаковки = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Стр.ЕдиницаИзмеренияВеса, "Числитель");
			ИначеЕсли ЗначениеЗаполнено(Стр.ЕдиницаИзмеренияОбъема) Тогда
				ВесУпаковки = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Стр.ЕдиницаИзмеренияОбъема, "Числитель");	
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(ВесУпаковки) Тогда
				ВесУпаковки = 0;
			КонецЕсли;
			Форма.ОбщийВесДокумента = Форма.ОбщийВесДокумента + (ВесУпаковки * Стр.КоличествоУпаковок);
			//-- cbr-larkovgeorg 09.02.2026 ПРД7
		КонецЦикла;	
		//++АшаеваАН 02.02.26 ПРД.11
//		Форма.Объект.cbr_СтоимостьДоставкиБезНДС = Форма.Объект.cbr_ТранспортныйТариф * Форма.ОбщийВесДокумента;
//		Форма.Объект.cbr_СтоимостьДоставкиСНДС = Форма.Объект.cbr_СтоимостьДоставкиБезНДС * 1.22;
		//--АшаеваАН 02.02.26 ПРД.11
	КонецЕсли;
	//-- Тусеев Н.Ю. 27.01.2026 ПРД7
	#КонецВставки
	Если КоллекцияТовары.Итог("СуммаСНДСОтменено") = КоллекцияТовары.Итог("СуммаСНДС") Тогда
		Форма.ВсеСтрокиОтменены = Истина;
	Иначе
		Форма.ВсеСтрокиОтменены = Ложь;
	КонецЕсли;

	// Выбор страницы отображения НДС
	ОтображатьИтогСуммыНДС = УчетНДСУПКлиентСервер.ОтображатьНДСВИтогахДокументаПродажи(Форма.Объект.НалогообложениеНДС);

	Если ОтображатьИтогСуммыНДС Тогда

		Форма.Элементы.ГруппаСтраницыВсего.ТекущаяСтраница = Форма.Элементы.СтраницаВсегоСНДС;
		Форма.Элементы.ГруппаСтраницыНДС.ТекущаяСтраница   = Форма.Элементы.СтраницаСНДС;

	Иначе

		Форма.Элементы.ГруппаСтраницыВсего.ТекущаяСтраница = Форма.Элементы.СтраницаВсегоБезНДС;
		Форма.Элементы.ГруппаСтраницыНДС.ТекущаяСтраница   = Форма.Элементы.СтраницаБезНДС;

	КонецЕсли;
	#Вставка
	//++ Тусеев Н.Ю. 27.01.2026 ПРД24 
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма.Элементы, "ТоварыЦенаСоСкидкой") Тогда
		Для Каждого Стр Из КоллекцияТовары Цикл
			Если Стр.КоличествоУпаковок <> 0 И Стр.Сумма <> 0 И Не Стр.cbr_СтрокаИзСоглашенияСКлиентом Тогда
				Стр.cbr_ЦенаСоСкидкой = Стр.Сумма / Стр.КоличествоУпаковок;		
			КонецЕсли;
		КонецЦикла
	КонецЕсли;
	//++ Тусеев Н.Ю. 27.01.2026 ПРД24
	#КонецВставки

КонецПроцедуры

&ИзменениеИКонтроль("ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок")
&НаКлиентеНаСервереБезКонтекста
Процедура cbr_ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий, Объект)

	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());
    #Вставка
    //++ Тусеев Н.Ю. 27.01.2026 ПРД7
	СтруктураДействий.Вставить("ЗаполнитьВесУпаковки", Новый Структура("Номенклатура, Упаковка", "ВесУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьОбъемУпаковки", Новый Структура("Номенклатура, Упаковка", "ОбъемУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияВеса", 
	Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияВеса"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияОбъема", 
	Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияОбъема"));
	СтруктураДействий.Вставить("ПересчитатьВесОбъем");
	//-- Тусеев Н.Ю. 27.01.2026 ПРД7
	#КонецВставки

КонецПроцедуры

#КонецОбласти
//-- cba AlopinAS 27.01.26

//++ cba AlopinAS 27.01.26
&НаКлиенте
&ИзменениеИКонтроль("ТоварыПриНачалеРедактирования")
Процедура cbr_ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	РаботаСТабличнымиЧастямиКлиент.КэшироватьТекущуюСтроку(Элементы.Товары, ЭтотОбъект);
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	#Вставка
	
	//++ cba AlopinAS 27.01.26
	ЭтоНеОбеспечивать = ?(ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"),
	Истина, Ложь);
	ЭтоПустоеЗначение = ?(ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.ПустаяСсылка"),
	Истина, Ложь);
	//-- cba AlopinAS 27.01.26
	
	#КонецВставки
	ПродажиКлиент.СтрокаНоменклатурыПриНачалеРедактирования(ЭтаФорма, "Товары", ТекущиеДанные, НоваяСтрока, Копирование);

	Если НоваяСтрока И Не Копирование
		И Объект.ХозяйственнаяОперация <> ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПоставкаПодПринципала") Тогда
		ТекущиеДанные.Подразделение = Объект.Подразделение;
	КонецЕсли;

	Если НоваяСтрока Тогда
		ТекущиеДанные.КодСтроки = 0;
		Если Копирование Тогда
			ТекущиеДанные.СуммаБонусныхБалловКСписанию = 0;
			ТекущиеДанные.СуммаБонусныхБалловКСписаниюВВалюте = 0;
		КонецЕсли;
		Если Объект.НеОтгружатьЧастями Тогда
			ТекущиеДанные.ДатаОтгрузки = Объект.ДатаОтгрузки;
		КонецЕсли;
	КонецЕсли;

	Если Копирование Тогда
		СтруктураДействий = Новый Структура;
		ДобавитьДействияОбеспеченияНаКлиенте(СтруктураДействий,
		"ВариантОбеспечения,Доступно,СкладОбязателен,ДатаОтгрузкиОбязательна");
		СтруктураДействий.Вставить("ПроверитьСериюРассчитатьСтатус",
		Новый Структура("Склад, ПараметрыУказанияСерий", ТекущиеДанные.Склад, ПараметрыУказанияСерий));
		ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
		ОбеспечениеВДокументахКлиентСервер.ЗаполнитьСлужебныеРеквизиты(Объект.Товары, ДатаОтгрузкиОбязательна, СкладОбязателен);
		#Вставка
		
		//++ cba AlopinAS 27.01.26
		Если ЭтоНеОбеспечивать или ЭтоПустоеЗначение Тогда
			ТекущиеДанные.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется");
			ДоступныеОстаткиПараметрыВстраивания.СлужебныеПараметры.ВариантОбеспеченияВыбранноеЗначение = Неопределено;
			
			ОбеспечениеВДокументахКлиент.ВариантОбеспеченияПриИзменении(
			ЭтотОбъект,
			Неопределено,
			"ОбработатьВыборСкладаИСерии",
			Истина,
			"ТоварыВариантОбеспеченияПриИзмененииПослеВопроса");
		КонецЕсли;
		//-- cba AlopinAS 27.01.26
		
		#КонецВставки	
	КонецЕсли;

	ВзаиморасчетыКлиент.ТабличнаяЧастьПриНачалеРедактирования(ЭтотОбъект);

КонецПроцедуры
//-- cba AlopinAS 27.01.26

//++ cba AlopinAS 27.01.26
&НаСервере
Процедура cbr_СоглашениеПриИзмененииНаСервере(Соглашение)
	Объект.cbr_Проект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Соглашение, "cbr_Проект");
КонецПроцедуры

//++АшаеваАН 29.01.26 ПРД.5
&НаКлиенте
Асинх Процедура	cbr_ЗадатьВопросОРаспределенииТоваровПоЗаказам(СтруктураСпецТребования)
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Обновить информацию по специальным требованиям?'"), РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда  
		cbr_ОбновитьИнформациюПоСпециальнымТребованиям(СтруктураСпецТребования); 
	КонецЕсли	
	
КонецПроцедуры  //--АшаеваАН 29.01.26 ПРД.5

//++АшаеваАН 29.01.26 ПРД.5
&НаСервере
Процедура cbr_СверитьИнформациюПоСпециальнымТребованиям(СтруктураСпецТребования)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|СоглашенияСКлиентами.cbr_ДокументыДляВодителя,
	|СоглашенияСКлиентами.cbr_ТаможенноеСопровождение,
	|СоглашенияСКлиентами.cbr_УсловияДоставки,
	|СоглашенияСКлиентами.cbr_ТребованияКУпаковке,
	|СоглашенияСКлиентами.cbr_ТребованияКЭтикетке,
	|СоглашенияСКлиентами.cbr_ТребованиеККомплектации,
	|СоглашенияСКлиентами.cbr_НеБолееДатыПроизводства,
	|СоглашенияСКлиентами.cbr_ДопустимыйОстатокГСХНеМенееПроцент,
	|СоглашенияСКлиентами.cbr_ДопустимыйОстатокГСХНеМенееМес,
	|СоглашенияСКлиентами.cbr_ПрочиеТребования
	|ИЗ
	|	Справочник.СоглашенияСКлиентами КАК СоглашенияСКлиентами
	|ГДЕ
	|	СоглашенияСКлиентами.Ссылка = &Соглашение
	|	И (ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_ДокументыДляВодителя КАК СТРОКА(100)) <> ВЫРАЗИТЬ(&ДокументыДляВодителя КАК СТРОКА(100))
	|	ИЛИ ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_ТаможенноеСопровождение КАК СТРОКА(1000)) <> ВЫРАЗИТЬ(&ТаможенноеСопровождение КАК СТРОКА(1000))
	|	ИЛИ ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_УсловияДоставки КАК СТРОКА(100)) <> ВЫРАЗИТЬ(&УсловияДоставки КАК СТРОКА(100))
	|	ИЛИ ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_ТребованияКУпаковке КАК СТРОКА(1000)) <> ВЫРАЗИТЬ(&ТребованияКУпаковке КАК СТРОКА(1000))
	|	ИЛИ ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_ТребованияКЭтикетке КАК СТРОКА(1000)) <> ВЫРАЗИТЬ(&ТребованияКЭтикетке КАК СТРОКА(1000))
	|	ИЛИ ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_ТребованиеККомплектации КАК СТРОКА(1000)) <> ВЫРАЗИТЬ(&ТребованиеККомплектации КАК СТРОКА(1000))
	|	ИЛИ СоглашенияСКлиентами.cbr_НеБолееДатыПроизводства <> &НеБолееДатыПроизводства
	|	ИЛИ СоглашенияСКлиентами.cbr_ДопустимыйОстатокГСХНеМенееПроцент <> &ДопустимыйОстатокГСХНеМенееПроцент
	|	ИЛИ СоглашенияСКлиентами.cbr_ДопустимыйОстатокГСХНеМенееМес <> &ДопустимыйОстатокГСХНеМенееМес
	|	ИЛИ ВЫРАЗИТЬ(СоглашенияСКлиентами.cbr_ПрочиеТребования КАК СТРОКА(1000)) <> ВЫРАЗИТЬ(&ПрочиеТребования КАК СТРОКА(1000)))";
	
	Запрос.УстановитьПараметр("ДокументыДляВодителя", Объект.cbr_ДокументыДляВодителя);
	Запрос.УстановитьПараметр("ТаможенноеСопровождение", Объект.cbr_ТаможенноеСопровождение);
	Запрос.УстановитьПараметр("УсловияДоставки", Объект.cbr_УсловияДоставки);
	Запрос.УстановитьПараметр("Соглашение", Объект.Соглашение);
	Запрос.УстановитьПараметр("ТребованияКУпаковке", Объект.cbr_ТребованияКУпаковке);
	Запрос.УстановитьПараметр("ТребованияКЭтикетке", Объект.cbr_ТребованияКЭтикетке);
	Запрос.УстановитьПараметр("ТребованиеККомплектации", Объект.cbr_ТребованиеККомплектации);
	Запрос.УстановитьПараметр("НеБолееДатыПроизводства", Объект.cbr_НеБолееДатыПроизводства);
	Запрос.УстановитьПараметр("ДопустимыйОстатокГСХНеМенееПроцент", Объект.cbr_ДопустимыйОстатокГСХНеМенееПроцент);
	Запрос.УстановитьПараметр("ДопустимыйОстатокГСХНеМенееМес", Объект.cbr_ДопустимыйОстатокГСХНеМенееМес);
	Запрос.УстановитьПараметр("ПрочиеТребования", Объект.cbr_ПрочиеТребования);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		СтруктураСпецТребования.ЕстьРазличия = Истина;
		СтруктураСпецТребования.ДокументыДляВодителя = Выборка.cbr_ДокументыДляВодителя;
		СтруктураСпецТребования.ТаможенноеСопровождение = Выборка.cbr_ТаможенноеСопровождение;
		СтруктураСпецТребования.УсловияДоставки = Выборка.cbr_УсловияДоставки;
		СтруктураСпецТребования.ТребованияКУпаковке = Выборка.cbr_ТребованияКУпаковке;
		СтруктураСпецТребования.ТребованияКЭтикетке = Выборка.cbr_ТребованияКЭтикетке;
		СтруктураСпецТребования.ТребованиеККомплектации = Выборка.cbr_ТребованиеККомплектации;
		СтруктураСпецТребования.НеБолееДатыПроизводства = Выборка.cbr_НеБолееДатыПроизводства;
		СтруктураСпецТребования.ДопустимыйОстатокГСХНеМенееПроцент = Выборка.cbr_ДопустимыйОстатокГСХНеМенееПроцент;
		СтруктураСпецТребования.ДопустимыйОстатокГСХНеМенееМес = Выборка.cbr_ДопустимыйОстатокГСХНеМенееМес;
		СтруктураСпецТребования.ПрочиеТребования = Выборка.cbr_ПрочиеТребования;
		
	КонецЕсли;	
	
КонецПроцедуры  //--АшаеваАН 29.01.26 ПРД.5

//++АшаеваАН 29.01.26 ПРД.5
&НаСервере
Процедура cbr_ОбновитьИнформациюПоСпециальнымТребованиям(СтруктураСпецТребования)
	
	Объект.cbr_ДокументыДляВодителя = СтруктураСпецТребования.ДокументыДляВодителя;
	Объект.cbr_ТаможенноеСопровождение = СтруктураСпецТребования.ТаможенноеСопровождение;
	Объект.cbr_УсловияДоставки = СтруктураСпецТребования.УсловияДоставки;
	Объект.cbr_ТребованияКУпаковке = СтруктураСпецТребования.ТребованияКУпаковке;
	Объект.cbr_ТребованияКЭтикетке = СтруктураСпецТребования.ТребованияКЭтикетке;
	Объект.cbr_ТребованиеККомплектации = СтруктураСпецТребования.ТребованиеККомплектации;
	Объект.cbr_НеБолееДатыПроизводства = СтруктураСпецТребования.НеБолееДатыПроизводства;
	Объект.cbr_ДопустимыйОстатокГСХНеМенееПроцент = СтруктураСпецТребования.ДопустимыйОстатокГСХНеМенееПроцент;
	Объект.cbr_ДопустимыйОстатокГСХНеМенееМес = СтруктураСпецТребования.ДопустимыйОстатокГСХНеМенееМес;
	Объект.cbr_ПрочиеТребования = СтруктураСпецТребования.ПрочиеТребования;
	
КонецПроцедуры  //--АшаеваАН 29.01.26 ПРД.5

#Область Обеспечение

&ИзменениеИКонтроль("ПараметрыФормыЗапросаКоличестваИСерий")
&НаКлиенте
Функция cbr_ПараметрыФормыЗапросаКоличестваИСерий(Режим)

	ПараметрыПроверки = ОбеспечениеКлиентСервер.ИнициализироватьПараметрыПроверкиЗаполнения();

	Если Не ОбеспечениеКлиент.ПроверитьЗаполнение(
				Объект,
				Объект.Товары,
				Элементы.Товары.ТекущаяСтрока,
				ПараметрыПроверки,
				Объект.Склад,
				Режим) Тогда
		Возврат Неопределено;
	КонецЕсли;

	ПараметрыФормы = ПараметрыВыбораОбеспечения(Режим);
	ПараметрыФормы.ПараметрыУказанияСерий = ПараметрыУказанияСерий;
	#Вставка
	//++АшаеваАН 29.01.26 ПРД.5
	ПараметрыФормы.Вставить("cbr_НеБолееДатыПроизводства", Объект.cbr_НеБолееДатыПроизводства);
	ПараметрыФормы.Вставить("cbr_ДопустимыйОстатокГСХНеМенееПроцент", Объект.cbr_ДопустимыйОстатокГСХНеМенееПроцент);
	ПараметрыФормы.Вставить("cbr_ДопустимыйОстатокГСХНеМенееМес", Объект.cbr_ДопустимыйОстатокГСХНеМенееМес);
	//--АшаеваАН 29.01.26 ПРД.5
	#КонецВставки

	Возврат ПараметрыФормы;

КонецФункции

#КонецОбласти

&ИзменениеИКонтроль("ТоварыУпаковкаПриИзменении")
&НаКлиенте
Процедура cbr_ТоварыУпаковкаПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;

	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);

	ОчиститьСообщения();
	ПрименитьОплатуБонуснымиБалламиКлиент(, Истина);

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");

	Если ТекущаяСтрока.Количество > 0 И Не ЗначениеЗаполнено(ТекущаяСтрока.ВидЦены) Тогда
		СтруктураДействий.Вставить("ПересчитатьЦенуЗаУпаковку", ТекущаяСтрока.Количество);
	ИначеЕсли ТекущаяСтрока.Количество > 0 И ЗначениеЗаполнено(ТекущаяСтрока.ВидЦены) Тогда
		СтруктураДействий.Вставить("ЗаполнитьЦенуПродажи", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияЦеныВСтрокеТЧ(Объект));
	ИначеЕсли (ИспользоватьСоглашенияСКлиентами И ЗначениеЗаполнено(Объект.Соглашение))
		Или (Не ИспользоватьСоглашенияСКлиентами И ИспользуетсяЦенообразование25) Тогда
		СтруктураДействий.Вставить("ЗаполнитьУсловияПродаж", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияУсловийПродажВСтрокеТЧ(Объект));
	КонецЕсли;

	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());
	#Вставка
	//++ Тусеев Н.Ю. 27.01.2026 ПРД7
	СтруктураДействий.Вставить("ЗаполнитьВесУпаковки", 
	Новый Структура("Номенклатура, Упаковка", "ВесУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьОбъемУпаковки", 
	Новый Структура("Номенклатура, Упаковка", "ОбъемУпаковки"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияВеса", 
	Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияВеса"));
	СтруктураДействий.Вставить("ЗаполнитьЕдиницуИзмеренияОбъема", 
	Новый Структура("Номенклатура, Упаковка", "ЕдиницаИзмеренияОбъема"));
	СтруктураДействий.Вставить("ПересчитатьВесОбъем");
	//-- Тусеев Н.Ю. 27.01.2026 ПРД7
	#КонецВставки	
	ДобавитьДействияОбеспеченияНаКлиенте(СтруктураДействий, "ДоступноВДругихСтроках");

	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	Если ОбеспечениеВДокументахКлиент.ТребуетсяОбновитьКолонкуДоступно(СтруктураДействий) Тогда
		ОбновитьКолонкуДоступноСервер();
	КонецЕсли;
	#Вставка
    ТекущаяСтрока.ВесУпаковки = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(ТекущаяСтрока.Упаковка, "Числитель").Числитель;
    #КонецВставки 
	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);

	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);

КонецПроцедуры
//-- cba AlopinAS 27.01.26

//++ БаженоваЕО 02.02.2026 ПРД.18
&НаКлиенте
Процедура cbr_ТЧПересечениеМенеджеровПроцентПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.cbr_ТЧПересечениеМенеджеров.ТекущиеДанные;
	
	Если ТекущаяСтрока.Процент > 100 Тогда
		ТекущаяСтрока.Процент = 100;
	КонецЕсли;
	
КонецПроцедуры //-- БаженоваЕО 02.02.2026

//++ Тусеев Н.Ю. 04.02.2026 ПРД12
&НаКлиенте
Процедура cbr_АгентскоеВознаграждениеПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
	"СтраницаАгентскоеВознаграждение", "Видимость", 
	Объект.cbr_АгентскоеВознаграждение);
	
КонецПроцедуры

&НаСервере
Функция СовпадениеАгентскогоВознаграждения() 
	
	ДанныеСоглашения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Соглашение, "cbr_АгентскоеВознаграждение, cbr_АгентскиеВознаграждения");
	
	ТчСоглашения = ДанныеСоглашения.cbr_АгентскиеВознаграждения.Выгрузить();
	ТчЗаказа = Объект.cbr_АгентскиеВознаграждения.Выгрузить(, "НомерСтроки, Агент, ДоговорАгента, СтавкаАВ").Скопировать();
	
	АдресТЗ = ПоместитьВоВременноеХранилище(ТчСоглашения, Новый УникальныйИдентификатор);
	
	СовпадаютТч = ОбщегоНазначения.КоллекцииИдентичны(ТчЗаказа, ТчСоглашения);
	
	Возврат Новый Структура("СовпадлаетАгентскоеВознаграждение, СовпадаетТч, ЗначениеАгентскоеВознаграждение, АдресТЗ", 
	Объект.cbr_АгентскоеВознаграждение = ДанныеСоглашения.cbr_АгентскоеВознаграждение, 
	СовпадаютТч, ДанныеСоглашения.cbr_АгентскоеВознаграждение, АдресТЗ); 
		
КонецФункции

&НаКлиенте
Асинх Процедура	cbr_ЗадатьВопросОбОбновленииАгентскогоВознаграждения(ДанныеСоглашения)
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Обновить информацию по агентскому вознаграждению?'"), РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда  
		cbr_ОбновитьАгентскогоВознаграждения(ДанныеСоглашения);
	КонецЕсли	
	
КонецПроцедуры

&НаСервере
Процедура cbr_ОбновитьАгентскогоВознаграждения(ДанныеСоглашения)
	
	Объект.cbr_АгентскоеВознаграждение = ДанныеСоглашения.ЗначениеАгентскоеВознаграждение;
	
	Объект.cbr_АгентскиеВознаграждения.Очистить();
	ТчСоглашения = ПолучитьИзВременногоХранилища(ДанныеСоглашения.АдресТЗ);
	Для Каждого СтрокаТч Из ТчСоглашения Цикл
		НоваяСтр = Объект.cbr_АгентскиеВознаграждения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтр, СтрокаТч);
	КонецЦикла;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СтраницаАгентскоеВознаграждение", 
	"Видимость", Объект.cbr_АгентскоеВознаграждение);
	
КонецПроцедуры //++ Тусеев Н.Ю. 04.02.2026 ПРД12

//++ cba AlopinAS 27.01.26
&ИзменениеИКонтроль("ТоварыХарактеристикаПриИзменении")
&НаКлиенте
Процедура cbr_ТоварыХарактеристикаПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	#Вставка
	
	//++ cba AlopinAS 27.01.26
	ЭтоНеОбеспечивать = ?(ТекущаяСтрока.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется"),
	Истина, Ложь);
	//-- cba AlopinAS 27.01.26
	
	#КонецВставки
	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);

	ОчиститьСообщения();
	ПрименитьОплатуБонуснымиБалламиКлиент(, Истина);

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());
	СтруктураДействий.Вставить("ЗаполнитьСодержание", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСодержанияУслугиВСтрокеТЧ(Объект, Ложь));
	
	ДобавитьДействияОбеспеченияНаКлиенте(СтруктураДействий,
		"ВариантОбеспечения,ДоступноВДругихСтроках,СкладОбязателен,ДатаОтгрузкиОбязательна");
	
	ДобавитьДействиеЗаполненияЦен(СтруктураДействий);

	СтруктураДействий.Вставить("ХарактеристикаПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "Товары"));

	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);

	Если ОбеспечениеВДокументахКлиент.ТребуетсяОбновитьКолонкуДоступно(СтруктураДействий) Тогда
		ОбновитьКолонкуДоступноСервер();
	КонецЕсли;

	ОбеспечениеВДокументахКлиентСервер.ЗаполнитьСлужебныеРеквизиты(Объект.Товары, ДатаОтгрузкиОбязательна, СкладОбязателен);

	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);

	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);
	#Вставка
	
	//++ cba AlopinAS 27.01.26
	Если ЭтоНеОбеспечивать Тогда
		ТекущаяСтрока.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется");
		ДоступныеОстаткиПараметрыВстраивания.СлужебныеПараметры.ВариантОбеспеченияВыбранноеЗначение = Неопределено;
	
		ОбеспечениеВДокументахКлиент.ВариантОбеспеченияПриИзменении(
        	ЭтотОбъект,
       		Неопределено,
        	"ОбработатьВыборСкладаИСерии",
        	Истина,
        	"ТоварыВариантОбеспеченияПриИзмененииПослеВопроса");
	КонецЕсли;
	//-- cba AlopinAS 27.01.26
	
	//++AlopinAS 28.01.2026 ПРД.20
	ТекущаяСтрокаТаблицы = Элементы.Товары.ТекущаяСтрока;
	cbr_ОбработатьНоменклатуру(ТекущаяСтрокаТаблицы);
	
	ТоварыЦенаСоСкидкойПриИзменении(Элемент);
	//--AlopinAS 28.01.2026 ПРД.20
	#КонецВставки
	
КонецПроцедуры


&ИзменениеИКонтроль("ТоварыКоличествоУпаковокПриИзменении")
&НаКлиенте
Процедура cbr_ТоварыКоличествоУпаковокПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;

	ПроверкаОплатыБонуснымиБаллами(ТекущаяСтрока);

	СтруктураДействий = Новый Структура;
	ДобавитьВСтруктуруДействияПриИзмененииКоличестваУпаковок(СтруктураДействий,Объект);
	ДобавитьДействияОбеспеченияНаКлиенте(СтруктураДействий, "ДоступноВДругихСтроках");

	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	Если ОбеспечениеВДокументахКлиент.ТребуетсяОбновитьКолонкуДоступно(СтруктураДействий) Тогда
		ОбновитьКолонкуДоступноСервер();
	КонецЕсли;
	#Вставка
	Если ТекущаяСтрока.КоличествоУпаковок <> 0 Тогда
		ТекущаяСтрока.Сумма = ТекущаяСтрока.КоличествоУпаковок * ТекущаяСтрока.cbr_ЦенаСоСкидкой;
		ТоварыСуммаПриИзменении(Элемент);
	КонецЕсли;
	#КонецВставки
	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);

	СкидкиНаценкиЗаполнениеКлиент.СброситьФлагСкидкиРассчитаны(ЭтаФорма);

КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаВыбораПодборНаСервере")
&НаСервере
Процедура cbr_ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)

	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));

	ПараметрыЗаполненияСтавкиНДС = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(Объект, Истина);
	Если Объект.НеОтгружатьЧастями И ЗначениеЗаполнено(Объект.ДатаОтгрузки) Тогда
		ПараметрыЗаполненияСтавкиНДС.Дата = Объект.ДатаОтгрузки;
	КонецЕсли;

	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", ПараметрыЗаполненияСтавкиНДС);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДСВозвратнойТары", Объект.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ЗаполнитьПризнакБезВозвратнойТары", Объект.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ЗаполнитьПризнакОтмененоБезВозвратнойТары", Объект.ВернутьМногооборотнуюТару);
	СтруктураДействий.Вставить("ЗаполнитьСодержание", ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруЗаполненияСодержанияУслугиВСтрокеТЧ(Объект, Ложь));
	Если Объект.ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПоставкаПодПринципала Тогда
		СтруктураДействий.Вставить("ЗаполнитьПодразделениеВСтрокеТЧ", Новый Структура("Подразделение", Объект.Подразделение));
	КонецЕсли;

	СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
	СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");

	СтруктураДействий.Вставить("ЗаполнитьДубликатыЗависимыхРеквизитов", ЗависимыеРеквизиты());

	ДобавитьДействияОбеспеченияНаСервере(СтруктураДействий, "СкладОбязателен,ДатаОтгрузкиОбязательна");

	НачалоДня = НачалоДня(ТекущаяДатаСеанса());

	ПоляДляЗаполнения = "НоменклатураНабора, ХарактеристикаНабора, Номенклатура, Характеристика, Упаковка, Склад, ДатаОтгрузки,
		|ВидЦены, Цена, КоличествоУпаковок, Количество, Сумма, СуммаНДС, СуммаСНДС, СуммаРучнойСкидки, ПроцентРучнойСкидки,
		|СрокПоставки, ВариантОбеспечения, Обособленно, Серия";
	ПоляПоиска = "НоменклатураНабора, ХарактеристикаНабора, Номенклатура, Характеристика, Упаковка, Склад,
		|ВидЦены, ВариантОбеспечения, Обособленно, Серия, Цена";
	Если Не Объект.НеОтгружатьЧастями Тогда
		ПоляПоиска = ПоляПоиска + ", ДатаОтгрузки";
	КонецЕслИ;
	ПараметрыОтбора = Новый Структура(ПоляПоиска);
	ОбрабатываемыеСтроки = Новый Массив;
	ДатыОтгрузки = Новый Соответствие;

	ТребуетсяПересчетУпаковок = Ложь;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл

		Если ЗначениеЗаполнено(СтрокаТовара.НоменклатураНабора) Тогда
			// Для наборов следует увеличивать количество в строках набора, которые уже добавлены в ТЧ.

			ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаТовара);
			НайденныеСтроки = Объект.Товары.НайтиСтроки(ПараметрыОтбора);

			Если НайденныеСтроки.Количество() > 0 Тогда
				ТекущаяСтрока = НайденныеСтроки[0];
				ТекущаяСтрока.Количество = ТекущаяСтрока.Количество + СтрокаТовара.Количество;
				ТребуетсяПересчетУпаковок = Истина;
			Иначе
				ТекущаяСтрока = Объект.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, ПоляДляЗаполнения);
			КонецЕсли;
		Иначе
			// Для не-наборов следует всегда добавлять новую строку, независимо от наличия такого товара в ТЧ.

			ТекущаяСтрока = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, ПоляДляЗаполнения);
		КонецЕсли;

		ТекущаяСтрока.ИндексНабора = ?(ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураНабора), 1, 0);
		ОбрабатываемыеСтроки.Добавить(ТекущаяСтрока);
		ДатыОтгрузки.Вставить(ТекущаяСтрока, СтрокаТовара.ДатаОтгрузки);

	КонецЦикла;
	Если ТребуетсяПересчетУпаковок Тогда
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	КонецЕсли;

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		Объект.Товары,
		КэшированныеЗначения);

	Для Каждого ТекущаяСтрока Из ОбрабатываемыеСтроки Цикл

		ТекущаяСтрока.ДатаОтгрузки = ОбеспечениеВДокументахСервер.ДатаОтгрузкиОбработкаВыбораПодбор(
			ДатыОтгрузки.Получить(ТекущаяСтрока),
			ТекущаяСтрока.ВариантОбеспечения,
			НачалоДня);

	КонецЦикла;

	ОбеспечениеВДокументахСервер.АктуализироватьДатуОтгрузкиОднойДатой(
		Объект.НеОтгружатьЧастями,
		Объект.ДатаОтгрузки,
		Объект.Товары,
		"ДатаОтгрузки");
	ОбновитьКолонкуДоступноСервер();

	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре();
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);

	СкладыСервер.ПриИзмененииСкладаВТабличнойЧасти(Объект.Товары, ТаблицаСкладов, СкладГруппа);
	ВсегоСкладов = ТаблицаСкладов.Количество();
	СкладыКлиентСервер.ОбновитьКартинкуГруппыСкладов(НадписьНесколькоСкладов, Элементы.КартинкаНесколькоСкладов, ВсегоСкладов);
	#Вставка
	ТоварыТЧЦенаСоСкидкойПриИзмененииНаСервере();
	#КонецВставки
	РассчитатьИтоговыеПоказателиЗаказа(ЭтаФорма);
	МногооборотнаяТараСервер.ОбновитьСостояниеЗаполненияМногооборотнойТары(Объект.СостояниеЗаполненияМногооборотнойТары);

КонецПроцедуры
#КонецОбласти
//-- cba AlopinAS 27.01.26




