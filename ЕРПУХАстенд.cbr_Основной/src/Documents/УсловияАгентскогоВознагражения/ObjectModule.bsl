#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") 
		И Не ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту")
		Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаРеквизитов = "Партнер, Контрагент, Организация, Договор";
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.КоммерческоеПредложениеКлиенту") Тогда
		СтрокаРеквизитов = "Контрагент, Организация, Контрагент.Партнер";
	КонецЕсли;
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, СтрокаРеквизитов);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОснования);
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ПартнерКлиента = РеквизитыОснования.Партнер;
	Иначе
		ПартнерКлиента = РеквизитыОснования.КонтрагентПартнер;
	КонецЕсли;
	Клиент = РеквизитыОснования.Контрагент;
	Основание = ДанныеЗаполнения;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазовыеУсловияАгентскогоВознагражденияСрезПоследних.Агент КАК Агент,
		|	БазовыеУсловияАгентскогоВознагражденияСрезПоследних.ДоговорАгента КАК ДоговорАгента,
		|	БазовыеУсловияАгентскогоВознагражденияСрезПоследних.Агент.Партнер КАК ПартнерАгент,
		|	БазовыеУсловияАгентскогоВознагражденияСрезПоследних.БазоваяСтавка КАК БазоваяСтавка,
		|	БазовыеУсловияАгентскогоВознагражденияСрезПоследних.БазоваяСтавка КАК СтавкаАгента
		|ПОМЕСТИТЬ ВТ_Агенты
		|ИЗ
		|	РегистрСведений.БазовыеУсловияАгентскогоВознаграждения.СрезПоследних(
		|			&ДатаСреза,
		|			Организация = &Организация
		|				И Клиент = &Клиент
		|				И ДатаРеализации >= &ДатаСреза) КАК БазовыеУсловияАгентскогоВознагражденияСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Агенты.Агент КАК Агент,
		|	ВТ_Агенты.ДоговорАгента КАК ДоговорАгента,
		|	ВТ_Агенты.ПартнерАгент КАК ПартнерАгент,
		|	ВТ_Агенты.БазоваяСтавка КАК БазоваяСтавка,
		|	ВТ_Агенты.СтавкаАгента КАК СтавкаАгента
		|ИЗ
		|	ВТ_Агенты КАК ВТ_Агенты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ВТ_Агенты.Агент КАК Агент,
		|	ВТ_Агенты.СтавкаАгента КАК СтавкаАгента
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары,
		|	ВТ_Агенты КАК ВТ_Агенты
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Документ
		|	И НЕ ЗаказКлиентаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|	И ТИПЗНАЧЕНИЯ(&Документ) = ТИП(Документ.ЗаказКлиента)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика,
		|	ВТ_Агенты.Агент,
		|	ВТ_Агенты.СтавкаАгента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ВТ_Агенты.Агент КАК Агент,
		|	ВТ_Агенты.СтавкаАгента КАК СтавкаАгента
		|ИЗ
		|	Документ.КоммерческоеПредложениеКлиенту.Товары КАК ЗаказКлиентаТовары,
		|	ВТ_Агенты КАК ВТ_Агенты
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Документ
		|	И НЕ ЗаказКлиентаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
		|	И ТИПЗНАЧЕНИЯ(&Документ) = ТИП(Документ.КоммерческоеПредложениеКлиенту)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика,
		|	ВТ_Агенты.Агент,
		|	ВТ_Агенты.СтавкаАгента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	УсловияАгентскогоВознаграждения.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрСведений.УсловияАгентскогоВознаграждения КАК УсловияАгентскогоВознаграждения
		|ГДЕ
		|	УсловияАгентскогоВознаграждения.Основание = &Документ";
	
	Запрос.УстановитьПараметр("ДатаСреза", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Клиент", Клиент);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Документ", ДанныеЗаполнения);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаУсловий = РезультатЗапроса[3].Выбрать();
	Если ВыборкаУсловий.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Агенты.Очистить();
	Номенклатура.Очистить();
	
	ВыборкаАгентов = РезультатЗапроса[1].Выбрать();	
	Пока ВыборкаАгентов.Следующий() Цикл
		НоваяСтрока = Агенты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаАгентов);		
	КонецЦикла;
	
	ВыборкаНоменклатуры = РезультатЗапроса[2].Выбрать();
	Пока ВыборкаНоменклатуры.Следующий() Цикл
		НоваяСтрока = Номенклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаНоменклатуры);		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтатусСогласования = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(
													Ссылка,
													ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
													
	Если Не СтатусСогласования = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Утверждена") Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	СтатусСогласования = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(
													Ссылка,
													ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
													
	Если СтатусСогласования = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Утверждена") Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru='Документ уже согласован. Отмена проведения невозможна!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры  

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли