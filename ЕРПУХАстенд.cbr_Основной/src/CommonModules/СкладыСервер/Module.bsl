#Область СлужебныеПроцедурыИФункции
&ИзменениеИКонтроль("ПодобратьЯчейкиДляОтбора")
Процедура cbr_ПодобратьЯчейкиДляОтбора(МенеджерВременныхТаблиц, Склад, Помещение, СтруктураТаблиц, ОтборТары)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	// В запросе выбираются все ячейки по которым есть остатки и ячейки сортируются по правилам.

	Если ОтборТары Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТоварыВЯчейкахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыВЯчейкахОстатки.Характеристика КАК Характеристика,
		|	ТоварыВЯчейкахОстатки.Назначение КАК Назначение,
		|	ТоварыВЯчейкахОстатки.Упаковка КАК Упаковка,
		|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток  КАК ВНаличииОстаток,
		|	ТоварыВЯчейкахОстатки.КОтборуОстаток  КАК КОтборуОстаток
		|ПОМЕСТИТЬ ТоварыВЯчейкахОстаткиДляЗапроса
		|ИЗ
		|	РегистрНакопления.ТоварыВЯчейках.Остатки(
		|			,
		|			(Номенклатура, Характеристика, Назначение) В
		|					(ВЫБРАТЬ
		|						ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|						ТаблицаНоменклатуры.Характеристика КАК Характеристика,
		|						ТаблицаНоменклатуры.Назначение КАК Назначение
		|					ИЗ
		|						ТаблицаНоменклатурыРасширенная КАК ТаблицаНоменклатуры)
		|				И Ячейка В
		|					(ВЫБРАТЬ
		|						СпрЯчейки.Ссылка КАК СкладскаяЯчейка
		|					ИЗ
		|						Справочник.СкладскиеЯчейки КАК СпрЯчейки
		|							ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БлокировкиСкладскихЯчеек КАК БлокировкиЯчеек
		|							ПО
		|								СпрЯчейки.Ссылка = БлокировкиЯчеек.Ячейка
		|									И (БлокировкиЯчеек.ТипБлокировки = ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Полная)
		|										ИЛИ БлокировкиЯчеек.ТипБлокировки = ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Отбор))
		|					ГДЕ
		|						БлокировкиЯчеек.ТипБлокировки ЕСТЬ NULL
		|						И СпрЯчейки.Помещение = &Помещение
		|						И СпрЯчейки.Владелец = &Склад
		|						И НЕ СпрЯчейки.ЭтоГруппа
		|						И (СпрЯчейки.ТипСкладскойЯчейки = ЗНАЧЕНИЕ(Перечисление.ТипыСкладскихЯчеек.Хранение)
		|							ИЛИ СпрЯчейки.ТипСкладскойЯчейки = ЗНАЧЕНИЕ(Перечисление.ТипыСкладскихЯчеек.Архив)))) КАК ТоварыВЯчейкахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВЯчейкахОстатки.Ячейка,
		|	ТоварыВЯчейкахОстатки.Упаковка,
		|	ТоварыВЯчейкахОстатки.Назначение,
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток КАК ВНаличии,
		|	ТоварыВЯчейкахОстатки.КОтборуОстаток КАК КОтбору
		|ПОМЕСТИТЬ ОстаткиУпаковокВТаре
		|ИЗ
		|	РегистрНакопления.ТоварыВЯчейках.Остатки(
		|			,
		|			Ячейка В
		|					(ВЫБРАТЬ
		|						ТоварыВЯчейкахОстаткиДляЗапроса.Ячейка
		|					ИЗ
		|						ТоварыВЯчейкахОстаткиДляЗапроса КАК ТоварыВЯчейкахОстаткиДляЗапроса)
		|				И Упаковка.ПоставляетсяВМногооборотнойТаре) КАК ТоварыВЯчейкахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Ячейка,
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Назначение КАК Назначение,
		|	СУММА(ВложенныйЗапрос.ВНаличии) КАК ВНаличии,
		|	СУММА(ВложенныйЗапрос.КОтбору) КАК КОтбору
		|ПОМЕСТИТЬ ТараПодТовары
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОстаткиУпаковокВТаре.Ячейка КАК Ячейка,
		|		ОстаткиУпаковокВТаре.Упаковка.НоменклатураМногооборотнаяТара КАК Номенклатура,
		|		ОстаткиУпаковокВТаре.Упаковка.ХарактеристикаМногооборотнаяТара КАК Характеристика,
		|		ОстаткиУпаковокВТаре.Назначение КАК Назначение,
		|		ОстаткиУпаковокВТаре.ВНаличии КАК ВНаличии,
		|		ОстаткиУпаковокВТаре.КОтбору КАК КОтбору
		|	ИЗ
		|		ОстаткиУпаковокВТаре КАК ОстаткиУпаковокВТаре
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ОстаткиУпаковокВТаре.Ячейка,
		|		ВариантыПереупаковки.Упаковка.НоменклатураМногооборотнаяТара,
		|		ВариантыПереупаковки.Упаковка.ХарактеристикаМногооборотнаяТара,
		|		ОстаткиУпаковокВТаре.Назначение,
		|		ОстаткиУпаковокВТаре.ВНаличии * ВариантыПереупаковки.Количество,
		|		ОстаткиУпаковокВТаре.КОтбору * ВариантыПереупаковки.Количество
		|	ИЗ
		|		ОстаткиУпаковокВТаре КАК ОстаткиУпаковокВТаре
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВариантыПереупаковки КАК ВариантыПереупаковки
		|			ПО ОстаткиУпаковокВТаре.Упаковка = ВариантыПереупаковки.Источник) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.Ячейка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.Ячейка,
		|	СУММА(ВложенныйЗапрос.ВНаличииОстаток) КАК ВНаличииОстаток,
		|	СУММА(ВложенныйЗапрос.КОтборуОстаток) КАК КОтборуОстаток,
		|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
		|	ВложенныйЗапрос.Ячейка.Код КАК Код,
		|	ВложенныйЗапрос.Ячейка.УровеньДоступности КАК УровеньДоступности,
		|	ВложенныйЗапрос.Ячейка.РабочийУчасток КАК РабочийУчасток,
		|	ВложенныйЗапрос.Ячейка.ПорядокОбхода КАК ПорядокОбхода,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
		|	1 КАК КоличествоВУпаковке,
		|	&ТекстЗапросаВесУпаковки КАК Вес,
		|	ЗНАЧЕНИЕ(Справочник.СкладскиеГруппыУпаковок.ПустаяСсылка) КАК СкладскаяГруппаУпаковок,
		|	&ТекстЗапросаОбъемУпаковки КАК Объем,
		|	ЛОЖЬ КАК Безразмерная,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ПриоритетОтбораИзМонотоварныхЯчеек КАК ПриоритетОтбораИзМонотоварныхЯчеек,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ПриоритетОтбораИзСмешанныхЯчеек КАК ПриоритетОтбораИзСмешанныхЯчеек,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ПриоритетОтбораПодОстаток КАК ПриоритетОтбораПодОстаток,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ОписаниеМонотоварности КАК ОписаниеМонотоварности,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.СтрогаяМонотоварность КАК СтрогаяМонотоварность,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения КАК ОбластьХранения
		|ПОМЕСТИТЬ ТоварыВЯчейкахОстатки
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыВЯчейкахОстаткиДляЗапроса.Номенклатура КАК Номенклатура,
		|		ТоварыВЯчейкахОстаткиДляЗапроса.Характеристика КАК Характеристика,
		|		ТоварыВЯчейкахОстаткиДляЗапроса.Назначение КАК Назначение,
		|		ТоварыВЯчейкахОстаткиДляЗапроса.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстаткиДляЗапроса.ВНаличииОстаток КАК ВНаличииОстаток,
		|	ТоварыВЯчейкахОстаткиДляЗапроса.КОтборуОстаток КАК КОтборуОстаток
		|	ИЗ
		|		ТоварыВЯчейкахОстаткиДляЗапроса КАК ТоварыВЯчейкахОстаткиДляЗапроса
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТараПодТовары.Номенклатура,
		|		ТараПодТовары.Характеристика,
		|		ТараПодТовары.Назначение,
		|		ТараПодТовары.Ячейка,
		|		-ТараПодТовары.ВНаличии,
		|		-ТараПодТовары.КОтбору
		|	ИЗ
		|		ТараПодТовары КАК ТараПодТовары) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.Ячейка,
		|	ВложенныйЗапрос.Ячейка.Код,
		|	ВложенныйЗапрос.Ячейка.УровеньДоступности,
		|	ВложенныйЗапрос.Ячейка.РабочийУчасток,
		|	ВложенныйЗапрос.Ячейка.ПорядокОбхода,
		|	&ТекстЗапросаВесУпаковки,
		|	&ТекстЗапросаОбъемУпаковки,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ПриоритетОтбораИзМонотоварныхЯчеек,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ПриоритетОтбораИзСмешанныхЯчеек,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ПриоритетОтбораПодОстаток,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.ОписаниеМонотоварности,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения.СтрогаяМонотоварность,
		|	ВложенныйЗапрос.Ячейка.ОбластьХранения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТоварыВЯчейкахОстаткиДляЗапроса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОстаткиУпаковокВТаре
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТараПодТовары";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВесУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения", "ВложенныйЗапрос.Номенклатура", Ложь) );
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаОбъемУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения", "ВложенныйЗапрос.Номенклатура", Ложь));

	Иначе

		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТоварыВЯчейкахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыВЯчейкахОстатки.Характеристика КАК Характеристика,
		|	ТоварыВЯчейкахОстатки.Назначение КАК Назначение,
		|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток КАК ВНаличииОстаток,
		|	ТоварыВЯчейкахОстатки.КОтборуОстаток КАК КОтборуОстаток,
		|	ТоварыВЯчейкахОстатки.Серия КАК Серия,
		|	ТоварыВЯчейкахОстатки.Ячейка.Код КАК Код,
		|	ТоварыВЯчейкахОстатки.Ячейка.УровеньДоступности КАК УровеньДоступности,
		|	ТоварыВЯчейкахОстатки.Ячейка.РабочийУчасток КАК РабочийУчасток,
		|	ТоварыВЯчейкахОстатки.Ячейка.ПорядокОбхода КАК ПорядокОбхода,
		|	ТоварыВЯчейкахОстатки.Упаковка КАК Упаковка,
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоВУпаковке,
		|	&ТекстЗапросаВесУпаковки КАК Вес,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка.СкладскаяГруппа, ЗНАЧЕНИЕ(Справочник.СкладскиеГруппыУпаковок.ПустаяСсылка)) КАК СкладскаяГруппаУпаковок,
		|	&ТекстЗапросаОбъемУпаковки КАК Объем,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка.Безразмерная, ЛОЖЬ) КАК Безразмерная,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ПриоритетОтбораИзМонотоварныхЯчеек КАК ПриоритетОтбораИзМонотоварныхЯчеек,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ПриоритетОтбораИзСмешанныхЯчеек КАК ПриоритетОтбораИзСмешанныхЯчеек,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ПриоритетОтбораПодОстаток КАК ПриоритетОтбораПодОстаток,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ОписаниеМонотоварности КАК ОписаниеМонотоварности,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.СтрогаяМонотоварность КАК СтрогаяМонотоварность,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения КАК ОбластьХранения
		|ПОМЕСТИТЬ ТоварыВЯчейкахОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыВЯчейках.Остатки(
		|			,
		|			(Номенклатура, Характеристика, Назначение) В
		|					(ВЫБРАТЬ
		|						ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|						ТаблицаНоменклатуры.Характеристика КАК Характеристика,
		|						ТаблицаНоменклатуры.Назначение КАК Назначение
		|					ИЗ
		|						ТаблицаНоменклатурыРасширенная КАК ТаблицаНоменклатуры)
		|				И Ячейка В
		|					(ВЫБРАТЬ
		|						СпрЯчейки.Ссылка КАК СкладскаяЯчейка
		|					ИЗ
		|						Справочник.СкладскиеЯчейки КАК СпрЯчейки
		|							ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БлокировкиСкладскихЯчеек КАК БлокировкиЯчеек
		|							ПО
		|								СпрЯчейки.Ссылка = БлокировкиЯчеек.Ячейка
		|									И (БлокировкиЯчеек.ТипБлокировки = ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Полная)
		|										ИЛИ БлокировкиЯчеек.ТипБлокировки = ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Отбор))
		|					ГДЕ
		|						БлокировкиЯчеек.ТипБлокировки ЕСТЬ NULL
		|						И СпрЯчейки.Помещение = &Помещение
		|						И СпрЯчейки.Владелец = &Склад
		|						И НЕ СпрЯчейки.ЭтоГруппа
		|						И (СпрЯчейки.ТипСкладскойЯчейки = ЗНАЧЕНИЕ(Перечисление.ТипыСкладскихЯчеек.Хранение)
		|							ИЛИ СпрЯчейки.ТипСкладскойЯчейки = ЗНАЧЕНИЕ(Перечисление.ТипыСкладскихЯчеек.Архив)))) КАК ТоварыВЯчейкахОстатки
		|ГДЕ
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка.ТипУпаковки, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВесУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("ТоварыВЯчейкахОстатки.Упаковка", "ТоварыВЯчейкахОстатки.Номенклатура", Ложь));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаОбъемУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("ТоварыВЯчейкахОстатки.Упаковка", "ТоварыВЯчейкахОстатки.Номенклатура", Ложь));

	КонецЕсли;
	ТекстЗапроса = 	ТекстЗапроса + ";" + Символы.ПС +
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаНоменклатуры.Назначение КАК Назначение,
	|	ТаблицаНоменклатуры.Серия КАК Серия,
	|	ТаблицаНоменклатуры.Распоряжение КАК Распоряжение,
	|	ТаблицаНоменклатуры.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ТаблицаНоменклатуры.Приоритет КАК Приоритет,
	|	ТаблицаНоменклатуры.Количество КАК Количество,
	|	ТаблицаНоменклатуры.СкладскаяГруппаНоменклатуры КАК СкладскаяГруппаНоменклатуры,
	|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,		
	|	ТоварыВЯчейкахОстатки.Код КАК Код,
	|	СУММА(ЕСТЬNULL(ТоварыВЯчейкахОстатки.ВНаличииОстаток, 0) - ЕСТЬNULL(ТоварыВЯчейкахОстатки.КОтборуОстаток, 0)) КАК СвободныйОстаток,
	|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.УровеньДоступности, 9999999999) КАК УровеньДоступности,
	|	ТоварыВЯчейкахОстатки.РабочийУчасток КАК РабочийУчасток,
	|	ТоварыВЯчейкахОстатки.ПорядокОбхода КАК ПорядокОбхода,
	|	ТоварыВЯчейкахОстатки.Упаковка КАК Упаковка,
	|	ТоварыВЯчейкахОстатки.КоличествоВУпаковке КАК КоличествоВУпаковке,
	|	ТоварыВЯчейкахОстатки.Вес КАК Вес,
	|	ТоварыВЯчейкахОстатки.СкладскаяГруппаУпаковок КАК СкладскаяГруппаУпаковок,
	|	ТоварыВЯчейкахОстатки.Объем КАК Объем,
	|	ТоварыВЯчейкахОстатки.Безразмерная КАК Безразмерная,
	|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзМонотоварныхЯчеек КАК ПриоритетОтбораИзМонотоварныхЯчеек,
	|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзСмешанныхЯчеек КАК ПриоритетОтбораИзСмешанныхЯчеек,
	|	ТоварыВЯчейкахОстатки.ПриоритетОтбораПодОстаток КАК ПриоритетОтбораПодОстаток,
	|	ТоварыВЯчейкахОстатки.ОписаниеМонотоварности КАК ОписаниеМонотоварности,
	|	ТоварыВЯчейкахОстатки.СтрогаяМонотоварность КАК СтрогаяМонотоварность,
	|	ТоварыВЯчейкахОстатки.ОбластьХранения КАК ОбластьХранения
	|ПОМЕСТИТЬ ПулЯчеек
	|ИЗ
	|	ТаблицаНоменклатурыРасширенная КАК ТаблицаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыВЯчейкахОстатки КАК ТоварыВЯчейкахОстатки
	|		ПО ТаблицаНоменклатуры.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
	|			И ТаблицаНоменклатуры.Характеристика = ТоварыВЯчейкахОстатки.Характеристика
	|			И ТаблицаНоменклатуры.Назначение = ТоварыВЯчейкахОстатки.Назначение
	|			И (ВЫБОР
	|				КОГДА ТаблицаНоменклатуры.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ТаблицаНоменклатуры.Серия = ТоварыВЯчейкахОстатки.Серия
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаНоменклатуры.Номенклатура,
	|	ТаблицаНоменклатуры.Характеристика,
	|	ТаблицаНоменклатуры.Назначение,
	|	ТаблицаНоменклатуры.Серия,
	|	ТаблицаНоменклатуры.Распоряжение,
	|	ТаблицаНоменклатуры.ДатаОтгрузки,
	|	ТаблицаНоменклатуры.Приоритет,
	|	ТоварыВЯчейкахОстатки.Код,
	|	ТаблицаНоменклатуры.СкладскаяГруппаНоменклатуры,
	|	ТоварыВЯчейкахОстатки.Ячейка,
	|	ТаблицаНоменклатуры.Количество,
	|	ТоварыВЯчейкахОстатки.РабочийУчасток,
	|	ТоварыВЯчейкахОстатки.ПорядокОбхода,
	|	ТоварыВЯчейкахОстатки.Вес,
	|	ТоварыВЯчейкахОстатки.Упаковка,
	|	ТоварыВЯчейкахОстатки.Безразмерная,
	|	ТоварыВЯчейкахОстатки.СкладскаяГруппаУпаковок,
	|	ТоварыВЯчейкахОстатки.Объем,
	|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзМонотоварныхЯчеек,
	|	ТоварыВЯчейкахОстатки.КоличествоВУпаковке,
	|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзСмешанныхЯчеек,
	|	ТоварыВЯчейкахОстатки.ОбластьХранения,
	|	ТоварыВЯчейкахОстатки.ПриоритетОтбораПодОстаток,
	|	ТоварыВЯчейкахОстатки.ОписаниеМонотоварности,
	|	ТоварыВЯчейкахОстатки.СтрогаяМонотоварность,
	|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.УровеньДоступности, 9999999999)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПулЯчеек.Ячейка КАК Ячейка,
	|	ПулЯчеек.Номенклатура КАК Номенклатура,
	|	ПулЯчеек.Характеристика,
	|	ПулЯчеек.Назначение,
	|	ПулЯчеек.Серия,
	|	ВЫБОР
	|		КОГДА ПулЯчеек.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьДругаяНоменклатура,
	|	ВЫБОР
	|		КОГДА ПулЯчеек.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
	|				И ПулЯчеек.Характеристика = ТоварыВЯчейкахОстатки.Характеристика
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьДругаяНоменклатураИЛИХарактеристика,
	|	ВЫБОР
	|		КОГДА ПулЯчеек.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
	|				И ПулЯчеек.Характеристика = ТоварыВЯчейкахОстатки.Характеристика
	|				И ПулЯчеек.Серия = ТоварыВЯчейкахОстатки.Серия
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия
	|ПОМЕСТИТЬ ДругиеТоварыВЯчейках
	|ИЗ
	|	ПулЯчеек КАК ПулЯчеек
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВЯчейках.Остатки(
	|				,
	|				Ячейка В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ПулЯчеек.Ячейка
	|					ИЗ
	|						ПулЯчеек КАК ПулЯчеек)) КАК ТоварыВЯчейкахОстатки
	|		ПО ПулЯчеек.Ячейка = ТоварыВЯчейкахОстатки.Ячейка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДругиеТоварыВЯчейках.Номенклатура,
	|	ДругиеТоварыВЯчейках.Ячейка,
	|	МАКСИМУМ(ДругиеТоварыВЯчейках.ЕстьДругаяНоменклатура) КАК ЕстьДругаяНоменклатура
	|ПОМЕСТИТЬ НоменклатураВЯчейках
	|ИЗ
	|	ДругиеТоварыВЯчейках КАК ДругиеТоварыВЯчейках
	|
	|СГРУППИРОВАТЬ ПО
	|	ДругиеТоварыВЯчейках.Номенклатура,
	|	ДругиеТоварыВЯчейках.Ячейка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДругиеТоварыВЯчейках.Номенклатура,
	|	ДругиеТоварыВЯчейках.Ячейка,
	|	ДругиеТоварыВЯчейках.Характеристика,
	|	МАКСИМУМ(ДругиеТоварыВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристика) КАК ЕстьДругаяНоменклатураИЛИХарактеристика
	|ПОМЕСТИТЬ НоменклатураИХарактеристикаВЯчейках
	|ИЗ
	|	ДругиеТоварыВЯчейках КАК ДругиеТоварыВЯчейках
	|
	|СГРУППИРОВАТЬ ПО
	|	ДругиеТоварыВЯчейках.Номенклатура,
	|	ДругиеТоварыВЯчейках.Ячейка,
	|	ДругиеТоварыВЯчейках.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДругиеТоварыВЯчейках.Номенклатура,
	|	ДругиеТоварыВЯчейках.Ячейка,
	|	ДругиеТоварыВЯчейках.Характеристика,
	|	МАКСИМУМ(ДругиеТоварыВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия) КАК ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия,
	|	ДругиеТоварыВЯчейках.Серия
	|ПОМЕСТИТЬ НоменклатураИХарактеристикаИСерияВЯчейках
	|ИЗ
	|	ДругиеТоварыВЯчейках КАК ДругиеТоварыВЯчейках
	|
	|СГРУППИРОВАТЬ ПО
	|	ДругиеТоварыВЯчейках.Номенклатура,
	|	ДругиеТоварыВЯчейках.Ячейка,
	|	ДругиеТоварыВЯчейках.Характеристика,
	|	ДругиеТоварыВЯчейках.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПулЯчеек.Номенклатура КАК Номенклатура,
	|	ПулЯчеек.Характеристика КАК Характеристика,
	|	ПулЯчеек.Назначение КАК Назначение,
	|	ПулЯчеек.Серия КАК Серия,
	|	ПулЯчеек.Распоряжение КАК Распоряжение,
	|	ПулЯчеек.Количество КАК Количество,
	|	ПулЯчеек.Ячейка КАК Ячейка,
	|	ПулЯчеек.СвободныйОстаток КАК СвободныйОстаток,
	|	ПулЯчеек.РабочийУчасток КАК РабочийУчасток,
	|	ПулЯчеек.ПорядокОбхода КАК ПорядокОбхода,
	|	ПулЯчеек.Код КАК Код,
	|	ПулЯчеек.Упаковка КАК Упаковка,
	|	ПулЯчеек.КоличествоВУпаковке КАК КоличествоВУпаковке,
	|	ПулЯчеек.Вес КАК Вес,
	|	ПулЯчеек.Объем КАК Объем,
	|	ВЫБОР
	|		КОГДА ПулЯчеек.СвободныйОстаток <= ПулЯчеек.Количество / ПулЯчеек.КоличествоВУпаковке
	|			ТОГДА ПулЯчеек.ПриоритетОтбораПодОстаток
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПулЯчеек.ОписаниеМонотоварности = ЗНАЧЕНИЕ(Перечисление.ОписаниеМонотоварности.ПоНоменклатуре)
	|					ТОГДА ВЫБОР
	|							КОГДА НЕ НоменклатураВЯчейках.ЕстьДругаяНоменклатура
	|								ТОГДА ПулЯчеек.ПриоритетОтбораИзМонотоварныхЯчеек
	|							ИНАЧЕ ПулЯчеек.ПриоритетОтбораИзСмешанныхЯчеек
	|						КОНЕЦ
	|				ИНАЧЕ ВЫБОР
	|						КОГДА ПулЯчеек.ОписаниеМонотоварности = ЗНАЧЕНИЕ(Перечисление.ОписаниеМонотоварности.ПоХарактеристике)
	|							ТОГДА ВЫБОР
	|									КОГДА НЕ НоменклатураИХарактеристикаВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристика
	|										ТОГДА ПулЯчеек.ПриоритетОтбораИзМонотоварныхЯчеек
	|									ИНАЧЕ ПулЯчеек.ПриоритетОтбораИзСмешанныхЯчеек
	|								КОНЕЦ
	|						ИНАЧЕ ВЫБОР
	|								КОГДА НЕ НоменклатураИХарактеристикаИСерияВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия
	|									ТОГДА ПулЯчеек.ПриоритетОтбораИзМонотоварныхЯчеек
	|								ИНАЧЕ ПулЯчеек.ПриоритетОтбораИзСмешанныхЯчеек
	|							КОНЕЦ
	|					КОНЕЦ
	|			КОНЕЦ
	|	КОНЕЦ КАК ПриоритетСтратегии,
	|	ВЫБОР
	|		КОГДА ПравилаРазмещенияТоваровВЯчейках.ОбластьХранения ЕСТЬ NULL
	|			ТОГДА 1000
	|		ИНАЧЕ ПравилаРазмещенияТоваровВЯчейках.Приоритет
	|	КОНЕЦ КАК ПриоритетОбласти
	|ИЗ
	|	ПулЯчеек КАК ПулЯчеек
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураВЯчейках КАК НоменклатураВЯчейках
	|		ПО ПулЯчеек.Номенклатура = НоменклатураВЯчейках.Номенклатура
	|			И ПулЯчеек.Ячейка = НоменклатураВЯчейках.Ячейка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаРазмещенияТоваровВЯчейках КАК ПравилаРазмещенияТоваровВЯчейках
	|		ПО ПулЯчеек.СкладскаяГруппаНоменклатуры = ПравилаРазмещенияТоваровВЯчейках.СкладскаяГруппаНоменклатуры
	|			И ПулЯчеек.СкладскаяГруппаУпаковок = ПравилаРазмещенияТоваровВЯчейках.СкладскаяГруппаУпаковок
	|			И ПулЯчеек.ОбластьХранения = ПравилаРазмещенияТоваровВЯчейках.ОбластьХранения
	|			И (ПравилаРазмещенияТоваровВЯчейках.Помещение = &Помещение)
	|			И (ПравилаРазмещенияТоваровВЯчейках.Склад = &Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИХарактеристикаВЯчейках КАК НоменклатураИХарактеристикаВЯчейках
	|		ПО ПулЯчеек.Номенклатура = НоменклатураИХарактеристикаВЯчейках.Номенклатура
	|			И ПулЯчеек.Ячейка = НоменклатураИХарактеристикаВЯчейках.Ячейка
	|			И ПулЯчеек.Характеристика = НоменклатураИХарактеристикаВЯчейках.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИХарактеристикаИСерияВЯчейках КАК НоменклатураИХарактеристикаИСерияВЯчейках
	|		ПО ПулЯчеек.Ячейка = НоменклатураИХарактеристикаИСерияВЯчейках.Ячейка
	|			И ПулЯчеек.Номенклатура = НоменклатураИХарактеристикаИСерияВЯчейках.Номенклатура
	|			И ПулЯчеек.Характеристика = НоменклатураИХарактеристикаИСерияВЯчейках.Характеристика
	|			И ПулЯчеек.Серия = НоменклатураИХарактеристикаИСерияВЯчейках.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияПересчетовЯчеек КАК СостоянияПересчетовЯчеек
	|		ПО ПулЯчеек.Ячейка = СостоянияПересчетовЯчеек.Ячейка
	|			И (СостоянияПересчетовЯчеек.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияПересчетовЯчеек.ТребуетсяПересчет))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НАЧАЛОПЕРИОДА(ПулЯчеек.ДатаОтгрузки, ДЕНЬ),
	|	ПулЯчеек.Приоритет УБЫВ,
	|	Распоряжение,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия,
	|	СостоянияПересчетовЯчеек.Состояние,
	|	ПриоритетОбласти УБЫВ,
	|	КоличествоВУпаковке УБЫВ,
	|	ПриоритетСтратегии,
	|	СвободныйОстаток,
	|	ПулЯчеек.УровеньДоступности,
	|	ПорядокОбхода,
	|	Код
	|ИТОГИ ПО
	|	Распоряжение,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НоменклатураИХарактеристикаИСерияВЯчейках
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НоменклатураИХарактеристикаВЯчейках
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НоменклатураВЯчейках
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДругиеТоварыВЯчейках
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПулЯчеек
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыВЯчейкахОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНоменклатурыРасширенная";

	#Вставка
	//++ СкребковЮМ 16.02.26 СКЛ.13
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПулЯчеек.Ячейка КАК Ячейка,",
		"ПулЯчеек.Ячейка КАК Ячейка, ПулЯчеек.Ячейка.cbr_СборкаСухих КАК СборкаСухих,");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПулЯчеек.Объем КАК Объем,",
		"	ПулЯчеек.Объем КАК Объем,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЕстьЭтапПроизводства,
		|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Упаковка КАК УпаковкаНоменклатуры,");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (СостоянияПересчетовЯчеек.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияПересчетовЯчеек.ТребуетсяПересчет))",
		"		И (СостоянияПересчетовЯчеек.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияПересчетовЯчеек.ТребуетсяПересчет))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
		|		ПО ПулЯчеек.Распоряжение = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка
		|			И ПулЯчеек.Номенклатура = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Номенклатура
		|			И ПулЯчеек.Характеристика = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Характеристика
		|			И ПулЯчеек.Назначение = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Назначение
		|			И ПулЯчеек.Серия = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Серия");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (СостоянияПересчетовЯчеек.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияПересчетовЯчеек.ТребуетсяПересчет))",
		"		И (СостоянияПересчетовЯчеек.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияПересчетовЯчеек.ТребуетсяПересчет))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК РасходныйОрдерНаТоварыОтгружаемыеТовары
		|		ПО ПулЯчеек.Распоряжение = РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка
		|			И ПулЯчеек.Номенклатура = РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура
		|			И ПулЯчеек.Характеристика = РасходныйОрдерНаТоварыОтгружаемыеТовары.Характеристика
		|			И ПулЯчеек.Назначение = РасходныйОрдерНаТоварыОтгружаемыеТовары.Назначение
		|			И ПулЯчеек.Серия = РасходныйОрдерНаТоварыОтгружаемыеТовары.Серия");
	//-- СкребковЮМ 16.02.26 СКЛ.13
	#КонецВставки

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ТоварыВЯчейкахОстатки.Упаковка", Неопределено));

	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("Склад",Склад);
	Запрос.УстановитьПараметр("Помещение",Помещение);

	УстановитьПривилегированныйРежим(Истина);
	ВыборкаПоРаспоряжениям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОтборСтрокИзТаблицыРасходаИзЯчеек = Новый Структура("Ячейка,Номенклатура,Упаковка,Характеристика,Назначение,Серия");
	
	#Вставка
	//++ СкребковЮМ 16.02.26 СКЛ.13
	ТаблицаОтбора = СтруктураТаблиц.ТаблицаРезультатов.СкопироватьКолонки();
	ТаблицаРазмещения = СтруктураТаблиц.ТаблицаРезультатов.СкопироватьКолонки();
	//-- СкребковЮМ 16.02.26 СКЛ.13
	#КонецВставки
	
	Пока ВыборкаПоРаспоряжениям.Следующий() Цикл
		
		ВыборкаПоНоменклатуре =  ВыборкаПоРаспоряжениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПоНоменклатуре.Следующий() Цикл
			
			ВыборкаПоХарактеристикам = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаПоХарактеристикам.Следующий() Цикл

				ВыборкаПоНазначению = ВыборкаПоХарактеристикам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаПоНазначению.Следующий() Цикл

					ВыборкаПоСериям = ВыборкаПоНазначению.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

					Пока ВыборкаПоСериям.Следующий() Цикл
						
						//детали
						ВыборкаПоЯчейкам = ВыборкаПоСериям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						
						#Вставка
						//++ СкребковЮМ 16.02.26 СКЛ.13
						ОстаткиСборкаСухих = 0;
						ОстаткиБезСборкиСухих = 0;
												
						Если ВыборкаПоЯчейкам.Следующий() Тогда
								
							ЕстьЭтапПроизводства = ВыборкаПоЯчейкам.ЕстьЭтапПроизводства;
							КоличествоКОтбору = ВыборкаПоЯчейкам.Количество;
														
							Если ВыборкаПоЯчейкам.СборкаСухих Тогда
								ОстаткиСборкаСухих = ВыборкаПоЯчейкам.СвободныйОстаток;
							ИначеЕсли НЕ ВыборкаПоЯчейкам.СборкаСухих Тогда
								ОстаткиБезСборкиСухих = ВыборкаПоЯчейкам.СвободныйОстаток;
							КонецЕсли;
							
							Если ВыборкаПоЯчейкам.УпаковкаНоменклатуры <> Неопределено
								И НЕ ВыборкаПоЯчейкам.УпаковкаНоменклатуры.Пустая() Тогда
									
								ЧислительУпаковки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаПоЯчейкам.Упаковка, "Числитель");
								ЧислительУпаковки = ?(ЧислительУпаковки = Неопределено, 0, ЧислительУпаковки);
								
							Иначе
								
								ЧислительУпаковки = 0;
								
							КонецЕсли;
							
						КонецЕсли;
						
						Пока ВыборкаПоЯчейкам.Следующий() Цикл
							
							Если ВыборкаПоЯчейкам.СборкаСухих Тогда
								ОстаткиСборкаСухих = ОстаткиСборкаСухих + ВыборкаПоЯчейкам.СвободныйОстаток;
							Иначе
								ОстаткиБезСборкиСухих = ОстаткиБезСборкиСухих + ВыборкаПоЯчейкам.СвободныйОстаток;
							КонецЕсли;
							
						КонецЦикла;
						
						Если ЕстьЭтапПроизводства Тогда
							
							КоличествоКОтборуСухих = Неопределено;
							КоличествоКОтборуБезСухих = Неопределено;
							
							СборкаСухих = Неопределено;
							Перемещение = Ложь;
							
							Если ЧислительУпаковки <> 0 Тогда
								КОбеспечению = Цел(КоличествоКОтбору / ЧислительУпаковки);
								КОбеспечениюОстаток = КоличествоКОтбору % ЧислительУпаковки;
							Иначе
								КОбеспечению = 0;
								КОбеспечениюОстаток = 0;
							КонецЕсли;
							
							Если КОбеспечению = 0 И КОбеспечениюОстаток > 0 И ОстаткиСборкаСухих >= КОбеспечениюОстаток Тогда
								
								СборкаСухих = Истина;
								КоличествоКОтборуСухих = КОбеспечениюОстаток;
								
							ИначеЕсли КОбеспечениюОстаток > 0 И ОстаткиСборкаСухих < КОбеспечениюОстаток Тогда
								
								Перемещение = Истина;
								СборкаСухих = Истина;
								КоличествоКОтборуСухих = КОбеспечениюОстаток;
								
								ЯчейкаПеремещения = Неопределено;
								КоличествоОтобрано = 0;
										
							ИначеЕсли КОбеспечениюОстаток = 0 Тогда

								СборкаСухих = Ложь;
								
								Если ЧислительУпаковки <> 0 Тогда
									КоличествоКОтборуБезСухих = КоличествоКОтбору / ЧислительУпаковки;
								КонецЕсли;
								
							ИначеЕсли КОбеспечению > 0 И КОбеспечениюОстаток > 0 И ОстаткиСборкаСухих >= КОбеспечениюОстаток Тогда
								
								КоличествоКОтборуСухих = КОбеспечениюОстаток;
								КоличествоКОтборуБезСухих = КОбеспечению;
								
							ИначеЕсли КОбеспечению > 0 И КОбеспечениюОстаток > 0 И ОстаткиСборкаСухих < КОбеспечениюОстаток Тогда
								
								Перемещение = Истина;
								КоличествоКОтборуСухих = КОбеспечениюОстаток;
								КоличествоКОтборуБезСухих = КОбеспечению;
								
								ЯчейкаПеремещения = Неопределено;
								КоличествоОтобрано = 0;
								
							КонецЕсли;
							
						КонецЕсли;
						
						ВыборкаПоЯчейкам.Сбросить();
						//-- СкребковЮМ 16.02.26 СКЛ.13
						#КонецВставки
						
						КоличествоКОтбору = Неопределено;
						
						#Вставка
						//++ СкребковЮМ 16.02.26 СКЛ.13
						Если ЕстьЭтапПроизводства Тогда
							
							Если КоличествоКОтборуСухих = Неопределено И КоличествоКОтборуБезСухих = Неопределено Тогда
								
								ЕстьЭтапПроизводства = Ложь;
								
							Иначе
								
								ТекущаяСборка = Неопределено;
								
								Если СборкаСухих Тогда
									КоличествоКОтбору = КоличествоКОтборуСухих;
								ИначеЕсли НЕ СборкаСухих Тогда
									КоличествоКОтбору = КоличествоКОтборуБезСухих;
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЕсли;
						//-- СкребковЮМ 16.02.26 СКЛ.13
						#КонецВставки
						
						Пока ВыборкаПоЯчейкам.Следующий() Цикл
							
							#Вставка
							//++ СкребковЮМ 16.02.26 СКЛ.13
							Если ЕстьЭтапПроизводства Тогда
																
								Если СборкаСухих <> Неопределено И СборкаСухих <> ВыборкаПоЯчейкам.СборкаСухих И НЕ Перемещение Тогда
									Продолжить;
								КонецЕсли;
								
								Если ТекущаяСборка = Неопределено Тогда
									ТекущаяСборка = ВыборкаПоЯчейкам.СборкаСухих;
								Иначе
									Если ТекущаяСборка = ВыборкаПоЯчейкам.СборкаСухих Тогда
										КоличествоКОтборуСухих = КоличествоКОтбору;
									Иначе
										КоличествоКОтборуБезСухих = КоличествоКОтбору;
									КонецЕсли;
								КонецЕсли;
								
								Если ВыборкаПоЯчейкам.СборкаСухих Тогда
									КоличествоКОтбору = КоличествоКОтборуСухих;
								Иначе
									КоличествоКОтбору = КоличествоКОтборуБезСухих;
								КонецЕсли;
								
							КонецЕсли;
							//-- СкребковЮМ 16.02.26 СКЛ.13
							#КонецВставки
							
							Если КоличествоКОтбору = Неопределено Тогда

								КоличествоКОтбору = ВыборкаПоЯчейкам.Количество;
								
							КонецЕсли;
							
							Если ЗначениеЗаполнено(ВыборкаПоЯчейкам.Упаковка)
								И КоличествоКОтбору < ВыборкаПоЯчейкам.КоличествоВУпаковке Тогда
								Продолжить;
							КонецЕсли;
							
							// Если ячейка заполнена, значит в ней есть остаток в нужной упаковке,
							// иначе таблица остатков не соединилась бы с таблицей номенклатуры.
							Если ЗначениеЗаполнено(ВыборкаПоЯчейкам.Ячейка)
								И ВыборкаПоЯчейкам.СвободныйОстаток > 0 Тогда
								
								ЗаполнитьЗначенияСвойств(ОтборСтрокИзТаблицыРасходаИзЯчеек, ВыборкаПоЯчейкам);

								МассивНайденныхСтрок = СтруктураТаблиц.ТаблицаРасходаИзЯчеек.НайтиСтроки(ОтборСтрокИзТаблицыРасходаИзЯчеек);

								Если МассивНайденныхСтрок.Количество() = 0 Тогда
									СтрокаРасхода 						= СтруктураТаблиц.ТаблицаРасходаИзЯчеек.Добавить();
									ЗаполнитьЗначенияСвойств(СтрокаРасхода, ОтборСтрокИзТаблицыРасходаИзЯчеек);

									СтрокаРасхода.КоличествоУпаковок 	= ВыборкаПоЯчейкам.СвободныйОстаток;

									РабочийУчасток = СтруктураТаблиц.МассивУчастков.Найти(ВыборкаПоЯчейкам.РабочийУчасток);
									Если РабочийУчасток = Неопределено Тогда
										СтруктураТаблиц.МассивУчастков.Добавить(ВыборкаПоЯчейкам.РабочийУчасток);
									КонецЕсли;
								Иначе
									СтрокаРасхода = МассивНайденныхСтрок[0];
								КонецЕсли;

								Если ЗначениеЗаполнено(ВыборкаПоЯчейкам.Упаковка) Тогда
									КоличествоИзЯчейки = Мин(Цел(КоличествоКОтбору/ВыборкаПоЯчейкам.КоличествоВУпаковке),СтрокаРасхода.КоличествоУпаковок);
								Иначе
									КоличествоИзЯчейки = Мин(КоличествоКОтбору,СтрокаРасхода.КоличествоУпаковок);
								КонецЕсли;
								
								#Удаление
								Если КоличествоИзЯчейки > 0 Тогда
									СтрокаРезультата = СтруктураТаблиц.ТаблицаРезультатов.Добавить();
									ЗаполнитьЗначенияСвойств(СтрокаРезультата, ВыборкаПоЯчейкам);
									СтрокаРезультата.КоличествоУпаковок = КоличествоИзЯчейки;
									СтрокаРезультата.Количество         = КоличествоИзЯчейки * ВыборкаПоЯчейкам.КоличествоВУпаковке;
									СтрокаРезультата.ОбъемУпаковки		= ВыборкаПоЯчейкам.Объем;
									СтрокаРезультата.ВесУпаковки		= ВыборкаПоЯчейкам.Вес;

									СтрокаРезультата.КлючСвязиСТарой = СтруктураТаблиц.ТаблицаРезультатов.Количество();

									СтрокаРасхода.КоличествоУпаковок = СтрокаРасхода.КоличествоУпаковок - КоличествоИзЯчейки;
									КоличествоКОтбору = КоличествоКОтбору - КоличествоИзЯчейки * ВыборкаПоЯчейкам.КоличествоВУпаковке;
								КонецЕсли;
								#КонецУдаления
								
								#Вставка
								//++ СкребковЮМ 16.02.26 СКЛ.13
								Если КоличествоИзЯчейки > 0 Тогда
									
									Если ЕстьЭтапПроизводства Тогда
										
										Если Перемещение Тогда
											
											Если КоличествоОтобрано < КоличествоКОтборуСухих Тогда
												
												Если НЕ ВыборкаПоЯчейкам.СборкаСухих Тогда
																										
													cbr_ЗаполнитьТаблицу(ТаблицаОтбора, ВыборкаПоЯчейкам, КоличествоИзЯчейки);
													
													СтрокаРасхода.КоличествоУпаковок = СтрокаРасхода.КоличествоУпаковок - КоличествоИзЯчейки;
													
													КоличествоОтобрано = КоличествоОтобрано + КоличествоИзЯчейки;
													
												КонецЕсли;
											
											Иначе
												
												cbr_ЗаполнитьТаблицу(ТаблицаРазмещения, ЯчейкаПеремещения, КоличествоКОтборуСухих);
												cbr_ЗаполнитьТаблицу(СтруктураТаблиц.ТаблицаРезультатов, ЯчейкаПеремещения, КоличествоКОтборуСухих);
												
											КонецЕсли;
											
										Иначе
											
											cbr_ЗаполнитьТаблицу(СтруктураТаблиц.ТаблицаРезультатов, ВыборкаПоЯчейкам, КоличествоИзЯчейки);
											
											СтрокаРасхода.КоличествоУпаковок = СтрокаРасхода.КоличествоУпаковок - КоличествоИзЯчейки;
											КоличествоКОтбору = КоличествоКОтбору - КоличествоИзЯчейки * ВыборкаПоЯчейкам.КоличествоВУпаковке;
											
										КонецЕсли;
										
									Иначе
										
										cbr_ЗаполнитьТаблицу(СтруктураТаблиц.ТаблицаРезультатов, ВыборкаПоЯчейкам, КоличествоИзЯчейки);
	
										СтрокаРасхода.КоличествоУпаковок = СтрокаРасхода.КоличествоУпаковок - КоличествоИзЯчейки;
										КоличествоКОтбору = КоличествоКОтбору - КоличествоИзЯчейки * ВыборкаПоЯчейкам.КоличествоВУпаковке;
										
									КонецЕсли;
									
								ИначеЕсли ЕстьЭтапПроизводства Тогда
										
									Если Перемещение И ВыборкаПоЯчейкам.СборкаСухих И ЯчейкаПеремещения = Неопределено Тогда
										ЯчейкаПеремещения = ВыборкаПоЯчейкам;
									КонецЕсли;
										
								КонецЕсли;
								//-- СкребковЮМ 16.02.26 СКЛ.13
								#КонецВставки
										
							КонецЕсли;
							
							#Удаление
							Если КоличествоКОтбору = 0 Тогда

								Прервать;

							КонецЕсли;
							#КонецУдаления
							
							#Вставка
							//++ СкребковЮМ 16.02.26 СКЛ.13
							Если НЕ ЕстьЭтапПроизводства Тогда
								
								Если КоличествоКОтбору = 0 Тогда
	
									Прервать;
	
								КонецЕсли;
								
							Иначе
								
								Если КоличествоКОтборуСухих + КоличествоКОтборуБезСухих = 0 Тогда
	
									Прервать;
	
								КонецЕсли;
								
							КонецЕсли;
							//-- СкребковЮМ 16.02.26 СКЛ.13
							#КонецВставки
							
						КонецЦикла;
						
						#Удаление
						Если КоличествоКОтбору <> 0 Тогда

							СтрокаОшибки = СтруктураТаблиц.ТаблицаОшибок.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаОшибки, ВыборкаПоСериям);
							СтрокаОшибки.Количество = КоличествоКОтбору;

						КонецЕсли;
						#КонецУдаления
						
						#Вставка
						//++ СкребковЮМ 16.02.26 СКЛ.13
						Если НЕ ЕстьЭтапПроизводства Тогда
							
							Если КоличествоКОтбору <> 0 Тогда
	
								СтрокаОшибки = СтруктураТаблиц.ТаблицаОшибок.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаОшибки, ВыборкаПоСериям);
								СтрокаОшибки.Количество = КоличествоКОтбору;
	
							КонецЕсли;
							
						Иначе
							
							КоличествоКОтборуСухих = ?(КоличествоКОтборуСухих = Неопределено, 0, КоличествоКОтборуСухих);
							КоличествоКОтборуБезСухих = ?(КоличествоКОтборуСухих = Неопределено, 0, КоличествоКОтборуБезСухих);
							
							Если КоличествоКОтборуСухих + КоличествоКОтборуБезСухих <> 0 Тогда
							
								СтрокаОшибки = СтруктураТаблиц.ТаблицаОшибок.Добавить();
								ЗаполнитьЗначенияСвойств(СтрокаОшибки, ВыборкаПоСериям);
								СтрокаОшибки.Количество = КоличествоКОтборуСухих + КоличествоКОтборуБезСухих;
							
							КонецЕсли;
							
						КонецЕсли;
						//-- СкребковЮМ 16.02.26 СКЛ.13
						#КонецВставки

					КонецЦикла;

				КонецЦикла;

			КонецЦикла;

		КонецЦикла;

	КонецЦикла;
	
	#Вставка
	//++ СкребковЮМ 16.02.26 СКЛ.13
	Если ТаблицаОтбора.Количество() > 0 И ТаблицаРазмещения.Количество() > 0 Тогда
		Документы.ОтборРазмещениеТоваров.cbr_СоздатьОтборРазмещениеНаПеремещение(ТаблицаОтбора, ТаблицаРазмещения, Склад);
	КонецЕсли;
	//-- СкребковЮМ 16.02.26 СКЛ.13
	#КонецВставки
	
	Если Не ОтборТары
		И МенеджерВременныхТаблиц.Таблицы.Найти("ТаблицаТоварныеМестаКОтбору") <> Неопределено // МРМ не работает с товарными местами.
		И (ОбщегоНазначенияУТ.ПоказатьВременнуюТаблицу(МенеджерВременныхТаблиц, "ТаблицаТоварныеМестаКОтбору").Количество() <> 0
			Или СтруктураТаблиц.ТаблицаОшибок.Количество() <> 0) Тогда

		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВнешняяТаблица.Распоряжение КАК Распоряжение,
		|	ВнешняяТаблица.Номенклатура КАК Номенклатура,
		|	ВнешняяТаблица.Характеристика КАК Характеристика,
		|	ВнешняяТаблица.Назначение КАК Назначение,
		|	ВнешняяТаблица.Серия КАК Серия,
		|	ВнешняяТаблица.Упаковка КАК Упаковка,
		|	ВнешняяТаблица.Количество КАК Количество
		|ПОМЕСТИТЬ ТаблицаОшибок
		|ИЗ
		|	&ТаблицаОшибок КАК ВнешняяТаблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОшибок.Распоряжение КАК Распоряжение,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.СкладскаяГруппа, ЗНАЧЕНИЕ(Справочник.СкладскиеГруппыНоменклатуры.ПустаяСсылка)) КАК
		|		СкладскаяГруппаНоменклатуры,
		|	ТаблицаОшибок.Характеристика КАК Характеристика,
		|	ТаблицаОшибок.Назначение КАК Назначение,
		|	ТаблицаОшибок.Серия КАК Серия,
		|	ЕСТЬNULL(ТаблицаУпаковок.Ссылка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка,
		|	ТаблицаОшибок.Количество КАК Количество
		|ПОМЕСТИТЬ ТаблицаНераспределенных
		|ИЗ
		|	ТаблицаОшибок КАК ТаблицаОшибок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
		|		ПО ТаблицаОшибок.Номенклатура = ТаблицаНоменклатуры.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ТаблицаУпаковок
		|		ПО ТаблицаОшибок.Упаковка = ТаблицаУпаковок.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНераспределенных.Распоряжение,
		|	ТаблицаНераспределенных.Номенклатура,
		|	ТаблицаНераспределенных.Характеристика,
		|	ТаблицаНераспределенных.Назначение,
		|	ТаблицаНераспределенных.Серия,
		|	ТаблицаНераспределенных.СкладскаяГруппаНоменклатуры,
		|	ТаблицаНераспределенных.Количество * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) *
		|		УпаковкиЕдиницыИзмерения.КоличествоУпаковок КАК Количество,
		|	УпаковкиЕдиницыИзмерения.Ссылка КАК ТоварноеМесто
		|ПОМЕСТИТЬ ТаблицаОшибокТоварныеМеста
		|ИЗ
		|	ТаблицаНераспределенных КАК ТаблицаНераспределенных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО ТаблицаНераспределенных.Номенклатура = УпаковкиЕдиницыИзмерения.Владелец
		|ГДЕ
		|	УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
		|	И НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаНераспределенных.Распоряжение,
		|	ТаблицаНераспределенных.Номенклатура,
		|	ТаблицаНераспределенных.Характеристика,
		|	ТаблицаНераспределенных.Назначение,
		|	ТаблицаНераспределенных.Серия,
		|	ТаблицаНераспределенных.СкладскаяГруппаНоменклатуры,
		|	ТаблицаНераспределенных.Количество * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) *
		|		УпаковкиЕдиницыИзмерения.КоличествоУпаковок,
		|	УпаковкиЕдиницыИзмерения.Ссылка
		|ИЗ
		|	ТаблицаНераспределенных КАК ТаблицаНераспределенных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
		|		ПО ТаблицаНераспределенных.Номенклатура.НаборУпаковок = УпаковкиЕдиницыИзмерения.Владелец
		|ГДЕ
		|	УпаковкиЕдиницыИзмерения.ТипУпаковки = ЗНАЧЕНИЕ(Перечисление.ТипыУпаковокНоменклатуры.ТоварноеМесто)
		|	И НЕ УпаковкиЕдиницыИзмерения.ПометкаУдаления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОшибокТоварныеМеста.Распоряжение,
		|	ТаблицаОшибокТоварныеМеста.Номенклатура,
		|	ТаблицаОшибокТоварныеМеста.Характеристика,
		|	ТаблицаОшибокТоварныеМеста.Назначение,
		|	ТаблицаОшибокТоварныеМеста.Серия,
		|	ТаблицаОшибокТоварныеМеста.СкладскаяГруппаНоменклатуры,
		|	ТаблицаОшибокТоварныеМеста.Количество,
		|	ТаблицаОшибокТоварныеМеста.ТоварноеМесто КАК ТоварноеМесто
		|ПОМЕСТИТЬ ТаблицаНоменклатурыТоварныеМеста
		|ИЗ
		|	ТаблицаОшибокТоварныеМеста КАК ТаблицаОшибокТоварныеМеста
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаТоварныеМестаКОтбору.Распоряжение,
		|	ТаблицаТоварныеМестаКОтбору.Номенклатура,
		|	ТаблицаТоварныеМестаКОтбору.Характеристика,
		|	ТаблицаТоварныеМестаКОтбору.Назначение,
		|	ТаблицаТоварныеМестаКОтбору.Серия,
		|	ТаблицаТоварныеМестаКОтбору.СкладскаяГруппаНоменклатуры,
		|	ТаблицаТоварныеМестаКОтбору.Количество,
		|	ТаблицаТоварныеМестаКОтбору.ТоварноеМесто
		|ИЗ
		|	ТаблицаТоварныеМестаКОтбору КАК ТаблицаТоварныеМестаКОтбору
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыВЯчейкахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыВЯчейкахОстатки.Характеристика КАК Характеристика,
		|	ТоварыВЯчейкахОстатки.Назначение КАК Назначение,
		|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток КАК ВНаличииОстаток,
		|	ТоварыВЯчейкахОстатки.КОтборуОстаток КАК КОтборуОстаток,
		|	ТоварыВЯчейкахОстатки.Серия КАК Серия,
		|	ТоварыВЯчейкахОстатки.Ячейка.Код КАК Код,
		|	ТоварыВЯчейкахОстатки.Ячейка.УровеньДоступности КАК УровеньДоступности,
		|	ТоварыВЯчейкахОстатки.Ячейка.РабочийУчасток КАК РабочийУчасток,
		|	ТоварыВЯчейкахОстатки.Ячейка.ПорядокОбхода КАК ПорядокОбхода,
		|	ТоварыВЯчейкахОстатки.Упаковка КАК Упаковка,
		|	&ТекстЗапросаВесУпаковки КАК Вес,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка.СкладскаяГруппа,
		|		ЗНАЧЕНИЕ(Справочник.СкладскиеГруппыУпаковок.ПустаяСсылка)) КАК СкладскаяГруппаУпаковок,
		|	&ТекстЗапросаОбъемУпаковки КАК Объем,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка.Безразмерная, ЛОЖЬ) КАК Безразмерная,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ПриоритетОтбораИзМонотоварныхЯчеек КАК
		|		ПриоритетОтбораИзМонотоварныхЯчеек,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ПриоритетОтбораИзСмешанныхЯчеек КАК ПриоритетОтбораИзСмешанныхЯчеек,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ПриоритетОтбораПодОстаток КАК ПриоритетОтбораПодОстаток,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.ОписаниеМонотоварности КАК ОписаниеМонотоварности,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения.СтрогаяМонотоварность КАК СтрогаяМонотоварность,
		|	ТоварыВЯчейкахОстатки.Ячейка.ОбластьХранения КАК ОбластьХранения
		|ПОМЕСТИТЬ ТоварыВЯчейкахОстатки
		|ИЗ
		|	РегистрНакопления.ТоварыВЯчейках.Остатки(, (Номенклатура, Характеристика, Назначение, Упаковка) В
		|		(ВЫБРАТЬ
		|			ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|			ТаблицаНоменклатуры.Характеристика КАК Характеристика,
		|			ТаблицаНоменклатуры.Назначение КАК Назначение,
		|			ТаблицаНоменклатуры.ТоварноеМесто КАК Упаковка
		|		ИЗ
		|			ТаблицаНоменклатурыТоварныеМеста КАК ТаблицаНоменклатуры)
		|	И Ячейка В
		|		(ВЫБРАТЬ
		|			СпрЯчейки.Ссылка КАК СкладскаяЯчейка
		|		ИЗ
		|			Справочник.СкладскиеЯчейки КАК СпрЯчейки
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БлокировкиСкладскихЯчеек КАК БлокировкиЯчеек
		|				ПО СпрЯчейки.Ссылка = БлокировкиЯчеек.Ячейка
		|				И (БлокировкиЯчеек.ТипБлокировки = ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Полная)
		|				ИЛИ БлокировкиЯчеек.ТипБлокировки = ЗНАЧЕНИЕ(Перечисление.ТипыБлокировокСкладскихЯчеек.Отбор))
		|		ГДЕ
		|			БлокировкиЯчеек.ТипБлокировки ЕСТЬ NULL
		|			И СпрЯчейки.Помещение = &Помещение
		|			И СпрЯчейки.Владелец = &Склад
		|			И НЕ СпрЯчейки.ЭтоГруппа
		|			И (СпрЯчейки.ТипСкладскойЯчейки = ЗНАЧЕНИЕ(Перечисление.ТипыСкладскихЯчеек.Хранение)
		|			ИЛИ СпрЯчейки.ТипСкладскойЯчейки = ЗНАЧЕНИЕ(Перечисление.ТипыСкладскихЯчеек.Архив)))) КАК ТоварыВЯчейкахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|	ТаблицаНоменклатуры.Характеристика КАК Характеристика,
		|	ТаблицаНоменклатуры.Назначение КАК Назначение,
		|	ТаблицаНоменклатуры.Серия КАК Серия,
		|	ТаблицаНоменклатуры.Распоряжение КАК Распоряжение,
		|	ТаблицаНоменклатуры.Количество КАК Количество,
		|	ТаблицаНоменклатуры.СкладскаяГруппаНоменклатуры КАК СкладскаяГруппаНоменклатуры,
		|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстатки.Код КАК Код,
		|	СУММА(ЕСТЬNULL(ТоварыВЯчейкахОстатки.ВНаличииОстаток, 0) - ЕСТЬNULL(ТоварыВЯчейкахОстатки.КОтборуОстаток, 0)) КАК
		|		СвободныйОстаток,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.УровеньДоступности, 9999999999) КАК УровеньДоступности,
		|	ТоварыВЯчейкахОстатки.РабочийУчасток КАК РабочийУчасток,
		|	ТоварыВЯчейкахОстатки.ПорядокОбхода КАК ПорядокОбхода,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка, ТаблицаНоменклатуры.ТоварноеМесто) КАК Упаковка,
		|	ТоварыВЯчейкахОстатки.Вес КАК Вес,
		|	ТоварыВЯчейкахОстатки.СкладскаяГруппаУпаковок КАК СкладскаяГруппаУпаковок,
		|	ТоварыВЯчейкахОстатки.Объем КАК Объем,
		|	ТоварыВЯчейкахОстатки.Безразмерная КАК Безразмерная,
		|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзМонотоварныхЯчеек КАК ПриоритетОтбораИзМонотоварныхЯчеек,
		|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзСмешанныхЯчеек КАК ПриоритетОтбораИзСмешанныхЯчеек,
		|	ТоварыВЯчейкахОстатки.ПриоритетОтбораПодОстаток КАК ПриоритетОтбораПодОстаток,
		|	ТоварыВЯчейкахОстатки.ОписаниеМонотоварности КАК ОписаниеМонотоварности,
		|	ТоварыВЯчейкахОстатки.СтрогаяМонотоварность КАК СтрогаяМонотоварность,
		|	ТоварыВЯчейкахОстатки.ОбластьХранения КАК ОбластьХранения
		|ПОМЕСТИТЬ ПулЯчеек
		|ИЗ
		|	ТаблицаНоменклатурыТоварныеМеста КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыВЯчейкахОстатки КАК ТоварыВЯчейкахОстатки
		|		ПО ТаблицаНоменклатуры.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
		|		И ТаблицаНоменклатуры.Характеристика = ТоварыВЯчейкахОстатки.Характеристика
		|		И ТаблицаНоменклатуры.Назначение = ТоварыВЯчейкахОстатки.Назначение
		|		И (ВЫБОР
		|			КОГДА ТаблицаНоменклатуры.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ТаблицаНоменклатуры.Серия = ТоварыВЯчейкахОстатки.Серия
		|		КОНЕЦ)
		|		И ТаблицаНоменклатуры.ТоварноеМесто = ТоварыВЯчейкахОстатки.Упаковка
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаНоменклатуры.Номенклатура,
		|	ТаблицаНоменклатуры.Характеристика,
		|	ТаблицаНоменклатуры.Назначение,
		|	ТаблицаНоменклатуры.Серия,
		|	ТаблицаНоменклатуры.Распоряжение,
		|	ТоварыВЯчейкахОстатки.Код,
		|	ТаблицаНоменклатуры.СкладскаяГруппаНоменклатуры,
		|	ТоварыВЯчейкахОстатки.Ячейка,
		|	ТаблицаНоменклатуры.Количество,
		|	ТоварыВЯчейкахОстатки.РабочийУчасток,
		|	ТоварыВЯчейкахОстатки.ПорядокОбхода,
		|	ТоварыВЯчейкахОстатки.Вес,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка, ТаблицаНоменклатуры.ТоварноеМесто),
		|	ТоварыВЯчейкахОстатки.Безразмерная,
		|	ТоварыВЯчейкахОстатки.СкладскаяГруппаУпаковок,
		|	ТоварыВЯчейкахОстатки.Объем,
		|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзМонотоварныхЯчеек,
		|	ТоварыВЯчейкахОстатки.ПриоритетОтбораИзСмешанныхЯчеек,
		|	ТоварыВЯчейкахОстатки.ОбластьХранения,
		|	ТоварыВЯчейкахОстатки.ПриоритетОтбораПодОстаток,
		|	ТоварыВЯчейкахОстатки.ОписаниеМонотоварности,
		|	ТоварыВЯчейкахОстатки.СтрогаяМонотоварность,
		|	ЕСТЬNULL(ТоварыВЯчейкахОстатки.УровеньДоступности, 9999999999)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПулЯчеек.Ячейка КАК Ячейка,
		|	ПулЯчеек.Номенклатура КАК Номенклатура,
		|	ПулЯчеек.Характеристика,
		|	ПулЯчеек.Назначение,
		|	ПулЯчеек.Серия,
		|	ВЫБОР
		|		КОГДА ПулЯчеек.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьДругаяНоменклатура,
		|	ВЫБОР
		|		КОГДА ПулЯчеек.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
		|		И ПулЯчеек.Характеристика = ТоварыВЯчейкахОстатки.Характеристика
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьДругаяНоменклатураИЛИХарактеристика,
		|	ВЫБОР
		|		КОГДА ПулЯчеек.Номенклатура = ТоварыВЯчейкахОстатки.Номенклатура
		|		И ПулЯчеек.Характеристика = ТоварыВЯчейкахОстатки.Характеристика
		|		И ПулЯчеек.Серия = ТоварыВЯчейкахОстатки.Серия
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия
		|ПОМЕСТИТЬ ДругиеТоварыВЯчейках
		|ИЗ
		|	ПулЯчеек КАК ПулЯчеек
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВЯчейках.Остатки(, Ячейка В
		|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|				ПулЯчеек.Ячейка
		|			ИЗ
		|				ПулЯчеек КАК ПулЯчеек)) КАК ТоварыВЯчейкахОстатки
		|		ПО ПулЯчеек.Ячейка = ТоварыВЯчейкахОстатки.Ячейка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДругиеТоварыВЯчейках.Номенклатура,
		|	ДругиеТоварыВЯчейках.Ячейка,
		|	МАКСИМУМ(ДругиеТоварыВЯчейках.ЕстьДругаяНоменклатура) КАК ЕстьДругаяНоменклатура
		|ПОМЕСТИТЬ НоменклатураВЯчейках
		|ИЗ
		|	ДругиеТоварыВЯчейках КАК ДругиеТоварыВЯчейках
		|СГРУППИРОВАТЬ ПО
		|	ДругиеТоварыВЯчейках.Номенклатура,
		|	ДругиеТоварыВЯчейках.Ячейка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДругиеТоварыВЯчейках.Номенклатура,
		|	ДругиеТоварыВЯчейках.Ячейка,
		|	ДругиеТоварыВЯчейках.Характеристика,
		|	МАКСИМУМ(ДругиеТоварыВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристика) КАК ЕстьДругаяНоменклатураИЛИХарактеристика
		|ПОМЕСТИТЬ НоменклатураИХарактеристикаВЯчейках
		|ИЗ
		|	ДругиеТоварыВЯчейках КАК ДругиеТоварыВЯчейках
		|СГРУППИРОВАТЬ ПО
		|	ДругиеТоварыВЯчейках.Номенклатура,
		|	ДругиеТоварыВЯчейках.Ячейка,
		|	ДругиеТоварыВЯчейках.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДругиеТоварыВЯчейках.Номенклатура,
		|	ДругиеТоварыВЯчейках.Ячейка,
		|	ДругиеТоварыВЯчейках.Характеристика,
		|	МАКСИМУМ(ДругиеТоварыВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия) КАК
		|		ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия,
		|	ДругиеТоварыВЯчейках.Серия
		|ПОМЕСТИТЬ НоменклатураИХарактеристикаИСерияВЯчейках
		|ИЗ
		|	ДругиеТоварыВЯчейках КАК ДругиеТоварыВЯчейках
		|СГРУППИРОВАТЬ ПО
		|	ДругиеТоварыВЯчейках.Номенклатура,
		|	ДругиеТоварыВЯчейках.Ячейка,
		|	ДругиеТоварыВЯчейках.Характеристика,
		|	ДругиеТоварыВЯчейках.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПулЯчеек.Номенклатура КАК Номенклатура,
		|	ПулЯчеек.Характеристика КАК Характеристика,
		|	ПулЯчеек.Назначение КАК Назначение,
		|	ПулЯчеек.Серия КАК Серия,
		|	ПулЯчеек.Распоряжение КАК Распоряжение,
		|	ПулЯчеек.Количество КАК Количество,
		|	ПулЯчеек.Ячейка КАК Ячейка,
		|	ПулЯчеек.СвободныйОстаток КАК СвободныйОстаток,
		|	ПулЯчеек.РабочийУчасток КАК РабочийУчасток,
		|	ПулЯчеек.ПорядокОбхода КАК ПорядокОбхода,
		|	ПулЯчеек.Код КАК Код,
		|	ПулЯчеек.Упаковка КАК Упаковка,
		|	ПулЯчеек.Упаковка.КоличествоУпаковок КАК КоличествоВКомплекте,
		|	ПулЯчеек.Вес КАК Вес,
		|	ПулЯчеек.Объем КАК Объем,
		|	ВЫБОР
		|		КОГДА ПулЯчеек.СвободныйОстаток <= ПулЯчеек.Количество
		|			ТОГДА ПулЯчеек.ПриоритетОтбораПодОстаток
		|		ИНАЧЕ ВЫБОР
		|			КОГДА ПулЯчеек.ОписаниеМонотоварности = ЗНАЧЕНИЕ(Перечисление.ОписаниеМонотоварности.ПоНоменклатуре)
		|				ТОГДА ВЫБОР
		|					КОГДА НЕ НоменклатураВЯчейках.ЕстьДругаяНоменклатура
		|						ТОГДА ПулЯчеек.ПриоритетОтбораИзМонотоварныхЯчеек
		|					ИНАЧЕ ПулЯчеек.ПриоритетОтбораИзСмешанныхЯчеек
		|				КОНЕЦ
		|			ИНАЧЕ ВЫБОР
		|				КОГДА ПулЯчеек.ОписаниеМонотоварности = ЗНАЧЕНИЕ(Перечисление.ОписаниеМонотоварности.ПоХарактеристике)
		|					ТОГДА ВЫБОР
		|						КОГДА НЕ НоменклатураИХарактеристикаВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристика
		|							ТОГДА ПулЯчеек.ПриоритетОтбораИзМонотоварныхЯчеек
		|						ИНАЧЕ ПулЯчеек.ПриоритетОтбораИзСмешанныхЯчеек
		|					КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|					КОГДА НЕ НоменклатураИХарактеристикаИСерияВЯчейках.ЕстьДругаяНоменклатураИЛИХарактеристикаИЛИСерия
		|						ТОГДА ПулЯчеек.ПриоритетОтбораИзМонотоварныхЯчеек
		|					ИНАЧЕ ПулЯчеек.ПриоритетОтбораИзСмешанныхЯчеек
		|				КОНЕЦ
		|			КОНЕЦ
		|		КОНЕЦ
		|	КОНЕЦ КАК ПриоритетСтратегии,
		|	ВЫБОР
		|		КОГДА ПравилаРазмещенияТоваровВЯчейках.ОбластьХранения ЕСТЬ NULL
		|			ТОГДА 1000
		|		ИНАЧЕ ПравилаРазмещенияТоваровВЯчейках.Приоритет
		|	КОНЕЦ КАК ПриоритетОбласти
		|ИЗ
		|	ПулЯчеек КАК ПулЯчеек
		|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураВЯчейках КАК НоменклатураВЯчейках
		|		ПО ПулЯчеек.Номенклатура = НоменклатураВЯчейках.Номенклатура
		|		И ПулЯчеек.Ячейка = НоменклатураВЯчейках.Ячейка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаРазмещенияТоваровВЯчейках КАК ПравилаРазмещенияТоваровВЯчейках
		|		ПО ПулЯчеек.СкладскаяГруппаНоменклатуры = ПравилаРазмещенияТоваровВЯчейках.СкладскаяГруппаНоменклатуры
		|		И ПулЯчеек.СкладскаяГруппаУпаковок = ПравилаРазмещенияТоваровВЯчейках.СкладскаяГруппаУпаковок
		|		И (ПравилаРазмещенияТоваровВЯчейках.Помещение = &Помещение)
		|		И (ПравилаРазмещенияТоваровВЯчейках.Склад = &Склад)
		|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИХарактеристикаВЯчейках КАК НоменклатураИХарактеристикаВЯчейках
		|		ПО ПулЯчеек.Номенклатура = НоменклатураИХарактеристикаВЯчейках.Номенклатура
		|		И ПулЯчеек.Ячейка = НоменклатураИХарактеристикаВЯчейках.Ячейка
		|		И ПулЯчеек.Характеристика = НоменклатураИХарактеристикаВЯчейках.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ НоменклатураИХарактеристикаИСерияВЯчейках КАК НоменклатураИХарактеристикаИСерияВЯчейках
		|		ПО ПулЯчеек.Ячейка = НоменклатураИХарактеристикаИСерияВЯчейках.Ячейка
		|		И ПулЯчеек.Номенклатура = НоменклатураИХарактеристикаИСерияВЯчейках.Номенклатура
		|		И ПулЯчеек.Характеристика = НоменклатураИХарактеристикаИСерияВЯчейках.Характеристика
		|		И ПулЯчеек.Серия = НоменклатураИХарактеристикаИСерияВЯчейках.Серия
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияПересчетовЯчеек КАК СостоянияПересчетовЯчеек
		|		ПО ПулЯчеек.Ячейка = СостоянияПересчетовЯчеек.Ячейка
		|		И (СостоянияПересчетовЯчеек.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияПересчетовЯчеек.ТребуетсяПересчет))
		|
		|УПОРЯДОЧИТЬ ПО
		|	СостоянияПересчетовЯчеек.Состояние,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Серия,
		|	ПриоритетОбласти УБЫВ,
		|	ПриоритетСтратегии,
		|	ПулЯчеек.УровеньДоступности,
		|	ПорядокОбхода,
		|	Код
		|ИТОГИ
		|ПО
		|	Распоряжение,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Серия,
		|	Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНераспределенных.Распоряжение,
		|	ТаблицаНераспределенных.Номенклатура,
		|	ТаблицаНераспределенных.СкладскаяГруппаНоменклатуры,
		|	ТаблицаНераспределенных.Характеристика,
		|	ТаблицаНераспределенных.Назначение,
		|	ТаблицаНераспределенных.Серия,
		|	ТаблицаНераспределенных.Упаковка,
		|	ТаблицаНераспределенных.Количество
		|ИЗ
		|	ТаблицаНераспределенных КАК ТаблицаНераспределенных
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОшибокТоварныеМеста КАК ТаблицаОшибокТоварныеМеста
		|		ПО ТаблицаНераспределенных.Номенклатура = ТаблицаОшибокТоварныеМеста.Номенклатура
		|		И ТаблицаНераспределенных.Распоряжение = ТаблицаОшибокТоварныеМеста.Распоряжение
		|ГДЕ
		|	ТаблицаОшибокТоварныеМеста.Номенклатура ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НоменклатураИХарактеристикаИСерияВЯчейках
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НоменклатураИХарактеристикаВЯчейках
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НоменклатураВЯчейках
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДругиеТоварыВЯчейках
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПулЯчеек
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТоварыВЯчейкахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаНоменклатурыТоварныеМеста";

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаНераспределенных.Упаковка", "ТаблицаНераспределенных.Номенклатура"));

		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВесУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("ТоварыВЯчейкахОстатки.Упаковка", "ТоварыВЯчейкахОстатки.Номенклатура", Ложь));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаОбъемУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("ТоварыВЯчейкахОстатки.Упаковка", "ТоварыВЯчейкахОстатки.Номенклатура", Ложь));

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = ТекстЗапроса;

		Запрос.УстановитьПараметр("ТаблицаОшибок", СтруктураТаблиц.ТаблицаОшибок.Скопировать());
		Запрос.УстановитьПараметр("Склад",Склад);
		Запрос.УстановитьПараметр("Помещение",Помещение);

		СтруктураТаблиц.ТаблицаОшибок.Очистить();

		УстановитьПривилегированныйРежим(Истина);
		МассивРезультатов = Запрос.ВыполнитьПакет();
		УстановитьПривилегированныйРежим(Ложь);

		ТаблицаОшибок = МассивРезультатов[11].Выгрузить();

		ВыборкаПоРаспоряжениям = МассивРезультатов[10].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоРаспоряжениям.Следующий() Цикл

			ВыборкаПоНоменклатуре =  ВыборкаПоРаспоряжениям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоНоменклатуре.Следующий() Цикл

				ВыборкаПоХарактеристикам = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоХарактеристикам.Следующий() Цикл

					ВыборкаПоНазначению = ВыборкаПоХарактеристикам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаПоНазначению.Следующий() Цикл

						ВыборкаПоСериям = ВыборкаПоНазначению.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						Пока ВыборкаПоСериям.Следующий() Цикл

							ВыборкаПоУпаковкам = ВыборкаПоСериям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							Пока ВыборкаПоУпаковкам.Следующий() Цикл

								КоличествоКОтбору = Неопределено;

								//детали
								ВыборкаПоЯчейкам = ВыборкаПоУпаковкам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
								Пока ВыборкаПоЯчейкам.Следующий() Цикл

									Если КоличествоКОтбору = Неопределено Тогда

										КоличествоКОтбору = ВыборкаПоЯчейкам.Количество;

									КонецЕсли;

									// Если ячейка заполнена, значит в ней есть остаток в нужной упаковке,
									// иначе таблица остатков не соединилась бы с таблицей номенклатуры.
									Если ЗначениеЗаполнено(ВыборкаПоЯчейкам.Ячейка)
										И ВыборкаПоЯчейкам.СвободныйОстаток > 0 Тогда

										ОтборСтрокИзТаблицыРасходаИзЯчеек = Новый Структура("Ячейка,Номенклатура,Упаковка,Характеристика,Назначение,Серия");
										ЗаполнитьЗначенияСвойств(ОтборСтрокИзТаблицыРасходаИзЯчеек, ВыборкаПоЯчейкам);

										МассивНайденныхСтрок = СтруктураТаблиц.ТаблицаРасходаИзЯчеек.НайтиСтроки(ОтборСтрокИзТаблицыРасходаИзЯчеек);

										Если МассивНайденныхСтрок.Количество() = 0 Тогда
											СтрокаРасхода 						= СтруктураТаблиц.ТаблицаРасходаИзЯчеек.Добавить();
											ЗаполнитьЗначенияСвойств(СтрокаРасхода, ОтборСтрокИзТаблицыРасходаИзЯчеек);

											СтрокаРасхода.КоличествоУпаковок 	= ВыборкаПоЯчейкам.СвободныйОстаток;

											РабочийУчасток = СтруктураТаблиц.МассивУчастков.Найти(ВыборкаПоЯчейкам.РабочийУчасток);
											Если РабочийУчасток = Неопределено Тогда
												СтруктураТаблиц.МассивУчастков.Добавить(ВыборкаПоЯчейкам.РабочийУчасток);
											КонецЕсли;
										Иначе
											СтрокаРасхода = МассивНайденныхСтрок[0];
										КонецЕсли;

										КоличествоИзЯчейки = Мин(КоличествоКОтбору, СтрокаРасхода.КоличествоУпаковок);

										Если КоличествоИзЯчейки > 0 Тогда
											СтрокаРезультата = СтруктураТаблиц.ТаблицаРезультатов.Добавить();
											ЗаполнитьЗначенияСвойств(СтрокаРезультата, ВыборкаПоЯчейкам);
											СтрокаРезультата.КоличествоУпаковок = КоличествоИзЯчейки;
											СтрокаРезультата.Количество         = КоличествоИзЯчейки / ВыборкаПоЯчейкам.КоличествоВКомплекте;
											СтрокаРезультата.ОбъемУпаковки		= ВыборкаПоЯчейкам.Объем;
											СтрокаРезультата.ВесУпаковки		= ВыборкаПоЯчейкам.Вес;

											СтрокаРезультата.КлючСвязиСТарой = СтруктураТаблиц.ТаблицаРезультатов.Количество();

											СтрокаРасхода.КоличествоУпаковок = СтрокаРасхода.КоличествоУпаковок - КоличествоИзЯчейки;
											КоличествоКОтбору = КоличествоКОтбору - КоличествоИзЯчейки;
										КонецЕсли;

									КонецЕсли;

									Если КоличествоКОтбору = 0 Тогда

										Прервать;

									КонецЕсли;

								КонецЦикла;

								Если КоличествоКОтбору <> 0 Тогда

									СтрокаОшибки = СтруктураТаблиц.ТаблицаОшибок.Добавить();
									ЗаполнитьЗначенияСвойств(СтрокаОшибки, ВыборкаПоУпаковкам);
									СтрокаОшибки.Количество = КоличествоКОтбору / ВыборкаПоУпаковкам.КоличествоВКомплекте;
									СтрокаОшибки.КоличествоУпаковок = КоличествоКОтбору;

								КонецЕсли;

							КонецЦикла;

						КонецЦикла;

					КонецЦикла;

				КонецЦикла;

			КонецЦикла;

		КонецЦикла;

		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОшибок, СтруктураТаблиц.ТаблицаОшибок);

	КонецЕсли;

КонецПроцедуры

//++ СкребковЮМ 16.02.26 СКЛ.13
Процедура cbr_ЗаполнитьТаблицу(Таблица, ВыборкаПоЯчейкам, КоличествоИзЯчейки)
	
	СтрокаТаблицы = Таблица.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаПоЯчейкам);
	СтрокаТаблицы.КоличествоУпаковок = КоличествоИзЯчейки;
	СтрокаТаблицы.Количество         = КоличествоИзЯчейки * ВыборкаПоЯчейкам.КоличествоВУпаковке;
	СтрокаТаблицы.ОбъемУпаковки		= ВыборкаПоЯчейкам.Объем;
	СтрокаТаблицы.ВесУпаковки		= ВыборкаПоЯчейкам.Вес;
	
	СтрокаТаблицы.КлючСвязиСТарой = Таблица.Количество();
	
КонецПроцедуры
//-- СкребковЮМ 16.02.26 СКЛ.13

#КонецОбласти