
// Процедура - Установить значение константы по имени
//	Для получения константы использовать функцию ОбщегоНазначенияУТПовтИсп.ЗначениеКонстанты
//											 или ОбщегоНазначенияПовтИспУХ.ПолучитьЗначениеКонстанты
//
// Параметры:
//  ИмяМенеджера - Строка - название константы в системе
//  Значение - любое - устанавливаемое значение константы
//
Процедура УстановитьЗначениеКонстантыПоИмени(Знач ИмяМенеджера, Значение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяМенеджера = СтрШаблон("Константа.%1", ИмяМенеджера);
	
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяМенеджера); 
	Менеджер.Установить(Значение);

КонецПроцедуры

// Функция - Значение константы
//
// Параметры:
//  Имя	 - Строка - Имя получаемой константы
// 
// Возвращаемое значение:
//  Произвольное - значение получаемой константы
//
Функция ЗначениеКонстанты(Имя) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы[Имя].Получить();

КонецФункции // ЗначениеКонстанты()

// Процедура - Свернуть табличную часть - сворачивает табличные части с типом ТабличнаяЧасть
// 			Не сворачивает ДанныеФормы
//
// Параметры:
//  ТабличнаяЧасть		 - ТабличнаяЧасть - Табличная часть для сворачивания
//  КолонкиНеСуммировать - Строка - 
//
Процедура СвернутьТабличнуюЧасть(Знач ТабличнаяЧасть, КолонкиНеСуммировать = "") Экспорт
	
	Измерения = Новый Массив;
	Ресурсы = Новый Массив;
	МассивКолонокНеСуммировать = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КолонкиНеСуммировать, ",", Истина, Истина);
	
	МетаданныеТабличнойЧасти = Метаданные.НайтиПоТипу(ТипЗнч(ТабличнаяЧасть));
	
	Для Каждого МетаданныеКолонки Из МетаданныеТабличнойЧасти.Реквизиты Цикл
		
		ИмяКолонки = МетаданныеКолонки.Имя;
		Если МетаданныеКолонки.Тип.СодержитТип(Тип("Число")) 
			И МассивКолонокНеСуммировать.Найти(ИмяКолонки) = Неопределено Тогда
			Ресурсы.Добавить(ИмяКолонки);
		Иначе
			Измерения.Добавить(ИмяКолонки);
		КонецЕсли;
	КонецЦикла;
	
	ИменаИзмерений = СтрСоединить(Измерения, ",");
	ИменаРесурсов = СтрСоединить(Ресурсы, ",");
	
	ТабличнаяЧасть.Свернуть(ИменаИзмерений, ИменаРесурсов);
	
КонецПроцедуры

Процедура СвернутьТаблицуЗначений(Таблица, КолонкиНеСуммировать = "") Экспорт
	
	Измерения = Новый Массив;
	Ресурсы = Новый Массив;
	МассивКолонокНеСуммировать = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КолонкиНеСуммировать, ",", Истина, Истина);

	Для Каждого КолонкаТаблицы Из Таблица.Колонки Цикл
		
		ИмяКолонки = КолонкаТаблицы.Имя;
		Если КолонкаТаблицы.ТипЗначения.СодержитТип(Тип("Число")) 
			И МассивКолонокНеСуммировать.Найти(ИмяКолонки) = Неопределено Тогда
			Ресурсы.Добавить(ИмяКолонки);
		Иначе
			Измерения.Добавить(ИмяКолонки);
		КонецЕсли;
	КонецЦикла;
	
	ИменаИзмерений = СтрСоединить(Измерения, ",");
	ИменаРесурсов = СтрСоединить(Ресурсы, ",");
	
	Таблица.Свернуть(ИменаИзмерений, ИменаРесурсов);
	
КонецПроцедуры

// +- Алейников М.Н 27.06.2025
Функция ЗаписатьСвойствоУОбъекта(ВладелецСвойств, Знач Свойство, Значение) Экспорт
	
	Результат = Ложь;
	Если Не ТипЗнч(Свойство) = Тип("Строка") И Не ТипЗнч(Свойство) = Тип("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения") Тогда
		Возврат Результат;
	КонецЕсли;	
	
	Если ТипЗнч(Свойство) = Тип("Строка") Тогда
		
		Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", Свойство);
		Если Свойство = Неопределено Тогда
			Возврат Результат;
		КонецЕсли;

	КонецЕсли;	
			
	ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
	ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство");
	ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");
	СтрокаТаблицыСвойствИЗначений = ТаблицаСвойствИЗначений.Добавить();
	СтрокаТаблицыСвойствИЗначений.Свойство = Свойство;
	СтрокаТаблицыСвойствИЗначений.Значение = Значение;

	УправлениеСвойствами.ЗаписатьСвойстваУОбъекта(ВладелецСвойств, ТаблицаСвойствИЗначений);
		
	Результат = Истина;   
	
	Возврат Результат
	
Конецфункции