#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция СтруктураОтбораТранспортныхТарифов() Экспорт
	
	Возврат Новый Структура("Регион, Зона, Адрес, Маршрут, Вес");
	
КонецФункции

Функция АктуальныеТранспортныеТарифы(СтруктураОтбора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатФункции = Новый Структура("Регион, Зона, Адрес, Маршрут, ТарифГазель, Тариф5т, Тариф10т, Тариф20т, ТарифТК, ДопНадбавка",
									СтруктураОтбора.Регион,
									СтруктураОтбора.Зона,
									СтруктураОтбора.Адрес,
									СтруктураОтбора.Маршрут,
									0,
									0,
									0,
									0,
									0,
									0);
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТранспортныеТарифыСрезПоследних.Регион КАК Регион,
	|	ТранспортныеТарифыСрезПоследних.Зона КАК Зона,
	|	ТранспортныеТарифыСрезПоследних.Адрес КАК Адрес,
	|	ТранспортныеТарифыСрезПоследних.Маршрут КАК Маршрут,
	|	ТранспортныеТарифыСрезПоследних.ТарифГазель КАК ТарифГазель,
	|	ТранспортныеТарифыСрезПоследних.Тариф5т КАК Тариф5т,
	|	ТранспортныеТарифыСрезПоследних.Тариф10т КАК Тариф10т,
	|	ТранспортныеТарифыСрезПоследних.Тариф20т КАК Тариф20т,
	|	ТранспортныеТарифыСрезПоследних.ТарифТК КАК ТарифТК,
	|	ТранспортныеТарифыСрезПоследних.ДопНадбавка КАК ДопНадбавка
	|ИЗ
	|	РегистрСведений.ТранспортныеТарифы.СрезПоследних(, {(Регион), (Зона), (Адрес), (Маршрут)}) КАК ТранспортныеТарифыСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период УБЫВ";
	
	Для Каждого КлючЗначение Из СтруктураОтбора Цикл
		Если Не КлючЗначение.Значение = Неопределено Тогда
			НовыйОтбор = ПостроительЗапроса.Отбор.Добавить(КлючЗначение.Ключ);
			НовыйОтбор.ВидСравнения = ВидСравнения.Равно;
			НовыйОтбор.Использование = Истина;
			НовыйОтбор.Значение = КлючЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ПостроительЗапроса.Выполнить();
	РезультатЗапроса = ПостроительЗапроса.Результат;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		
		Для Каждого КолонкаДанных Из РезультатЗапроса.Колонки Цикл
			РезультатФункции.Вставить(КолонкаДанных.Имя, Выборка[КолонкаДанных.Имя]);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
			
КонецФункции

Функция ТарифПоВесу(СтруктураТарифа, Вес) Экспорт
	
	РезультатФункции = 0;
	
	Если Не ЗначениеЗаполнено(Вес) Или Вес <= 0 Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Если Вес < 1500 Тогда
		РезультатФункции = СтруктураТарифа.ТарифГазель;
	ИначеЕсли Вес < 5500 Тогда
		РезультатФункции = СтруктураТарифа.Тариф5т;
	ИначеЕсли Вес < 12000 Тогда
		РезультатФункции = СтруктураТарифа.Тариф10т;
	ИначеЕсли Вес < 24000 Тогда
		РезультатФункции = СтруктураТарифа.Тариф20т;
	Иначе
		РезультатФункции = СтруктураТарифа.ТарифТК;
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти

#КонецЕсли