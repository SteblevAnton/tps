#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//++AlopinAS 28.01.2026 ПРД.20
Функция ДобавитьЗаписи(Ссылка) Экспорт

	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		Результат = ОбработатьЗаказКлиента(Ссылка);
	КонецЕсли;

	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.СоглашенияСКлиентами") Тогда
		Результат = ОбработатьСоглашение(Ссылка);
	КонецЕсли;

	Возврат Результат;

КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

#КонецОбласти

#Область ОбработчикиСобытий

// Код процедур и функций

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

//++AlopinAS 28.01.2026 ПРД.20
Функция ОбработатьЗаказКлиента(Ссылка)

	РезультатЗапроса = ПолучитьТекущиеДанныеТЧЦеныПоТоварамИзЗаказа(Ссылка);

	СоглашениеСсылка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Соглашение");

	НачатьТранзакцию();
	Попытка

		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Соглашение.Установить(СоглашениеСсылка);

		Набор.Отбор.ВидДвижения.Установить(ПредопределенноеЗначение(
				"Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Расход"));
		Набор.Прочитать();
		Для Каждого Стр Из Набор Цикл
			Если Стр.ЗаказКлиента = Ссылка Тогда
				Набор.Удалить(Стр);
			КонецЕсли;
		КонецЦикла;

		Для Каждого Стр Из РезультатЗапроса Цикл
			НоваяСтрока = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		КонецЦикла;
		Набор.Записать();
		РезультатПроверки = ПроверитьНаличиеПревышенияПоОстаткам(СоглашениеСсылка);

		Если РезультатПроверки Тогда
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецЕсли;

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Произошла ошибка при записи в регистр!'"));		
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
Функция ОбработатьСоглашение(Ссылка)
	РезультатЗапроса = ПолучитьТекущиеДанныеТЧЦеныПоТоварам(Ссылка);

	НачатьТранзакцию();
	Попытка

		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Соглашение.Установить(Ссылка);
		Набор.Отбор.ВидДвижения.Установить(ПредопределенноеЗначение(
				"Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Приход"));
		Набор.Прочитать();
		Набор.Очистить();
		Набор.Загрузить(РезультатЗапроса);
		Набор.Записать();

		РезультатПроверки = ПроверитьНаличиеПревышенияПоОстаткам(Ссылка);

		Если РезультатПроверки Тогда
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецЕсли;

		ЗафиксироватьТранзакцию();

	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Произошла ошибка при записи в регистр!'"));
		Возврат Ложь;
	КонецПопытки;
	Возврат Истина;
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
Функция ПолучитьТекущиеДанныеТЧЦеныПоТоварам(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Количество КАК Количество,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка КАК Соглашение,
	|	ЗНАЧЕНИЕ(Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Приход) КАК ВидДвижения,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
	|ГДЕ
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка = &Ссылка
	|	И СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Количество > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.НомерСтроки";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Возврат РезультатЗапроса;
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
Функция ПроверитьНаличиеПревышенияПоОстаткам(СоглашениеСсылка)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	cbr_ОстаткиНоменклатурыПоСоглашению.Номенклатура КАК Номенклатура,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.Характеристика КАК Характеристика,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.ВидДвижения КАК ВидДвижения,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.Количество КАК Количество,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.ДатаНачала КАК ДатаНачала,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТРегистр
	|ИЗ
	|	РегистрСведений.cbr_ОстаткиНоменклатурыПоСоглашению КАК cbr_ОстаткиНоменклатурыПоСоглашению
	|ГДЕ
	|	cbr_ОстаткиНоменклатурыПоСоглашению.Соглашение = &Соглашение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРегистр.Номенклатура КАК Номенклатура,
	|	ВТРегистр.Характеристика КАК Характеристика,
	|	ВТРегистр.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ВТРегистр.Количество) КАК Количество,
	|	ВТРегистр.ДатаНачала КАК ДатаНачала,
	|	ВТРегистр.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТПриход
	|ИЗ
	|	ВТРегистр КАК ВТРегистр
	|ГДЕ
	|	ВТРегистр.ВидДвижения = &ВидДвиженияПлюс
	|СГРУППИРОВАТЬ ПО
	|	ВТРегистр.Номенклатура,
	|	ВТРегистр.Характеристика,
	|	ВТРегистр.ЕдиницаИзмерения,
	|	ВТРегистр.ДатаНачала,
	|	ВТРегистр.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРегистр.Номенклатура КАК Номенклатура,
	|	ВТРегистр.Характеристика КАК Характеристика,
	|	ВТРегистр.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ВТРегистр.Количество) КАК Количество,
	|	ВТРегистр.ДатаНачала КАК ДатаНачала,
	|	ВТРегистр.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТРасход
	|ИЗ
	|	ВТРегистр КАК ВТРегистр
	|ГДЕ
	|	ВТРегистр.ВидДвижения = &ВидДвиженияРасход
	|СГРУППИРОВАТЬ ПО
	|	ВТРегистр.Номенклатура,
	|	ВТРегистр.Характеристика,
	|	ВТРегистр.ЕдиницаИзмерения,
	|	ВТРегистр.ДатаНачала,
	|	ВТРегистр.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТПриход.Номенклатура, ВТРасход.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ВТПриход.Характеристика, ВТРасход.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(ВТПриход.ЕдиницаИзмерения, ВТРасход.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	-(ЕСТЬNULL(ВТПриход.Количество, 0) - ЕСТЬNULL(ВТРасход.Количество, 0)) КАК Превышение,
	|	ЕСТЬNULL(ВТПриход.ДатаНачала, ВТРасход.ДатаНачала) КАК ДатаНачала,
	|	ЕСТЬNULL(ВТПриход.ДатаОкончания, ВТРасход.ДатаОкончания) КАК ДатаОкончания
	|ИЗ
	|	ВТПриход КАК ВТПриход
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТРасход КАК ВТРасход
	|		ПО ВТПриход.Номенклатура = ВТРасход.Номенклатура
	|		И ВТПриход.Характеристика = ВТРасход.Характеристика
	|		И ВТПриход.ЕдиницаИзмерения = ВТРасход.ЕдиницаИзмерения
	|		И ВТПриход.ДатаНачала = ВТРасход.ДатаНачала
	|		И ВТПриход.ДатаОкончания = ВТРасход.ДатаОкончания
	|ГДЕ
	|	-(ЕСТЬNULL(ВТПриход.Количество, 0) - ЕСТЬNULL(ВТРасход.Количество, 0)) > 0";

	Запрос.УстановитьПараметр("Соглашение", СоглашениеСсылка);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", ПредопределенноеЗначение(
		"Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Расход"));
	Запрос.УстановитьПараметр("ВидДвиженияПлюс", ПредопределенноеЗначение(
		"Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Приход"));

	РезультатЗапроса = Запрос.Выполнить();

	РезультатПроверки = Ложь;

	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Превышение остатка по номенклатуре %1 на количество %2",
				ВыборкаДетальныеЗаписи.Номенклатура, ВыборкаДетальныеЗаписи.Превышение));
		КонецЦикла;
		РезультатПроверки = Истина;
	КонецЕсли;

	Возврат РезультатПроверки;
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
Функция ПолучитьТекущиеДанныеТЧЦеныПоТоварамИзЗаказа(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
	|	ЗНАЧЕНИЕ(Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Расход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА НоменклатураСправочник.ЕдиницаИзмерения
	|		ИНАЧЕ ЗаказКлиентаТовары.Упаковка
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ЗаказКлиентаТовары.Количество КАК Количество,
	|	ЗаказКлиента.Соглашение КАК Соглашение,
	|	ЗаказКлиента.Ссылка КАК ЗаказКлиента,
	|	ЗаказКлиентаТовары.ДатаОтгрузки КАК ДатаОтгрузки
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО ЗаказКлиентаТовары.Номенклатура = НоменклатураСправочник.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО ЗаказКлиентаТовары.Ссылка = ЗаказКлиента.Ссылка
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТовары.Соглашение КАК Соглашение,
	|	ВТТовары.Номенклатура КАК Номенклатура,
	|	ВТТовары.Характеристика КАК Характеристика,
	|	ВТТовары.ВидДвижения КАК ВидДвижения,
	|	ВТТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТТовары.Количество КАК Количество,
	|	ВТТовары.ЗаказКлиента КАК ЗаказКлиента,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.ДатаНачала КАК ДатаНачала,
	|	cbr_ОстаткиНоменклатурыПоСоглашению.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	РегистрСведений.cbr_ОстаткиНоменклатурыПоСоглашению КАК cbr_ОстаткиНоменклатурыПоСоглашению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТовары КАК ВТТовары
	|		ПО cbr_ОстаткиНоменклатурыПоСоглашению.Соглашение = ВТТовары.Соглашение
	|		И cbr_ОстаткиНоменклатурыПоСоглашению.Номенклатура = ВТТовары.Номенклатура
	|		И cbr_ОстаткиНоменклатурыПоСоглашению.Характеристика = ВТТовары.Характеристика
	|		И cbr_ОстаткиНоменклатурыПоСоглашению.ЕдиницаИзмерения = ВТТовары.ЕдиницаИзмерения
	|		И
	|			cbr_ОстаткиНоменклатурыПоСоглашению.ВидДвижения = ЗНАЧЕНИЕ(Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Приход)
	|		И ВТТовары.ДатаОтгрузки
	|			МЕЖДУ cbr_ОстаткиНоменклатурыПоСоглашению.ДатаНачала И cbr_ОстаткиНоменклатурыПоСоглашению.ДатаОкончания";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Возврат РезультатЗапроса;
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20
#КонецОбласти

#КонецЕсли