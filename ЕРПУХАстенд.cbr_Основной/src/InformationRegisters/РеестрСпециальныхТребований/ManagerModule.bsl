#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция СпециальныеТребованияПоОбъекту(Знач СсылкаНаОбъект) Экспорт
		
	РезультатФункции = Новый Структура("НайденыЗаписи", Ложь);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВерсияСоглашенияКоммерческийДоговор") Тогда
		СсылкаНаОбъект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента");
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2")	Тогда
		СсылкаНаОбъект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДокументОснование");
		Если Не ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			Возврат РезультатФункции;
		КонецЕсли;
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументОбъект.ЗаказНаПроизводство2_2") Тогда
		СсылкаНаОбъект = СсылкаНаОбъект.ДокументОснование;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеестрСпециальныхТребованийСрезПоследних.Период КАК Период,
		|	РеестрСпециальныхТребованийСрезПоследних.Регистратор КАК ДокументСпецтребование,
		|	РеестрСпециальныхТребованийСрезПоследних.Договор КАК Договор,
		|	РеестрСпециальныхТребованийСрезПоследних.ДокументыДляВодителя КАК ДокументыДляВодителя,
		|	РеестрСпециальныхТребованийСрезПоследних.ДопустимыйОстатокГСХВМесяцах КАК ДопустимыйОстатокГСХВМесяцах,
		|	РеестрСпециальныхТребованийСрезПоследних.ДопустимыйОстатокГСХВПроцентах КАК ДопустимыйОстатокГСХВПроцентах,
		|	РеестрСпециальныхТребованийСрезПоследних.НеБолееЧемОтДатыПроизводства КАК НеБолееЧемОтДатыПроизводства,
		|	РеестрСпециальныхТребованийСрезПоследних.ПрочиеТребования КАК ПрочиеТребования,
		|	РеестрСпециальныхТребованийСрезПоследних.ТаможенноеСопровождение КАК ТаможенноеСопровождение,
		|	РеестрСпециальныхТребованийСрезПоследних.ТребованиеККомплектации КАК ТребованиеККомплектации,
		|	РеестрСпециальныхТребованийСрезПоследних.ТребованиеКУпаковке КАК ТребованиеКУпаковке,
		|	РеестрСпециальныхТребованийСрезПоследних.ТребованиеКЭтикетке КАК ТребованиеКЭтикетке,
		|	РеестрСпециальныхТребованийСрезПоследних.УсловияДоставки КАК УсловияДоставки,
		|	РеестрСпециальныхТребованийСрезПоследних.Основание КАК Основание
		|ИЗ
		|	РегистрСведений.РеестрСпециальныхТребований.СрезПоследних(, Основание = &СсылкаНаОбъект) КАК РеестрСпециальныхТребованийСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период УБЫВ";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого КолонкаДанных Из РезультатЗапроса.Колонки Цикл
		РезультатФункции.Вставить(КолонкаДанных.Имя, Неопределено);
	КонецЦикла;
	РезультатФункции.Вставить("СтатусСогласования", ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
	
	Пока Выборка.Следующий() Цикл
		
		СтатусСогласования = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(
													Выборка.ДокументСпецтребование,
													ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
													
		Если СтатусСогласования = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Утверждена") Тогда
			ЗаполнитьЗначенияСвойств(РезультатФункции, Выборка);
			РезультатФункции.СтатусСогласования = СтатусСогласования;
			РезультатФункции.НайденыЗаписи = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

Функция ПоследнийВариантСпециальныхТребований(СсылкаНаОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Договор = Неопределено;

	Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "Договор, Договор.ВидСоглашения, Договор.БазовыйДоговор");
		Договор = РеквизитыОснования.Договор;
		
		Если РеквизитыОснования.ДоговорВидСоглашения = Перечисления.ВидыСоглашений.Спецификация Тогда
			Если ЗначениеЗаполнено(РеквизитыОснования.ДоговорБазовыйДоговор) Тогда
				Договор = РеквизитыОснования.ДоговорБазовыйДоговор;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
		
		ОснованиеЗаказКлиента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ДокументОснование");
		Если ТипЗнч(ОснованиеЗаказКлиента) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОснованиеЗаказКлиента, "Договор, Договор.ВидСоглашения, Договор.БазовыйДоговор");
			Договор = РеквизитыОснования.Договор;
			
			Если РеквизитыОснования.ДоговорВидСоглашения = Перечисления.ВидыСоглашений.Спецификация Тогда
				Если ЗначениеЗаполнено(РеквизитыОснования.ДоговорБазовыйДоговор) Тогда
					Договор = РеквизитыОснования.ДоговорБазовыйДоговор;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ВерсияСоглашенияКоммерческийДоговор") Тогда
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ДоговорКонтрагента, ДоговорКонтрагента.ВидСоглашения, ДоговорКонтрагента.БазовыйДоговор");
		Договор = РеквизитыОснования.ДоговорКонтрагента;
		
		Если РеквизитыОснования.ДоговорКонтрагентаВидСоглашения = Перечисления.ВидыСоглашений.Спецификация Тогда
			Если ЗначениеЗаполнено(РеквизитыОснования.ДоговорКонтрагентаБазовыйДоговор) Тогда
				Договор = РеквизитыОснования.ДоговорКонтрагентаБазовыйДоговор;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаОбъект, "ВидСоглашения, БазовыйДоговор");
		Договор = СсылкаНаОбъект;
		
		Если РеквизитыОснования.ВидСоглашения = Перечисления.ВидыСоглашений.Спецификация Тогда
			Если ЗначениеЗаполнено(РеквизитыОснования.БазовыйДоговор) Тогда
				Договор = РеквизитыОснования.БазовыйДоговор;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Договор) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеестрСпециальныхТребованийСрезПоследних.Регистратор КАК ДокументСпецтребование,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(РеестрСпециальныхТребованийСрезПоследних.Регистратор) КАК ПредставлениеСпецтребования,
		|	РеестрСпециальныхТребованийСрезПоследних.ДокументыДляВодителя КАК ДокументыДляВодителя,
		|	РеестрСпециальныхТребованийСрезПоследних.ДопустимыйОстатокГСХВМесяцах КАК ДопустимыйОстатокГСХВМесяцах,
		|	РеестрСпециальныхТребованийСрезПоследних.ДопустимыйОстатокГСХВПроцентах КАК ДопустимыйОстатокГСХВПроцентах,
		|	РеестрСпециальныхТребованийСрезПоследних.НеБолееЧемОтДатыПроизводства КАК НеБолееЧемОтДатыПроизводства,
		|	РеестрСпециальныхТребованийСрезПоследних.ПрочиеТребования КАК ПрочиеТребования,
		|	РеестрСпециальныхТребованийСрезПоследних.ТаможенноеСопровождение КАК ТаможенноеСопровождение,
		|	РеестрСпециальныхТребованийСрезПоследних.ТребованиеККомплектации КАК ТребованиеККомплектации,
		|	РеестрСпециальныхТребованийСрезПоследних.ТребованиеКУпаковке КАК ТребованиеКУпаковке,
		|	РеестрСпециальныхТребованийСрезПоследних.ТребованиеКЭтикетке КАК ТребованиеКЭтикетке,
		|	РеестрСпециальныхТребованийСрезПоследних.УсловияДоставки КАК УсловияДоставки,
		|	РеестрСпециальныхТребованийСрезПоследних.Основание КАК Основание
		|ИЗ
		|	РегистрСведений.РеестрСпециальныхТребований.СрезПоследних(, Договор = &Договор) КАК РеестрСпециальныхТребованийСрезПоследних";
		
		Запрос.УстановитьПараметр("Договор", Договор);
		
		РезультатЗапроса = Запрос.Выполнить();

		СтруктураВозврата = Новый Структура("НайденыЗаписи", Ложь);
			
		Если Не РезультатЗапроса.Пустой() Тогда
		
			ТаблицаРезультат = РезультатЗапроса.Выгрузить();
			СтруктураВозврата = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРезультат[0]);
			СтатусСогласования = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(
													СтруктураВозврата.ДокументСпецтребование,
													ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
			СтруктураВозврата.Вставить("СтатусСогласования", СтатусСогласования);
			СтруктураВозврата.Вставить("НайденыЗаписи", Истина);
			
		КонецЕсли;
		
		СтруктураВозврата.Вставить("Договор", Договор);
		Возврат СтруктураВозврата;
		
	КонецЕсли;
	
	Возврат Неопределено;
			
КонецФункции

#КонецОбласти

#КонецЕсли