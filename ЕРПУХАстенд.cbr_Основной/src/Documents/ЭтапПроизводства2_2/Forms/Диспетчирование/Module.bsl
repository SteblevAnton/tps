
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура cbr_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	ПодготовитьФорму();
	
	СкладыОтслеживанияСборки = Новый СписокЗначений;
	СкладыОтслеживанияСборки.Добавить(Справочники.Склады.НайтиПоНаименованию("Склад сборки сухих компонентов и пигментов"));
	
	Список.Параметры.УстановитьЗначениеПараметра("СписокСкладовСборки", СкладыОтслеживанияСборки);
	// КОНЕЦ Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Список

&НаСервере
&ИзменениеИКонтроль("ТекстЗапросаДинамическогоСпискаДиспетчированиеЭтапов")
Функция cbr_ТекстЗапросаДинамическогоСпискаДиспетчированиеЭтапов()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	#Вставка
	// НАЧАЛО Османов Н.М. 17.02.2025 
	|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки КАК Этап,
	|	СУММА(ТоварыКОтгрузкеОстатки.КСборкеОстаток) КАК КСборкеОстаток,
	|	СУММА(ТоварыКОтгрузкеОстатки.СобираетсяОстаток) КАК СобираетсяОстаток,
	|	СУММА(ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток) КАК КОтгрузкеОстаток,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура КАК Номенклатура,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Характеристика КАК Характеристика,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Назначение КАК Назначение,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Серия КАК Серия,
	|	СУММА(ВЫБОР
	|			КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.ВариантОбеспечения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.НеТребуется), ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.КОбеспечению))
	|				ТОГДА 10000
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток = 0
	|						ТОГДА 1
	|					ИНАЧЕ 100000
	|				КОНЕЦ
	|		КОНЕЦ) КАК СтатусЧисло
	|ПОМЕСТИТЬ ВТ_СтатусСборкиПредварительно
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|				,
	|				ДокументОтгрузки ССЫЛКА Документ.ЭтапПроизводства2_2
	|					И Склад В (&СписокСкладовСборки)) КАК ТоварыКОтгрузкеОстатки
	|		ПО ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка = ТоварыКОтгрузкеОстатки.ДокументОтгрузки
	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура = ТоварыКОтгрузкеОстатки.Номенклатура
	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Характеристика = ТоварыКОтгрузкеОстатки.Характеристика
	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Назначение = ТоварыКОтгрузкеОстатки.Назначение
	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Серия = ТоварыКОтгрузкеОстатки.Серия
	|			И ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад = ТоварыКОтгрузкеОстатки.Склад
	|ГДЕ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Склад В(&СписокСкладовСборки)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Характеристика,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Назначение,
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Серия,
	|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтатусСборкиПредварительно.Этап КАК Этап,
	|	ВЫБОР
	|		КОГДА СУММА(ВТ_СтатусСборкиПредварительно.СтатусЧисло) >= 100000
	|			ТОГДА ""Собирается""
	|		КОГДА СУММА(ВТ_СтатусСборкиПредварительно.СтатусЧисло) >= 10000
	|			ТОГДА """"
	|		ИНАЧЕ ""Собран""
	|	КОНЕЦ КАК Статус
	|ПОМЕСТИТЬ ВТ_СтатусСборки
	|ИЗ
	|	ВТ_СтатусСборкиПредварительно КАК ВТ_СтатусСборкиПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СтатусСборкиПредварительно.Этап
	|;
	|
	|ВЫБРАТЬ
	// КОНЕЦ Османов Н.М. 17.02.2025 
	// НАЧАЛО Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	|	ЭтапПроизводства2_2.Ссылка КАК ЭтапПроизводства,
	|	ЗаказНаПроизводство2_2Продукция.Номенклатура КАК Номенклатура,
	|	ЗаказНаПроизводство2_2Продукция.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ВТ_Этапы
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ПО ЗаказНаПроизводство2_2Продукция.Ссылка = ЭтапПроизводства2_2.Распоряжение
	|         И (ЗаказНаПроизводство2_2Продукция.НомерСтроки = 1)
	|СГРУППИРОВАТЬ ПО
	|	ЭтапПроизводства2_2.Ссылка,
	|	ЗаказНаПроизводство2_2Продукция.Номенклатура,
	|	ЗаказНаПроизводство2_2Продукция.Характеристика
	|;
	|
	// НАЧАЛО Османов Н.М. 27.11.2024
	|ВЫБРАТЬ
	|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ЭтапыПолуфабрикатов
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК ДополнительныеРеквизитыИСведения
	|			ПО ДополнительныеСведения.Свойство = ДополнительныеРеквизитыИСведения.Ссылка
	|				И (ДополнительныеРеквизитыИСведения.Имя = ""ЭтоПолуфабрикатВидНоменклатуры"")
	|		ПО ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ВидНоменклатуры = ДополнительныеСведения.Объект
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка КАК Этап,
	|	СУММА(ЕСТЬNULL(ВЫБОР
	|				КОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|						И СОКРЛП(ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Номенклатура.ЕдиницаИзмерения.Код) = ""163""
	|					ТОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Количество / 1000
	|				КОГДА НЕ ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|						И СОКРЛП(ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Упаковка.Код) = ""163""
	|					ТОГДА ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Количество / 1000
	|				ИНАЧЕ ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Количество
	|			КОНЕЦ, 0)) КАК Количество
	|ПОМЕСТИТЬ ВТ_КоличествоОбеспечения
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
	|ГДЕ
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ВТ_ЭтапыПолуфабрикатов КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка
	|;
	// КОНЕЦ Османов Н.М. 27.11.2024
	// НАЧАЛО Османов Н.М. 16.01.2025
	|
	|ВЫБРАТЬ
	|	ЭтапПроизводства2_2ВыходныеИзделияСерии.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ЭтапПроизводства2_2ВыходныеИзделияСерии.Серия) КАК Серия
	|ПОМЕСТИТЬ ВТ_Серии
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделияСерии КАК ЭтапПроизводства2_2ВыходныеИзделияСерии
	|СГРУППИРОВАТЬ ПО 
	|	ЭтапПроизводства2_2ВыходныеИзделияСерии.Ссылка
	|;
	|
	// КОНЕЦ Османов Н.М. 16.01.2025
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	// КОНЕЦ Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	// НАЧАЛО Османов Н.М. 26.03.2025
	|	тпсКонтрольЭтапов.Этап КАК Этап,
	|	СУММА(тпсКонтрольЭтапов.ВремяИспытания) КАК ОбщееВремяИспытания
	|ПОМЕСТИТЬ ВТ_КонтролиЭтаповРРК
	|ИЗ
	|	РегистрСведений.тпсКонтрольЭтапов КАК тпсКонтрольЭтапов
	|ГДЕ
	|	тпсКонтрольЭтапов.СтатусКонтроляРРК = ЗНАЧЕНИЕ(Перечисление.лимсСтатусКонтроля.Утвержден)
	|СГРУППИРОВАТЬ ПО 
	|	тпсКонтрольЭтапов.Этап
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	тпсКонтрольЭтапов.Этап КАК Этап,
	|	ВТ_КонтролиЭтаповРРК.ОбщееВремяИспытания КАК ОбщееВремяИспытания,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ тпсКонтрольЭтапов.ЗаявкаНаКонтроль) КАК КоличествоЗаявок,
	|	МИНИМУМ(тпсКонтрольЭтапов.ДатаЗаявки) КАК МинимальноеВремяЗаявки,
	|	МАКСИМУМ(тпсКонтрольЭтапов.ДатаЗаявки) КАК МаксимальноеВремяЗаявки
	|ПОМЕСТИТЬ ВТ_КонтролиЭтаповЗаявка
	|ИЗ
	|	РегистрСведений.тпсКонтрольЭтапов КАК тпсКонтрольЭтапов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КонтролиЭтаповРРК КАК ВТ_КонтролиЭтаповРРК
	|		ПО тпсКонтрольЭтапов.Этап = ВТ_КонтролиЭтаповРРК.Этап
	|СГРУППИРОВАТЬ ПО 
	|	тпсКонтрольЭтапов.Этап,
	|	ВТ_КонтролиЭтаповРРК.ОбщееВремяИспытания
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	тпсКонтрольЭтапов.Этап КАК Этап,
	|	ВТ_КонтролиЭтаповЗаявка.МинимальноеВремяЗаявки КАК ВремяСозданияЗаявки,
	|	тпсКонтрольЭтапов.СтатусКонтроля КАК СтатусКонтроля,
	|	тпсКонтрольЭтапов.ДатаОкончанияИспытания КАК ДатаОкончанияИспытания,
	|	ВТ_КонтролиЭтаповЗаявка.ОбщееВремяИспытания КАК ОбщееВремяИспытанияЧисло,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(10)) КАК ОбщееВремяИспытания,
	|	ВТ_КонтролиЭтаповЗаявка.КоличествоЗаявок КАК КоличествоЗаявок
	|ПОМЕСТИТЬ ВТ_КонтролиЭтапов
	|ИЗ
	|	РегистрСведений.тпсКонтрольЭтапов КАК тпсКонтрольЭтапов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КонтролиЭтаповЗаявка КАК ВТ_КонтролиЭтаповЗаявка
	|		ПО тпсКонтрольЭтапов.Этап = ВТ_КонтролиЭтаповЗаявка.Этап
	|			И тпсКонтрольЭтапов.ДатаЗаявки = ВТ_КонтролиЭтаповЗаявка.МаксимальноеВремяЗаявки
	|СГРУППИРОВАТЬ ПО
	|	тпсКонтрольЭтапов.Этап,
	|	ВТ_КонтролиЭтаповЗаявка.МинимальноеВремяЗаявки,
	|	тпсКонтрольЭтапов.СтатусКонтроля,
	|	тпсКонтрольЭтапов.ДатаОкончанияИспытания,
	|	ВТ_КонтролиЭтаповЗаявка.ОбщееВремяИспытания,
	|	ВТ_КонтролиЭтаповЗаявка.КоличествоЗаявок
	|;
	|
	|ВЫБРАТЬ
	// КОНЕЦ Османов Н.М. 26.03.2025 
	#КонецВставки
	|	Этапы.Статус КАК Статус,
	|	&ПредставлениеЭтапа КАК ПредставлениеЭтапа,
	|	Этапы.Распоряжение КАК Распоряжение,
	|	Этапы.Распоряжение.Подразделение КАК ПодразделениеДиспетчер,
	|	Этапы.НаименованиеЭтапа КАК НаименованиеЭтапа,
	|	Этапы.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
	|	Этапы.Подразделение КАК Подразделение,
	|	Этапы.Спецификация КАК Спецификация,
	|	Этапы.ПартияПроизводства КАК ПартияПроизводства,
	|	Этапы.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
	|	Этапы.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа,
	|	ВЫБОР
	|		КОГДА &ПланируетсяГрафикПроизводства
	|				И ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						РегистрСведений.ЗаданияКРасчетуГрафикаПроизводства КАК Т
	|					ГДЕ
	|						НЕ Этапы.ДинамическаяСтруктура
	|						И Т.Распоряжение = Этапы.Распоряжение
	|						И Т.ЭтапПроизводства = Этапы.Ссылка
	|					
	|					ОБЪЕДИНИТЬ ВСЕ
	|					
	|					ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Т
	|					ГДЕ
	|						Этапы.ДинамическаяСтруктура
	|						И Т.ЭтапПроизводства = Этапы.Ссылка
	|					)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяПланироватьГрафик,
	|
	|	ЕСТЬNULL(СостоянияЭтаповПроизводства.СостояниеОбеспечения, &ПустаяСсылкаСостояниеОбеспечения) КАК СостояниеОбеспечения,
	|	ЕСТЬNULL(СостоянияЭтаповПроизводства.СостояниеОбеспечения, &ПустаяСсылкаСостояниеОбеспечения)
	|		= ЗНАЧЕНИЕ(Перечисление.СостоянияОбеспеченияЭтапаПроизводства.ОжидаетсяОбеспечение) КАК ОжидаетсяОбеспечение,
	|
	|	ВЫБОР
	|		КОГДА &ПланируетсяГрафикПроизводства
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ГрафикПроизводства.ОграничиваетСрокВыпуска, ЛОЖЬ) = ИСТИНА
	|						ТОГДА 3
	|					КОГДА ЕСТЬNULL(ГрафикПроизводства.НаКритическомПути, ЛОЖЬ) = ИСТИНА
	|						ТОГДА 2
	|					ИНАЧЕ -1
	|				КОНЕЦ
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК СостояниеОграниченийВГрафике,
	|	ВЫБОР
	|		КОГДА Этапы.Статус = &СтатусЗавершен
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияЭтаповНаМежцеховомУровне.Завершен)
	|		ИНАЧЕ ЕСТЬNULL(СостоянияЭтаповПроизводства.СостояниеНаМежцеховомУровне, &ПустаяСсылкаСостояние)
	|	КОНЕЦ КАК СостояниеЭтапа,
	|	
	|	ЕСТЬNULL(СостоянияЭтаповПроизводства.СостояниеОпераций, &ПустаяСсылкаСостояниеОпераций) КАК СостояниеОпераций,
	|	ВЫБОР
	|		КОГДА Этапы.ТребуетсяЗаполнитьПоОперациям
	|			ТОГДА 3
	|		КОГДА СостоянияЭтаповПроизводства.СостояниеОпераций = ЗНАЧЕНИЕ(Перечисление.СостоянияОперацийЭтапаПроизводства.ОжидаетНазначения)
	|			ТОГДА 0
	|		КОГДА СостоянияЭтаповПроизводства.СостояниеОпераций = ЗНАЧЕНИЕ(Перечисление.СостоянияОперацийЭтапаПроизводства.ОжидаетЗавершения)
	|			ТОГДА 1
	|		КОГДА СостоянияЭтаповПроизводства.СостояниеОпераций = ЗНАЧЕНИЕ(Перечисление.СостоянияОперацийЭтапаПроизводства.Выполнено)
	|			ТОГДА 2
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК КодСостоянияОпераций,
	|	Этапы.ТребуетсяЗаполнитьПоОперациям КАК ТребуетсяЗаполнитьПоОперациям,
	|	НЕ Этапы.ТребуетсяЗаполнитьПоОперациям
	|		И СостоянияЭтаповПроизводства.СостояниеОпераций = ЗНАЧЕНИЕ(Перечисление.СостоянияОперацийЭтапаПроизводства.ОжидаетНазначения) КАК ТребуетсяНазначитьОперации,
	|	
	|	ВЫБОР
	|		КОГДА Этапы.ДинамическаяСтруктура ИЛИ &ПланируетсяГрафикПроизводства
	|			ТОГДА НЕ ГрафикПроизводства.Этап ЕСТЬ NULL
	|		ИНАЧЕ НЕ НормативныйГрафикПроизводства.ЭтапПроизводства ЕСТЬ NULL
	|	КОНЕЦ КАК ГрафикРассчитан,
	|	ВЫБОР
	|		КОГДА &ПланируетсяГрафикПроизводства
	|				И НЕ ГрафикПроизводства.Этап ЕСТЬ NULL 
	|			ТОГДА НАЧАЛОПЕРИОДА(ГрафикПроизводства.Начало, ДЕНЬ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(Этапы.Распоряжение.НачатьНеРанее, СЕКУНДА, ЕСТЬNULL(НормативныйГрафикПроизводства.ДлительностьДоЗапуска, 0)), ДЕНЬ)
	|	КОНЕЦ КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА &ДатаАктуальности > ВЫБОР
	|				КОГДА &ПланируетсяГрафикПроизводства
	|						И НЕ ГрафикПроизводства.Этап ЕСТЬ NULL 
	|					ТОГДА ГрафикПроизводства.Начало
	|				ИНАЧЕ ДОБАВИТЬКДАТЕ(Этапы.Распоряжение.НачатьНеРанее, СЕКУНДА, ЕСТЬNULL(НормативныйГрафикПроизводства.ДлительностьДоЗапуска, 0))
	|			КОНЕЦ И Этапы.Статус В (&СтатусФормируется, &СтатусСформирован, &СтатусКВыполнению)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Просрочен,
	|	ВЫБОР
	|		КОГДА Этапы.ДинамическаяСтруктура ИЛИ &ПланируетсяГрафикПроизводства
	|			ТОГДА ЕСТЬNULL(ГрафикПроизводства.Начало, ДАТАВРЕМЯ(1,1,1))
	|		КОГДА НормативныйГрафикПроизводства.ДлительностьДоЗапуска ЕСТЬ НЕ NULL
	|			ТОГДА ДОБАВИТЬКДАТЕ(Этапы.Распоряжение.НачатьНеРанее, СЕКУНДА, НормативныйГрафикПроизводства.ДлительностьДоЗапуска)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ КАК ДатаНачала,
	|	ВЫБОР
	|		КОГДА Этапы.ДинамическаяСтруктура ИЛИ &ПланируетсяГрафикПроизводства
	|			ТОГДА ЕСТЬNULL(ГрафикПроизводства.Окончание, ДАТАВРЕМЯ(1,1,1))
	|		КОГДА НормативныйГрафикПроизводства.ДлительностьДоЗапуска ЕСТЬ НЕ NULL
	|			ТОГДА ДОБАВИТЬКДАТЕ(Этапы.Распоряжение.НачатьНеРанее, СЕКУНДА, НормативныйГрафикПроизводства.ДлительностьДоЗапуска + НормативныйГрафикПроизводства.Ресурсоемкость)
	|		ИНАЧЕ ДАТАВРЕМЯ(1,1,1)
	|	КОНЕЦ КАК ДатаЗавершения,
	|	ВЫБОР 
	|		КОГДА ГрафикПроизводства.Начало ЕСТЬ НЕ NULL ТОГДА
	|			ВЫБОР 
	|				КОГДА Этапы.Статус В (&СтатусФормируется, &СтатусСформирован, &СтатусКВыполнению)
	|						И РАЗНОСТЬДАТ(ГрафикПроизводства.Начало, &ДатаАктуальности, ДЕНЬ) > 0 ТОГДА
	|					РАЗНОСТЬДАТ(ГрафикПроизводства.Начало, &ДатаАктуальности, ДЕНЬ)
	|				КОГДА Этапы.Статус = &СтатусНачат
	|						И РАЗНОСТЬДАТ(ГрафикПроизводства.Окончание, &ДатаАктуальности, ДЕНЬ) > 0 ТОГДА
	|					РАЗНОСТЬДАТ(ГрафикПроизводства.Окончание, &ДатаАктуальности, ДЕНЬ)
	|				КОГДА Этапы.Статус = &СтатусЗавершен
	|						И РАЗНОСТЬДАТ(ГрафикПроизводства.Окончание, Этапы.ФактическоеОкончаниеЭтапа, ДЕНЬ) > 0 ТОГДА
	|					РАЗНОСТЬДАТ(ГрафикПроизводства.Окончание, Этапы.ФактическоеОкончаниеЭтапа, ДЕНЬ)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Задержка,
	|	ВЫБОР
	|		КОГДА ГрафикПроизводства.Начало ЕСТЬ НЕ NULL
	|			ТОГДА ВЫБОР
	|					КОГДА &ДатаАктуальности > ГрафикПроизводства.Начало
	|							И Этапы.Статус В (&СтатусФормируется, &СтатусСформирован, &СтатусКВыполнению)
	|						ИЛИ &ДатаАктуальности > ГрафикПроизводства.Окончание
	|							И Этапы.Статус <> &СтатусЗавершен
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НормативныйГрафикПроизводства.ЭтапПроизводства ЕСТЬ НЕ NULL
	|						И (&ДатаАктуальности > ДОБАВИТЬКДАТЕ(Этапы.Распоряжение.НачатьНеРанее, СЕКУНДА, ЕСТЬNULL(НормативныйГрафикПроизводства.ДлительностьДоЗапуска, 0))
	|								И Этапы.Статус В (&СтатусФормируется, &СтатусСформирован, &СтатусКВыполнению)
	|							ИЛИ &ДатаАктуальности > ДОБАВИТЬКДАТЕ(Этапы.Распоряжение.НачатьНеРанее, СЕКУНДА, ЕСТЬNULL(НормативныйГрафикПроизводства.ДлительностьДоЗапуска + НормативныйГрафикПроизводства.Ресурсоемкость, 0))
	|								И Этапы.Статус <> &СтатусЗавершен)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕстьОтставаниеОтГрафика,
	|	
	|	ВЫБОР
	|		КОГДА Этапы.РучноеРазмещениеВГрафике И &ПланируетсяГрафикПроизводства
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК ДатаНачалаИндексКартинки,
	|	
	|	ВЫБОР
	|		КОГДА Этапы.РучноеРазмещениеВГрафике И &ПланируетсяГрафикПроизводства
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК ДатаОкончанияИндексКартинки,
	|	
	|	Этапы.ДатаПроизводства КАК ДатаПроизводства,
	|	Этапы.ДинамическаяСтруктура КАК ДинамическаяСтруктура,
	|
	|	Этапы.Подразделение.ИспользоватьПооперационноеУправление КАК ИспользоватьПооперационноеУправление,
	|	Этапы.Подразделение.ИспользоватьПооперационноеПланирование КАК ИспользоватьПооперационноеПланирование,
	|	Этапы.Подразделение.ИспользоватьСменныеЗадания КАК ИспользоватьСменныеЗадания,
	|	&ПараметрыТехпроцесса,
	|
	|	Этапы.КоличествоУпаковокПлан     КАК КоличествоПлан,
	|	Этапы.КоличествоУпаковокФакт     КАК КоличествоФакт,
	|	Этапы.КоличествоУпаковокОтменено КАК КоличествоОтменено,
	|	ВЫБОР
	|		КОГДА Этапы.УпаковкаПлан = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА Этапы.ПартияПроизводства.ОсновноеИзделиеНоменклатура.ЕдиницаИзмерения
	|		ИНАЧЕ Этапы.УпаковкаПлан
	|	КОНЕЦ КАК КоличествоЕдиницаИзмерения,
	|
	|	0 КАК ОстатокТрудоемкости,
	|	0 КАК ПроцентВыполнения,
	|	
	#Вставка
	// НАЧАЛО Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	|	Этапы.РабочийЦентр КАК ЭтапыРабочийЦентр,
	|	Этапы.Распоряжение.Комментарий КАК КомментарийОПиУ,
	|	Этапы.Комментарий КАК КомментарийПоЭтапу,
	|	Этапы.КоличествоУпаковокПлан КАК Запланировано,
	|	Этапы.Очередь КАК Очередь,
	|	ВТ_Этапы.Номенклатура КАК Номенклатура,
	|	ВТ_Этапы.Характеристика КАК Характеристика,
	// КОНЕЦ Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	// НАЧАЛО Османов Н.М. 27.11.2024 
	|	ВТ_КоличествоОбеспечения.Количество КАК КоличествоОбеспечения,
	// КОНЕЦ Османов Н.М. 27.11.2024
	// НАЧАЛО Османов Н.М. 16.01.2025
	|	ВТ_Серии.Серия КАК Серия,
	// КОНЕЦ Османов Н.М. 16.01.2025
	// НАЧАЛО Османов Н.М. 15.02.2025
	|	ВТ_СтатусСборки.Статус КАК Сборка,
	// КОНЕЦ Османов Н.М. 15.02.2025 
	// НАЧАЛО Османов Н.М. 26.03.2025 
	|	ВТ_КонтролиЭтапов.ВремяСозданияЗаявки КАК ВремяСозданияЗаявки,
	|	ВТ_КонтролиЭтапов.СтатусКонтроля КАК СтатусКонтроля,
	|	ВТ_КонтролиЭтапов.ДатаОкончанияИспытания КАК ДатаОкончанияИспытания,
	|	ВТ_КонтролиЭтапов.ОбщееВремяИспытания КАК ОбщееВремяИспытания,
	|	ВТ_КонтролиЭтапов.ОбщееВремяИспытанияЧисло КАК ОбщееВремяИспытанияЧисло,
	|	ВТ_КонтролиЭтапов.КоличествоЗаявок КАК КоличествоЗаявокНаКонтроль,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(200)) КАК Лаборатория,
	// КОНЕЦ Османов Н.М. 26.03.2025
	//++ cbr-larkovgeorg 09.02.26 ПРЗ.6
	|ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра КАК РабочийЦентр,
	//-- cbr-larkovgeorg 09.02.26 ПРЗ.6 
	#КонецВставки
	|	Этапы.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Этапы
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикПроизводства
	|		ПО Этапы.Ссылка = НормативныйГрафикПроизводства.ЭтапПроизводства
	|			И Этапы.Распоряжение = НормативныйГрафикПроизводства.Распоряжение
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЭтаповПроизводства КАК СостоянияЭтаповПроизводства
	|		ПО Этапы.Распоряжение = СостоянияЭтаповПроизводства.Распоряжение
	|			И Этапы.Ссылка = СостоянияЭтаповПроизводства.Этап
	|
	#Вставка
	// НАЧАЛО Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Этапы КАК ВТ_Этапы
	|		ПО Этапы.Ссылка = ВТ_Этапы.ЭтапПроизводства
	// КОНЕЦ Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	// НАЧАЛО Османов Н.М. 27.11.2024 
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоОбеспечения КАК ВТ_КоличествоОбеспечения
	|		ПО Этапы.Ссылка = ВТ_КоличествоОбеспечения.Этап
	// КОНЕЦ Османов Н.М. 27.11.2024
	// НАЧАЛО Османов Н.М. 16.01.2025
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Серии КАК ВТ_Серии
	|		ПО Этапы.Ссылка = ВТ_Серии.Ссылка
	// КОНЕЦ Османов Н.М. 16.01.2025
	// НАЧАЛО Османов Н.М. 17.02.2025 
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатусСборки КАК ВТ_СтатусСборки
	|		ПО Этапы.Ссылка = ВТ_СтатусСборки.Этап
	// КОНЕЦ Османов Н.М. 17.02.2025 
	// НАЧАЛО Османов Н.М. 26.03.2025 
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КонтролиЭтапов КАК ВТ_КонтролиЭтапов
	|		ПО Этапы.Ссылка = ВТ_КонтролиЭтапов.Этап
	// КОНЕЦ Османов Н.М. 26.03.2025 
	#КонецВставки
	#Вставка
	//++ cbr-larkovgeorg 09.02.26 ПРЗ.6
	| ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
	| ПО (ДоступностьВидовРабочихЦентров.Регистратор = Этапы.Ссылка
	| И ТИПЗНАЧЕНИЯ(ДоступностьВидовРабочихЦентров.Регистратор) = ТИП(Документ.ЭтапПроизводства2_2)
	| И ДоступностьВидовРабочихЦентров.НомерСтроки = 1)
	//-- cbr-larkovgeorg 09.02.26 ПРЗ.6
	#КонецВставки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикПроизводства
	|		ПО Этапы.Распоряжение = ГрафикПроизводства.ЗаказНаПроизводство
	|			И Этапы.Ссылка = ГрафикПроизводства.Этап
	|			И (ГрафикПроизводства.СтатусГрафика = &СтатусРабочийГрафик)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцесс
	|		ПО Этапы.Ссылка = Техпроцесс.Этап
	|	
	|ГДЕ
	|	НЕ Этапы.ПометкаУдаления";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура ПодготовитьФорму()
	
	// НАЧАЛО Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством
	Если ФормыУХ.ЭлементыФормыУХУжеСозданы(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	ЭлементРабочийЦентр = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокРабочийЦентр",
												"Рабочий центр",
												"Список.РабочийЦентр",
												,
												Элементы.Список,
												Элементы.ПредставлениеЭтапа);
	
	ЭлементРаспоряжениеЗаказ = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокРаспоряжениеЗаказ",
												"Заказ на производство",
												"Список.Распоряжение",
												,
												Элементы.Список,
												Элементы.СписокЭтаповГруппаСостояние);
												
	ЭлементНоменклатура = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокНоменклатура",
												"Номенклатура",
												"Список.Номенклатура",
												,
												Элементы.Список,
												Элементы.СписокПодразделение);
												
	ПараметрыЭлемента = Новый Структура("АвтоОтметкаНезаполненного", Ложь);
	ЭлементХарактеристика = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокХарактеристика",
												"Характеристика",
												"Список.Характеристика",
												,
												Элементы.Список,
												Элементы.СписокПодразделение,
												ПараметрыЭлемента);
												
	ЭлементЗапланировано = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокЗапланировано",
												"Запланировано",
												"Список.Запланировано",
												,
												Элементы.Список,
												Элементы.СписокПодразделение);
												
	ЭлементОчередь = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокОчередь",
												"Очередь",
												"Список.Очередь",
												,
												Элементы.СписокЭтаповГруппаПериод,
												Элементы.Задержка);											
												
	ЭлементКомментарийОПиУ = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокКомментарийОПиУ",
												"Комментарий ОПиУ",
												"Список.КомментарийОПиУ",
												,
												Элементы.Список);
												
	ЭлементКомментарийПоЭтапу = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокКомментарийПоЭтапу",
												"Комментарий по этапу",
												"Список.КомментарийПоЭтапу",
												,
												Элементы.Список);
												
	// НАЧАЛО Османов Н.М. 27.11.2024
	ЭлементКоличествоОбеспечение = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокКоличествоОбеспечения",
												"Количество обеспечения",
												"Список.КоличествоОбеспечения",
												,
												Элементы.Список,
												Элементы.СписокЗапланировано);
	// КОНЕЦ Османов Н.М. 27.11.2024
	
	// НАЧАЛО Османов Н.М. 16.01.2025
	ЭлементСерия = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокСерия",
												"Серия",
												"Список.Серия",
												,
												Элементы.Список,
												Элементы.СписокКоличествоОбеспечения);
	// КОНЕЦ Османов Н.М. 16.01.2025
	
	// НАЧАЛО Османов Н.М. 15.02.2025 
	ЭлементСерия = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокСборка",
												"Сборка",
												"Список.Сборка",
												,
												Элементы.Список,
												Элементы.СписокКоличествоОбеспечения);
	// КОНЕЦ Османов Н.М. 15.02.2025 
	
	// НАЧАЛО Османов Н.М. 26.03.2025 
	ПараметрыГруппыЛаборатории = Новый Структура("Группировка", ГруппировкаКолонок.Горизонтальная);
	ГруппаКолонокЛаборатория = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаКолонокЛаборатория",
												"Лаборатория",
												ВидГруппыФормы.ГруппаКолонок,
												Элементы.Список,
												Элементы.СписокКоличествоОбеспечения,
												ПараметрыГруппыЛаборатории);
												
	ЭлементЛаборатория = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокЛаборатория",
												"Лаборатория",
												"Список.Лаборатория",
												,
												ГруппаКолонокЛаборатория);
												
	ПараметрыЭлементаЛаборатории = Новый Структура("Видимость", Истина);
	ЭлементВремяСозданияЗаявки = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокВремяСозданияЗаявки",
												"Время создания заявки",
												"Список.ВремяСозданияЗаявки",
												,
												ГруппаКолонокЛаборатория,
												,
												ПараметрыЭлементаЛаборатории);
	Список.УстановитьОбязательноеИспользование("ВремяСозданияЗаявки", Истина);												

	ЭлементСтатусКонтроля = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокСтатусКонтроля",
												"Статус контроля",
												"Список.СтатусКонтроля",
												,
												ГруппаКолонокЛаборатория,
												,
												ПараметрыЭлементаЛаборатории);
	Список.УстановитьОбязательноеИспользование("СтатусКонтроля", Истина);
												
	ЭлементДатаОкончанияИспытания = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокДатаОкончанияИспытания",
												"Дата окончания испытания",
												"Список.ДатаОкончанияИспытания",
												,
												ГруппаКолонокЛаборатория,
												,
												ПараметрыЭлементаЛаборатории);
	Список.УстановитьОбязательноеИспользование("ДатаОкончанияИспытания", Истина);
												
	ЭлементОбщееВремяИспытания = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокОбщееВремяИспытания",
												"Общее время испытания",
												"Список.ОбщееВремяИспытания",
												,
												ГруппаКолонокЛаборатория,
												,
												ПараметрыЭлементаЛаборатории);
	Список.УстановитьОбязательноеИспользование("ОбщееВремяИспытания", Истина);
												
	ЭлементКоличествоЗаявокНаКонтроль = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокКоличествоЗаявокНаКонтроль",
												"Количество заявок",
												"Список.КоличествоЗаявокНаКонтроль",
												,
												ГруппаКолонокЛаборатория,
												,
												ПараметрыЭлементаЛаборатории);
	Список.УстановитьОбязательноеИспользование("КоличествоЗаявокНаКонтроль", Истина);
	
	ПараметрыЭлементаЛаборатории = Новый Структура("Видимость", Ложь);
	ЭлементОбщееВремяИспытанияЧисло = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокОбщееВремяИспытанияЧисло",
												"Общее время испытания число",
												"Список.ОбщееВремяИспытанияЧисло",
												,
												ГруппаКолонокЛаборатория,
												,
												ПараметрыЭлементаЛаборатории);
	Список.УстановитьОбязательноеИспользование("ОбщееВремяИспытанияЧисло", Истина);
												
	// КОНЕЦ Османов Н.М. 26.03.2025 
	ДанныеТаблицыРРК = ОписаниеТаблицыКонтроля();
	ДобавляемыеРеквизиты = РаботаСФормамиСервер.МассивРеквизитовТаблицыЗначений(ДанныеТаблицыРРК);
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОтборПоРабочемуЦентру", Новый ОписаниеТипов("СправочникСсылка.РабочиеЦентры")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОтборДатаНаработки", Новый ОписаниеТипов("СтандартныйПериод")));
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ПараметрыПоляОтбора = Новый Структура("ПоложениеЗаголовка, КнопкаОчистки", ПоложениеЗаголовкаЭлементаФормы.Нет, Истина);
	СобытиеПоляОтбора =  Новый Структура("ПриИзменении", "ОтборПоРабочемуЦентруПриИзменении");
	ЭлементОтборРабочийЦентр = ФормыУХ.СоздатьПолеФормы(Элементы,
												"ОтборПоРабочемуЦентру",
												"Рабочий центр",
												"ОтборПоРабочемуЦентру",
												,
												Элементы.ГруппаОтборы,
												,
												ПараметрыПоляОтбора,
												СобытиеПоляОтбора);
	ЭлементОтборРабочийЦентр.ПодсказкаВвода = ЭлементОтборРабочийЦентр.Заголовок;
	
	СобытиеПоляОтбора =  Новый Структура("ПриИзменении", "ОтборДатыНаработкиПриИзменении");
	ЭлементОтборПериодНаработки = ФормыУХ.СоздатьПолеФормы(Элементы,
												"ОтборДатаНаработки",
												"Период наработки",
												"ОтборДатаНаработки",
												,
												Элементы.ГруппаОтборы,
												,
												ПараметрыПоляОтбора,
												СобытиеПоляОтбора);
	ЭлементОтборПериодНаработки.ПодсказкаВвода = ЭлементОтборПериодНаработки.Заголовок;
													
	// НАЧАЛО Османов Н.М. 27.09.2024
	ИмяКомандыВперед = "П3_ПереместитьОчередьВперед";
	Команда = ФормыУХ.СоздатьКоманду(ЭтотОбъект, ИмяКомандыВперед, "", ИмяКомандыВперед);
	ИмяКомандыНазад = "П3_ПереместитьОчередьНазад";
	Команда = ФормыУХ.СоздатьКоманду(ЭтотОбъект, ИмяКомандыНазад, "", ИмяКомандыНазад);
	
	СтруктураПараметровКнопок = Новый Структура;
	СтруктураПараметровКнопок.Вставить("Картинка",    БиблиотекаКартинок.ПереместитьВверх);   
	СтруктураПараметровКнопок.Вставить("Отображение", ОтображениеКнопки.Картинка);
	
	ФормыУХ.СоздатьКнопкуФормы(Элементы, 
							  "П3_ПереместитьОчередьВперед", 
							  НСтр("ru = 'Переместить в очереди вперед'"),
							  ИмяКомандыВперед,
							  ВидКнопкиФормы.КнопкаКоманднойПанели,
							  Элементы.ГруппаКоманднаяПанель,
							  Элементы.ПодменюСоздатьНаОсновании,
							  СтруктураПараметровКнопок);
							  
	СтруктураПараметровКнопок.Вставить("Картинка", БиблиотекаКартинок.ПереместитьВниз);
	
	ФормыУХ.СоздатьКнопкуФормы(Элементы, 
							  "П3_ПереместитьОчередьНазад", 
							  НСтр("ru = 'Переместить в очереди назад'"),
							  ИмяКомандыНазад,
							  ВидКнопкиФормы.КнопкаКоманднойПанели,
							  Элементы.ГруппаКоманднаяПанель,
							  Элементы.ПодменюСоздатьНаОсновании,
							  СтруктураПараметровКнопок);
							  
	Список.Порядок.Элементы.Очистить();
	
	НовыйПорядок = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	НовыйПорядок.Использование = Истина;
	НовыйПорядок.Поле = Новый ПолеКомпоновкиДанных("Очередь");
	НовыйПорядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	НовыйПорядок = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	НовыйПорядок.Использование = Истина;
	НовыйПорядок.Поле = Новый ПолеКомпоновкиДанных("ДатаНачала");
	НовыйПорядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	Элементы.Список.РазрешитьПеретаскивание = Истина;
	// КОНЕЦ Османов Н.М. 27.09.2024
	
	// НАЧАЛО Османов Н.М. 30.01.2025 
	//ИмяКомандыОбеспечитьВсе = "П3_ОтгрузитьСразуДляВсех";
	//Команда = ФормыУХ.СоздатьКоманду(ЭтаФорма, ИмяКомандыОбеспечитьВсе, "", ИмяКомандыОбеспечитьВсе);
	//
	//ФормыУХ.СоздатьКнопкуФормы(Элементы, 
	//						  "П3_КнопкаОтгрузитьВсе", 
	//						  НСтр("ru = 'Обеспечить всё'"),
	//						  ИмяКомандыОбеспечитьВсе,
	//						  ВидКнопкиФормы.КнопкаКоманднойПанели,
	//						  Элементы.ГруппаКоманднаяПанельДействияСтатусы,
	//						  Элементы.ОтметитьНачалоВыполнения);
	// КОНЕЦ Османов Н.М. 30.01.2025
	
	// НАЧАЛО Османов Н.М. 28.03.2025 
	ПараметрыГруппыТаблицыРРК = Новый Структура("Поведение, Заголовок, ЗаголовокСвернутогоОтображения", 
												ПоведениеОбычнойГруппы.Свертываемая,
												НСтр("ru = '> Детальная информация (скрыть)'"),
												НСтр("ru = '˅ Детальная информация (показать)'"));
	ГруппаТаблицыКонтролейРРК = ФормыУХ.СоздатьГруппуФормы(Элементы,
														   "ГруппаТаблицыКонтролейРРК",
														   "",
														   ,
														   ЭтотОбъект,
														   ,
														   ПараметрыГруппыТаблицыРРК);
	
	
	МетаданныеТаблицы = Новый Структура("Реквизиты", ДанныеТаблицыРРК.Колонки);
	ТаблицаРРК = РаботаСФормамиСервер.СоздатьТаблицуНаФорме(Элементы, 
											   				"ЭлементТаблицаКонтролейРРК",
											   				"Таблица контролей РРК",
											   				"ТаблицаКонтролейРРК",
											   				ГруппаТаблицыКонтролейРРК,
											   				МетаданныеТаблицы,
											   				Ложь);
	ТаблицаРРК.Таблица.ТолькоПросмотр = Истина;
	ТаблицаРРК.Таблица.КоманднаяПанель.Видимость = Ложь;
	ТаблицаРРК.Таблица.ЦветРамки = Новый Цвет(240, 240, 240);
	ТаблицаРРК.Таблица.УстановитьДействие("Выбор", "П3_ТаблицаРРКВыбор");
	
	Элементы.Список.ЦветРамки = Новый Цвет(240, 240, 240);
	// КОНЕЦ Османов Н.М. 28.03.2025 											   
	
	ФормыУХ.ЭлементыФормыУХДобавлены(ЭтотОбъект);
	// КОНЕЦ Османов Н.М. 28.11.2023 ЧТЗ №2 Управление производством

КонецПроцедуры

&НаКлиенте
Процедура ОтборПоРабочемуЦентруПриИзменении(Элемент)
	
	УстановитьОтбор = Истина;
	Если Не ЗначениеЗаполнено(ЭтотОбъект["ОтборПоРабочемуЦентру"]) Тогда
		УстановитьОтбор = Ложь;
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, 
														"РабочийЦентр",
														ЭтотОбъект["ОтборПоРабочемуЦентру"],
														УстановитьОтбор,
														ВидСравненияКомпоновкиДанных.Равно);
	
КонецПроцедуры
													
&НаКлиенте
Процедура ОтборДатыНаработкиПриИзменении(Элемент)
	
	УстановитьОтбор = Истина;
	Если Не ЗначениеЗаполнено(ЭтотОбъект["ОтборДатаНаработки"]) Тогда
		УстановитьОтбор = Ложь;
	КонецЕсли;
	
	Период = ЭтотОбъект["ОтборДатаНаработки"];
												
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, 
														"ДатаНачала",
														Период.ДатаНачала,
														УстановитьОтбор,
														ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
														
	УстановитьЭлементОтбораКоллекции(Список.КомпоновщикНастроек.Настройки.Отбор.Элементы, 
														"ДатаНачала",
														Период.ДатаОкончания,
														ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
														УстановитьОтбор);
														
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЭлементОтбораКоллекции(КоллекцияЭлементов, ИмяПоля, ПравоеЗначение, ВидСравнения = Неопределено, Использование, Представление = "")
	
	ЭлементОтбора = КоллекцияЭлементов.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементОтбора.ВидСравнения     = ?(ВидСравнения = Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравнения);
	ЭлементОтбора.Использование    = Использование;
	ЭлементОтбора.ПравоеЗначение   = ПравоеЗначение;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	ЭлементОтбора.Представление    = Представление;
	
КонецПроцедуры

&НаКлиенте
Процедура П3_ПереместитьОчередьВперед(Команда)
	
	ВыбранныеЭтапы = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыбранныеЭтапы.ВГраница() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПереместитьЭтапВОчереди(ВыбранныеЭтапы[0], -1);
	
КонецПроцедуры

&НаКлиенте
Процедура П3_ПереместитьОчередьНазад(Команда)
	
	ВыбранныеЭтапы = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыбранныеЭтапы.ВГраница() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПереместитьЭтапВОчереди(ВыбранныеЭтапы[0], 1);
	
КонецПроцедуры

&НаСервере
Процедура ПереместитьЭтапВОчереди(ТекущийЭтап, Направление)
	
	Если НЕ Документы.ЭтапПроизводства2_2.МожноПереместитьДокументВОчереди(ТекущийЭтап, Направление) Тогда
		Возврат;
	КонецЕсли;
	
	Документы.ЭтапПроизводства2_2.ПереместитьДокументВОчереди(ТекущийЭтап, Направление);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура cbr_ПриОткрытииПосле(Отказ)
	
	// НАЧАЛО Османов Н.М. 27.09.2024
	Список.Порядок.Элементы.Очистить();
	
	НовыйПорядок = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	НовыйПорядок.Использование = Истина;
	НовыйПорядок.Поле = Новый ПолеКомпоновкиДанных("Очередь");
	НовыйПорядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	
	
	НовыйПорядок = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
	НовыйПорядок.Использование = Истина;
	НовыйПорядок.Поле = Новый ПолеКомпоновкиДанных("ДатаНачала");
	НовыйПорядок.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	// КОНЕЦ Османов Н.М. 27.09.2024
	
КонецПроцедуры

&НаКлиенте
Процедура cbr_СписокНачалоПеретаскиванияПосле(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Выполнение = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура cbr_СписокПроверкаПеретаскиванияПосле(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если РазрешеноПеретаскивание(ПараметрыПеретаскивания.Значение, Строка) Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение;
	Иначе
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция РазрешеноПеретаскивание(МассивЗаказов, ПроверяемаяПозиция)
	
	Если ТипЗнч(ПроверяемаяПозиция) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ПроверяемаяПозиция <> Неопределено 
		И ТипЗнч(МассивЗаказов) = Тип("Массив") И МассивЗаказов.ВГраница() = 0 
		И ТипЗнч(МассивЗаказов[0]) = Тип("ДокументСсылка.ЭтапПроизводства2_2")
		И МожноВставитьДокументВОчередь(МассивЗаказов[0], ПроверяемаяПозиция) Тогда
		
		НоваяПозицияВОчереди = ПроверяемаяПозиция;
		
	Иначе
		
		НоваяПозицияВОчереди = Неопределено;
		
	КонецЕсли;
	
	Возврат (НоваяПозицияВОчереди <> Неопределено);
	
КонецФункции

&НаКлиенте
Процедура cbr_СписокОкончаниеПеретаскиванияПосле(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(НоваяПозицияВОчереди) И ПараметрыПеретаскивания.Значение.ВГраница() = 0 Тогда
		
		ВставитьДокументВОчередь(ПараметрыПеретаскивания.Значение[0], НоваяПозицияВОчереди);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура П3_ОтгрузитьСразуДляВсех(Команда)
	
	ВыбранныеЭтапы = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыбранныеЭтапы.ВГраница() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОтгрузитьИПровести(ВыбранныеЭтапы[0]);
			
КонецПроцедуры

&НаКлиенте
Асинх Процедура П3_ТаблицаРРКВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 28.03.2025 
	ТекущаяСтрока = Элементы.ЭлементТаблицаКонтролейРРК.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПоля = СтрЗаменить(Поле.Имя, "ЭлементТаблицаКонтролейРРК", "");
	ЗначениеДляОткрытия = ТекущаяСтрока[ИмяПоля];
	ОткрытьЗначениеАсинх(ЗначениеДляОткрытия);
	// КОНЕЦ Османов Н.М. 28.03.2025 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноВставитьДокументВОчередь(ТекущийЭтап, НоваяПозицияВОчереди)
	
	Возврат Документы.ЭтапПроизводства2_2.МожноВставитьДокументВОчередь(ТекущийЭтап, НоваяПозицияВОчереди);
	
КонецФункции

&НаСервере
Процедура ВставитьДокументВОчередь(ТекущийЭтап, НоваяПозицияВОчереди)
	
	ЗаблокироватьДанныеДляРедактирования(ТекущийЭтап);
	ЗаблокироватьДанныеДляРедактирования(НоваяПозицияВОчереди);
	
	Документы.ЭтапПроизводства2_2.ВставитьДокументВОчередь(ТекущийЭтап, НоваяПозицияВОчереди);
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьОтгрузитьИПровести(ДокументСсылка)
	
	СтатусДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Статус");
	Если Не СтатусДокумента = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.КВыполнению") 
		И Не СтатусДокумента = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Начат") 
		Тогда
		ТекстСообщения = НСтр("ru='Операция возможна только для документа в статусах ""К выполнению"" или ""Начат""!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
		Возврат;
	КонецЕсли;
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	Для Каждого СтрокаОбеспечения Из ДокументОбъект.ОбеспечениеМатериаламиИРаботами Цикл
		СтрокаОбеспечения.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
	КонецЦикла;
	
	РежимЗаписи = РежимЗаписиДокумента.Запись;
	Если ДокументОбъект.Проведен Тогда
		РежимЗаписи = РежимЗаписиДокумента.Проведение;
	КонецЕсли;
	
	ДокументОбъект.Записать(РежимЗаписи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеТаблицыКонтроля()
	
	РезультатФункции = Новый Структура;
	
	РезультатФункции.Вставить("Наименование", "ТаблицаКонтролейРРК");
	
	МассивКолонок = Новый Массив;
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "ЗаявкаНаКонтроль";
	ОписаниеКолонки.Синоним = "Заявка на контроль";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("ДокументСсылка.лимсЗаявкаНаКонтроль");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "Номер";
	ОписаниеКолонки.Синоним = "Номер";
	ОписаниеКолонки.ТипДанных = ОбщегоНазначения.ОписаниеТипаСтрока(11);
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "ДатаЗаявки";
	ОписаниеКолонки.Синоним = "Дата заявки";
	ОписаниеКолонки.ТипДанных = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "ВидПроверки";
	ОписаниеКолонки.Синоним = "Вид проверки";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("СправочникСсылка.лимсВидыПроверок");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "Серия";
	ОписаниеКолонки.Синоним = "Серия";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "СтатусКонтроля";
	ОписаниеКолонки.Синоним = "Статус контроля";
	ОписаниеКолонки.ТипДанных = ОбщегоНазначения.ОписаниеТипаСтрока(30);
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "АвторЗаявки";
	ОписаниеКолонки.Синоним = "Автор по заявке";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "НомерРРК";
	ОписаниеКолонки.Синоним = "Номер РРК";
	ОписаниеКолонки.ТипДанных = ОбщегоНазначения.ОписаниеТипаСтрока(11);
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "ДокументРРК";
	ОписаниеКолонки.Синоним = "РРК";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("ДокументСсылка.лимсРегистрацияРезультатовКонтроля");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "СтатусСерии";
	ОписаниеКолонки.Синоним = "Статус серии";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("СправочникСсылка.лимсСтатусСерии");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "СтатусКонтроляРРК";
	ОписаниеКолонки.Синоним = "Статус контроля РРК";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("ПеречислениеСсылка.лимсСтатусКонтроля");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "ДатаОкончанияИспытания";
	ОписаниеКолонки.Синоним = "Дата окончания испытания";
	ОписаниеКолонки.ТипДанных = ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя);
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "ВремяИспытания";
	ОписаниеКолонки.Синоним = "Время испытания (минут)";
	ОписаниеКолонки.ТипДанных = ОбщегоНазначения.ОписаниеТипаЧисло(20, 0, ДопустимыйЗнак.Неотрицательный);
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	ОписаниеКолонки = Новый Структура("Имя, Синоним, ТипДанных");	
	ОписаниеКолонки.Имя = "АвторРРК";
	ОписаниеКолонки.Синоним = "Автор РРК";
	ОписаниеКолонки.ТипДанных = Новый ОписаниеТипов("СправочникСсылка.Пользователи");
	МассивКолонок.Добавить(ОписаниеКолонки);
	
	РезультатФункции.Вставить("Колонки", МассивКолонок);
	
	Возврат РезультатФункции;
	
КонецФункции

&НаКлиенте
&После("Подключаемый_ОбновитьКомандыСписка")
Процедура cbr_Подключаемый_ОбновитьКомандыСписка()
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока.Свойство("ГруппировкаСтроки") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуКонтролейПоЭтапу(ТекущаяСтрока.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуКонтролейПоЭтапу(Этап)
	
	Если Не ЗначениеЗаполнено(Этап) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тпсКонтрольЭтапов.ЗаявкаНаКонтроль КАК ЗаявкаНаКонтроль,
		|	тпсКонтрольЭтапов.ЗаявкаНаКонтроль.Номер КАК Номер,
		|	тпсКонтрольЭтапов.ДатаЗаявки КАК ДатаЗаявки,
		|	тпсКонтрольЭтапов.ЗаявкаНаКонтроль.ВидПроверки КАК ВидПроверки,
		|	тпсКонтрольЭтапов.Серия КАК Серия,
		|	тпсКонтрольЭтапов.СтатусКонтроля КАК СтатусКонтроля,
		|	тпсКонтрольЭтапов.ЗаявкаНаКонтроль.Автор КАК АвторЗаявки,
		|	тпсКонтрольЭтапов.РегистрацияРезультатовКонтроля.Номер КАК НомерРРК,
		|	тпсКонтрольЭтапов.РегистрацияРезультатовКонтроля КАК ДокументРРК,
		|	тпсКонтрольЭтапов.СтатусСерии КАК СтатусСерии,
		|	тпсКонтрольЭтапов.СтатусКонтроляРРК КАК СтатусКонтроляРРК,
		|	тпсКонтрольЭтапов.ДатаОкончанияИспытания КАК ДатаОкончанияИспытания,
		|	тпсКонтрольЭтапов.ВремяИспытания КАК ВремяИспытания,
		|	тпсКонтрольЭтапов.РегистрацияРезультатовКонтроля.Автор КАК АвторРРК
		|ИЗ
		|	РегистрСведений.тпсКонтрольЭтапов КАК тпсКонтрольЭтапов
		|ГДЕ
		|	тпсКонтрольЭтапов.Этап = &Этап";
	
	Запрос.УстановитьПараметр("Этап", Этап);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаПоЭтапу = РезультатЗапроса.Выгрузить();
	
	ЗначениеВРеквизитФормы(ТаблицаПоЭтапу, "ТаблицаКонтролейРРК");
	
КонецПроцедуры

&НаСервереБезКонтекста
&После("СписокПриПолученииДанныхНаСервере")
Процедура cbr_СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СписокКлючей = Строки.ПолучитьКлючи();
	
	Если СписокКлючей.ВГраница() = -1 Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьКолонкаОбщееВремяИспытанияЧисло = Строки[СписокКлючей[0]].Данные.Свойство("ОбщееВремяИспытанияЧисло");
	
	ЕстьКолонкаЛаборатория = Строки[СписокКлючей[0]].Данные.Свойство("Лаборатория");
	
	Для Каждого ДанныеСтроки Из Строки Цикл
		
		Если ЕстьКолонкаОбщееВремяИспытанияЧисло Тогда
			
			ОбщееВремяИспытанияЧисло = ДанныеСтроки.Значение.Данные["ОбщееВремяИспытанияЧисло"];
			Если ОбщееВремяИспытанияЧисло > 0 Тогда
				
				КоличествоЧасов = Цел(ОбщееВремяИспытанияЧисло / 60);
				КоличествоМинут = ОбщееВремяИспытанияЧисло - КоличествоЧасов * 60;
				ОбщееВремяИспытания = "";
				Если КоличествоЧасов > 0 Тогда
					ОбщееВремяИспытания = СтрШаблон("%1 ч ", КоличествоЧасов);
				КонецЕсли;
				Если КоличествоМинут > 0 Тогда
					ОбщееВремяИспытания = СтрШаблон("%1%2 м", ОбщееВремяИспытания, КоличествоМинут);
				КонецЕсли;
				ОбщееВремяИспытания = СокрЛП(ОбщееВремяИспытания);
				
				ДанныеСтроки.Значение.Данные["ОбщееВремяИспытания"] = ОбщееВремяИспытания;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЕстьКолонкаЛаборатория Тогда
			
			Если Не ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["ВремяСозданияЗаявки"])
				И Не ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["СтатусКонтроля"]) 
				И Не ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["ДатаОкончанияИспытания"])
				И Не ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["ОбщееВремяИспытания"])
				И Не ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["КоличествоЗаявокНаКонтроль"]) Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстЯчейки = СтрШаблон(НСтр("ru = 'Время начала: %1'"), ДанныеСтроки.Значение.Данные["ВремяСозданияЗаявки"]);
			
			Если ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["ДатаОкончанияИспытания"]) Тогда
				ТекстЯчейки = СтрШаблон(НСтр("ru = '%1%2Время окончания: %3'"),
										ТекстЯчейки,
										Символы.ПС,
										ДанныеСтроки.Значение.Данные["ДатаОкончанияИспытания"]);
			КонецЕсли;
			
			ТекстЯчейки = СтрШаблон(НСтр("ru = '%1%2Статус: %3'"),
										ТекстЯчейки,
										Символы.ПС,
										ДанныеСтроки.Значение.Данные["СтатусКонтроля"]);
										
			Если ЗначениеЗаполнено(ДанныеСтроки.Значение.Данные["ОбщееВремяИспытания"]) Тогда
				ТекстЯчейки = СтрШаблон(НСтр("ru = '%1 | Время: %2'"),
										ТекстЯчейки,
										ДанныеСтроки.Значение.Данные["ОбщееВремяИспытания"]);
			КонецЕсли;
			
			ТекстЯчейки = СтрШаблон(НСтр("ru = '%1 | Всего заявок: %2'"),
										ТекстЯчейки,
										ДанныеСтроки.Значение.Данные["КоличествоЗаявокНаКонтроль"]);
										
			ДанныеСтроки.Значение.Данные["Лаборатория"] = ТекстЯчейки;
			
		КонецЕсли;
	
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти


