#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УстановитьУсловноеОформление();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьФормуПоВремени();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ЗаполнитьПоступлениеОтгрузка();
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ЗаполнитьПоступлениеОтгрузка();
КонецПроцедуры

&НаКлиенте
Процедура ВидДоставкиПриИзменении(Элемент)
	ЗаполнитьПоступлениеОтгрузка();
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	ЗаполнитьПоступлениеОтгрузка();
КонецПроцедуры

&НаКлиенте
Процедура УчитываяОтгруженныеПриИзменении(Элемент)
	ЗаполнитьПоступлениеОтгрузка();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПоступлениеОтгрузка

&НаКлиенте
Процедура ПоступлениеОтгрузкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле = Элементы.ПоступлениеОтгрузкаПодгруженныеФайлы Тогда
		Для Каждого СтрРеализация Из Элементы.ПоступлениеОтгрузка.ТекущиеДанные.РеализацияТоваровУслуг Цикл
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ВладелецФайла", СтрРеализация.Значение);
			ПараметрыФормы.Вставить("СкрыватьВладельца", Истина);
			ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы", ПараметрыФормы);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	ЗаполнитьПоступлениеОтгрузка();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)

	МассивПриходныхОрдеров = СоздатьДокументыНаСервере();

	Для Каждого Документ Из МассивПриходныхОрдеров Цикл
		ОткрытьЗначение(Документ);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПоступлениеОтгрузкаВремяСПо.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПоступлениеОтгрузка.ВремяПо");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = ТекущаяДата();

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуПоВремени()
	ЗаполнитьПоступлениеОтгрузка();
	ПодключитьОбработчикОжидания("ОбновитьФормуПоВремени", 600, Истина);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоступлениеОтгрузка()

	ПоступлениеОтгрузка.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Склад КАК Склад,
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение КАК Распоряжение,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Операция КАК Операция,
	|	ЗаданиеНаПеревозкуМаршрут.ВремяС КАК Дата,
	|	ЗаданиеНаПеревозкуРаспоряжения.ВремяС КАК ВремяС,
	|	ЗаданиеНаПеревозкуРаспоряжения.ВремяПо КАК ВремяПо,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.ЗаданиеВыполняет КАК ВидДоставки,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.ТранспортноеСредство.Тип КАК ВидТранспортногоСредства,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Водитель КАК Водитель,	
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Водитель.ФИО КАК ВодительФИО,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.ТранспортноеСредство.Марка КАК МаркаТС,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.ТранспортноеСредство.Код КАК НомерТС,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Статус КАК Статус,
	|	ЛОЖЬ КАК Выписка,
	|	ЗаданиеНаПеревозкуРаспоряжения.Вес КАК Вес,
	|	ЗаданиеНаПеревозкуРаспоряжения.Объем КАК Объем,
	|	ЗаданиеНаПеревозкуМаршрут.Доставлено КАК Доставлено,
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ_ДанныеЗаданийНаПеревозку
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаданиеНаПеревозкуМаршрут
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|		ПО ЗаданиеНаПеревозкуМаршрут.КлючСвязи = ЗаданиеНаПеревозкуРаспоряжения.КлючСвязи
	|ГДЕ
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка <> ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка)
	|	И ЗаданиеНаПеревозкуРаспоряжения.Ссылка.ПометкаУдаления = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Водитель,
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеЗаданийНаПеревозку.Склад КАК Склад,
	|	ВТ_ДанныеЗаданийНаПеревозку.Распоряжение КАК Распоряжение,
	|	ВТ_ДанныеЗаданийНаПеревозку.Операция КАК Операция,
	|	ВТ_ДанныеЗаданийНаПеревозку.Дата КАК Дата,
	|	ВТ_ДанныеЗаданийНаПеревозку.ВремяС КАК ВремяС,
	|	ВТ_ДанныеЗаданийНаПеревозку.ВремяПо КАК ВремяПо,
	|	ВТ_ДанныеЗаданийНаПеревозку.ВидДоставки КАК ВидДоставки,
	|	ВТ_ДанныеЗаданийНаПеревозку.ВидТранспортногоСредства КАК ВидТранспортногоСредства,
	|	ВТ_ДанныеЗаданийНаПеревозку.ВодительФИО КАК ВодительФИО,
	|	ФизическиеЛицаКонтактнаяИнформация.НомерТелефона КАК НомерТелефона,
	|	ДокументыФизическихЛицСрезПоследних.Серия КАК Серия,
	|	ДокументыФизическихЛицСрезПоследних.Номер КАК Номер,
	|	ВТ_ДанныеЗаданийНаПеревозку.МаркаТС КАК МаркаТС,
	|	ВТ_ДанныеЗаданийНаПеревозку.НомерТС КАК НомерТС,
	|	ВТ_ДанныеЗаданийНаПеревозку.Статус КАК Статус,
	|	ВТ_ДанныеЗаданийНаПеревозку.Выписка КАК Выписка,
	|	ВТ_ДанныеЗаданийНаПеревозку.Вес КАК Вес,
	|	ВТ_ДанныеЗаданийНаПеревозку.Объем КАК Объем,
	|	РеализацияТоваровУслуг.Номер КАК НомерРеализации,
	|	СчетФактураВыданный.Номер КАК НомерСчетФактура,
	|	РеализацияТоваровУслуг.Ссылка КАК РеализацияТоваровУслуг,
	|	ВТ_ДанныеЗаданийНаПеревозку.Доставлено КАК Доставлено,
	|	ВТ_ДанныеЗаданийНаПеревозку.Ссылка КАК ЗаданиеНаПеревозку
	|ИЗ
	|	ВТ_ДанныеЗаданийНаПеревозку КАК ВТ_ДанныеЗаданийНаПеревозку
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(&МоментВремени, Физлицо В
	|			(ВЫБРАТЬ
	|				Вт_ДанныеЗаданийНаПеревозку.Водитель
	|			ИЗ
	|				Вт_ДанныеЗаданийНаПеревозку КАК Вт_ДанныеЗаданийНаПеревозку)) КАК ДокументыФизическихЛицСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.КонтактнаяИнформация КАК ФизическиеЛицаКонтактнаяИнформация
	|			ПО ДокументыФизическихЛицСрезПоследних.Физлицо = ФизическиеЛицаКонтактнаяИнформация.Ссылка
	|		ПО ВТ_ДанныеЗаданийНаПеревозку.Водитель = ДокументыФизическихЛицСрезПоследних.Физлицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|			ПО РеализацияТоваровУслуг.Ссылка = СчетФактураВыданный.ДокументОснование
	|		ПО ВТ_ДанныеЗаданийНаПеревозку.Распоряжение = РеализацияТоваровУслуг.ЗаказКлиента
	|		&ТекстУсловия
	|ИТОГИ ПО
	|	Распоряжение";

	ТекстУсловия = "";
	Если ЗначениеЗаполнено(Склад) Или ЗначениеЗаполнено(Период) Или ЗначениеЗаполнено(ВидДоставки)
		Или ЗначениеЗаполнено(Статус) Или Не УчитываяОтгруженные Тогда

		ТекстУсловия = "ГДЕ";

		Если ЗначениеЗаполнено(Склад) Тогда

			ТекстУсловия = ТекстУсловия + " ВТ_ДанныеЗаданийНаПеревозку.Склад В (&Склады)";
			Запрос.УстановитьПараметр("Склады", Склад);

		ИначеЕсли ЗначениеЗаполнено(Период) Тогда

			ТекстУсловия = ?(СтрДлина(ТекстУсловия) > 3, ТекстУсловия + " И", ТекстУсловия);
			ТекстУсловия = ТекстУсловия + " ВТ_ДанныеЗаданийНаПеревозку.Дата МЕЖДУ &ПериодН И &ПериодК";
			Запрос.УстановитьПараметр("ПериодН", НачалоМесяца(Период));
			Запрос.УстановитьПараметр("ПериодК", КонецМесяца(Период));

		ИначеЕсли ЗначениеЗаполнено(ВидДоставки) Тогда

			ТекстУсловия = ?(СтрДлина(ТекстУсловия) > 3, ТекстУсловия + " И", ТекстУсловия);
			ТекстУсловия = ТекстУсловия + " ВТ_ДанныеЗаданийНаПеревозку.ВидДоставки = &ВидДоставки";
			Запрос.УстановитьПараметр("ВидДоставки", ВидДоставки);

		ИначеЕсли ЗначениеЗаполнено(Статус) Тогда

			ТекстУсловия = ?(СтрДлина(ТекстУсловия) > 3, ТекстУсловия + " И", ТекстУсловия);
			ТекстУсловия = ТекстУсловия + " ВТ_ДанныеЗаданийНаПеревозку.Статус = &Статус";
			Запрос.УстановитьПараметр("Статус", Статус);

		ИначеЕсли Не УчитываяОтгруженные Тогда

			ТекстУсловия = ?(СтрДлина(ТекстУсловия) > 3, ТекстУсловия + " И", ТекстУсловия);
			ТекстУсловия = ТекстУсловия + " ВТ_ДанныеЗаданийНаПеревозку.Доставлено = &УчитываяОтгруженные";
			Запрос.УстановитьПараметр("УчитываяОтгруженные", УчитываяОтгруженные);

		КонецЕсли;

	КонецЕсли;

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстУсловия", ТекстУсловия);

	Запрос.УстановитьПараметр("МоментВремени", ТекущаяДата());

	ВыборкаГруппировка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаГруппировка.Следующий() Цикл

		НоваяСтрока = ПоступлениеОтгрузка.Добавить();

		Выборка = ВыборкаГруппировка.Выбрать();

		Пока Выборка.Следующий() Цикл

			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, , "РеализацияТоваровУслуг");

			Если ЗначениеЗаполнено(Выборка.ВремяС) Тогда
				НоваяСтрока.ВремяСПо = Формат(Выборка.ВремяС, "ДФ='HH:mm'") + " - " + Формат(Выборка.ВремяПо,
					"ДФ='HH:mm'");
			КонецЕсли;

			Если ЗначениеЗаполнено(Выборка.РеализацияТоваровУслуг) Тогда
				НоваяСтрока.РеализацияТоваровУслуг.Добавить(Выборка.РеализацияТоваровУслуг);
			КонецЕсли;

			Если ЗначениеЗаполнено(Выборка.НомерРеализации) И ЗначениеЗаполнено(Выборка.НомерСчетФактура) Тогда
				НоваяСтрока.НомераСформированныхПервичныхДокументов = НоваяСтрока.НомераСформированныхПервичныхДокументов
					+ Строка(Выборка.НомерРеализации) + ", " + Строка(Выборка.НомерСчетФактура) + Символы.ПС;
				НоваяСтрока.ПодгруженныеФайлы = "Файлы";
			ИначеЕсли ЗначениеЗаполнено(Выборка.НомерРеализации) Тогда
				НоваяСтрока.НомераСформированныхПервичныхДокументов = НоваяСтрока.НомераСформированныхПервичныхДокументов
					+ Строка(Выборка.НомерРеализации) + Символы.ПС;
				НоваяСтрока.ПодгруженныеФайлы = "Файлы";
			ИначеЕсли ЗначениеЗаполнено(Выборка.НомерСчетФактура) Тогда
				НоваяСтрока.НомераСформированныхПервичныхДокументов = НоваяСтрока.НомераСформированныхПервичныхДокументов
					+ Строка(Выборка.НомерСчетФактура) + Символы.ПС;
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

	Распоряжения = ПоступлениеОтгрузка.Выгрузить( , "Распоряжение");
	СтруктураКладовщика = НайтиКладовщикаВОрдереПоРаспоряжению(Распоряжения);
	Если СтруктураКладовщика.Количество() <> 0 Тогда
		Для Каждого СтрокаКладовщика Из СтруктураКладовщика Цикл
			Для Каждого СтрокаТЗ Из ПоступлениеОтгрузка Цикл
				Если СтрокаКладовщика.Распоряжение = СтрокаТЗ.Распоряжение Тогда
					СтрокаТЗ.ФИОКладовщика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаКладовщика.Ответственный,
						"Наименование");
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СоздатьДокументыНаСервере()

	ВыбранныеРаспоряжения = Новый ТаблицаЗначений;
	ВыбранныеРаспоряжения.Колонки.Добавить("ЗаданиеНаПеревозку",
		Новый ОписаниеТипов("ДокументСсылка.ЗаданиеНаПеревозку"));
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	ВыбранныеРаспоряжения.Колонки.Добавить("Распоряжение", Новый ОписаниеТипов(МассивТипов));

	Для Каждого Строка Из ПоступлениеОтгрузка Цикл

		Если Строка.Выписка Тогда
			НоваяСтр = ВыбранныеРаспоряжения.Добавить();
			НоваяСтр.Распоряжение = Строка.Распоряжение;
			НоваяСтр.ЗаданиеНаПеревозку = Строка.ЗаданиеНаПеревозку;
		КонецЕсли;

	КонецЦикла;

	МассивРаспоряжений = ВыбранныеРаспоряжения.ВыгрузитьКолонку("Распоряжение");
	ДанныеРаспоряжения = НайтиТоварыКДоставке(МассивРаспоряжений);
	НайденныеРаспоряжения = НайтиРаспоряженияВРегистрах(ДанныеРаспоряжения);

	МассивПриходныхОрдеров = Новый Массив;

	Если НайденныеРаспоряжения <> Неопределено Тогда
		Для Каждого Стр Из НайденныеРаспоряжения Цикл

			Распоряжение = Стр.Распоряжение;

			Если Стр.Операция = Перечисления.ВидыДоставки.НаСклад Тогда
				
				//создание ПриходныйОрдерНаТовары
				НовыйОрдер = Документы.ПриходныйОрдерНаТовары.СоздатьДокумент();

				СтруктураРаспоряжения = КонвертироватьОбъектВСтруктуру(Распоряжение);
				НовыйОрдер.Заполнить(СтруктураРаспоряжения);

				НовыйОрдер.Дата = ТекущаяДата();
				НовыйОрдер.Комментарий = "Создано автоматически: АРМ Поступление/Отгрузка";

				Для Каждого ВыбраннаяСтр Из ВыбранныеРаспоряжения Цикл
					Если Распоряжение = ВыбраннаяСтр.Распоряжение Тогда
						НовыйОрдер.cbr_ЗаданиеНаПеревозку = ВыбраннаяСтр.ЗаданиеНаПеревозку;
					КонецЕсли;
				КонецЦикла;

				НовыйОрдер.Товары.Очистить();

				Для Каждого СтрокаТЧ Из Распоряжение.Товары Цикл
					НоваяСтрокаТЧ = НовыйОрдер.Товары.Добавить();
					НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
					НоваяСтрокаТЧ.Характеристика = СтрокаТЧ.Характеристика;
					НоваяСтрокаТЧ.Назначение = СтрокаТЧ.Назначение;
					НоваяСтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
					НоваяСтрокаТЧ.Количество = СтрокаТЧ.Количество;
				КонецЦикла;

				НовыйОрдер.Записать(РежимЗаписиДокумента.Запись);
				МассивПриходныхОрдеров.Добавить(НовыйОрдер.Ссылка);

			ИначеЕсли Стр.Операция = Перечисления.ВидыДоставки.СоСклада Тогда
				
				//создание РасходныйОрдерНаТовары
				НовыйОрдер = Документы.РасходныйОрдерНаТовары.СоздатьДокумент();

				НовыйОрдер.Дата = ТекущаяДата();
				НовыйОрдер.ДатаОтгрузки = ТекущаяДата();
				НовыйОрдер.Ответственный = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Распоряжение, "Автор");
				НовыйОрдер.Склад = Распоряжение.Склад;
				НовыйОрдер.Получатель = Распоряжение.Партнер;
				НовыйОрдер.Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний");
				НовыйОрдер.Помещение = Справочники.СкладскиеПомещения.НайтиПоНаименованию("Теплое");
				НовыйОрдер.СкладскаяОперация = Перечисления.СкладскиеОперации.ОтгрузкаКлиенту;
				НовыйОрдер.Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору;

				Для Каждого СтрокаТЧ Из Распоряжение.Товары Цикл
					НоваяСтрокаТЧ = НовыйОрдер.ТоварыПоРаспоряжениям.Добавить();
					НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
					НоваяСтрокаТЧ.Характеристика = СтрокаТЧ.Характеристика;
					НоваяСтрокаТЧ.Серия = СтрокаТЧ.Серия;
					НоваяСтрокаТЧ.Количество = СтрокаТЧ.Количество;
					НоваяСтрокаТЧ.Распоряжение = Распоряжение;

					НоваяСтрокаТЧ = НовыйОрдер.ОтгружаемыеТовары.Добавить();
					НоваяСтрокаТЧ.Номенклатура = СтрокаТЧ.Номенклатура;
					НоваяСтрокаТЧ.Характеристика = СтрокаТЧ.Характеристика;
					НоваяСтрокаТЧ.Серия = СтрокаТЧ.Серия;
					НоваяСтрокаТЧ.Количество = СтрокаТЧ.Количество;
					НоваяСтрокаТЧ.КоличествоУпаковок = СтрокаТЧ.Количество;
					НоваяСтрокаТЧ.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
				КонецЦикла;

				Попытка
					НовыйОрдер.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					НовыйОрдер.Записать(РежимЗаписиДокумента.Запись);
				КонецПопытки;
				
				//создание ОтборРазмещениеТоваров	
				НовыйОтборРазмещениеТоваров = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
				НовыйОтборРазмещениеТоваров.Заполнить(НовыйОрдер.Ссылка);
				НовыйОтборРазмещениеТоваров.Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.Подготовлено;

				Попытка
					НовыйОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					НовыйОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Запись);
				КонецПопытки;

				Если НовыйОтборРазмещениеТоваров.Проведен Тогда

					НовыйОтборРазмещениеТоваров.Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.ВыполненоБезОшибок;
					НовыйОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Проведение);

					НовыйОрдер.Статус = Перечисления.СтатусыРасходныхОрдеров.Отгружен;
					НовыйОрдер.Записать(РежимЗаписиДокумента.Проведение);

				КонецЕсли;
				
				//создание РеализацияТоваровУслуг
				НовыйРеализацияТоваровУслуг = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
				НовыйРеализацияТоваровУслуг.Заполнить(Стр.Распоряжение);

				НовыйРеализацияТоваровУслуг.Дата = ТекущаяДата();

				Для Каждого СтрокаТЧ Из Распоряжение.Товары Цикл
					НоваяСтрокаТЧ = НовыйРеализацияТоваровУслуг.Товары.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТЧ, СтрокаТЧ);
				КонецЦикла;

				Попытка
					НовыйРеализацияТоваровУслуг.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					НовыйРеализацияТоваровУслуг.Записать(РежимЗаписиДокумента.Запись);
				КонецПопытки;
				
				
				//создание СчетФактураВыданный
				НовыйСчетФактураВыданный = Документы.СчетФактураВыданный.СоздатьДокумент();
				НовыйСчетФактураВыданный.Заполнить(НовыйРеализацияТоваровУслуг.Ссылка);

				Попытка
					НовыйСчетФактураВыданный.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					НовыйСчетФактураВыданный.Записать(РежимЗаписиДокумента.Запись);
				КонецПопытки;

				ТекстСообщения = "Документы успешно созданы";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Возврат МассивПриходныхОрдеров;

КонецФункции

Функция НайтиТоварыКДоставке(МассивРаспоряжений)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКДоставке.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ТоварыКДоставке.Распоряжение КАК Распоряжение,
	|	ВЫБОР
	|		КОГДА ТоварыКДоставке.ВсеТовары
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ТоварыКДоставке.Распоряжение) = ТИП(Документ.ЗаказПоставщику)
	|					ТОГДА ЗаказПоставщикуТовары.Номенклатура
	|				ИНАЧЕ ЗаказКлиентаТовары.Номенклатура
	|			КОНЕЦ
	|		ИНАЧЕ ТоварыКДоставке.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТоварыКДоставке.ВсеТовары
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ТоварыКДоставке.Распоряжение) = ТИП(Документ.ЗаказПоставщику)
	|					ТОГДА ЗаказПоставщикуТовары.Характеристика
	|				ИНАЧЕ ЗаказКлиентаТовары.Характеристика
	|			КОНЕЦ
	|		ИНАЧЕ ТоварыКДоставке.Характеристика
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТоварыКДоставке.ВсеТовары
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ТоварыКДоставке.Распоряжение) = ТИП(Документ.ЗаказПоставщику)
	|					ТОГДА ЗаказПоставщикуТовары.Назначение
	|				ИНАЧЕ NULL
	|			КОНЕЦ
	|		ИНАЧЕ ТоварыКДоставке.Назначение
	|	КОНЕЦ КАК Назначение,
	|	ВЫБОР
	|		КОГДА ТоварыКДоставке.ВсеТовары
	|			ТОГДА ВЫБОР
	|				КОГДА ТИПЗНАЧЕНИЯ(ТоварыКДоставке.Распоряжение) = ТИП(Документ.ЗаказПоставщику)
	|					ТОГДА NULL
	|				ИНАЧЕ ЗаказКлиентаТовары.Серия
	|			КОНЕЦ
	|		ИНАЧЕ ТоварыКДоставке.Серия
	|	КОНЕЦ КАК Серия
	|ИЗ
	|	РегистрСведений.ТоварыКДоставке КАК ТоварыКДоставке
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
	|		ПО ТоварыКДоставке.Распоряжение = ЗаказПоставщикуТовары.Ссылка
	|		И ТоварыКДоставке.ВсеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ПО ТоварыКДоставке.Распоряжение = ЗаказКлиентаТовары.Ссылка
	|		И ТоварыКДоставке.ВсеТовары
	|ГДЕ
	|	ТоварыКДоставке.Распоряжение В (&Распоряжение)";

	Запрос.УстановитьПараметр("Распоряжение", МассивРаспоряжений);

	Результат = Запрос.Выполнить().Выгрузить();

	Возврат Результат;

КонецФункции

Функция НайтиРаспоряженияВРегистрах(ДанныеРаспоряжения)

	Распоряжения = ДанныеРаспоряжения.ВыгрузитьКолонку("Распоряжение");
	Номенклатура = ДанныеРаспоряжения.ВыгрузитьКолонку("Номенклатура");
	Назначение = ДанныеРаспоряжения.ВыгрузитьКолонку("Назначение");
	Характеристика = ДанныеРаспоряжения.ВыгрузитьКолонку("Характеристика");

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКПоступлениюОстатки.ДокументПоступления КАК Распоряжение,
	|	ТоварыКПоступлениюОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлениюОстатки.Характеристика КАК Характеристика,
	|	ТоварыКПоступлениюОстатки.Назначение КАК Назначение,
	|	ТоварыКПоступлениюОстатки.КОформлениюПоступленийПоРаспоряжениюОстаток КАК Количество,
	|	ТоварыКПоступлениюОстатки.Склад КАК Склад,
	|	ТоварыКПоступлениюОстатки.Отправитель КАК Контрагент,
	|	ТоварыКПоступлениюОстатки.Серия КАК Серия,
	|	ТоварыКПоступлениюОстатки.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТоварыКПоступлениюОстатки.КОформлениюНакладныхПоРаспоряжениюОстаток КАК КоличествоКОформлению
	|ПОМЕСТИТЬ ВТИтог
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки КАК ТоварыКПоступлениюОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки,
	|	ТоварыКОтгрузкеОстатки.Номенклатура,
	|	ТоварыКОтгрузкеОстатки.Характеристика,
	|	ТоварыКОтгрузкеОстатки.Назначение,
	|	ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток,
	|	ТоварыКОтгрузкеОстатки.Склад,
	|	ТоварыКОтгрузкеОстатки.Получатель,
	|	ТоварыКОтгрузкеОстатки.Серия,
	|	NULL,
	|	ТоварыКОтгрузкеОстатки.КОформлениюОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки КАК ТоварыКОтгрузкеОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТИтог.Распоряжение КАК Распоряжение,
	|	ВТИтог.Номенклатура КАК Номенклатура,
	|	ВТИтог.Характеристика КАК Характеристика,
	|	ВТИтог.Назначение КАК Назначение,
	|	ВТИтог.Количество КАК Количество,
	|	ВТИтог.Склад КАК Склад,
	|	ВТИтог.Контрагент КАК Контрагент,
	|	ВТИтог.Серия КАК Серия,
	|	ВТИтог.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ВТИтог.КоличествоКОформлению КАК КоличествоКОформлению
	|ИЗ
	|	ВТИтог КАК ВТИтог
	|ГДЕ
	|	ВТИтог.Распоряжение В (&Распоряжения)
	|	И ВТИтог.Номенклатура В (&Номенклатура)
	|	И ВТИтог.Характеристика В (&Характеристика)
	|	И ВТИтог.Назначение В (&Назначение)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Распоряжение";

	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);

	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Свернуть("Распоряжение");

	Результат.Колонки.Добавить("Операция", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоставки"));

	Для Каждого СтрокаРезультат Из Результат Цикл
		Для Каждого СтрокаРаспоряжения Из ДанныеРаспоряжения Цикл
			Если СтрокаРезультат.Распоряжение = СтрокаРаспоряжения.Распоряжение Тогда
				СтрокаРезультат.Операция = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					СтрокаРаспоряжения.ЗаданиеНаПеревозку, "Операция");
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция КонвертироватьОбъектВСтруктуру(Распоряжение)

	СтруктураОбъекта = Новый Структура;
	СтруктураОбъекта.Вставить("Склад", Распоряжение.Склад);
	СтруктураОбъекта.Вставить("Помещение", Справочники.СкладскиеПомещения.НайтиПоНаименованию("Теплое"));
	СтруктураОбъекта.Вставить("Распоряжение", Распоряжение);
	СтруктураОбъекта.Вставить("ДатаПоступления", Распоряжение.ДатаПоступления);
	СтруктураОбъекта.Вставить("Отправитель", Распоряжение.Партнер);
	СтруктураОбъекта.Вставить("ХозяйственнаяОперация", Распоряжение.ХозяйственнаяОперация);

	Возврат СтруктураОбъекта;

КонецФункции

Функция НайтиКладовщикаВОрдереПоРаспоряжению(Распоряжения)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Ответственный КАК Ответственный,
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение = ЗаказКлиента.Ссылка
	|ГДЕ
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение В (&Распоряжение)
	|СГРУППИРОВАТЬ ПО
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Ответственный,
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение";

	Запрос.УстановитьПараметр("Распоряжение", Распоряжения);

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	Возврат РезультатЗапроса;

КонецФункции

#КонецОбласти