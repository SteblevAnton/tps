
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Изделие") 
	ИЛИ Не Параметры.Свойство("ХарактеристикаИзделия") 
	ИЛИ Не Параметры.Свойство("СсылкаНаТекущийЭтап") 
	ИЛИ Не Параметры.Свойство("МассивЭтапов") Тогда
		Возврат;
	КонецЕсли;
	
	Изделие = Параметры.Изделие;
	ХарактеристикаИзделия = Параметры.ХарактеристикаИзделия;
	СсылкаНаТекущийЭтап = Параметры.СсылкаНаТекущийЭтап;
	МассивЭтапов = Параметры.МассивЭтапов;
	
	ЗаполнитьЭтапыПроизводства();
	
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура Перенести(Команда)
	
	ЕстьВыбрано = ?(ЭтапыПроизводства.НайтиСтроки(Новый Структура("Выбран",Истина)).Количество() > 0, Истина, Ложь);
	
	Если ЕстьВыбрано Тогда
		
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("АдресТЗДанных", ВыгрузитьНаСервере());
		
		ВладелецФормы.cbr_ВозвратРезультатаПодбораЭтапов(ДанныеВозврата);
		Закрыть(); 
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выберите хотя бы один элемент'"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВыгрузитьНаСервере()
	
	ЭтапыПроизводстваСОтбором = ЭтапыПроизводства.НайтиСтроки(Новый Структура("Выбран",Истина));
	
	ВыбранныеЭтапы = Новый Массив();
	Для Каждого Строка Из ЭтапыПроизводстваСОтбором Цикл
		ВыбранныеЭтапы.Добавить(Строка.Этап);
	КонецЦикла;

	Возврат ПоместитьВоВременноеХранилище(ВыбранныеЭтапы);	
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЭтапыПроизводства()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ДвижениеПродукцииИМатериалов.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ ВТПередачаМатериаловВПроизводство
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК ДвижениеПродукцииИМатериалов
	|ГДЕ
	|	ДвижениеПродукцииИМатериалов.Проведен
	|	И ДвижениеПродукцииИМатериалов.Распоряжение ССЫЛКА Документ.ЭтапПроизводства2_2
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеПродукцииИМатериалов.Распоряжение
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
	|ГДЕ
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Проведен
	|	И РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение ССЫЛКА Документ.ЭтапПроизводства2_2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Ссылка КАК Этап,
	|	ЭтапПроизводства2_2.КоличествоУпаковокПлан КАК Количество
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПередачаМатериаловВПроизводство КАК ВТПередачаМатериаловВПроизводство
	|		ПО ВТПередачаМатериаловВПроизводство.Распоряжение = ЭтапПроизводства2_2.Ссылка
	|ГДЕ
	|	ЭтапПроизводства2_2.ПартияПроизводства.ОсновноеИзделиеНоменклатура = &ОсновноеИзделиеНоменклатура
	|	И ЭтапПроизводства2_2.ПартияПроизводства.ОсновноеИзделиеХарактеристика = &ОсновноеИзделиеХарактеристика
	|	И ВТПередачаМатериаловВПроизводство.Распоряжение ЕСТЬ NULL
	|	И ЭтапПроизводства2_2.Ссылка <> &Ссылка
	|	И НЕ ЭтапПроизводства2_2.Ссылка В (&МассивЭтапов)
	|	И ЭтапПроизводства2_2.Проведен";
	
	Запрос.УстановитьПараметр("ОсновноеИзделиеНоменклатура", Изделие);
	Запрос.УстановитьПараметр("ОсновноеИзделиеХарактеристика", ХарактеристикаИзделия);
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаТекущийЭтап);
	Запрос.УстановитьПараметр("МассивЭтапов", МассивЭтапов);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ЭтапыПроизводства.Загрузить(Результат);

КонецПроцедуры

#КонецОбласти
