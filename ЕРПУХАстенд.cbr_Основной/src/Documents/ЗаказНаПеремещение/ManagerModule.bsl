#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

#Область Обеспечение

&ИзменениеИКонтроль("ПослеЗаполненияОбеспечения")
Процедура cbr_ПослеЗаполненияОбеспечения(Объект, Изменения, Режим, ПараметрыЗаполнения) Экспорт

	Если ОбеспечениеВДокументахКлиентСервер.НужноПроверитьЗаполнитьДатуОтгрузкиДляСтрокОтгрузить(Режим) Тогда

		МассивСтрок = Изменения.ВыгрузитьКолонку("Строка");

		ВариантОтгрузить = Перечисления.ВариантыОбеспечения.Отгрузить;

		Для Каждого Строка Из МассивСтрок Цикл

			ДатаПоступленияРасчетная = Макс(
				НачалоДня(ТекущаяДатаСеанса()) + 86400 * Объект.ДлительностьПеремещения,
				Объект.ЖелаемаяДатаПоступления);

			НужноЗаполнитьДатуОтгрузки = Строка.ВариантОбеспечения = ВариантОтгрузить;

			Если НужноЗаполнитьДатуОтгрузки Тогда
				Строка.НачалоОтгрузки = ДатаПоступленияРасчетная - 86400 * Объект.ДлительностьПеремещения;
				Строка.ОкончаниеПоступления = ДатаПоступленияРасчетная;
			КонецЕсли;
			#Вставка
			//++АшаеваАН 13.02.26 СКЛ.3
			Если Строка.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.НеТребуется
				 ИЛИ Строка.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.КОбеспечению Тогда
				 Строка.cbr_ЗаказПоставщику = Неопределено;
				 Строка.cbr_ПТУ = Неопределено;
			КонецЕсли;
			//--АшаеваАН 13.02.26 СКЛ.3
			#КонецВставки
		КонецЦикла;

	КонецЕсли;

	ОбеспечениеВДокументахСервер.ПересчитатьКоличествоЕдиниц(Изменения, Объект.Товары);
	ПараметрыУказанияСерий = ПараметрыУказанияСерий(Объект);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);

КонецПроцедуры


#КонецОбласти
#КонецОбласти

#Область Прочее
&ИзменениеИКонтроль("ТекстЗапросаТоварыДокумента")
Функция cbr_ТекстЗапросаТоварыДокумента() Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка                       КАК ЗаказНаПеремещение,
	|	Таблица.Номенклатура                 КАК Номенклатура,
	|	Таблица.Характеристика               КАК Характеристика,
	|	Таблица.НоменклатураНабора           КАК НоменклатураНабора,
	|	Таблица.ХарактеристикаНабора         КАК ХарактеристикаНабора,
	|	Таблица.Назначение,
	|	ВЫБОР
	|		КОГДА Таблица.Обособленно
	|				И Таблица.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|				И НЕ (Таблица.Ссылка.ОбосабливатьПоНазначениюЗаказа
	|					И Таблица.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Собственное))
	|			ТОГДА Таблица.Назначение
	|		КОГДА Таблица.Обособленно
	|				И (Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|					ИЛИ (Таблица.Ссылка.ОбосабливатьПоНазначениюЗаказа)
	|						И Таблица.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Собственное))
	|			ТОГДА Таблица.Ссылка.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК НазначениеОтправителя,
	|	Таблица.КодСтроки                    КАК КодСтроки,
	|	Таблица.Серия                        КАК Серия,
	|	Таблица.Количество                   КАК Количество,
	|	Таблица.Упаковка                     КАК Упаковка,
	|	Таблица.НачалоОтгрузки               КАК ДатаОтгрузки,
	|	Таблица.Ссылка.СкладОтправитель      КАК Склад,
	|	Таблица.Ссылка.Сделка                КАК Сделка,
	|	ЕСТЬNULL(ДанныеУчета.КОформлению, 0) КАК КОформлению,
	#Вставка
	//++АшаеваАН 13.02.26 СКЛ.3
	|	Таблица.cbr_ЗаказПоставщику КАК cbr_ЗаказПоставщику,
	|	Таблица.cbr_ПТУ КАК cbr_ПТУ,
	//--АшаеваАН 13.02.26 СКЛ.3
	#КонецВставки
	|	ЕСТЬNULL(ДанныеУчета.Заказано, 0)    КАК Заказано
	|ИЗ
	|	Документ.ЗаказНаПеремещение.Товары КАК Таблица
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеУчета КАК ДанныеУчета
	|	ПО Таблица.Ссылка = ДанныеУчета.ЗаказНаПеремещение
	|	И Таблица.КодСтроки = ДанныеУчета.КодСтроки
	|ГДЕ
	|	НЕ ДанныеУчета.ЗаказНаПеремещение ЕСТЬ NULL
	|		И Таблица.Ссылка В(&Распоряжения)";

	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти
#КонецОбласти

#КонецЕсли

	