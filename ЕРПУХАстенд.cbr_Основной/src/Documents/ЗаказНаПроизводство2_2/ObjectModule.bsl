#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура П3_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// НАЧАЛО Османов Н.М. 21.03.2025 
	ОрганизацияТПС = Константы.ОрганизацияТПС.ЗначениеЭтойКонстанты();

	Если Не Организация = ОрганизацияТПС Тогда
		ТекстСообщения = НСтр("ru='Организация для этого документа должна быть: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОрганизацияТПС);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 21.03.2025 
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	ТаблицаПродукции = Продукция.Выгрузить(, "Номенклатура, Характеристика, Спецификация");
	ТаблицаПродукции.Свернуть("Номенклатура, Характеристика, Спецификация");
	Если ТаблицаПродукции.Количество() > 1 Тогда
		
		ТекстСообщения = НСтр("ru='В таблице ""Продукция"" присутствуют разные позиции Номенклатуры, Характеристики и Спецификации!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ); 
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьПоЗаказуКлиента")
Процедура П3_ЗаполнитьПоЗаказуКлиента(ДанныеЗаполнения)

	ЗаказКлиента = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказКлиента.Приоритет          КАК Приоритет,
	|	ЗаказКлиента.Организация        КАК Организация,
	|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР 
	|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
	|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка = &ЗаказКлиента");

	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);

	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	// Заполнение шапки
	Организация             = Реквизиты.Организация;
	ДокументОснование       = ЗаказКлиента;
	ЗаказПодДеятельность    = Реквизиты.НалогообложениеНДС;
	Приоритет               = Реквизиты.Приоритет;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;

	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
	#Вставка // НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	Если Не Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству") Тогда
		СпецификацииЗаполнены = Истина;
		Для Каждого СтрокаПродукции Из Продукция Цикл
			Если Не ЗначениеЗаполнено(СтрокаПродукции.Спецификация) Тогда
				СпецификацииЗаполнены = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СпецификацииЗаполнены Тогда
			Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству"); 
		Иначе
			Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2.АктуализацияСпецификаций"); 
		КонецЕсли;
	КонецЕсли;
	#КонецВставки // КОНЕЦ Алейников М.Н. 24.03.2025 Б121
	
	#Вставка // НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	СвойствоУпаковка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "УпаковкаРеквизитХарактеристики");
	МассивСвойств = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвойствоУпаковка);
	
	Для Каждого СтрокаПродукции Из Продукция Цикл
		ТаблицаСвойствХарактеристик = УправлениеСвойствами.ЗначенияСвойств(СтрокаПродукции.Характеристика, Истина, , МассивСвойств);
		
		Если ТаблицаСвойствХарактеристик.Количество() Тогда
			ЗначениеСвойстваХарактеристика = ТаблицаСвойствХарактеристик[0].Значение;
			Если Не СтрокаПродукции.Упаковка = ЗначениеСвойстваХарактеристика Тогда
				КоэффициентУпаковки= Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки
				(ЗначениеСвойстваХарактеристика, СтрокаПродукции.Номенклатура);
				СтрокаПродукции.КоличествоУпаковок = СтрокаПродукции.КоличествоУпаковок / КоэффициентУпаковки;
				СтрокаПродукции.Упаковка = ЗначениеСвойстваХарактеристика;
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЦикла;
	#КонецВставки // КОНЕЦ Алейников М.Н. 24.03.2025 Б121

КонецПроцедуры


&ИзменениеИКонтроль("ЗаполнитьПоТаблицеТовары")
Процедура cbr_ЗаполнитьПоТаблицеТовары(ТаблицаТоваров)

	Если ТаблицаТоваров.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ЕстьСпецификация = ТаблицаТоваров.Колонки.Найти("Спецификация") <> Неопределено;

	ЕстьНачатьНеРанее = ТаблицаТоваров.Колонки.Найти("НачатьНеРанее") <> Неопределено;
	Если ЕстьНачатьНеРанее Тогда
		НачатьНеРанее = ТаблицаТоваров[0].НачатьНеРанее;
	КонецЕсли;

	ДоступноНесколькоДатПотребности = ДинамическаяСтруктура;
	НесколькоДатПотребности = Ложь;

	ЕстьДатаПотребности = ТаблицаТоваров.Колонки.Найти("ДатаПотребности") <> Неопределено;
	Если ЕстьДатаПотребности Тогда
		ДатаПотребности = ТаблицаТоваров[0].ДатаПотребности;
	КонецЕсли;

	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	СтруктураДействий = Новый Структура;

	Если ТаблицаТоваров.Колонки.Найти("КоличествоУпаковок") = Неопределено Тогда
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	конецЕсли;
	
	#Вставка
	//++ Мохов ПРЗ.17
	ВывестиСообщение = Ложь;
	Если Тип("ДокументСсылка.ЗаказКлиента") = ТипЗнч(ДокументОснование) Тогда
		ВыгрузкаЗапросТоварыЗаказКлиента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование,"Товары").Выгрузить();	
	КонецЕсли;
	//-- Мохов ПРЗ.17
	#КонецВставки
		
	ОбрабатываемыеСтроки = Новый Массив;
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл

		#Вставка
		//++ Мохов ПРЗ.17
		Если Тип("ДокументСсылка.ЗаказКлиента") = ТипЗнч(ДокументОснование) Тогда 
			СтрокаПоискаИзОснования = ВыгрузкаЗапросТоварыЗаказКлиента.Найти(СтрокаТовара.Номенклатура,"Номенклатура");
			Если СтрокаПоискаИзОснования.cbr_ЭтоПФ И НЕ СтрокаПоискаИзОснования.cbr_Актуальность Тогда
				ВывестиСообщение = Истина;
				Продолжить;	
			КонецЕсли;
		КонецЕсли;	
		//-- Мохов ПРЗ.17
		#КонецВставки

		СтрокаПродукция = Продукция.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаПродукция, СтрокаТовара);

		Если ЕстьНачатьНеРанее Тогда
			НачатьНеРанее = Мин(НачатьНеРанее, СтрокаТовара.НачатьНеРанее);
		КонецЕсли;

		Если ЕстьДатаПотребности Тогда

			Если ДатаПотребности <> СтрокаТовара.ДатаПотребности И Не НесколькоДатПотребности Тогда
				НесколькоДатПотребности = Истина;
			КонецЕсли;

			ДатаПотребности = Мин(ДатаПотребности, СтрокаТовара.ДатаПотребности);

		КонецЕсли;

		ОбрабатываемыеСтроки.Добавить(СтрокаПродукция);

	КонецЦикла;

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		Продукция,
		КэшированныеЗначения);

	Если ЗначениеЗаполнено(ДатаПотребности) Тогда
		РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КОкончанию;
	КонецЕсли;
	Если НесколькоДатПотребности Тогда
		Если Не ДоступноНесколькоДатПотребности Тогда
			ПроизводствоСервер.ЗаполнитьРеквизитВКоллекции(Продукция, "ДатаПотребности", ДатаПотребности);
		Иначе
			ДатаПотребностиУказываетсяВСтроках = Истина;
		КонецЕсли;
	КонецЕсли;

	ЗаполнитьДанныеСпецификацииВСтроках();

	// если вместе с товарами передается спецификация, нужно исключить возможные конфликты
	Если ЕстьСпецификация И ДинамическаяСтруктура Тогда
		ИсключитьДублированиеСпецификацийВТабличнойЧасти(Продукция);
	КонецЕсли;

	#Вставка
	//++ Мохов ПРЗ.17
	Если ВывестиСообщение Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Из заказа клиента были перенесены не все ресурсные спецификации!
		|Были созданы задачи по их актуализации!'"));		
	КонецЕсли;
	//-- Мохов ПРЗ.17
	#КонецВставки

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("ЗаполнитьДанныеСпецификацииВСтроках")
Процедура cbr_ЗаполнитьДанныеСпецификацииВСтроках(ТолькоНезаполненные)

	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделиях.ПараметрыВыбораСпецификаций(ЭтотОбъект, Документы.ЗаказНаПроизводство2_2);

	// Заполнение спецификаций
	МассивДанных = Новый Массив;
	МассивСтрок = Новый Массив;
	
	#Вставка
	//++ Мохов 20.02.2026 ПРЗ.17
	Если Тип("ДокументСсылка.ЗаказКлиента") = ТипЗнч(ДокументОснование) Тогда
		ВыгрузкаЗапросТоварыЗаказКлиента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование,"Товары").Выгрузить();	
	КонецЕсли;	
	//-- Мохов 20.02.2026 ПРЗ.17
	#КонецВставки
	
	Для каждого Строка Из Продукция Цикл

		Если ТолькоНезаполненные И Не Строка.Спецификация.Пустая() Тогда
			Продолжить;
		КонецЕсли;

		ДанныеОбИзделии = УправлениеДаннымиОбИзделияхКлиентСервер.СобратьДанныеОбИзделииДляВыбораСпецификации(
			ЭтотОбъект, Строка, ПараметрыВыбораСпецификаций);

		МассивДанных.Добавить(ДанныеОбИзделии);
		МассивСтрок.Добавить(Строка);
		
		#Вставка
		//++ Мохов 20.02.2026 ПРЗ.17
		Если Тип("ДокументСсылка.ЗаказКлиента") = ТипЗнч(ДокументОснование) Тогда
			СтрокаИзЗапроса = ВыгрузкаЗапросТоварыЗаказКлиента.Найти(Строка.Номенклатура,"Номенклатура");
			Если НЕ СтрокаИзЗапроса = Неопределено
				И СтрокаИзЗапроса.cbr_ЭтоПФ		
				И СтрокаИзЗапроса.cbr_Актуальность = Перечисления.СтатусыСпецификаций.Действует Тогда
					Строка.Спецификация = СтрокаИзЗапроса.Спецификация;															
			КонецЕсли;	
		КонецЕсли;
		//-- Мохов 20.02.2026 ПРЗ.17
		#КонецВставки
		
	КонецЦикла;

	#Удаление
	// Заполним спецификации
	УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций);
	#КонецУдаления
	
	#Вставка
	//++ Мохов 20.02.2026 ПРЗ.17	
	УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций,, ИСТИНА);
	// -- Мохов 20.02.2026 ПРЗ.17
	#КонецВставки
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли