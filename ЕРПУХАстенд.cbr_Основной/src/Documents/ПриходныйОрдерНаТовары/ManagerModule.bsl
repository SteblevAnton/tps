#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//++ СкребковЮМ 10.02.26 СКЛ.19
Функция cbr_СоздатьПриходныйОрдерИзДвиженияПродукцииИМатериалов(ДвижениеПродукции, Распоряжение, Помещение) Экспорт

	Результат = Неопределено;

	ПриходныеОрдера = cbr_ПолучитьПриходныеОрдераПоРаспоряжениюПомещению(Распоряжение, Помещение);

	Если ПриходныеОрдера.Количество() > 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДвижениеПродукцииИМатериалов.Отправитель КАК Отправитель,
		|	ДвижениеПродукцииИМатериалов.Получатель КАК Получатель,
		|	ДвижениеПродукцииИМатериалов.Дата КАК Дата,
		|	ДвижениеПродукцииИМатериалов.Номер КАК Номер,
		|	ДвижениеПродукцииИМатериалов.ХозяйственнаяОперация КАК ХозяйственнаяОперация
		|ИЗ
		|	Документ.ДвижениеПродукцииИМатериалов КАК ДвижениеПродукцииИМатериалов
		|ГДЕ
		|	ДвижениеПродукцииИМатериалов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДвижениеПродукции);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Основание = Новый Структура;
	Основание.Вставить("Распоряжение", Распоряжение);
	Основание.Вставить("Отправитель", Выборка.Отправитель);
	Основание.Вставить("Склад", Выборка.Получатель);
	Основание.Вставить("Помещение", Помещение);
	Основание.Вставить("ДатаВходящегоДокумента", Выборка.Дата);
	Основание.Вставить("НомерВходящегоДокумента", Выборка.Номер);
	Основание.Вставить("ДатаПоступления", ТекущаяДатаСеанса());
	Основание.Вставить("ХозяйственнаяОперация", Выборка.ХозяйственнаяОперация);

	ПриходныйОрдер = Документы.ПриходныйОрдерНаТовары.СоздатьДокумент();
	ПриходныйОрдер.Заполнить(Основание);
	ПриходныйОрдер.Дата = ТекущаяДатаСеанса();
	ПриходныйОрдер.Статус = Перечисления.СтатусыПриходныхОрдеров.Принят;

	ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ПриходныйОрдер,
		Документы.ПриходныйОрдерНаТовары));
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ПриходныйОрдер, ПараметрыУказанияСерий);

	Ошибка = Ложь;
	ОписаниеОшибки = "";

	Попытка
		ПриходныйОрдер.Записать(РежимЗаписиДокумента.Проведение);
		Результат = ПриходныйОрдер.Ссылка;
	Исключение
		Ошибка = Истина;
		ОписаниеОшибки = ИнформацияОбОшибке();
	КонецПопытки;

	Если Не Ошибка Тогда
		ТекстСообщения = НСтр("ru = 'Создан и проведен документ %1'");
	Иначе
		ТекстСообщения = НСтр("ru = 'Ошибка проведения документа %1'");
		ЗаписьЖурналаРегистрации("ОшибкаВыполнения", УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПриходныйОрдерНаТовары, , ОбработкаОшибок.ПодробноеПредставлениеОшибки(ОписаниеОшибки));
	КонецЕсли;

	ТекстСообщения = СтрШаблон(ТекстСообщения, ПриходныйОрдер.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ПриходныйОрдер.Ссылка);

	Возврат Результат;

КонецФункции //-- СкребковЮМ 10.02.26 СКЛ.19

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ СкребковЮМ 10.02.26 СКЛ.19
Функция cbr_ПолучитьПриходныеОрдераПоРаспоряжениюПомещению(Распоряжение, Помещение)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПриходныйОрдерНаТовары.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриходныйОрдерНаТовары КАК ПриходныйОрдерНаТовары
	|ГДЕ
	|	ПриходныйОрдерНаТовары.Проведен
	|	И ПриходныйОрдерНаТовары.Распоряжение = &Распоряжение
	|	И ПриходныйОрдерНаТовары.Помещение = &Помещение";

	Запрос.УстановитьПараметр("Распоряжение", Распоряжение);
	Запрос.УстановитьПараметр("Помещение", Помещение);

	РезультатЗапроса = Запрос.Выполнить();

	Возврат РезультатЗапроса.Выгрузить();

КонецФункции //-- СкребковЮМ 10.02.26 СКЛ.19

#КонецОбласти

#КонецЕсли