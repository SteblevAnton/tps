#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура cbr_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

    //++ НЕ УТ
    Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПереработчику2_5")
         Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетПереработчика2_5") Тогда
        ЗаполнитьДокументНаОснованииДанныхПереработчика2_5(ДанныеЗаполнения);
    //++ Устарело_Переработка24
    ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПереработчику")
        Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
        ЗаполнитьДокументНаОснованииДанныхПереработчика(ДанныеЗаполнения);
    //-- Устарело_Переработка24
    КонецЕсли;
    //-- НЕ УТ

    #Вставка
    //++АшаеваАН 03.02.26 СКЛ.9
    Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаданиеНаПеревозку") Тогда
    	Если cbr_ПроверитьНаСуществующийДокументПТУА(ДанныеЗаполнения) Тогда
   			ВызватьИсключение "Ввод приобретение услуг и прочих активов на основании заказа на перевозку невозможен, так в базе уже существует такой документ!
   			|Связанные документы можно посмотреть в отчете ""Связанные документы"".";
    	КонецЕсли;
        cbr_ЗаполнитьДокументНаОснованииДанныхЗаданияНаПеревозку(ДанныеЗаполнения);
    КонецЕсли;
    //--АшаеваАН 03.02.26 СКЛ.9
    #КонецВставки
    ПараметрыЗаполнения = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыЗаполненияНалогообложенияНДСЗакупки(ЭтотОбъект);
    УчетНДСУП.ЗаполнитьНалогообложениеНДСЗакупки(НалогообложениеНДС, ПараметрыЗаполнения);

    ПараметрыЗаполнения = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыЗаполненияВидаДеятельностиНДС(ЭтотОбъект);
    УчетНДСУП.ЗаполнитьВидДеятельностиНДС(ЗакупкаПодДеятельность, ПараметрыЗаполнения);

    ПараметрыВыбораСтатейИАналитик = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация);
    ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);

    ВзаиморасчетыСервер.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения);

    ПриобретениеУслугПрочихАктивовЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

    ИнициализироватьДокумент(ДанныеЗаполнения);
    ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);

КонецПроцедуры

#КонецОбласти

#Область Прочее

//++АшаеваАН 03.02.26 СКЛ.9
Процедура cbr_ЗаполнитьДокументНаОснованииДанныхЗаданияНаПеревозку(ДокументОснование)

        Дата = ТекущаяДатаСеанса();

        Запрос = Новый Запрос;
        Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
        Запрос.Текст =
        "ВЫБРАТЬ
        |    ЗаданиеНаПеревозку.Перевозчик КАК Партнер,
        |    ЗаданиеНаПеревозку.Ответственный КАК Менеджер,
        |    ЗаданиеНаПеревозку.АвтомобильТип КАК cbr_Транспорт,
        |    ЗаданиеНаПеревозку.АвтомобильМарка КАК cbr_Марка,
        |    ЗаданиеНаПеревозку.АвтомобильГосударственныйНомер КАК cbr_НомерМашины,
        |    ЗаданиеНаПеревозку.ГосударственныйНомерПрицепа КАК cbr_НомерПрицепа,
        |    ЗаданиеНаПеревозку.cbr_НомерЗаявкиПоДаннымПеревозчика КАК cbr_НомерЗаявкиПоДаннымПеревозчика,
        |    ЗаданиеНаПеревозку.cbr_НомерПеревозкиAtrucks КАК cbr_НомерПеревозкиAtrucks,
        |    &Ссылка КАК cbr_ЗаданиеНаПеревозку,
        |    ВложенныйЗапрос.КолРазОрганизация КАК КолРазОрганизация
        |ИЗ
        |    Документ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
        |    (ВЫБРАТЬ
        |        КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаданиеНаПеревозкуРаспоряжения.Распоряжение.Организация) КАК КолРазОрганизация
        |    ИЗ
        |        Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
        |    ГДЕ
        |        ЗаданиеНаПеревозкуРаспоряжения.Ссылка = &Ссылка
        |        И ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Проведен) КАК ВложенныйЗапрос
        |ГДЕ
        |    ЗаданиеНаПеревозку.Ссылка = &Ссылка";

        Результат = Запрос.Выполнить();
        Шапка = Результат.Выбрать();
        Шапка.Следующий();

		НесколькоОрганизаций = Ложь;
		
        Если Шапка.КолРазОрганизация > 1 Тогда
            НесколькоОрганизаций = Истина;
        КонецЕсли;
        
        cbr_ЗаполнитьРасходы(ДокументОснование, НесколькоОрганизаций);
    
        // Заполнение шапки.
        ЗаполнитьЗначенияСвойств(ЭтотОбъект, Шапка);
        Подразделение = Справочники.СтруктураПредприятия.ТПСОтделЛогистикиИВЭД;

        Если ЗначениеЗаполнено(Партнер) И ЗначениеЗаполнено(Организация) Тогда
            ЗаполнитьУсловияЗакупокПоУмолчанию();
        Иначе
            Валюта = Константы.БазоваяВалютаПоУмолчанию.Получить();
            ВалютаВзаиморасчетов = Константы.БазоваяВалютаПоУмолчанию.Получить();
        КонецЕсли;

КонецПроцедуры //--АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
Функция cbr_ПроверитьНаСуществующийДокументПТУА(ДокументОснование)

    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
    Запрос.Текст =
    "ВЫБРАТЬ
    |	ПриобретениеУслугПрочихАктивов.Ссылка КАК Ссылка
    |ИЗ
    |	Документ.ПриобретениеУслугПрочихАктивов КАК ПриобретениеУслугПрочихАктивов
    |ГДЕ
    |	ПриобретениеУслугПрочихАктивов.cbr_ЗаданиеНаПеревозку = &Ссылка
    |	И ПриобретениеУслугПрочихАктивов.Проведен";

    Результат = Запрос.Выполнить();
 
	 Если НЕ Результат.Пустой() Тогда
	 	Возврат Истина;
	 КонецЕсли;

	Возврат Ложь;

КонецФункции //--АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
Процедура cbr_ЗаполнитьРасходы(ДокументОснование, НесколькоОрганизаций) Экспорт

    Расходы.Очистить();
    ОбрабатываемыеСтроки = Новый Массив;

    Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("Ссылка", ДокументОснование);

    Запрос.Текст =
    "ВЫБРАТЬ
    |    Распоряжения.Распоряжение КАК Распоряжение,
    |    Распоряжения.Распоряжение.Организация КАК Организация,
    |    Распоряжения.Распоряжение.ДополнительнаяИнформация КАК ДополнительнаяИнформация,
    |    Распоряжения.Вес КАК Вес,
    |    Распоряжения.Распоряжение.НаправлениеДеятельности КАК НаправлениеДеятельности,
    |    ЕСТЬNULL(ЗаданиеНаПеревозкуМаршрут.Адрес,"""") КАК Адрес
    |ИЗ
    |    Документ.ЗаданиеНаПеревозку.Распоряжения КАК Распоряжения
    |        ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Маршрут КАК ЗаданиеНаПеревозкуМаршрут
    |        ПО Распоряжения.КлючСвязи = ЗаданиеНаПеревозкуМаршрут.КлючСвязи
    |ГДЕ
    |    Распоряжения.Ссылка = &Ссылка
    |    И Распоряжения.Распоряжение.Проведен";

    ТЗ = Запрос.Выполнить().Выгрузить();
	Если НЕ ТЗ.Количество() Тогда
		Возврат
	КонецЕсли;
	
    Вес = ТЗ.Итог("Вес");
    Если НЕ ЗначениеЗаполнено(Вес) Тогда
        Вес = 1;
    КонецЕсли;

    Если НЕ НесколькоОрганизаций Тогда
        Организация = ТЗ[0].Организация;
        ОтветственныеЛицаСервер.ПриИзмененииСвязанныхРеквизитовДокумента(ЭтотОбъект);
    КонецЕсли;

    Подразделение = Справочники.СтруктураПредприятия.ТПСОтделЛогистикиИВЭД;
    СтавкаНДС = СтавкаНДС();
    
    РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование,"Склад, cbr_ФактическаяСтоимостьПеревозки, Операция", Истина);
    
    Если ЗначениеЗаполнено(РеквизитыДокументаОснования.Склад) Тогда
        АдресСклада = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(РеквизитыДокументаОснования.Склад,
        Справочники.ВидыКонтактнойИнформации.АдресСклада, ?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()), Истина);
    Иначе
        АдресСклада = "";
    КонецЕсли;
    
    Если РеквизитыДокументаОснования.Операция = Перечисления.ВидыДоставки.НаСклад Тогда
        Содержание = "Транспортно-экспедиционные услуги (%2 - %1 %3)";
    Иначе
        Содержание = "Транспортно-экспедиционные услуги (%1 - %2 %3)";
    КонецЕсли;
    
    Для Каждого Строка Из ТЗ Цикл
        
        Если Строка.ДополнительнаяИнформация = "" Тогда
            ДополнительнаяИнформация = "";
        Иначе
            ДополнительнаяИнформация = ", " + Строка.ДополнительнаяИнформация;
        КонецЕсли;

        НоваяСтрока = Расходы.Добавить();
        НоваяСтрока.Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Содержание, АдресСклада, Строка.Адрес, ДополнительнаяИнформация);
        НоваяСтрока.Цена = Строка.Вес / Вес * РеквизитыДокументаОснования.cbr_ФактическаяСтоимостьПеревозки;
        НоваяСтрока.Сумма = НоваяСтрока.Цена;
        НоваяСтрока.СтавкаНДС = СтавкаНДС;
        Если ТипЗнч(Строка.Распоряжение) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
            Если Строка.Организация = Справочники.Организации.НайтиПоРеквизиту("ИНН", "5042063026") Тогда
                НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.cbr_26ТранспортныеУслугиДоставкаПокупателюТранспортнойКомпании;
            Иначе
                НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.cbr_44ТранспортныеУслугиДоставкаПокупателюТранспортнойКомпании;
            КонецЕсли;
        ИначеЕсли ТипЗнч(Строка.Распоряжение) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
            НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.cbr_ТранспортныеУслугиДоставкаПокупателюТранспортнойКомпании;
        КонецЕсли;
        НоваяСтрока.Подразделение = Подразделение;
        НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
        НоваяСтрока.АналитикаРасходов = Строка.Распоряжение;

        ОбрабатываемыеСтроки.Добавить(НоваяСтрока);

    КонецЦикла;

    Если ОбрабатываемыеСтроки.Количество() Тогда

        СтруктураДействий = Новый Структура;
        СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЭтотОбъект);
        СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
        СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);

        ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
        ОбрабатываемыеСтроки, СтруктураДействий, Расходы, Неопределено);

    КонецЕсли;

КонецПроцедуры //--АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
Функция СтавкаНДС()

    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
                 |    СтавкиНДС.Ссылка КАК Ссылка
                 |ИЗ
                 |    Справочник.СтавкиНДС КАК СтавкиНДС
                 |ГДЕ
                 |    СтавкиНДС.ПеречислениеСтавкаНДС = &ПеречислениеСтавкаНДС
                 |    И СтавкиНДС.Основная";

    Запрос.УстановитьПараметр("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС22);
    Выборка = Запрос.Выполнить().Выбрать();

    Если Выборка.Следующий() Тогда
        Возврат Выборка.Ссылка;
    КонецЕсли;

    Возврат Справочники.СтавкиНДС.БезНДС;

КонецФункции //--АшаеваАН 03.02.26 СКЛ.9

#КонецОбласти

#КонецЕсли