#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура СоздатьОтборРазмещениеИзРасходногоОрдера(РасходныйОрдер) Экспорт

	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РасходныйОрдер,
		"Статус, Склад, Помещение, ЗонаОтгрузки, ДатаОтгрузки");

	Если Не СкладыСервер.ИспользоватьАдресноеХранение(РеквизитыОснования.Склад, РеквизитыОснования.Помещение,
		РеквизитыОснования.ДатаОтгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Отбор");

	ОтборРазмещениеТоваров = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
	ОтборРазмещениеТоваров.Дата = ТекущаяДатаСеанса();
	ОтборРазмещениеТоваров.ВидОперации = ВидОперации;
	ОтборРазмещениеТоваров.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВРаботе");
	ОтборРазмещениеТоваров.Распоряжение = РасходныйОрдер;

	ЗаполнитьЗначенияСвойств(ОтборРазмещениеТоваров, РеквизитыОснования, "ЗонаОтгрузки, Склад, Помещение");

	ОтборРазмещениеТоваров.ЗаполнитьТоварыОтборПоРаспоряжению();

	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОтборРазмещениеТоваров,
		Документы.ОтборРазмещениеТоваров);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);

	ОтборРазмещениеТоваров.ОтобратьТовары("ТоварыОтбор");

	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);

	ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Запись);

	Ошибка = Ложь;

	МассивЯчеек = ОтборРазмещениеТоваров.ТоварыОтбор.ВыгрузитьКолонку("Ячейка");
	Для Каждого ЭлементМассива Из МассивЯчеек Цикл
		Если Не ЗначениеЗаполнено(ЭлементМассива) Тогда
			Ошибка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Ошибка Тогда
		ТекстСообщения = НСтр("ru='Ошибка проведения документа %1. Не заполнены некоторые ячейки.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Попытка

		ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Проведение);

	Исключение

		Ошибка = Истина;

	КонецПопытки;

	ТекстСообщения = НСтр("ru = 'Создан документ %1'");

	Если Ошибка Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка при записи документа %1'");
	КонецЕсли;

	ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);

КонецПроцедуры

//++ СкребковЮМ 16.02.26 СКЛ.13
Процедура cbr_СоздатьОтборРазмещениеНаПеремещение(ТоварыОтбор, ТоварыРазмещение, Склад) Экспорт
	
	ОтборРазмещениеТоваров = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
	ОтборРазмещениеТоваров.Дата = ТекущаяДатаСеанса();
	ОтборРазмещениеТоваров.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Перемещение");
	ОтборРазмещениеТоваров.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВРаботе");
	ОтборРазмещениеТоваров.Склад = Склад;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОтборРазмещениеТоваров,
		Документы.ОтборРазмещениеТоваров);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);
		
	Для Каждого Товар Из ТоварыОтбор Цикл
		ТоварОтбор = ОтборРазмещениеТоваров.ТоварыОтбор.Добавить();
		ЗаполнитьЗначенияСвойств(ТоварОтбор, Товар);
	КонецЦикла;
	
	Для Каждого Товар Из ТоварыРазмещение Цикл
		ТоварРазмещение = ОтборРазмещениеТоваров.ТоварыРазмещение.Добавить();
		ЗаполнитьЗначенияСвойств(ТоварРазмещение, Товар);
	КонецЦикла;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОтборРазмещениеТоваров,
		Документы.ОтборРазмещениеТоваров);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);
	
	ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Запись);

	Ошибка = Ложь;

	МассивЯчеек = ОтборРазмещениеТоваров.ТоварыОтбор.ВыгрузитьКолонку("Ячейка");
	Для Каждого ЭлементМассива Из МассивЯчеек Цикл
		Если Не ЗначениеЗаполнено(ЭлементМассива) Тогда
			Ошибка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Ошибка Тогда
		ТекстСообщения = НСтр("ru='Ошибка проведения документа %1. Не заполнены некоторые ячейки.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	Попытка
		ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		Ошибка = Истина;
	КонецПопытки;

	ТекстСообщения = НСтр("ru = 'Создан документ %1'");

	Если Ошибка Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка при записи документа %1'");
	КонецЕсли;

	ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);

КонецПроцедуры //-- СкребковЮМ 16.02.26 СКЛ.13

#КонецОбласти

#КонецЕсли