#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Если Не ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") 
		И Не ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
		Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	СпецТребования = Неопределено;
	СсылкаНаОбъект = ДанныеЗаполнения;
	Пока Истина Цикл
	
		СпецТребования = РегистрыСведений.РеестрСпециальныхТребований.СпециальныеТребованияПоОбъекту(СсылкаНаОбъект);
		Если СпецТребования.НайденыЗаписи = Ложь Тогда
			
			Если ТипЗнч(СсылкаНаОбъект) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
				СсылкаНаОбъект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "Договор");
				Если ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				БазовыйДоговор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "БазовыйДоговор");
				Если ЗначениеЗаполнено(БазовыйДоговор) Тогда
					СсылкаНаОбъект = БазовыйДоговор;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		
		КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
	Если Не СпецТребования = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СпецТребования);
	КонецЕсли;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры  

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли