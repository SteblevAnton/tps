#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("ПриСозданииНаСервере")
&НаСервере
Процедура cbr_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Параметры.Номенклатура) Тогда
		ВызватьИсключение НСтр("ru = 'Данная форма не предназначена для непосредственного открытия.';
								|en = 'This form is not intended for direct opening.'");
	КонецЕсли;

	КлючевыеПараметры = ОбеспечениеВДокументахКлиентСервер.КлючевыеПараметрыФормыЗапросаКоличестваИСерий();
	ЗаполнитьЗначенияСвойств(КлючевыеПараметры, Параметры);

	ДанныеЗаполнения  = ОбеспечениеВДокументахКлиентСервер.ПараметрыФормыЗапросаКоличестваИСерий(Ложь);
	ЗаполнитьЗначенияСвойств(ДанныеЗаполнения,  Параметры);

	// В незаписанном документе назначение пустое, а алгоритмы получения остатков рассчитывают на заполненное.
	Если КлючевыеПараметры.ПодборТоваров И Не ЗначениеЗаполнено(ДанныеЗаполнения.Назначение) Тогда
		ДанныеЗаполнения.Назначение = Справочники.Назначения.ПолучитьСсылку();
	КонецЕсли;

	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.Заполнить(КлючевыеПараметры, ДанныеЗаполнения);

	Обработки.ЗапросКоличестваИСерий.ПроверитьЗаполнение(Объект);

	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	#Вставка
	//++АшаеваАН 29.01.26 ПРД.5
	Если ТипЗнч(Объект.Регистратор) = Тип("ДокументСсылка.ЗаказКлиента") Тогда 
		
		cbr_РаботаСФормами.ДобавитьРеквизитФормы(ЭтотОбъект, "cbr_ПоказыватьСерииБезУчетаСпецТребований", Новый ОписаниеТипов("Число"));

		НовыйЭлемент = cbr_РаботаСФормами.ДобавитьПолеФормы(ЭтотОбъект,
		 "cbr_ПоказыватьСерииБезУчетаСпецТребований",
		 "cbr_ПоказыватьСерииБезУчетаСпецТребований",,
		 ВидПоляФормы.ПолеПереключателя,
		 Элементы.КоманднаяПанельСерии,
		 Элементы.КоманднаяПанельСерииСлева,
		 "ПриИзменении");
		НовыйЭлемент.ПоложениеЗаголовка  = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовыйЭлемент.КоличествоКолонок = 2;
		НовыйЭлемент.ВидПереключателя = ВидПереключателя.Тумблер;
		НовыйЭлемент.СписокВыбора.добавить(0,"Серии с учетом спец. требований");
		НовыйЭлемент.СписокВыбора.добавить(1,"Показывать серии без учета спец. требований");
		
		cbr_РаботаСФормами.ДобавитьРеквизитФормы(ЭтотОбъект, "cbr_ПоказыватьСерииСУчетомСпецТребований", Новый ОписаниеТипов("Булево"));
		cbr_РаботаСФормами.ДобавитьРеквизитФормы(ЭтотОбъект, "cbr_НеБолееДатыПроизводства", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
		cbr_РаботаСФормами.ДобавитьРеквизитФормы(ЭтотОбъект, "cbr_ДопустимыйОстатокГСХНеМенееПроцент", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
		cbr_РаботаСФормами.ДобавитьРеквизитФормы(ЭтотОбъект, "cbr_ДопустимыйОстатокГСХНеМенееМес", ОбщегоНазначения.ОписаниеТипаЧисло(3, 0));
		
		Если Параметры.Свойство("cbr_НеБолееДатыПроизводства") 
				И Параметры.Свойство("cbr_ДопустимыйОстатокГСХНеМенееПроцент") 
				И Параметры.Свойство("cbr_ДопустимыйОстатокГСХНеМенееМес") Тогда		
			ЭтотОбъект.cbr_НеБолееДатыПроизводства = Параметры.cbr_НеБолееДатыПроизводства;
			ЭтотОбъект.cbr_ДопустимыйОстатокГСХНеМенееПроцент = Параметры.cbr_ДопустимыйОстатокГСХНеМенееПроцент;
			ЭтотОбъект.cbr_ДопустимыйОстатокГСХНеМенееМес = Параметры.cbr_ДопустимыйОстатокГСХНеМенееМес;
		
			Если ЗНАЧЕНИЕЗАПОЛНЕНО(ЭтотОбъект.cbr_НеБолееДатыПроизводства) 
				 ИЛИ ЗНАЧЕНИЕЗАПОЛНЕНО(ЭтотОбъект.cbr_ДопустимыйОстатокГСХНеМенееПроцент)
				 ИЛИ ЗНАЧЕНИЕЗАПОЛНЕНО(ЭтотОбъект.cbr_ДопустимыйОстатокГСХНеМенееМес) Тогда
				ЭтотОбъект.cbr_ПоказыватьСерииСУчетомСпецТребований = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	//--АшаеваАН 29.01.26 ПРД.5
	#КонецВставки

	Если ТипЗнч(ДанныеЗаполнения.Склады) = Тип("Массив") Тогда
		Склады.ЗагрузитьЗначения(ДанныеЗаполнения.Склады);
	КонецЕсли;

	// Инициализация реквизитов формы, для интерактивной работы пользователя.
	ТекущиеРеквизиты = Обработки.ЗапросКоличестваИСерий.ТекущиеРеквизиты(Объект);

	ТекущаяНоменклатура          = ТекущиеРеквизиты.Номенклатура;
	ТекущаяХарактеристика        = ТекущиеРеквизиты.Характеристика;
	ТекущаяУпаковка              = ТекущиеРеквизиты.Упаковка;
	ТекущийИндексТовараДляОтбора = ТекущиеРеквизиты.ИндексТовараДляОтбора;

	РежимОтображенияТоваров = "ОстаткиИГрафик";
	Если Объект.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить
			Или Объект.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.СоСклада Тогда
			РежимОтображенияТоваров = "СвободныеОстатки";
	КонецЕсли;

	ТолькоСкладыВыбраннойГруппы = Истина;

	РежимОтображенияСерий       = "СвободныеОстатки";
	ВариантПредоставленияСкидки = "СКИДКА";

	МаксимальныйПроцентСкидки   = 0;
	МаксимальныйПроцентНаценки  = 0;
	Если ЭтоАдресВременногоХранилища(Параметры.АдресВоВременномХранилище) Тогда
		СтруктураТаблиц = ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
		МаксимальныйПроцентСкидки  = СтруктураТаблиц.Ограничения[0].МаксимальныйПроцентРучнойСкидки;
		МаксимальныйПроцентНаценки = СтруктураТаблиц.Ограничения[0].МаксимальныйПроцентРучнойНаценки;
	КонецЕсли;

	ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Номенклатура, "ТипНоменклатуры");
	ОкруглятьКоличество = ОкруглятьКоличество();

	Если КлючевыеПараметры.ПодборТоваров И КлючевыеПараметры.ПодборВариантовОбеспечения Тогда

		ДопустимыеВариантыОбеспечения = ОбеспечениеВДокументахКлиентСервер.ДопустимыеВариантыОбеспечения(
			ТипНоменклатуры,
			Параметры.ДопустимыеОбособленныеДействия,
			Параметры.ДопустимыеНеобособленныеДействия,
			КлючевыеПараметры.ОграничиватьВариантыОбеспечения,
			Параметры.ВариантОбеспеченияИФлагОбособленно);

	КонецЕсли;

	РассчитатьЕдиницуИзмерения();
	Если Объект.ИндексСтрокиДокумента > -1 Тогда

		Элементы.Товары.ТекущаяСтрока = Объект.Товары[Объект.ИндексСтрокиДокумента].ПолучитьИдентификатор();
		УправлениеЭлементамиФормыПриСозданииИИзмененииРеквизитов(Элементы.КУказаниюСерий); // инициализация элементов серий
		УправлениеЭлементамиФормыПриСозданииИИзмененииРеквизитов(Элементы.КВыборуДействий);
		Обработки.ЗапросКоличестваИСерий.ЗаполнитьСерииДляСтрокиТовары(
			ЭтотОбъект,
			КлючевыеПараметры,
			Объект.ИндексСтрокиДокумента,
			Параметры.ПараметрыУказанияСерий);

		СтрокаДокумента = Объект.Товары[Объект.ИндексСтрокиДокумента];

		КоличествоПодобрано = Объект.КоличествоУпаковок;
		Если СтрокаДокумента.ПроверятьДоступность
				И КлючевыеПараметры.Режим = ОбеспечениеВДокументахКлиентСервер.РежимВыборДействия() Тогда

			ДоступноПоСтроке = Мин(СтрокаДокумента.ВНаличии, Объект.КоличествоУпаковок);
			Если ДоступноПоСтроке < Объект.КоличествоУпаковок Тогда

				КоэффициентУпаковки = КоэффициентУпаковки(Объект.Упаковка, Объект.Номенклатура);

				ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
				ПараметрыПроверки.ИмяТЧ = "Объект";
				СтруктураОбъект = Новый Структура();
				СтруктураОбъект.Вставить("Номенклатура", Объект.Номенклатура);
				СтруктураОбъект.Вставить("Упаковка", Объект.Упаковка);
				СтруктураОбъект.Вставить("Количество", ДоступноПоСтроке * КоэффициентУпаковки);
				СтруктураОбъект.Вставить("КоличествоУпаковок", Объект.КоличествоУпаковок);
				ОкруглениеНекорректно = НоменклатураСервер.НомераСтрокСНекорректнымОкруглениемКоличества(
					СтруктураОбъект,
					ПараметрыПроверки);
				Если ОкруглениеНекорректно.Количество() = 0 Тогда
					ДоступноПоСтроке = Объект.КоличествоУпаковок;
				КонецЕсли;

			КонецЕсли;
			КоличествоПодобрано = Макс(ДоступноПоСтроке, Объект.КоличествоОформлено);

		КонецЕсли;

		СтрокаДокумента.Подобрано            = КоличествоПодобрано;
		СтрокаДокумента.ПодобраноДоИзменения = КоличествоПодобрано;

		МассивСерий = Новый Массив();
		Если ТипЗнч(ДанныеЗаполнения.Серия) = Тип("Массив") Тогда
			МассивСерий = ДанныеЗаполнения.Серия;
		ИначеЕсли Не ДанныеЗаполнения.Серия.Пустая() Тогда
			МассивСерий.Добавить(Новый Структура("Серия,Количество", ДанныеЗаполнения.Серия, Объект.КоличествоУпаковок));
		КонецЕсли;

		Для Каждого СтруктураСерия Из МассивСерий Цикл

			НайденныеСтроки = Объект.Серии.НайтиСтроки(Новый Структура("Серия", СтруктураСерия.Серия));
			Если НайденныеСтроки.Количество() > 0 Тогда
				СтрокаСерии = НайденныеСтроки[0];
				СтрокаСерии.Подобрано = СтруктураСерия.Количество * ЕдиницаИзмеренияКоэффициент;
			КонецЕсли;

		КонецЦикла;

		ПересчитатьСтатусУказанияСерий(Объект, СтрокаДокумента, Параметры.ПараметрыУказанияСерий, ЭтоМернаяЕдиница, ДопустимоеОтклонение, ЕдиницаИзмеренияКоэффициент);

	КонецЕсли;

	Если Объект.ВариантКомплектацииНоменклатуры.Пустая() Тогда
		Обработки.ЗапросКоличестваИСерий.ПересчиатьОстаткиВУпаковке(Объект.Товары, ТекущаяУпаковка, ТекущаяНоменклатура, ТекущаяХарактеристика);
	КонецЕсли;

	УправлениеЭлементамиФормыПриСоздании();

	Если Параметры.ВыборСерии Тогда
		КУказаниюСерийНаСервере();
	КонецЕсли;

КонецПроцедуры

//++АшаеваАН 29.01.26 ПРД.5
&НаКлиенте
Процедура cbr_ПоказыватьСерииБезУчетаСпецТребованийПриИзменении()
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "cbr_ПоказыватьСерииБезУчетаСпецТребований")
		 И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "cbr_ПоказыватьСерииСУчетомСпецТребований") Тогда
		
		Если ЭтотОбъект.cbr_ПоказыватьСерииБезУчетаСпецТребований = 0 Тогда
			ЭтотОбъект.cbr_ПоказыватьСерииСУчетомСпецТребований = Истина;
		Иначе
			ЭтотОбъект.cbr_ПоказыватьСерииСУчетомСпецТребований = Ложь;	
		КонецЕсли;
		
		cbr_ПоказыватьСерииБезУчетаСпецТребований();
		
	КонецЕсли;
	
КонецПроцедуры //--АшаеваАН 29.01.26 ПРД.5

//++АшаеваАН 29.01.26 ПРД.5
&НаСервере
Процедура cbr_ПоказыватьСерииБезУчетаСпецТребований()
	
	КлючевыеПараметры = ОбеспечениеВДокументахКлиентСервер.КлючевыеПараметрыФормыЗапросаКоличестваИСерий();
	ЗаполнитьЗначенияСвойств(КлючевыеПараметры, Параметры);	
	КлючевыеПараметры.Вставить("ПерезаполнитьСерии", Истина);
	
	Обработки.ЗапросКоличестваИСерий.ЗаполнитьСерииДляСтрокиТовары(
		ЭтотОбъект,
		КлючевыеПараметры,
		Элементы.Товары.ТекущаяСтрока,
		Параметры.ПараметрыУказанияСерий);
	
КонецПроцедуры //--АшаеваАН 29.01.26 ПРД.5

#КонецОбласти
