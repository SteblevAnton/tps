
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++АшаеваАН 13.02.26 СКЛ.3
Процедура cbr_СформироватьДокументыПеремещенияНаОснованииПТУ(Параметры = Неопределено, АдресРезультата = Неопределено) Экспорт
	
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		Приобретение = Параметры.Приобретение;
	Иначе 
		Приобретение = Параметры;
	КонецЕсли;
		
	ПриобретениеТовары = ПриобритениеСтрокиСЗаказами(Приобретение);
	
	Если НЕ ПриобретениеТовары.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаказыСОстатками = cbr_Заказы(ПриобретениеТовары); 
	Если НЕ ЗаказыСОстатками.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаказыНаПеремещение = ЗаказыСОстатками.ВыгрузитьКолонку("ЗаказНаПеремещение");
	ОбщегоНазначенияУТ.УдалитьПовторяющиесяЭлементыМассива(ЗаказыНаПеремещение);

	Для каждого Заказ из ЗаказыНаПеремещение Цикл
		
		ЗаказОбъект = Заказ.ПолучитьОбъект(); 
		Склад = Заказ.СкладОтправитель;
		Организация = Заказ.Организация;
		
		Строки = ЗаказыСОстатками.НайтиСтроки(Новый Структура("ЗаказНаПеремещение",Заказ));
		ОбрабатываемыеСтроки = Новый Массив();
		
		Для каждого Строка Из Строки Цикл
			
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("Номенклатура", Строка.Номенклатура);
			ПараметрыОтбора.Вставить("Характеристика", Строка.Характеристика);
			ПараметрыОтбора.Вставить("ВариантОбеспечения", Перечисления.ВариантыОбеспечения.КОбеспечению);
			НайденныеСтроки = ЗаказОбъект.Товары.НайтиСтроки(ПараметрыОтбора);
			
			Для каждого СтрокаЗаказа Из НайденныеСтроки Цикл
			 	
			 	СтрокаРасчитана = Ложь;
			 	КоличествоДо = СтрокаЗаказа.КоличествоУпаковок;
			 	СтрокиПриобритения = ПриобретениеТовары.НайтиСтроки(Новый Структура("Номенклатура, Характеристика, Склад, Организация", 
			 						СтрокаЗаказа.Номенклатура, СтрокаЗаказа.Характеристика, Склад, Организация));
			 	
			 	Для Каждого СтрокаПриоб Из СтрокиПриобритения Цикл
					Если СтрокаПриоб.КоличествоУпаковок > 0 Тогда
							Если СтрокаПриоб.КоличествоУпаковок >= СтрокаЗаказа.КоличествоУпаковок Тогда
								СтрокаПриоб.КоличествоУпаковок = СтрокаПриоб.КоличествоУпаковок - СтрокаЗаказа.КоличествоУпаковок;
								
								РеквизитыПриобретения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаПриоб.ПТУ,"Дата,ЗаказПоставщику");
								
								СтрокаЗаказа.НачалоОтгрузки = РеквизитыПриобретения.Дата;
								СтрокаЗаказа.ОкончаниеПоступления = РеквизитыПриобретения.Дата;
								СтрокаЗаказа.cbr_ЗаказПоставщику = РеквизитыПриобретения.ЗаказПоставщику;
								СтрокаЗаказа.cbr_ПТУ = СтрокаПриоб.ПТУ;
											
								КоличествоДо = 0;
								СтрокаРасчитана = Истина;
							Иначе
								СтрокаЗаказа.КоличествоУпаковок = СтрокаПриоб.КоличествоУпаковок;
								
								РеквизитыПриобретения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаПриоб.ПТУ,"Дата,ЗаказПоставщику");
								
								СтрокаЗаказа.НачалоОтгрузки = РеквизитыПриобретения.Дата;
								СтрокаЗаказа.ОкончаниеПоступления = РеквизитыПриобретения.Дата;
								СтрокаЗаказа.cbr_ЗаказПоставщику = РеквизитыПриобретения.ЗаказПоставщику;
								СтрокаЗаказа.cbr_ПТУ = СтрокаПриоб.ПТУ;
								
								КоличествоДо = КоличествоДо - СтрокаЗаказа.КоличествоУпаковок; 
								СтрокаПриоб.КоличествоУпаковок = 0;
								СтрокаРасчитана = Истина;
							КонецЕсли;
							Прервать;
						КонецЕсли;
			 	КонецЦикла;
			 	
			 	Если НЕ СтрокаРасчитана Тогда
			 		Продолжить;
			 	Иначе
					Если КоличествоДо > 0 Тогда
						cbr_РазбитьСтроку(ЗаказОбъект.Товары, СтрокаЗаказа, КоличествоДо, ОбрабатываемыеСтроки);
					КонецЕсли;
						
					СтрокаЗаказа.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
					
					ОбрабатываемыеСтроки.Добавить(СтрокаЗаказа);
				КонецЕсли;
			 
			КонецЦикла;	
		КонецЦикла;
		
		Если ОбрабатываемыеСтроки.Количество() Тогда
			cbr_ОбработатьСтрокиТЧ(ЗаказОбъект.Товары, ОбрабатываемыеСтроки);
			НачатьТранзакцию();
			Попытка
				ЗаказОбъект.Записать(РежимЗаписиДокумента.Проведение); 
				cbr_СоздатьДокументПеремещениеТоваров(Заказ);
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				
				ТекстСообщения = НСтр("ru = 'Не удалось провести перемещение по заказу: %Заказ%'"); 
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Заказ%", Заказ); 
		
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.ПриобретениеТоваровУслуг,, ТекстСообщения);
				
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецПопытки;
		Конецесли;
			
	КонецЦикла;

КонецПроцедуры //--АшаеваАН 13.02.26 СКЛ.3

//++АшаеваАН 13.02.26 СКЛ.3
Функция ПриобритениеСтрокиСЗаказами(Приобретения)
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	cbr_ТоварыКПеремещениюОстатки.Номенклатура КАК Номенклатура,
	|	cbr_ТоварыКПеремещениюОстатки.Характеристика КАК Характеристика,
	|	cbr_ТоварыКПеремещениюОстатки.ПТУ КАК ПТУ,
	|	cbr_ТоварыКПеремещениюОстатки.Склад КАК Склад,
	|	cbr_ТоварыКПеремещениюОстатки.Организация КАК Организация,
	|	cbr_ТоварыКПеремещениюОстатки.КоличествоКПеремещениюОстаток КАК КоличествоУпаковок
	|ИЗ
	|	РегистрНакопления.cbr_ТоварыКПеремещению.Остатки КАК cbr_ТоварыКПеремещениюОстатки
	|ГДЕ
	|	&Условие";
	
	Если Приобретения = Неопределено Тогда
		Запрос.УстановитьПараметр("Условие", Истина);
	Иначе 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "cbr_ТоварыКПеремещениюОстатки.ПТУ В (&ПТУ)");
		Запрос.УстановитьПараметр("ПТУ", Приобретения);
	КонецЕсли; 	
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции //--АшаеваАН 13.02.26 СКЛ.3

//++АшаеваАН 13.02.26 СКЛ.3
Процедура cbr_СоздатьДокументПеремещениеТоваров(ЗаказНаПеремещение)
	
	МассивЗаказов = Новый Массив();
	МассивЗаказов.Добавить(ЗаказНаПеремещение);
	
	ПеремещениеТоваров = Документы.ПеремещениеТоваров.СоздатьДокумент();
	Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПеремещениеТоваров.Метаданные().ПолноеИмя());
	
	РеквизитыШапки = Менеджер.ДанныеЗаполненияНакладной(МассивЗаказов);
	
	ПараметрыЗаполнения = Менеджер.ПараметрыЗаполненияДокумента();
	ПараметрыЗаполнения.МассивЗаказов = МассивЗаказов;
	ПараметрыЗаполнения.РеквизитыШапки = РеквизитыШапки;
	ПараметрыЗаполнения.ЗаполнятьПоОрдеру = Ложь;
	
	ПеремещениеТоваров.Заполнить(ПараметрыЗаполнения);
	ПеремещениеТоваров.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры //--АшаеваАН 13.02.26 СКЛ.3

//++АшаеваАН 13.02.26 СКЛ.3
Функция cbr_Заказы(ПТУТовары)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПТУТовары.Номенклатура КАК Номенклатура,
	|	ПТУТовары.Характеристика КАК Характеристика,
	|	ПТУТовары.Организация КАК Организация,
	|	ПТУТовары.Склад КАК Склад
	|ПОМЕСТИТЬ втПТУТовары
	|ИЗ
	|	&ПТУТовары КАК ПТУТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение КАК ЗаказНаПеремещение,
	|	ЗаказыНаПеремещениеОстатки.Номенклатура КАК Номенклатура,
	|	ЗаказыНаПеремещениеОстатки.Характеристика КАК Характеристика,
	|	ЗаказыНаПеремещениеОстатки.ЗаказаноОстаток КАК ЗаказаноОстаток
	|ИЗ
	|	РегистрНакопления.ЗаказыНаПеремещение.Остатки(, (Номенклатура, Характеристика) В
	|		(ВЫБРАТЬ
	|			втПТУТовары.Номенклатура,
	|			втПТУТовары.Характеристика
	|		ИЗ
	|			втПТУТовары КАК втПТУТовары)) КАК ЗаказыНаПеремещениеОстатки
	|ГДЕ
	|	(ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение.СкладОтправитель,
	|		ЗаказыНаПеремещениеОстатки.ЗаказНаПеремещение.Организация) В
	|		(ВЫБРАТЬ
	|			втПТУТовары.Склад,
	|			втПТУТовары.Организация
	|		ИЗ
	|			втПТУТовары КАК втПТУТовары)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЗаказНаПеремещение";
	
	Запрос.УстановитьПараметр("ПТУТовары", ПТУТовары);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции //--АшаеваАН 13.02.26 СКЛ.3

//++АшаеваАН 13.02.26 СКЛ.3
Процедура cbr_РазбитьСтроку(ТоварыЗаказа, СтрокаЗаказа,Количество, ОбрабатываемыеСтроки)
	
	НоваяСтрока = ТоварыЗаказа.Вставить(ТоварыЗаказа.Индекс(СтрокаЗаказа) + 1);
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗаказа,,
	"НомерСтроки, cbr_ПТУ, ОкончаниеПоступления, НачалоОтгрузки, cbr_ЗаказПоставщику, КодСтроки");
	НоваяСтрока.КоличествоУпаковок = Количество;
	ОбрабатываемыеСтроки.Добавить(НоваяСтрока);
	
КонецПроцедуры //--АшаеваАН 13.02.26 СКЛ.3

//++АшаеваАН 13.02.26 СКЛ.3
Процедура cbr_ОбработатьСтрокиТЧ(ТЧТовары, ОбрабатываемыеСтроки)
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	КэшированныеЗначения = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
	ОбрабатываемыеСтроки,
	СтруктураДействий,
	ТЧТовары,
	КэшированныеЗначения);
	
КонецПроцедуры //--АшаеваАН 13.02.26 СКЛ.3

#КонецОбласти

#КонецЕсли
