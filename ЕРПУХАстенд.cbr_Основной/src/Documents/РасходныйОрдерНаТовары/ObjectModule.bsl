#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура cbr_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
     Возврат;
	КонецЕсли;
	//TODO: Вставить содержимое обработчика
	Если Отказ Тогда
		Возврат;	
	КонецЕсли;
	
	ТекущийСтатус = ЭтотОбъект.Статус;
	ПредыдущийСтатус = Неопределено;
	Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
		ПредыдущийСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка, "Статус");
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если Не ПредыдущийСтатус = ТекущийСтатус Тогда
			
			Если ТекущийСтатус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен") Тогда
				ДополнительныеСвойства.Вставить("НужноСоздатьДвижениеМатериалов", Истина);
			КонецЕсли;
			
			Документы.РасходныйОрдерНаТовары.УстановитьИсториюСтатусов(ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;	
КонецПроцедуры



&После("ОбработкаПроведения")
Процедура cbr_ОбработкаПроведения(Отказ, РежимПроведения)
	//TODO: Вставить содержимое обработчика
	НужноСоздатьДвижениеМатериалов = Ложь;
	ДополнительныеСвойства.Свойство("НужноСоздатьДвижениеМатериалов", НужноСоздатьДвижениеМатериалов);
	НужноСоздатьДвижениеМатериалов = ?(НужноСоздатьДвижениеМатериалов = Неопределено, Ложь, НужноСоздатьДвижениеМатериалов);
	
	Если НужноСоздатьДвижениеМатериалов Тогда
		ПередачаМатериалов = Документы.ДвижениеПродукцииИМатериалов.СоздатьПередачуМатериаловНаОснованииРасходногоОрдера(ЭтотОбъект.Ссылка);
		
		Если ПередачаМатериалов = Неопределено Тогда
			ТекстСообщения = НСтр("ru='Не удалось создать документ Передача материалов в производство'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Иначе
			ДокументПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПередачаМатериалов, "Проведен");
			Если ДокументПроведен Тогда
				ТекстСообщения = НСтр("ru='Создан документ: %1'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ПередачаМатериалов);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Иначе
				ТекстСообщения = НСтр("ru='Документ %1 не удалось провести'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ПередачаМатериалов);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
КонецПроцедуры

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура cbr_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Перем Отказ;

	Если ДанныеЗаполнения = "АвтоТест" Тогда
		Возврат;
	КонецЕсли;

	Если Не (ЗначениеЗаполнено(Склад)
		И ЗначениеЗаполнено(СкладскаяОперация)
		И (ЗначениеЗаполнено(Получатель) Или СкладскаяОперация = Перечисления.СкладскиеОперации.ОтгрузкаНаВнутренниеНужды)
		И ЗначениеЗаполнено(ДатаОтгрузки)) Тогда

		Отказ = Истина;

	Иначе

		ИспользоватьПомещения = СкладыСервер.ИспользоватьСкладскиеПомещения(Склад, ДатаОтгрузки, Истина);

		Если ИспользоватьПомещения
			И Не ЗначениеЗаполнено(Помещение) Тогда
			Отказ = Истина;
		КонецЕсли;

	КонецЕсли;

	Если Отказ = Истина Тогда
		ТекстСообщения = НСтр("ru = 'Расходный ордер на товары можно вводить только на основании распоряжения на отгрузку товаров.';
								|en = 'Goods issue can be entered only on the basis of a shipment reference.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;

	ИнициализироватьДокумент(ДанныеЗаполнения);

	ПараметрыУказанияСерий = Документы.РасходныйОрдерНаТовары.ПараметрыУказанияСерий(ЭтотОбъект);

	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий.ТоварыПоРаспоряжениям);

	Документы.РасходныйОрдерНаТовары.ЗаполнитьОтгружаемыеТоварыПоТоварамПоРаспоряжениям(ЭтотОбъект);

	Документы.РасходныйОрдерНаТовары.ЗаполнитьДействиеВСтроках(ЭтотОбъект);

	НоменклатураСервер.ЗаполнитьСерииПоFEFO(ЭтотОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары, Ложь);

	Если Не СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, ДатаОтгрузки)
		И ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры") Тогда
		#Удаление
		Документы.РасходныйОрдерНаТовары.РазбитьПоУпаковкамСправочно(ЭтотОбъект);
		#КонецУдаления
	КонецЕсли;

	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);

КонецПроцедуры
#КонецОбласти

#КонецЕсли