#Область ОбработчикиСобытийФормы

&Перед("ОбработкаПроверкиЗаполненияНаСервере")
&НаСервере
Процедура cbr_ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	НепроверяемыеРеквизиты = Новый Массив();
	
	Если Объект.Клиент и Не ЗначениеЗаполнено(Объект.cbr_ОтрасльКонтрагента) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не заполнена отрасль контрагента'");
		Сообщение.Сообщить();
		Отказ = Истина;
		НепроверяемыеРеквизиты.Добавить("cbr_ОтрасльКонтрагента");
	КонецЕсли;
	cbr_УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтрасльКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Настройки = Новый НастройкиКомпоновкиДанных();
	ГруппаОтбора = Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.Использование = Истина;
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	ГруппаОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	
	Если Объект.Клиент и Не Объект.Поставщик Тогда
		Элемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Клиент");
		Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = Истина;
	ИначеЕсли Объект.Поставщик и Не Объект.Клиент Тогда	
		Элемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Поставщик");
		Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = Истина;
	ИначеЕсли Не Объект.Клиент и Не Объект.Поставщик Тогда
		Элемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Клиент");
		Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = Ложь;
		
		Элемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Поставщик");
		Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = Ложь;
	ИначеЕсли Объект.Поставщик и Объект.Клиент Тогда 
		ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		
		Элемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Клиент");
		Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = Истина;
		
		Элемент = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Поставщик");
		Элемент.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Элемент.Использование = Истина;
		Элемент.ПравоеЗначение = Истина;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФиксированныеНастройки", Настройки);	
	
	СтруктураПараметров = Новый Структура;
	
	ОписаниеВыбора = Новый ОписаниеОповещения("cbr_ОбработкаВыбораОтраслиКонтрагента", ЭтотОбъект, СтруктураПараметров);
	
	ОткрытьФорму("Справочник.cbr_ОтраслиКонтрагентов.ФормаВыбора", ПараметрыФормы,,,,,ОписаниеВыбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура cbr_ОбработкаВыбораОтраслиКонтрагента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если Не ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли;
	Контейнер = Элементы.ОтрасльКонтрагента;
	Если Не Контейнер = Неопределено Тогда
		Объект.cbr_ОтрасльКонтрагента = РезультатЗакрытия;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура cbr_УдалитьНепроверяемыеРеквизитыИзМассива(МассивРеквизитов, МассивНепроверяемыхРеквизитов)

  Для Каждого ЭлементМассива Из МассивНепроверяемыхРеквизитов Цикл

    ПорядковыйНомер = МассивРеквизитов.Найти(ЭлементМассива);
    Если ПорядковыйНомер <> Неопределено Тогда
      МассивРеквизитов.Удалить(ПорядковыйНомер);
    КонецЕсли;
 
  КонецЦикла;
 
КонецПроцедуры

#КонецОбласти
