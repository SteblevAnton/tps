&НаСервере
Процедура ПолучитьДанныеНаСервере()
	//Получаем параметры для запроса
	ПараметрыЗапроса = ПолучитьПараметрыОтборов();
	
	//Формируем текст запроса
	ЕстьОтборыНоменклатуры = ПараметрыЗапроса.Свойство("НоменклатураОтборы") И ЗначениеЗаполнено(ПараметрыЗапроса["НоменклатураОтборы"]);
	ТекстЗапроса = Обработки.cbr_РеестрТМЦ.ТекстЗапросаТаблицаРасчетов(ЕстьОтборыНоменклатуры);
	
	//Обрабатываем результат
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Для каждого ЭлементОтбор из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ЭлементОтбор.Ключ, ЭлементОтбор.Значение);	
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	//Загружаем данные в ТЧ формы
	Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл		
		Пока Выборка.СледующийПоЗначениюПоля("Характеристика") Цикл
			НоваяСтрока = Объект.ТаблицаРасчетов.Добавить();
			Пока Выборка.Следующий() Цикл				
				ЗаполнитьЗначенияСвойств(Новаястрока, Выборка);  
				РазницаМесяцев = РазностьДатМесяц(Выборка.Период, ОбщегоНазначения.ТекущаяДатаПользователя());
				Если РазницаМесяцев >= 1 И РазницаМесяцев <= 11 Тогда
					НоваяСтрока["ИсторияИзмененияМесяц" + РазницаМесяцев] = Выборка.Цена;
				КонецЕсли;
				Если РазницаМесяцев = 0 Тогда
					НоваяСтрока["ИсторияИзмененияКрайняяЗакупка"] = Выборка.Цена;	
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;	
КонецПроцедуры

Функция РазностьДатМесяц(ДатаНачала,ДатаКонца)
	
	МесяцНачала = Месяц(ДатаНачала);
	МесяцКонца = Месяц(ДатаКонца);
	ЛетМеждуДатами = Год(ДатаКонца) - Год(ДатаНачала);
	РезультатВМесяцах = ЛетМеждуДатами * 12 - МесяцНачала + МесяцКонца;
	Возврат РезультатВМесяцах;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыОтборов()
	Результат = Новый Структура; 
	
	ЭлементыОтборов = Новый Массив;	
	ЭлементыОтборов.Добавить("НоменклатураОтборы");
	ЭлементыОтборов.Добавить("Объект.ДатаЗаписиПлановойЦены");
	ЭлементыОтборов.Добавить("Объект.ПризнакВключенияВДействующиеТехКарты");
	ЭлементыОтборов.Добавить("Объект.Статус");
	ЭлементыОтборов.Добавить("Объект.ТранспортнаяЧистаяЦена");
	ЭлементыОтборов.Добавить("Объект.ПризнакВключенияИндексаВАктуальнуюТехКарту");
		
	Для каждого ИмяЭлементаКДобавлению из ЭлементыОтборов Цикл
		
		Если ЗначениеЗаполнено(СтрНайти(ИмяЭлементаКДобавлению,"Объект")) Тогда
			ИмяЭлементаДляПоиска = Прав(ИмяЭлементаКДобавлению, СтрДлина(ИмяЭлементаКДобавлению) - СтрНайти(ИмяЭлементаКДобавлению,"."));
			ТекущийЭлементКДобавлению = ЭтотОбъект["Объект"][ИмяЭлементаДляПоиска];
		Иначе
			ТекущийЭлементКДобавлению = ЭтотОбъект[ИмяЭлементаКДобавлению];
			ИмяЭлементаДляПоиска = ИмяЭлементаКДобавлению;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущийЭлементКДобавлению) Тогда
			Результат.Вставить(ИмяЭлементаДляПоиска, ТекущийЭлементКДобавлению);
		Иначе
			Результат.Вставить(ИмяЭлементаДляПоиска, Неопределено);
		КонецЕсли;	
	КонецЦикла;
		
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПолучитьДанные(Команда)
	ПолучитьДанныеНаСервере();
КонецПроцедуры
