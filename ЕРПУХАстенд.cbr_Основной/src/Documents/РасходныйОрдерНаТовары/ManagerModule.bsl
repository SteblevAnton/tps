#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция СоздатьРасходныеОрдераИзЭтапа(ЭтапПроизводства) Экспорт

	РезультатФункции = Новый Массив;
	
	Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	СсылкаНаЭтап = ЭтапПроизводства;
	
	Если ТипЗнч(ЭтапПроизводства) = Тип("ДокументОбъект.ЭтапПроизводства2_2") Тогда
		Подразделение = ЭтапПроизводства.Подразделение;
		СсылкаНаЭтап = ЭтапПроизводства.Ссылка;
	ИначеЕсли ТипЗнч(ЭтапПроизводства) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		РеквизитыЭтапа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЭтапПроизводства, "Подразделение");
		Подразделение = РеквизитыЭтапа.Подразделение;
	Иначе
		ВызватьИсключение НСтр("ru = 'Тип переменной ЭтапПроизводства не соответствует ожидаемому'");
	КонецЕсли;
		
	// получаем номенклатуру и склады для оформления расходных ордеров
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыКОтгрузкеОстатки.Склад КАК Склад,
		|	ТоварыКОтгрузкеОстатки.Получатель КАК Получатель,
		|	ТоварыКОтгрузкеОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыКОтгрузкеОстатки.Характеристика КАК Характеристика,
		|	ТоварыКОтгрузкеОстатки.Назначение КАК Назначение,
		|	ТоварыКОтгрузкеОстатки.Серия КАК Серия,
		|	ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток - ТоварыКОтгрузкеОстатки.КСборкеОстаток - ТоварыКОтгрузкеОстатки.СобираетсяОстаток - ТоварыКОтгрузкеОстатки.СобраноОстаток КАК Количество
		|ПОМЕСТИТЬ ВТ_Основа
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Остатки(, ДокументОтгрузки = &ЭтапПроизводства) КАК ТоварыКОтгрузкеОстатки
		|ГДЕ
		|	ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток - ТоварыКОтгрузкеОстатки.КСборкеОстаток - ТоварыКОтгрузкеОстатки.СобираетсяОстаток - ТоварыКОтгрузкеОстатки.СобраноОстаток > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Основа.Склад КАК Склад,
		|	ВТ_Основа.Получатель КАК Получатель,
		|	СУММА(ВТ_Основа.Количество) КАК Количество
		|ИЗ
		|	ВТ_Основа КАК ВТ_Основа
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Основа.Склад,
		|	ВТ_Основа.Получатель
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВТ_Основа.Количество) > 0";
	
	Запрос.УстановитьПараметр("ЭтапПроизводства", СсылкаНаЭтап);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если НЕ СкладыСервер.ИспользоватьОрдернуюСхемуПриОтгрузке(Выборка.Склад, ТекущаяДатаСеанса()) Тогда
			ДокументПередача = Документы.ДвижениеПродукцииИМатериалов.СоздатьПередачуМатериаловНаОснованииЭтапа(СсылкаНаЭтап);
			Если ЗначениеЗаполнено(ДокументПередача) Тогда
				ТекстСообщения = НСтр("ru='Создан/перезаписан документ %1'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, ДокументПередача);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметровСозданияРасходныхОрдеров = СкладыСервер.ПараметрыПереоформленияРасходныхОрдеров();
		СтруктураПараметровСозданияРасходныхОрдеров.Склад = Выборка.Склад;
		СтруктураПараметровСозданияРасходныхОрдеров.Получатель = Выборка.Получатель;
		СтруктураПараметровСозданияРасходныхОрдеров.ЗонаОтгрузки = Справочники.СкладскиеЯчейки.ПустаяСсылка();
		СтруктураПараметровСозданияРасходныхОрдеров.РаспоряженияНаОтгрузку = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СсылкаНаЭтап);
		РезультатВыполнения = СкладыСервер.ПереоформитьРасходныеОрдера(СтруктураПараметровСозданияРасходныхОрдеров);
		
		Для Каждого СтрокаТаблицы Из РезультатВыполнения.ОформленныеОрдера Цикл
			
			РасходныйОрдер = СтрокаТаблицы.РасходныйОрдер;
			РеквизитыОрдера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РасходныйОрдер, "Проведен, Статус");
			Если Не РеквизитыОрдера.Проведен Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не СтрокаТаблицы.Действие = "Создан новый" Тогда
				Продолжить;
			КонецЕсли;
			
			Если РеквизитыОрдера.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Подготовлен") Тогда
				ОбъектОрдер = РасходныйОрдер.ПолучитьОбъект();
				ОбъектОрдер.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.КОтбору");
				ОбъектОрдер.Записать(РежимЗаписиДокумента.Проведение);
				
				РезультатФункции.Добавить(РасходныйОрдер);
				
				ТекстСообщения = НСтр("ru='Создан/перезаписан документ %1'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, РасходныйОрдер);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат РезультатФункции;

КонецФункции

Процедура УстановитьИсториюСтатусов(РасходныйОрдерОбъект) Экспорт

	НайденныеСтроки = РасходныйОрдерОбъект.cbr_ИсторияСтатусов.НайтиСтроки(Новый Структура("Статус", РасходныйОрдерОбъект.Статус));
	Если НайденныеСтроки.Количество() = 0 Тогда
		НоваяСтрока = РасходныйОрдерОбъект.cbr_ИсторияСтатусов.Добавить();
		НоваяСтрока.Статус = РасходныйОрдерОбъект.Статус;
		НоваяСтрока.ДатаИзменения = ТекущаяДатаСеанса();
		НоваяСтрока.Ответственный = Пользователи.ТекущийПользователь();
	КонецЕсли;

КонецПроцедуры

#КонецЕсли