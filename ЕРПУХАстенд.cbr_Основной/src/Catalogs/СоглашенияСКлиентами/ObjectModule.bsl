#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

//++AlopinAS 28.01.2026 ПРД.20
&После("ПриЗаписи")
Процедура cbr_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
    	Возврат;
	КонецЕсли;
	
	ФормированиеДокументаОперацияСоглашение();	

КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20


&После("ОбработкаПроверкиЗаполнения")
Процедура cbr_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПрверитьКореектностьЗаполнения() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&После("ПередЗаписью")
Процедура cbr_ПередЗаписью(Отказ)
		
	ОбработатьИзменениеДатыНачала();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++AlopinAS 16.02.2026 ПРД20
Процедура ОбработатьИзменениеДатыНачала()
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда    
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Тз.Номенклатура КАК Номенклатура,
		|	Тз.Характеристика КАК Характеристика,
		|	Тз.ДатаНачала КАК ДатаНачала,
		|	Тз.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТТЧАктуал
		|ИЗ
		|	&Тз КАК Тз
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТТЧИстор
		|ИЗ
		|	Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
		|ГДЕ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТТЧИстор.Номенклатура КАК Номенклатура,
		|	ВТТЧИстор.Характеристика КАК Характеристика,
		|	ВТТЧИстор.ДатаНачала КАК ДатаНачала,
		|	ВТТЧИстор.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТИсторИзмененнаяНоменклатура
		|ИЗ
		|	ВТТЧИстор КАК ВТТЧИстор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТЧАктуал КАК ВТТЧАктуал
		|		ПО ВТТЧИстор.Номенклатура = ВТТЧАктуал.Номенклатура
		|			И ВТТЧИстор.Характеристика = ВТТЧАктуал.Характеристика
		|			И ВТТЧИстор.ДатаНачала <> ВТТЧАктуал.ДатаНачала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	cbr_ОперацияСоглашенияСписокНоменклатура.Ссылка КАК Ссылка,
		|	cbr_ОперацияСоглашенияСписокНоменклатура.Номенклатура КАК Номенклатура,
		|	cbr_ОперацияСоглашенияСписокНоменклатура.Характеристика КАК Характеристика
		|ИЗ
		|	Документ.cbr_ОперацияСоглашения.СписокНоменклатура КАК cbr_ОперацияСоглашенияСписокНоменклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсторИзмененнаяНоменклатура КАК ВТИсторИзмененнаяНоменклатура
		|		ПО (ВТИсторИзмененнаяНоменклатура.Номенклатура = cbr_ОперацияСоглашенияСписокНоменклатура.Номенклатура)
		|			И (ВТИсторИзмененнаяНоменклатура.Характеристика = cbr_ОперацияСоглашенияСписокНоменклатура.Характеристика)
		|			И (ВТИсторИзмененнаяНоменклатура.ДатаНачала = cbr_ОперацияСоглашенияСписокНоменклатура.Ссылка.Дата)
		|			И (ВТИсторИзмененнаяНоменклатура.ДатаОкончания = cbr_ОперацияСоглашенияСписокНоменклатура.Ссылка.ДатаОкончания)
		|ГДЕ
		|	cbr_ОперацияСоглашенияСписокНоменклатура.Ссылка.ПометкаУдаления = ЛОЖЬ
		|ИТОГИ ПО
		|	Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Тз", cbr_ЦеныПоТоварам.Выгрузить());
	
	РезультатЗапроса = Запрос.Выполнить();
	//МассивДокументовДляИзменения = Новый Массив;
	ВыборкаИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);  
	Пока ВыборкаИтоги.Следующий() Цикл  
		ДокументОбъект = ВыборкаИтоги.Ссылка.ПолучитьОбъект();	
		СтруктураОтбора = Новый Структура;
		ВыборкаДетальныеЗаписи = ВыборкаИтоги.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл   
			СтруктураОтбора.Вставить("Номенклатура", ВыборкаДетальныеЗаписи.Номенклатура);
			СтруктураОтбора.Вставить("Характеристика", ВыборкаДетальныеЗаписи.Характеристика);
	        МассивСтрок = ДокументОбъект.СписокНоменклатура.НайтиСтроки(СтруктураОтбора);  
			Если МассивСтрок.Количество() > 0 Тогда
				СтрокаДляУдаления = МассивСтрок[0];
				ДокументОбъект.СписокНоменклатура.Удалить(СтрокаДляУдаления);
			КонецЕсли;  
		КонецЦикла; 
		
		Попытка     
			
			ТЧСписокНоменклатура = ДокументОбъект.СписокНоменклатура;
			
			Если ТЧСписокНоменклатура.Количество() тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Иначе     
				ДокументОбъект.ПометкаУдаления = Истина;
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);	
			КонецЕсли;
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка изменения документа операции по соглашению! Подробности смотри в журнале регистрации.'");
			ЗаписьЖурналаРегистрации("ОшибкаСозданияОперацииСоглашения", УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.cbr_ОперацияСоглашения, , ТекстСообщения + ОписаниеОшибки(),
				РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);

			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;	
	КонецЦикла;
	
КонецПроцедуры
//--AlopinAS 16.02.2026 ПРД20

//++AlopinAS 16.02.2026 ПРД20
Процедура ФормированиеДокументаОперацияСоглашение()
	
	Запрос = Новый Запрос;

	Запрос.Текст = "ВЫБРАТЬ
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Количество КАК Количество,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания,
	|	НоменклатураСписок.ЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТТЧ
	|ИЗ
	|	Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСписок
	|		ПО СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура = НоменклатураСписок.Ссылка
	|ГДЕ
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ЕдиницаИзмерения,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Количество,
	|	НоменклатураСписок.ЕдиницаИзмерения,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТЧ.ДатаНачала КАК ДатаНачала,
	|	ВТТЧ.ДатаОкончания КАК ДатаОкончания,
	|	ВТТЧ.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТГруппировка
	|ИЗ
	|	ВТТЧ КАК ВТТЧ
	|СГРУППИРОВАТЬ ПО
	|	ВТТЧ.ДатаНачала,
	|	ВТТЧ.ДатаОкончания,
	|	ВТТЧ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТГруппировка.ДатаНачала КАК ДатаНачала,
	|	ВТГруппировка.ДатаОкончания КАК ДатаОкончания,
	|	cbr_ОперацияСоглашения.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	ВТГруппировка КАК ВТГруппировка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.cbr_ОперацияСоглашения КАК cbr_ОперацияСоглашения
	|		ПО ВТГруппировка.ДатаНачала = cbr_ОперацияСоглашения.Дата
	|		И ВТГруппировка.ДатаОкончания = cbr_ОперацияСоглашения.ДатаОкончания
	|		И cbr_ОперацияСоглашения.Соглашение = ВТГруппировка.Ссылка
	|ГДЕ
	|	cbr_ОперацияСоглашения.Проведен = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТЧ.Номенклатура КАК Номенклатура,
	|	ВТТЧ.Характеристика КАК Характеристика,
	|	ВТТЧ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТТЧ.Количество КАК Количество,
	|	ВТТЧ.ДатаНачала КАК ДатаНачала,
	|	ВТТЧ.ДатаОкончания КАК ДатаОкончания,
	|	ВТТЧ.ОсновнаяЕдиницаИзмерения КАК ОсновнаяЕдиницаИзмерения,
	|	ВТДокументы.Ссылка КАК Ссылка
	|ИЗ
	|	ВТТЧ КАК ВТТЧ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
	|		ПО ВТТЧ.ДатаНачала = ВТДокументы.ДатаНачала
	|		И ВТТЧ.ДатаОкончания = ВТДокументы.ДатаОкончания
	|ИТОГИ
	|	МАКСИМУМ(Ссылка) КАК Ссылка
	|ПО
	|	ДатаНачала,
	|	ДатаОкончания";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДатаНачала = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДатаНачала.Следующий() Цикл
		
		ВыборкаДатаОкончания = ВыборкаДатаНачала.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, );
		Пока ВыборкаДатаОкончания.Следующий() Цикл
			
			Если ЗначениеЗаполнено(ВыборкаДатаОкончания.Ссылка) Тогда
				ДокументОбъект = ВыборкаДатаОкончания.Ссылка.ПолучитьОбъект();
				ДокументОбъект.СписокНоменклатура.Очистить();
				ДанныеДляФормирования = ВыборкаДатаОкончания.Выбрать();
				Пока ДанныеДляФормирования.Следующий() Цикл
					Новаястрока = ДокументОбъект.СписокНоменклатура.Добавить();
					ЗаполнитьЗначенияСвойств(Новаястрока, ДанныеДляФормирования);
				
					Коэфициент = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ДанныеДляФормирования.ЕдиницаИзмерения, ДанныеДляФормирования.Номенклатура);
					КоличетсвоВЭлементарныхЕдиницах = ДанныеДляФормирования.Количество * Коэфициент;
					Новаястрока.Количество = КоличетсвоВЭлементарныхЕдиницах;
					
				КонецЦикла;
				
			Иначе
				ДокументОбъект = Документы.cbr_ОперацияСоглашения.СоздатьДокумент();
				ДокументОбъект.Дата = ВыборкаДатаОкончания.ДатаНачала;
				ДокументОбъект.Соглашение = Ссылка;
				ДокументОбъект.Действие = Перечисления.cbr_ВидыДвиженияСоглашенияСКлиентами.Приход;
			
				ДокументОбъект.ДатаОкончания = ВыборкаДатаОкончания.ДатаОкончания;
			
				ДанныеДляФормирования = ВыборкаДатаОкончания.Выбрать();
			
				Пока ДанныеДляФормирования.Следующий() Цикл
				
					Новаястрока = ДокументОбъект.СписокНоменклатура.Добавить();
					ЗаполнитьЗначенияСвойств(Новаястрока, ДанныеДляФормирования);
				
					Коэфициент = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ДанныеДляФормирования.ЕдиницаИзмерения, ДанныеДляФормирования.Номенклатура);
					КоличетсвоВЭлементарныхЕдиницах = ДанныеДляФормирования.Количество * Коэфициент;
					Новаястрока.Количество = КоличетсвоВЭлементарныхЕдиницах;
				КонецЦикла;	  
				
			КонецЕсли;
		Попытка	
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка создания документа операции по соглашению! Подробности смотри в журнале регистрации.'");
			ЗаписьЖурналаРегистрации("ОшибкаСозданияОперацииСоглашения", УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.cbr_ОперацияСоглашения, , ТекстСообщения + ОписаниеОшибки(),
				РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецПопытки;	
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
//--AlopinAS 16.02.2026 ПРД20

//++AlopinAS 16.02.2026 ПРД20
Функция ПрверитьКореектностьЗаполнения()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.НомерСтроки КАК НомерСтроки,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
		|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТТЧ
		|ИЗ
		|	&Тз КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТТЧ.Номенклатура КАК Номенклатура,
		|	ВТТЧ.Характеристика КАК Характеристика,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТТЧ.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВТГруппировка
		|ИЗ
		|	ВТТЧ КАК ВТТЧ
		|СГРУППИРОВАТЬ ПО
		|	ВТТЧ.Номенклатура,
		|	ВТТЧ.Характеристика
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВТТЧ.НомерСтроки) > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТТЧ.Номенклатура КАК Номенклатура,
		|	ВТТЧ.Характеристика КАК Характеристика,
		|	ВТТЧ.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВТТЧ КАК ВТТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГруппировка КАК ВТГруппировка
		|		ПО ВТТЧ.Номенклатура = ВТГруппировка.Номенклатура
		|		И ВТТЧ.Характеристика = ВТГруппировка.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТТЧ.Номенклатура КАК Номенклатура,
		|	ВТТЧ.Характеристика КАК Характеристика
		|ИЗ
		|	ВТТЧ КАК ВТТЧ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТЧ КАК ВТТЧ1
		|		ПО ВТТЧ.Номенклатура = ВТТЧ1.Номенклатура
		|		И ВТТЧ.Характеристика = ВТТЧ1.Характеристика
		|		И (ВТТЧ.ДатаНачала <> ВТТЧ1.ДатаНачала
		|		ИЛИ ВТТЧ.ДатаОкончания <> ВТТЧ1.ДатаОкончания)
		|		И НЕ (ВТТЧ.ДатаОкончания < ВТТЧ1.ДатаНачала
		|		ИЛИ ВТТЧ.ДатаНачала > ВТТЧ1.ДатаОкончания)";
	
	
	Запрос.УстановитьПараметр("ТЗ", cbr_ЦеныПоТоварам.Выгрузить());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Если РезультатЗапроса[2].Пустой() и РезультатЗапроса[3].Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВыборкаДубли = РезультатЗапроса[2].Выбрать();
	
	Пока ВыборкаДубли.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Обнаружны дубли по номенклатуре ""%1"", характеристике ""%2"" на строке ""%3""'"),
			ВыборкаДубли.Номенклатура, ВыборкаДубли.Характеристика, ВыборкаДубли.НомерСтроки));	
	КонецЦикла;
	
	ВыборкаВхождениеДиапазонаДат = РезультатЗапроса[3].Выбрать();
	Пока ВыборкаВхождениеДиапазонаДат.Следующий() Цикл
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(НСтр("ru = 'Обнаружны пересечения промежутков дат по номенклатуре ""%1"", характеристике ""%2""'"),
			ВыборкаВхождениеДиапазонаДат.Номенклатура, ВыборкаВхождениеДиапазонаДат.Характеристика));
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции
//--AlopinAS 16.02.2026 ПРД20
#КонецОбласти

#КонецЕсли