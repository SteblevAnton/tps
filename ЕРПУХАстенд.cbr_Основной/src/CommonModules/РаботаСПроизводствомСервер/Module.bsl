
#Область СлужебныйПрограммныйИнтерфейс

Функция УстановленЗапретОткрытияСпецификации(РесурснаяСпецификация) Экспорт 
	
	// НАЧАЛО Османов Н.М. 18.12.2024 ЗАДАЧА Спецификация только чтение
	РезультатФункции = Ложь;
	
	Если Не УправлениеДоступом.ЕстьРоль("П3_ПросмотрФормыСпецификацииНеНаФасовку") Тогда
		
		ВидНоменклатуры = Неопределено;
		
		Если ТипЗнч(РесурснаяСпецификация) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
			ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
												РесурснаяСпецификация, 
												"ОсновноеИзделиеВидНоменклатуры");
		Иначе
			ВидНоменклатуры = РесурснаяСпецификация.ОсновноеИзделиеВидНоменклатуры;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
			Возврат РезультатФункции;			
		КонецЕсли;
		
		ЭтоПолуфабрикат = УправлениеСвойствами.ЗначениеСвойства(ВидНоменклатуры, "ЭтоПолуфабрикатВидНоменклатуры");
		
		Если ЭтоПолуфабрикат = Истина Тогда // не фасовка
			РезультатФункции = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат РезультатФункции;
	// КОНЕЦ Османов Н.М. 18.12.2024 ЗАДАЧА Спецификация только чтение
	
КонецФункции

Функция ВидыНоменклатурыПолуфабрикаты() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеСведения.Объект КАК ВидНоменклатуры
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|ГДЕ
		|	ДополнительныеСведения.Свойство = &Свойство
		|	И ДополнительныеСведения.Значение = ИСТИНА";
	
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ЭтоПолуфабрикатВидНоменклатуры"));
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	РезультатФункции = Выгрузка.ВыгрузитьКолонку("ВидНоменклатуры");

	Возврат РезультатФункции;
	
КонецФункции

Функция ДокументыОснованияПланаПроизводства(СписокПлановПроизводства) Экспорт

	РезультатФункции = Новый ТаблицаЗначений;
	
	ТаблицаГуидовДокументов = Новый ТаблицаЗначений;
	ТаблицаГуидовДокументов.Колонки.Добавить("ПланПроизводства", Новый ОписаниеТипов("ДокументСсылка.ПланПроизводства"));
	ТаблицаГуидовДокументов.Колонки.Добавить("ГУИД", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	Для Каждого ДокументПланПроизводства Из СписокПлановПроизводства Цикл
		НоваяСтрока = ТаблицаГуидовДокументов.Добавить();
		НоваяСтрока.ПланПроизводства = ДокументПланПроизводства.Значение;
		НоваяСтрока.ГУИД = Строка(ДокументПланПроизводства.Значение.УникальныйИдентификатор());
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаГУИДов.ПланПроизводства КАК ПланПроизводства,
		|	ТаблицаГУИДов.ГУИД КАК ГУИД
		|ПОМЕСТИТЬ ВТ_ТаблицаГУИДов
		|ИЗ
		|	&ТаблицаГУИДов КАК ТаблицаГУИДов
		|
		|;
		|ВЫБРАТЬ
		|	ТаблицаГУИДов.ПланПроизводства КАК ПланПроизводства,
		|	ПлановаяКалькуляция2_2ДополнительныеРеквизиты.Ссылка КАК ПлановаяКалькуляция,
		|	ПлановаяКалькуляция_Квартал.Значение КАК Квартал
		|ИЗ
		|	Документ.ПлановаяКалькуляция2_2.ДополнительныеРеквизиты КАК ПлановаяКалькуляция2_2ДополнительныеРеквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаГУИДов КАК ТаблицаГУИДов
		|		ПО ПлановаяКалькуляция2_2ДополнительныеРеквизиты.Значение = ТаблицаГУИДов.ГУИД
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция2_2.ДополнительныеРеквизиты КАК ПлановаяКалькуляция_Квартал
		|		ПО (ПлановаяКалькуляция2_2ДополнительныеРеквизиты.Ссылка = ПлановаяКалькуляция_Квартал.Ссылка)
		|			И (ПлановаяКалькуляция_Квартал.Свойство = &СвойствоКварталПлановаяКалькуляция)
		|ГДЕ
		|	ПлановаяКалькуляция2_2ДополнительныеРеквизиты.Свойство = &СвойствоДокументОснование
		|	И НЕ ПлановаяКалькуляция2_2ДополнительныеРеквизиты.Ссылка.ПометкаУдаления";
	
	СвойствоДокументОснование = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ДокументОснованиеПланаКалькуляции");
	СвойствоКвартал = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "КварталПлановаяКалькуляция");
	
	Запрос.УстановитьПараметр("ТаблицаГУИДов", ТаблицаГуидовДокументов);
	Запрос.УстановитьПараметр("ПланыПроизводства", СписокПлановПроизводства);
	Запрос.УстановитьПараметр("СвойствоДокументОснование", СвойствоДокументОснование);
	Запрос.УстановитьПараметр("СвойствоКварталПлановаяКалькуляция", СвойствоКвартал);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РезультатФункции = РезультатЗапроса.Выгрузить();
	
	Возврат РезультатФункции;
	
КонецФункции // ДокументыОснованияПланаПроизводства()


#КонецОбласти
