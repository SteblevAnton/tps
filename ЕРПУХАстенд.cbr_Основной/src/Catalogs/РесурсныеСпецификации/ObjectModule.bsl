#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура cbr_ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Обеспечение.Номенклатура КАК Номенклатура,
		|	Обеспечение.Характеристика КАК Характеристика,
		|	Обеспечение.Упаковка КАК Упаковка,
		|	Обеспечение.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ОбеспечениеПредварительно
		|ИЗ
		|	&Обеспечение КАК Обеспечение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Действия.Номенклатура КАК Номенклатура,
		|	Действия.Характеристика КАК Характеристика,
		|	Действия.Упаковка КАК Упаковка,
		|	Действия.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ДействияПредварительно
		|ИЗ
		|	&Действия КАК Действия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОбеспечениеПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ОбеспечениеПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Обеспечение
		|ИЗ
		|	ВТ_ОбеспечениеПредварительно КАК ВТ_ОбеспечениеПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ОбеспечениеПредварительно.Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДействияПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ДействияПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Действия
		|ИЗ
		|	ВТ_ДействияПредварительно КАК ВТ_ДействияПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДействияПредварительно.Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Действия.Номенклатура КАК Номенклатура,
		|	ВТ_Действия.Характеристика КАК Характеристика,
		|	ВТ_Действия.Упаковка КАК Упаковка,
		|	ВТ_Действия.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Разница
		|ИЗ
		|	ВТ_Действия КАК ВТ_Действия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Обеспечение.Номенклатура,
		|	ВТ_Обеспечение.Характеристика,
		|	ВТ_Обеспечение.Упаковка,
		|	-ВТ_Обеспечение.КоличествоУпаковок
		|ИЗ
		|	ВТ_Обеспечение КАК ВТ_Обеспечение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Разница.Номенклатура КАК Номенклатура,
		|	ВТ_Разница.Характеристика КАК Характеристика,
		|	ВТ_Разница.Упаковка КАК Упаковка,
		|	СУММА(ВТ_Разница.КоличествоУпаковок) КАК КоличествоУпаковок
		|ИЗ
		|	ВТ_Разница КАК ВТ_Разница
		|ГДЕ
		|	НЕ ВТ_Разница.Номенклатура ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Разница.Номенклатура,
		|	ВТ_Разница.Характеристика,
		|	ВТ_Разница.Упаковка
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВТ_Разница.КоличествоУпаковок) <> 0";
	
	Запрос.УстановитьПараметр("Обеспечение", МатериалыИУслуги.Выгрузить());
	Запрос.УстановитьПараметр("Действия", Действия.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.КоличествоУпаковок > 0 Тогда
			
			ТекстСообщения = НСтр("ru='ВНИМАНИЕ! По номенклатуре: %1 и характеристике: %2 количество на вкладке Действия превышает количество из вкладки Материалы и работы'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Номенклатура, Выборка.Характеристика);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		Иначе
			
			ТекстСообщения = НСтр("ru='Внимание! По номенклатуре: %1 и характеристике: %2 недостаточно количества (%3) на вкладке Действия'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Номенклатура, Выборка.Характеристика, -Выборка.КоличествоУпаковок);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
	// НАЧАЛО Османов Н.М. 18.10.2024
	
	Если ВыходныеИзделия.Количество() > 0 И ЗначениеЗаполнено(ВыходныеИзделия[0].Номенклатура) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(&Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
		|ПОМЕСТИТЬ ВТ_Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НормыТехнологическихПотерьИВремениИзготовленияСрезПоследних.МинимальныйОбъем КАК МинимальныйОбъем
		|ИЗ
		|	РегистрСведений.НормыТехнологическихПотерьИВремениИзготовления.СрезПоследних(
		|			,
		|			ГруппаНоменклатуры В
		|				(ВЫБРАТЬ
		|					Т.Номенклатура.Родитель
		|				ИЗ
		|					ВТ_Номенклатура КАК Т)) КАК НормыТехнологическихПотерьИВремениИзготовленияСрезПоследних";
		
		Запрос.УстановитьПараметр("Номенклатура", ВыходныеИзделия[0].Номенклатура);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru = 'Для номенклатуры продукции не заданы нормы технологических потерь.'");
			Если Статус = ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует") Тогда
				//ТекстСообщения = СтрШаблон("%1 Не удалось записать изменения.", ТекстСообщения);
				//Отказ = Истина;
			КонецЕсли;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 18.10.2024
	
	Если Не Отказ И П3_Активность Тогда
		
		УжеАктивнаяСпецификация = Справочники.РесурсныеСпецификации.АктивнаяСпецификация(ЭтотОбъект);
		Если ЗначениеЗаполнено(УжеАктивнаяСпецификация) Тогда
			ТекстОшибки = НСтр("ru = 'Уже существует действующая ресурсная спецификация со статусом ""Активная"": %1.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, УжеАктивнаяСпецификация);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&После("ОбработкаПроверкиЗаполнения")
Процедура cbr_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(ЭтотОбъект.ОсновноеИзделиеХарактеристика) Тогда
		УпаковкаИзХарактеристики = УправлениеСвойствами.ЗначениеСвойства(ЭтотОбъект.ОсновноеИзделиеХарактеристика, "УпаковкаРеквизитХарактеристики");
		
//			Если Не ЭтотОбъект.ОсновноеИзделиеУпаковка = УпаковкаИзХарактеристики Тогда
//				ТекстСообщения = НСтр("ru='Запись отменена! Выбранная на основной вкладке упаковка не соответствует упаковке в выбранной характеристике.'");
//				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
//				Если Не Пользователи.ЭтоПолноправныйПользователь(Пользователи.ТекущийПользователь()) Тогда
//					Отказ = Истина;
//				КонецЕсли;
//				Возврат;
//			КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли