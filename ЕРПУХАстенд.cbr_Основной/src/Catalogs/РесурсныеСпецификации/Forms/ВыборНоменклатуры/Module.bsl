
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ТаблицаВыбора") Тогда
		ТекстИсключения = НСтр("ru='Форма выбора предназначена только для работы с параметром ТаблицаВыбора'");
		ВызватьИсключение ТекстИсключения;		
	КонецЕсли;
	
	ТаблицаДанных = ПолучитьИзВременногоХранилища(Параметры.ТаблицаВыбора);
	ТаблицаВыбора.Загрузить(ТаблицаДанных);
	
	Элементы.ТаблицаВыбора.ОтборСтрок = Новый ФиксированнаяСтруктура("СоответствиеОтбору", Истина);
	Для Каждого СтрокаТаблицы Из ТаблицаВыбора Цикл
		СтрокаТаблицы.СоответствиеОтбору = ?(СтрокаТаблицы.Разница = 0, Ложь, Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказатьВсеПриИзменении(Элемент)
	
	Элементы.ТаблицаВыбораКоличествоОбеспечение.Видимость = ПоказатьВсе;
	Элементы.ТаблицаВыбораКоличествоДействия.Видимость = ПоказатьВсе;
	
	Для Каждого СтрокаТаблицы Из ТаблицаВыбора Цикл
		СтрокаТаблицы.СоответствиеОтбору = ?(СтрокаТаблицы.Разница = 0, Ложь ИЛИ ПоказатьВсе, Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаВыбора

&НаКлиенте
Процедура ТаблицаВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ОповещениеОВыборе(Элементы.ТаблицаВыбора.ДанныеСтроки(ВыбраннаяСтрока));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекущаяСтрока = Элементы.ТаблицаВыбора.ТекущиеДанные;
	Если Не ТекущаяСтрока = Неопределено Тогда
		ОповещениеОВыборе(ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОповещениеОВыборе(ТекущаяСтрока)
	
	СтруктураВозврата = Новый Структура("Номенклатура, Характеристика, Упаковка");
	ЗаполнитьЗначенияСвойств(СтруктураВозврата, ТекущаяСтрока);
	
	ОповеститьОВыборе(СтруктураВозврата);
	
КонецПроцедуры

#КонецОбласти