#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция УсловияВидимостиСпецтребований() Экспорт
	
	РезультатФункции = Новый Структура;
	
	РезультатФункции.Вставить("Логистические", Истина);
	РезультатФункции.Вставить("УпаковкаИКомплектация", Истина);
	РезультатФункции.Вставить("УсловияГСХ", Истина);
	РезультатФункции.Вставить("Прочие", Истина);
	
	Возврат РезультатФункции;
	
КонецФункции

Процедура ВывестиТаблицуСпецТребований(Форма, ГруппаСтраницы, УсловияВидимости, СсылкаНаОбъект = Неопределено) Экспорт
	
	Если СсылкаНаОбъект = Неопределено Тогда
		СсылкаНаОбъект = Форма.Объект.Ссылка;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСпецтребования = РегистрыСведений.РеестрСпециальныхТребований.СпециальныеТребованияПоОбъекту(СсылкаНаОбъект);
	
	Если ДанныеСпецтребования.НайденыЗаписи = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыГруппы = Новый Структура("ОтображатьЗаголовок, Группировка, РастягиватьПоГоризонтали", 
									  Истина, 
									  ГруппировкаПодчиненныхЭлементовФормы.Вертикальная,
									  Истина);
	СтраницаСпецТребований = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"СтраницаСпецтребований",
												"Специальные требований",
												ВидГруппыФормы.Страница,
												ГруппаСтраницы,
												,
												ПараметрыГруппы);
												
	ГруппаСтраницыСпецтребований = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"ГруппаСтраницыСпецтребований",
												"Специальные требований",
												ВидГруппыФормы.Страницы,
												СтраницаСпецТребований,
												,
												ПараметрыГруппы);
												
	СтраницаЛогистические = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"СтраницаЛогистические",
												"Логистические",
												ВидГруппыФормы.Страница,
												ГруппаСтраницыСпецтребований,
												,
												ПараметрыГруппы);
	СтраницаЛогистические.Видимость = УсловияВидимости.Логистические;
												
	СтраницаКомплектация = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"СтраницаКомплектация",
												"Требования к упаковке и комплектации",
												ВидГруппыФормы.Страница,
												ГруппаСтраницыСпецтребований,
												,
												ПараметрыГруппы);
	СтраницаКомплектация.Видимость = УсловияВидимости.УпаковкаИКомплектация;
	
	СтраницаУсловияГСХ = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"СтраницаУсловияГСХ",
												"Условия ГСХ при поставке",
												ВидГруппыФормы.Страница,
												ГруппаСтраницыСпецтребований,
												,
												ПараметрыГруппы);
	СтраницаУсловияГСХ.Видимость = УсловияВидимости.УсловияГСХ;
	
	СтраницаПрочиеТребования = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"СтраницаПрочиеТребования",
												"Прочие требования",
												ВидГруппыФормы.Страница,
												ГруппаСтраницыСпецтребований,
												,
												ПараметрыГруппы);
	СтраницаПрочиеТребования.Видимость = УсловияВидимости.Прочие;
	
	РеквизитыСпецтребований = ДобавитьРеквизитыСпецтребований(Форма);
	
	СвойстваРеквизиты = "ДокументыДляВодителя,ДопустимыйОстатокГСХВМесяцах,ДопустимыйОстатокГСХВПроцентах,
						|НеБолееЧемОтДатыПроизводства,ПрочиеТребования,ТаможенноеСопровождение,ДокументСпецтребование,
						|ТребованиеККомплектации,ТребованиеКУпаковке,ТребованиеКЭтикетке,УсловияДоставки,СтатусСогласования";
	МассивСвойствРеквизитов = СтрРазделить(СвойстваРеквизиты, ",", Ложь);
	СтруктураЗаполнения = Новый Структура;
	Для Каждого РеквизитСпецтребования Из МассивСвойствРеквизитов Цикл
		ИмяРеквизита = СтрШаблон("Спецтребование%1", СокрЛП(РеквизитСпецтребования)); 
		Форма[ИмяРеквизита] = ДанныеСпецтребования[СокрЛП(РеквизитСпецтребования)];
	КонецЦикла;
	
	ПараметрыГруппы = Новый Структура("ОтображатьЗаголовок, Группировка", Ложь, ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
	ГруппаДокументаИСогласования = ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, 
												"ГруппаДокументаИСогласования",
												"ГруппаДокументаИСогласования",
												ВидГруппыФормы.ОбычнаяГруппа,
												СтраницаСпецТребований,
												ГруппаСтраницыСпецтребований,
												ПараметрыГруппы);
	
	ПараметрыСпециальногоТребования = Новый Структура;
	ПараметрыСпециальногоТребования.Вставить("Гиперссылка", Истина);
	ПараметрыСпециальногоТребования.Вставить("ПоложениеЗаголовка", ПоложениеЗаголовкаЭлементаФормы.Нет);
	ЭлементСпецтребование = ФормыУХ.СоздатьПолеФормы(Форма.Элементы,
												"ЭлементСпецтребованиеДокумент",
												,
												"СпецтребованиеДокументСпецтребование",
												ВидПоляФормы.ПолеНадписи,
												ГруппаДокументаИСогласования,
												,
												ПараметрыСпециальногоТребования);
												
	Если Не ДанныеСпецтребования.СтатусСогласования = Неопределено Тогда
		ПараметрыСпециальногоТребования.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
		ЭлементСпецтребование = ФормыУХ.СоздатьПолеФормы(Форма.Элементы,
												"ЭлементСпецтребованиеСтатусСогласования",
												"Статус согласования:",
												"СпецтребованиеСтатусСогласования",
												ВидПоляФормы.ПолеНадписи,
												ГруппаДокументаИСогласования,
												,
												ПараметрыСпециальногоТребования);
	КонецЕсли;
	
	ДобавитьЭлементыСпецтребований(Форма, РеквизитыСпецтребований, СтраницаЛогистические);
	
	МногострочныеПоляСтрока = "ПрочиеТребования,ТаможенноеСопровождение,ТребованиеККомплектации,
							  |ТребованиеКУпаковке,ТребованиеКЭтикетке";
	МногострочныеПоля = СтрРазделить(МногострочныеПоляСтрока, ",");
	Для Каждого ПолеМногоСтрок Из МногострочныеПоля Цикл
		НазваниеЭлемента = СтрШаблон("ЭлементСпецтребование%1", СокрЛП(ПолеМногоСтрок));
		Форма.Элементы[НазваниеЭлемента].МногострочныйРежим = Истина;
	КонецЦикла;
	
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеТребованиеКУпаковке"], СтраницаКомплектация);
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеТребованиеКЭтикетке"], СтраницаКомплектация);
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеТребованиеККомплектации"], СтраницаКомплектация);
	
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеНеБолееЧемОтДатыПроизводства"], СтраницаУсловияГСХ);
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеДопустимыйОстатокГСХВПроцентах"], СтраницаУсловияГСХ);
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеДопустимыйОстатокГСХВМесяцах"], СтраницаУсловияГСХ);
	
	Форма.Элементы.Переместить(Форма.Элементы["ЭлементСпецтребованиеПрочиеТребования"], СтраницаПрочиеТребования);
		
КонецПроцедуры

Функция ДобавитьРеквизитыСпецтребований(Форма)
	
	РеквизитыФормы = ФормыУХ.НачатьИзменениеРеквизитовФормы(Форма);
	
	КромеРеквизитов = "Договор,Комментарий,Основание,Ответственный";
	МассивКромеРеквизитов = СтрРазделить(КромеРеквизитов, ",");
	
	РеквизитыСпецтребований = Метаданные.Документы.СпециальныеТребования.Реквизиты;
	Для Каждого РеквизитСпецтребования Из РеквизитыСпецтребований Цикл
		
		Если Не МассивКромеРеквизитов.Найти(РеквизитСпецтребования.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизита = СтрШаблон("Спецтребование%1", РеквизитСпецтребования.Имя);
		ФормыУХ.СоздатьДобавитьРеквизитФормы(Форма, 
											 РеквизитыФормы.Добавляемые,
											 ИмяРеквизита,
											 РеквизитСпецтребования.Тип,
											 ,
											 РеквизитСпецтребования.Синоним,
											 Ложь);
	КонецЦикла;
	
	ФормыУХ.СоздатьДобавитьРеквизитФормы(Форма, 
										 РеквизитыФормы.Добавляемые,
										 "СпецтребованиеДокументСпецтребование",
										 Новый ОписаниеТипов("ДокументСсылка.СпециальныеТребования"));
										 
	ФормыУХ.СоздатьДобавитьРеквизитФормы(Форма, 
										 РеквизитыФормы.Добавляемые,
										 "СпецтребованиеСтатусСогласования",
										 Новый ОписаниеТипов("ПеречислениеСсылка.СостоянияСогласования"));

	ФормыУХ.ЗавершитьИзменениеРеквизитовФормы(Форма, РеквизитыФормы);
	
	Возврат РеквизитыФормы.Добавляемые;
	
КонецФункции

Процедура ДобавитьЭлементыСпецтребований(Форма, Реквизиты, Родитель)
	
	ПараметрыЭлемента = Новый Структура("ТолькоПросмотр", Истина);
	
	Для Каждого РеквизитФормы Из Реквизиты Цикл
		
		Если РеквизитФормы.Имя = "СпецтребованиеДокументСпецтребование"
			Или РеквизитФормы.Имя = "СпецтребованиеСтатусСогласования"
			Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементСпецтребованиеДокументВодителя = ФормыУХ.СоздатьПолеФормы(Форма.Элементы,
												СтрШаблон("Элемент%1", РеквизитФормы.Имя),
												,
												РеквизитФормы.Имя,
												ВидПоляФормы.ПолеВвода,
												Родитель,
												,
												ПараметрыЭлемента);
												
	КонецЦикла;
		
КонецПроцедуры

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("ПроведениеСпециальныеТребования");
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица<ИмяРегистра> - ТаблицаЗначений - таблица данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.СпециальныеТребования") Тогда
		ДокументСсылка = Документ.Ссылка;
	Иначе
		ДокументСсылка = Документ;
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		ТекстЗапросаТаблицаСпециальныеТребования(Запрос, ТекстыЗапроса, Регистры);
		
	КонецЕсли;
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

// Добавляет команду создания документа "Специальные требования".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	ТаблицаЗначений, Неопределено - сформированные команды для вывода в подменю.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.СпециальныеТребования) Тогда
		
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.СпециальныеТребования.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.СпециальныеТребования);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период
	|ИЗ
	|	Документ.СпециальныеТребования КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	Реквизиты = РезультатЗапроса.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаСпециальныеТребования(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрСпециальныхТребований";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	&Период КАК Период,
	               |	СпециальныеТребования.Договор КАК Договор,
	               |	СпециальныеТребования.ДокументыДляВодителя КАК ДокументыДляВодителя,
	               |	СпециальныеТребования.ДопустимыйОстатокГСХВМесяцах КАК ДопустимыйОстатокГСХВМесяцах,
	               |	СпециальныеТребования.ДопустимыйОстатокГСХВПроцентах КАК ДопустимыйОстатокГСХВПроцентах,
	               |	СпециальныеТребования.НеБолееЧемОтДатыПроизводства КАК НеБолееЧемОтДатыПроизводства,
	               |	СпециальныеТребования.Основание КАК Основание,
	               |	СпециальныеТребования.ПрочиеТребования КАК ПрочиеТребования,
	               |	СпециальныеТребования.ТаможенноеСопровождение КАК ТаможенноеСопровождение,
	               |	СпециальныеТребования.ТребованиеККомплектации КАК ТребованиеККомплектации,
	               |	СпециальныеТребования.ТребованиеКУпаковке КАК ТребованиеКУпаковке,
	               |	СпециальныеТребования.ТребованиеКЭтикетке КАК ТребованиеКЭтикетке,
	               |	СпециальныеТребования.УсловияДоставки КАК УсловияДоставки
	               |ИЗ
	               |	Документ.СпециальныеТребования КАК СпециальныеТребования
	               |ГДЕ
	               |	СпециальныеТребования.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Отчеты.ДвиженияДокумента.ДобавитьКомандуОтчетОДвиженияхДокумента(КомандыОтчетов, Параметры);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли