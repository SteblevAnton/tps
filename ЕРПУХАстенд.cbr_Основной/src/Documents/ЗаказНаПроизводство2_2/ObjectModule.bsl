#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура П3_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// НАЧАЛО Османов Н.М. 21.03.2025 
	ОрганизацияТПС = Константы.ОрганизацияТПС.ЗначениеЭтойКонстанты();

	Если Не Организация = ОрганизацияТПС Тогда
		ТекстСообщения = НСтр("ru='Организация для этого документа должна быть: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОрганизацияТПС);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 21.03.2025 
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	ТаблицаПродукции = Продукция.Выгрузить(, "Номенклатура, Характеристика, Спецификация");
	ТаблицаПродукции.Свернуть("Номенклатура, Характеристика, Спецификация");
	Если ТаблицаПродукции.Количество() > 1 Тогда
		
		ТекстСообщения = НСтр("ru='В таблице ""Продукция"" присутствуют разные позиции Номенклатуры, Характеристики и Спецификации!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ); 
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	
КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьПоЗаказуКлиента")
Процедура П3_ЗаполнитьПоЗаказуКлиента(ДанныеЗаполнения)

	ЗаказКлиента = ДанныеЗаполнения.Основание;
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказКлиента.Приоритет          КАК Приоритет,
	|	ЗаказКлиента.Организация        КАК Организация,
	|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР 
	|		КОГДА ЗаказКлиента.НаправлениеДеятельности.УчетЗатрат
	|			ТОГДА ЗаказКлиента.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка = &ЗаказКлиента");

	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);

	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	// Заполнение шапки
	Организация             = Реквизиты.Организация;
	ДокументОснование       = ЗаказКлиента;
	ЗаказПодДеятельность    = Реквизиты.НалогообложениеНДС;
	Приоритет               = Реквизиты.Приоритет;
	НаправлениеДеятельности = Реквизиты.НаправлениеДеятельности;

	// Заполнение табличной части
	ЗаполнитьПоТаблицеТоварыИзХранилища(ДанныеЗаполнения.АдресТовары, "ДатаОтгрузки");
	
	#Вставка // НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	Если Не Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству") Тогда
		СпецификацииЗаполнены = Истина;
		Для Каждого СтрокаПродукции Из Продукция Цикл
			Если Не ЗначениеЗаполнено(СтрокаПродукции.Спецификация) Тогда
				СпецификацииЗаполнены = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СпецификацииЗаполнены Тогда
			Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству"); 
		Иначе
			Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2.АктуализацияСпецификаций"); 
		КонецЕсли;
	КонецЕсли;
	#КонецВставки // КОНЕЦ Алейников М.Н. 24.03.2025 Б121
	
	#Вставка // НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	СвойствоУпаковка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "УпаковкаРеквизитХарактеристики");
	МассивСвойств = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвойствоУпаковка);
	
	Для Каждого СтрокаПродукции Из Продукция Цикл
		ТаблицаСвойствХарактеристик = УправлениеСвойствами.ЗначенияСвойств(СтрокаПродукции.Характеристика, Истина, , МассивСвойств);
		
		Если ТаблицаСвойствХарактеристик.Количество() Тогда
			ЗначениеСвойстваХарактеристика = ТаблицаСвойствХарактеристик[0].Значение;
			Если Не СтрокаПродукции.Упаковка = ЗначениеСвойстваХарактеристика Тогда
				КоэффициентУпаковки= Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки
				(ЗначениеСвойстваХарактеристика, СтрокаПродукции.Номенклатура);
				СтрокаПродукции.КоличествоУпаковок = СтрокаПродукции.КоличествоУпаковок / КоэффициентУпаковки;
				СтрокаПродукции.Упаковка = ЗначениеСвойстваХарактеристика;
			КонецЕсли;	
		КонецЕсли;	
		
	КонецЦикла;
	#КонецВставки // КОНЕЦ Алейников М.Н. 24.03.2025 Б121

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&ИзменениеИКонтроль("ЗаполнитьДанныеСпецификацииВСтроках")
Процедура cbr_ЗаполнитьДанныеСпецификацииВСтроках(ТолькоНезаполненные)

	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделиях.ПараметрыВыбораСпецификаций(ЭтотОбъект, Документы.ЗаказНаПроизводство2_2);

	// Заполнение спецификаций
	МассивДанных = Новый Массив;
	МассивСтрок = Новый Массив;

	Для каждого Строка Из Продукция Цикл

		Если ТолькоНезаполненные И Не Строка.Спецификация.Пустая() Тогда
			Продолжить;
		КонецЕсли;

		ДанныеОбИзделии = УправлениеДаннымиОбИзделияхКлиентСервер.СобратьДанныеОбИзделииДляВыбораСпецификации(
			ЭтотОбъект, Строка, ПараметрыВыбораСпецификаций);

		МассивДанных.Добавить(ДанныеОбИзделии);
		МассивСтрок.Добавить(Строка);

	КонецЦикла;

	#Удаление
	// Заполним спецификации
	УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций);
	#КонецУдаления
	
	#Вставка
	
	//++Zhiend 13.02.2026 ПРЗ.30
	
	Если Не ДокументОснование = Неопределено Тогда
		cbr_ЗаполнитьСпецификациюВСтрокахПоСоглашению (МассивСтрок, МассивДанных, ДокументОснование);
		УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций,, ИСТИНА);
	Иначе
		УправлениеДаннымиОбИзделиях.ЗаполнитьСпецификациюВСтроках(МассивСтрок, МассивДанных, ПараметрыВыбораСпецификаций);
	КонецЕсли;
	
	//--Zhiend 13.02.2026 
	
	#КонецВставки
	
КонецПроцедуры

//++Zhiend 16.02.2026 ПРЗ.30
Процедура cbr_ЗаполнитьСпецификациюВСтрокахПоСоглашению (МассивСтрок, ДанныеОбИзделиях, Основание)
	
	СпецификацииИзделий = cbr_СпецификацииИзделийПоСоглашению(ДанныеОбИзделиях, Основание);
	
	ИмяПоля = "Спецификация";
	ИндексДанных = 0;
	Для каждого ТекущаяСтрока Из МассивСтрок Цикл
		
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока[ИмяПоля]) Тогда
			
			ДанныеСпецификации = СпецификацииИзделий[ИндексДанных];
			
			Если ДанныеСпецификации <> Неопределено Тогда
				ТекущаяСтрока[ИмяПоля] = ДанныеСпецификации.Спецификация;
			Иначе
				ТекущаяСтрока[ИмяПоля] = Неопределено;
			КонецЕсли;
						
		КонецЕсли;
		
		ДанныеОбИзделиях[ИндексДанных].Вставить("Спецификация", ТекущаяСтрока[ИмяПоля]);
		ИндексДанных = ИндексДанных + 1;
		
	КонецЦикла;
	
КонецПроцедуры//--Zhiend 16.02.2026

//++Zhiend 17.02.2026 ПРЗ.30
Функция cbr_СпецификацииИзделийПоСоглашению(ДанныеОбИзделиях, Основание)

	Результат = Новый Массив(ДанныеОбИзделиях.Количество());
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РесурсныеСпецификации.ОсновноеИзделиеНоменклатура КАК ОсновноеИзделиеНоменклатура,
		|	РесурсныеСпецификации.ОсновноеИзделиеХарактеристика КАК ОсновноеИзделиеХарактеристика,
		|	СоглашенияСКлиентамиcbr_РесурсныеСпецификации.РесурснаяСпецификация КАК РесурснаяСпецификация
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СоглашенияСКлиентами.cbr_РесурсныеСпецификации КАК
		|			СоглашенияСКлиентамиcbr_РесурсныеСпецификации
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РесурсныеСпецификации
		|			ПО СоглашенияСКлиентамиcbr_РесурсныеСпецификации.РесурснаяСпецификация = РесурсныеСпецификации.Ссылка
		|		ПО ЗаказКлиента.Соглашение = СоглашенияСКлиентамиcbr_РесурсныеСпецификации.Ссылка
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &СсылкаОснование
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОсновноеИзделиеХарактеристика
		|ИТОГИ
		|	МАКСИМУМ(РесурснаяСпецификация) КАК РесурснаяСпецификация
		|ПО
		|	ОсновноеИзделиеНоменклатура,
		|	ОсновноеИзделиеХарактеристика";
		
	Запрос.УстановитьПараметр("СсылкаОснование", Основание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"ОсновноеИзделиеНоменклатура");
	
	ИндексРезультата = 0;
	
	Для Каждого Элемент из ДанныеОбИзделиях цикл
		
		ОтборНоменклатура = Новый Структура("ОсновноеИзделиеНоменклатура");
		ОтборНоменклатура.ОсновноеИзделиеНоменклатура = Элемент.Номенклатура;
		
		ОтборХарактеристика = новый Структура("ОсновноеИзделиеХарактеристика");  
		ОтборХарактеристика.ОсновноеИзделиеХарактеристика = Элемент.Характеристика;
								
		Если ВыборкаНоменклатура.НайтиСледующий(ОтборНоменклатура) Тогда
			
			ВыборкаПоХарактеристикам = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаПоХарактеристикам.Следующий() Цикл
				
				Если ВыборкаПоХарактеристикам.ОсновноеИзделиеХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()
						Или ВыборкаПоХарактеристикам.ОсновноеИзделиеХарактеристика = Элемент.Характеристика Тогда
							
					Результат[ИндексРезультата] = Новый Структура("Спецификация");
					Результат[ИндексРезультата].Спецификация = ВыборкаПоХарактеристикам.РесурснаяСпецификация;
					 
				КонецЕсли;
				
			КонецЦикла;
			
			ВыборкаНоменклатура.Сбросить();
						
		КонецЕсли;
		
		ИндексРезультата = ИндексРезультата + 1;
		
	КонецЦикла;
	
	Возврат Результат;	

КонецФункции//--Zhiend 17.02.2026

#КонецОбласти

#КонецЕсли