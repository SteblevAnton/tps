
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура cbr_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	ПодготовитьФорму();
	// КОНЕЦ Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры


&НаКлиенте
Процедура cbr_ПриОткрытииПосле(Отказ)
	
	Если Объект.cbr_ВремяКонтроля = -1 Тогда
		Объект.cbr_ВремяКонтроля = Число(ОбщегоНазначенияКлиент.ДатаСеанса() - Объект.cbr_ДатаДокументКотнроля);
		cbr_ВремяКонтроляДата = cbr_СекундыВВремя(Объект.cbr_ВремяКонтроля);
	ИначеЕсли Объект.cbr_ВремяКонтроля > 0 Тогда
		cbr_ВремяКонтроляДата = cbr_СекундыВВремя(Объект.cbr_ВремяКонтроля);	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция cbr_СекундыВВремя(Секунды)
    // Проверка на корректность входных данных
    Если Не Число(Секунды) Или Секунды < 0 Тогда
        Возврат "";
    КонецЕсли;
    
    // Вычисляем часы, минуты и секунды
    Часы = Цел(Секунды / 3600);
    Остаток = Секунды % 3600;
    Минуты = Цел(Остаток / 60);
    Сек = Остаток % 60;
    
    // Форматируем каждую часть с ведущим нулем    	
    Если Часы = 0 Тогда
    	ЧасыСтр = "00";
    Иначе	
    	ЧасыСтр = Формат(Часы, "ЧЦ=2; ЧВН=");
    КонецЕсли;
    
    Если Минуты = 0 Тогда
    	МинутыСтр = "00";
    Иначе
    	МинутыСтр = Формат(Минуты, "ЧЦ=2; ЧВН=");
    КонецЕсли;
    
    Если Сек = 0 Тогда
    	СекундыСтр = "00";
    Иначе
    	СекундыСтр = Формат(Сек, "ЧЦ=2; ЧВН=");
    КонецЕсли;
    
    // Собираем итоговую строку
    Возврат ЧасыСтр + " " + МинутыСтр + " " + СекундыСтр;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Османов Н.М. 25.04.2025
&НаКлиенте
Асинх Процедура П3_КоличествоУпаковокФактПриИзмененииПосле(Элемент)
		
	Если Не Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Начат") Тогда
		Возврат;
	КонецЕсли;
		
	ТекстВопроса = НСтр("ru = 'Вы указали количество: %1. Вы уверены?'");
	ТекстВопроса = СтрШаблон(ТекстВопроса, Объект.КоличествоУпаковокФакт);
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Объект.КоличествоУпаковокФакт = 0;
		Возврат;
	КонецЕсли;
	
	КоличествоДоИзменения = Объект.ВыходныеИзделия.Итог("КоличествоУпаковок");
	
	Если КоличествоДоИзменения = Объект.КоличествоУпаковокФакт Тогда
		Возврат;	
	КонецЕсли;
	
	Если Объект.КоличествоУпаковокФакт = 0 Тогда		
		ПересчитатьВыпускОбратноНаСервере(Ложь);
		Возврат;
	КонецЕсли;
	
	ОтразитьТехпотериВыпускаНаСервере();
	
	КэшированныеЗначения = Неопределено;
	ИмяТЧ = "ВыходныеИзделия";
	
	Для Каждого ТекущиеДанные Из Объект[ИмяТЧ] Цикл
		УправлениеПроизводствомКлиент.ВыходныеИзделияЭтапаПолучательПриИзменении(ТекущиеДанные, ИмяТЧ, ЭтотОбъект, Объект, КэшированныеЗначения);
	КонецЦикла;
		
	ЗаполнитьСтатусыУказанияСерийСервер(ИмяТЧ, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура П3_НачатьЭтапПосле(Команда) Экспорт

	// НАЧАЛО Османов Н.М. 23.11.2023 ЧТЗ №Действия в производстве
	Если НачатоВыполнениеЭтапа И ЗначениеЗаполнено(Объект.ФактическоеНачалоЭтапа) Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("П3_НачатьЭтапПослеЗавершение", ЭтотОбъект), "Будут перезаполнены плановое время начала и завершения. Продолжить?", РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 23.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура П3_НачатьЭтапПослеЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	// НАЧАЛО Османов Н.М. 23.11.2023 ЧТЗ №Действия в производстве
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ТекущееВремя = Объект.ФактическоеНачалоЭтапа;
		
		Для Каждого СтрокаДействия Из Объект.Действия Цикл
			
			СтрокаДействия.ВремяНачалаПлан = ТекущееВремя;
			ТекущееВремя = ТекущееВремя + СтрокаДействия.Продолжительность * 60;
			СтрокаДействия.ВремяЗавершенияПлан = ТекущееВремя;
			
		КонецЦикла;
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 23.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура П3_РабочийЦентрПриИзменении(Команда) Экспорт

	// НАЧАЛО Османов Н.М. 29.01.2025
	Возврат;
	
	Если Не ЗначениеЗаполнено(Объект.РабочийЦентр) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЦентра = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(Объект.РабочийЦентр, "ВидРабочегоЦентра.Подразделение, Участок, Участок.Владелец");
	Объект.Подразделение = ?(ДанныеЦентра.Участок.Пустая(), 
							ДанныеЦентра.ВидРабочегоЦентраПодразделение, 
							ДанныеЦентра.УчастокВладелец);
	// КОНЕЦ Османов Н.М. 29.01.2025
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДействия

&НаКлиенте
Процедура ИзменениеВремениИлиПродолжительности(Команда) Экспорт
	
	// НАЧАЛО Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	ТекущаяСтрока = Элементы.ТаблицаДействий.ТекущиеДанные;
	
	Если Не ТекущаяСтрока = Неопределено Тогда
		
		Если ЗначениеЗаполнено(ТекущаяСтрока.ВремяНачалаПлан)
			И ЗначениеЗаполнено(ТекущаяСтрока.Продолжительность) Тогда
			
			ТекущаяСтрока.ВремяЗавершенияПлан = ТекущаяСтрока.ВремяНачалаПлан + ТекущаяСтрока.Продолжительность * 60;
			
		КонецЕсли;
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура("ТаблицаВыбора", ТаблицаОбеспеченияВХранилище());
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборСтрокиДляПодстановки", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ВыборНоменклатуры", ПараметрыОткрытия, , , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Если Не Поле.Имя = "ТаблицаДействийНоменклатура"
		И Не Поле.Имя = "ТаблицаДействийХарактеристика" 
		И Не Поле.Имя = "ТаблицаДействийУпаковка" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура("ТаблицаВыбора", ТаблицаОбеспеченияВХранилище());
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборСтрокиДляПодстановки", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ВыборНоменклатуры", ПараметрыОткрытия, , , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФорму()

	// НАЧАЛО Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	Если ФормыУХ.ЭлементыФормыУХУжеСозданы(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаДействия = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаДействия",
												НСтр("ru = 'Действия'"),
												ВидГруппыФормы.Страница,
												Элементы.Страницы,
												Элементы.СтраницаВидыРабочихЦентров);
								
	ДанныеПоТаблице = РаботаСФормамиСервер.СоздатьТаблицуНаФорме(Элементы,
																"ТаблицаДействий",
																"Действия",
																"Объект.Действия",
																Элементы.ГруппаДействия,
																Метаданные.Документы.ЭтапПроизводства2_2.ТабличныеЧасти.Действия,
																,
																"");
																
	ДанныеПоТаблице.Таблица.УстановитьДействие("Выбор", "ДействияВыбор");
	
	ПараметрыГруппыДействияУпаковкаЕдиницИзмерения = Новый Структура("Группировка", ГруппировкаКолонок.ВЯчейке);
	ГруппаДействияУпаковкаЕдиницИзмерения = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаДействияУпаковкаЕдиницИзмерения",
												НСтр("ru = 'Действия единицы измерения'"),
												ВидГруппыФормы.ГруппаКолонок,
												Элементы.ТаблицаДействий,
												Элементы.ТаблицаДействийВнесеноПлан,
												ПараметрыГруппыДействияУпаковкаЕдиницИзмерения);
												
	Элементы.Переместить(ДанныеПоТаблице.Колонки.Упаковка, ГруппаДействияУпаковкаЕдиницИзмерения);
												
	КолонкаНоменклатураЕдиницаИзмерения = ФормыУХ.СоздатьПолеФормы(Элементы,
																"ТаблицаДействийНоменклатураЕдиницаИзмерения",
																НСтр("ru = 'Ед. изм.'"),
																"Объект.Действия.Номенклатура.ЕдиницаИзмерения",
																ВидПоляФормы.ПолеНадписи,
																ГруппаДействияУпаковкаЕдиницИзмерения);
																
	ДанныеПоТаблице.Колонки.Номенклатура.УстановитьДействие("НачалоВыбора", "ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора");
	ДанныеПоТаблице.Колонки.Характеристика.УстановитьДействие("НачалоВыбора", "ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора");
	ДанныеПоТаблице.Колонки.Упаковка.УстановитьДействие("НачалоВыбора", "ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора");

	ДанныеПоТаблице.Колонки.Номенклатура.РедактированиеТекста = Ложь;
	ДанныеПоТаблице.Колонки.Номенклатура.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	ДанныеПоТаблице.Колонки.Характеристика.РедактированиеТекста = Ложь;
	ДанныеПоТаблице.Колонки.Характеристика.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	ДанныеПоТаблице.Колонки.Упаковка.РедактированиеТекста = Ложь;
	ДанныеПоТаблице.Колонки.Упаковка.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	Если ДанныеПоТаблице.Колонки.Свойство("ВремяНачалаПлан")
		И ДанныеПоТаблице.Колонки.Свойство("ВремяЗавершенияПлан")
		И ДанныеПоТаблице.Колонки.Свойство("Продолжительность") Тогда
		
		ДанныеПоТаблице.Колонки.ВремяНачалаПлан.УстановитьДействие("ПриИзменении", "ИзменениеВремениИлиПродолжительности");
		ДанныеПоТаблице.Колонки.Продолжительность.УстановитьДействие("ПриИзменении", "ИзменениеВремениИлиПродолжительности");
		
		ГруппаВремяНачала = Элементы.Добавить("ТаблицаДействийГруппаВремяНачала", Тип("ГруппаФормы"), Элементы.ТаблицаДействий);
		ГруппаВремяНачала.Вид = ВидГруппыФормы.ГруппаКолонок;
		ГруппаВремяНачала.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.Переместить(ГруппаВремяНачала, ДанныеПоТаблице.Таблица, ДанныеПоТаблице.Колонки.ВремяЗавершенияПлан);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ВремяНачалаПлан, ГруппаВремяНачала);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ВремяНачалаФакт, ГруппаВремяНачала);
		
		ГруппаВремяЗавершения = Элементы.Добавить("ТаблицаДействийГруппаВремяЗавершения", Тип("ГруппаФормы"), Элементы.ТаблицаДействий);
		ГруппаВремяЗавершения.Вид = ВидГруппыФормы.ГруппаКолонок;
		ГруппаВремяЗавершения.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.Переместить(ГруппаВремяЗавершения, ДанныеПоТаблице.Таблица, ДанныеПоТаблице.Колонки.Номенклатура);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ВремяЗавершенияПлан, ГруппаВремяЗавершения);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ВремяЗавершенияФакт, ГруппаВремяЗавершения);
		
		ГруппаВнесено = Элементы.Добавить("ТаблицаДействийГруппаВнесено", Тип("ГруппаФормы"), Элементы.ТаблицаДействий);
		ГруппаВнесено.Вид = ВидГруппыФормы.ГруппаКолонок;
		ГруппаВнесено.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.Переместить(ГруппаВнесено, ДанныеПоТаблице.Таблица, ДанныеПоТаблице.Колонки.ТемператураПлан);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ВнесеноПлан, ГруппаВнесено);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ВнесеноФакт, ГруппаВнесено);
		
		ГруппаТемпература = Элементы.Добавить("ТаблицаДействийГруппаТемпература", Тип("ГруппаФормы"), Элементы.ТаблицаДействий);
		ГруппаТемпература.Вид = ВидГруппыФормы.ГруппаКолонок;
		ГруппаТемпература.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.Переместить(ГруппаТемпература, ДанныеПоТаблице.Таблица, ДанныеПоТаблице.Колонки.ДавлениеПлан);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ТемператураПлан, ГруппаТемпература);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ТемператураФакт, ГруппаТемпература);
		
		ГруппаДавление = Элементы.Добавить("ТаблицаДействийГруппаДавление", Тип("ГруппаФормы"), Элементы.ТаблицаДействий);
		ГруппаДавление.Вид = ВидГруппыФормы.ГруппаКолонок;
		ГруппаДавление.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.Переместить(ГруппаДавление, ДанныеПоТаблице.Таблица, ДанныеПоТаблице.Колонки.СкоростьВращенияПлан);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ДавлениеПлан, ГруппаДавление);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.ДавлениеФакт, ГруппаДавление);
		
		ГруппаСкоростьВращения = Элементы.Добавить("ТаблицаДействийГруппаСкоростьВращения", Тип("ГруппаФормы"), Элементы.ТаблицаДействий);
		ГруппаСкоростьВращения.Вид = ВидГруппыФормы.ГруппаКолонок;
		ГруппаСкоростьВращения.Группировка = ГруппировкаКолонок.Вертикальная;
		Элементы.Переместить(ГруппаСкоростьВращения, ДанныеПоТаблице.Таблица, ДанныеПоТаблице.Колонки.Выполнено);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.СкоростьВращенияПлан, ГруппаСкоростьВращения);
		Элементы.Переместить(ДанныеПоТаблице.Колонки.СкоростьВращенияФакт, ГруппаСкоростьВращения);
		
	КонецЕсли;
	
	Элементы.ФактическоеНачалоЭтапа.УстановитьДействие("ПриИзменении", "П3_НачатьЭтапПосле");
	
	СобытияРабочегоЦентра = Новый Структура("ПриИзменении", "П3_РабочийЦентрПриИзменении");
	ЭлементРабочийЦентр = ФормыУХ.СоздатьПолеФормы(Элементы,
												"РабочийЦентр",
												НСтр("ru = 'Рабочий центр'"),
												"Объект.РабочийЦентр",
												,
												Элементы.ГруппаОсновное,
												Элементы.ГруппаПараметрыПланированияГрафика,
												,
												СобытияРабочегоЦентра);
												
	ИмяКомандыОбеспечитьВсе = "П3_ОтгрузитьСразуДляВсех";
	Команда = ФормыУХ.СоздатьКоманду(ЭтотОбъект, ИмяКомандыОбеспечитьВсе, "", ИмяКомандыОбеспечитьВсе);
	
	ФормыУХ.СоздатьКнопкуФормы(Элементы, 
							  "П3_КнопкаОтгрузитьВсе", 
							  НСтр("ru = 'Обеспечить всё'"),
							  ИмяКомандыОбеспечитьВсе,
							  ВидКнопкиФормы.ОбычнаяКнопка,
							  Элементы.ОбеспечениеМатериаламиИРаботамиГруппаДействий,
							  Элементы.ОбеспечениеМатериаламиИРаботамиОбеспечениеОтгрузить);
							  
	// НАЧАЛО Османов Н.М. 30.04.2025 
	ИмяКомандыОтменитьПересчет = "П3_ОтменитьПересчетОтмененныхСтрок";
	Команда = ФормыУХ.СоздатьКоманду(ЭтотОбъект, ИмяКомандыОтменитьПересчет, "", ИмяКомандыОтменитьПересчет);
	
	ФормыУХ.СоздатьКнопкуФормы(Элементы, 
							  "П3_КнопкаОтменитьПересчет", 
							  НСтр("ru = 'Отменить пересчёт отменённых строк'"),
							  ИмяКомандыОтменитьПересчет,
							  ВидКнопкиФормы.ОбычнаяКнопка,
							  Элементы.ВыходныеИзделия_ОтменаСтрок);
	// КОНЕЦ Османов Н.М. 30.04.2025 
	
	// НАЧАЛО Османов Н.М. 27.11.2024
	ИтогОбеспечения = ИтогПоОбеспечению();
	Если ИтогОбеспечения > 0 Тогда
		Элементы.ОбеспечениеМатериаламиИРаботами.Подвал = Истина;
		
		ДобавляемыеРеквизиты = Новый Массив();
		ФормыУХ.СоздатьДобавитьРеквизитФормы(ЭтотОбъект, 
											ДобавляемыеРеквизиты,
											"РеквизитИтогПоОбеспечению",
											ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Неотрицательный));
		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		ЭтотОбъект.РеквизитИтогПоОбеспечению = ИтогОбеспечения;
		
		Элементы.ОбеспечениеМатериаламиИРаботамиКоличествоУпаковок.ПутьКДаннымПодвала = "РеквизитИтогПоОбеспечению";
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 27.11.2024
	
	ФормыУХ.ЭлементыФормыУХДобавлены(ЭтотОбъект);
	// КОНЕЦ Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборСтрокиДляПодстановки(Результат, ДополнительныеПараметры) Экспорт

	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ТаблицаДействий.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Результат);
	
	ТаблицаПоОтбору = Объект.ОбеспечениеМатериаламиИРаботами.НайтиСтроки(Результат);
	
	ИтогоКоличество = 0;
	Для Каждого СтрокаТаблицыПоОтбору Из ТаблицаПоОтбору Цикл
		ИтогоКоличество = ИтогоКоличество + СтрокаТаблицыПоОтбору.КоличествоУпаковок;
	КонецЦикла;
	
	Если ИтогоКоличество > 0 Тогда
		ИтогоКоличествоВДействиях = 0;
		ТаблицаПоОтбору = Объект.Действия.НайтиСтроки(Результат);
		Для Каждого СтрокаДействий Из ТаблицаПоОтбору Цикл
			ИтогоКоличествоВДействиях = ИтогоКоличествоВДействиях + СтрокаДействий.ВнесеноПлан;
		КонецЦикла;
	КонецЕсли;
	
	Если ИтогоКоличество <> ИтогоКоличествоВДействиях Тогда
		Разница = ИтогоКоличествоВДействиях - ИтогоКоличество;
		ТекущаяСтрока.ВнесеноПлан = ТекущаяСтрока.ВнесеноПлан - Разница;
		Модифицированность = Истина;
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры

&НаСервере
Функция ТаблицаОбеспеченияВХранилище()
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Обеспечение.Номенклатура КАК Номенклатура,
		|	Обеспечение.Характеристика КАК Характеристика,
		|	Обеспечение.Упаковка КАК Упаковка,
		|	Обеспечение.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ОбеспечениеПредварительно
		|ИЗ
		|	&Обеспечение КАК Обеспечение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Действия.Номенклатура КАК Номенклатура,
		|	Действия.Характеристика КАК Характеристика,
		|	Действия.Упаковка КАК Упаковка,
		|	Действия.ВнесеноПлан КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ДействияПредварительно
		|ИЗ
		|	&Действия КАК Действия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОбеспечениеПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ОбеспечениеПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Обеспечение
		|ИЗ
		|	ВТ_ОбеспечениеПредварительно КАК ВТ_ОбеспечениеПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ОбеспечениеПредварительно.Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДействияПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ДействияПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Действия
		|ИЗ
		|	ВТ_ДействияПредварительно КАК ВТ_ДействияПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДействияПредварительно.Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Обеспечение.Номенклатура КАК Номенклатура,
		|	ВТ_Обеспечение.Характеристика КАК Характеристика,
		|	ВТ_Обеспечение.Упаковка КАК Упаковка,
		|	ЕСТЬNULL(ВТ_Обеспечение.КоличествоУпаковок, 0) КАК КоличествоОбеспечение,
		|	ЕСТЬNULL(ВТ_Действия.КоличествоУпаковок, 0) КАК КоличествоДействия,
		|	ЕСТЬNULL(ВТ_Обеспечение.КоличествоУпаковок, 0) - ЕСТЬNULL(ВТ_Действия.КоличествоУпаковок, 0) КАК Разница
		|ИЗ
		|	ВТ_Обеспечение КАК ВТ_Обеспечение
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Действия КАК ВТ_Действия
		|		ПО ВТ_Обеспечение.Номенклатура = ВТ_Действия.Номенклатура
		|			И ВТ_Обеспечение.Характеристика = ВТ_Действия.Характеристика
		|			И ВТ_Обеспечение.Упаковка = ВТ_Действия.Упаковка";
	
	Запрос.УстановитьПараметр("Обеспечение", Объект.ОбеспечениеМатериаламиИРаботами.Выгрузить());
	Запрос.УстановитьПараметр("Действия", Объект.Действия.Выгрузить());
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаДанных);
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецФункции

&НаСервере
Функция ИтогПоОбеспечению()
	
	РезультатФункции = 0;
	
	Если Объект.ВыходныеИзделия.Количество() = 0 Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Номенклатура = Объект.ВыходныеИзделия[0].Номенклатура;
	Если Не ЗначениеЗаполнено(Номенклатура) Тогда
		Возврат РезультатФункции;		
	КонецЕсли;
	
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры");
	ЭтоПолуфабрикат = УправлениеСвойствами.ЗначениеСвойства(ВидНоменклатуры, "ЭтоПолуфабрикатВидНоменклатуры");
	Если Не ЭтоПолуфабрикат = Истина Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Для Каждого СтрокаОбеспечения Из Объект.ОбеспечениеМатериаламиИРаботами Цикл
		
		Количество = СтрокаОбеспечения.Количество;
		Если Не ЗначениеЗаполнено(СтрокаОбеспечения.Упаковка) Тогда
			
			ЕдиницаИзмеренияКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаОбеспечения.Номенклатура, "ЕдиницаИзмерения.Код");
			Если СокрЛП(ЕдиницаИзмеренияКод) = "163" Тогда // это граммы
				Количество  = Количество  / 1000;
			КонецЕсли;
			
		Иначе
			
			ЕдиницаИзмеренияКод = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаОбеспечения.Упаковка, "Код");
			Если СокрЛП(ЕдиницаИзмеренияКод) = "163" Тогда // это граммы
				Количество  = Количество  / 1000;
			КонецЕсли;
			
		КонецЕсли;
		
		РезультатФункции = РезультатФункции + Количество;
		
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

&НаКлиенте
Процедура П3_ОбеспечениеМатериаламиИРаботамиПриОкончанииРедактированияПосле(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ИтогОбеспечения = ИтогПоОбеспечению();
	Если ИтогОбеспечения > 0 Тогда
		ЭтотОбъект.РеквизитИтогПоОбеспечению = ИтогОбеспечения;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура П3_ОтгрузитьСразуДляВсех(Команда)
	
	// НАЧАЛО Османов Н.М. 29.01.2025 
	Если Не Объект.Статус = СтатусКВыполнению() 
		И Не Объект.Статус = СтатусНачат() 
		Тогда
		ТекстСообщения = НСтр("ru='Операция возможна только для документа в статусах ""К выполнению"" или ""Начат""!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	МассивСкладов = Новый Массив;
	Для Каждого СтрокаОбеспечения Из Объект.ОбеспечениеМатериаламиИРаботами Цикл
		
		Если СтрокаОбеспечения.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПолуфабрикатыПроизводимыеВПроцессе") Тогда
			Продолжить;			
		КонецЕсли;
		
		Если СтрокаОбеспечения.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить") Тогда
			Продолжить;
		КонецЕсли;
		
		МассивСкладов.Добавить(СтрокаОбеспечения.Склад);
		
	КонецЦикла;
	
	МассивСкладов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивСкладов);
	СписокВыбораСкладов = Новый СписокЗначений;
	СписокВыбораСкладов.ЗагрузитьЗначения(МассивСкладов);
	
	ВыбранныеСклады = Ждать СписокВыбораСкладов.ОтметитьЭлементыАсинх("Выберите склады для отгрузки");
	
	Для Каждого СтрокаОбеспечения Из Объект.ОбеспечениеМатериаламиИРаботами Цикл
		
		Если СтрокаОбеспечения.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПолуфабрикатыПроизводимыеВПроцессе") Тогда
			Продолжить;			
		КонецЕсли;
		
		Если ВыбранныеСклады = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаСписка = ВыбранныеСклады.НайтиПоЗначению(СтрокаОбеспечения.Склад);
		Если СтрокаСписка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не СтрокаСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаОбеспечения.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
		
		ВариантВыбора = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
		ВариантВыбора.ВариантОбеспечения = СтрокаОбеспечения.ВариантОбеспечения;
		
		ОбеспечениеВДокументахКлиент.ВариантОбеспеченияОбработкаВыбора(ЭтотОбъект, СтрокаОбеспечения, ВариантВыбора);
		Элементы.ОбеспечениеМатериаламиИРаботами.ТекущаяСтрока = Объект.ОбеспечениеМатериаламиИРаботами.Индекс(СтрокаОбеспечения);
		ОбеспечениеМатериаламиИРаботамиВариантОбеспеченияПриИзмененииПослеВопроса("", Неопределено);
		
		Модифицированность = Истина;

	КонецЦикла;
	// КОНЕЦ Османов Н.М. 29.01.2025 
	
	// НАЧАЛО Османов Н.М. 22.04.2025 
	Если Объект.ОбеспечениеМатериаламиИРаботами.Количество() > 0 Тогда
		ЗаполнитьСерииВОбеспечении();
	КонецЕсли;	
	// КОНЕЦ Османов Н.М. 22.04.2025 
	
КонецПроцедуры

&НаКлиенте
Процедура П3_ОтменитьПересчетОтмененныхСтрок(Команда)
	
	Если Объект.П3_СлужебныйПересчетВыпуска < 0 Тогда
		ПересчитатьВыпускОбратноНаСервере();
	ИначеЕсли Объект.П3_СлужебныйПересчетВыпуска > 0 Тогда
		ПересчитатьВыпускОбратноДляПоложительногоЗначения();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСерииВОбеспечении()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВТ_ПервичныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПервичныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПервичныеДанные.Серия КАК Серия,
	|	ВТ_ПервичныеДанные.ВариантОбеспечения КАК ВариантОбеспечения,
	|	ВТ_ПервичныеДанные.Склад КАК Склад,
	|	ВТ_ПервичныеДанные.Назначение КАК Назначение,
	|	ВТ_ПервичныеДанные.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ВТ_ПервичныеДанные.СтатьяКалькуляции КАК СтатьяКалькуляции
	|ПОМЕСТИТЬ ВТ_ТаблицаДанных
	|ИЗ
	|	&ОбеспечениеМатериалов КАК ВТ_ПервичныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаДанных.Характеристика КАК Характеристика,
	|	ВТ_ТаблицаДанных.Склад КАК Склад,
	|	ВТ_ТаблицаДанных.Назначение КАК Назначение,
	|	ВТ_ТаблицаДанных.Назначение.ДвиженияПоСкладскимРегистрам КАК ДвиженияПоСкладскимРегистрам
	|ПОМЕСТИТЬ ВТ_ТаблицаДляСерий
	|ИЗ
	|	ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
	|ГДЕ
	|	ВТ_ТаблицаДанных.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И НЕ ВТ_ТаблицаДанных.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПолуфабрикатыПроизводимыеВПроцессе)
	|	И ВТ_ТаблицаДанных.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И ЕСТЬNULL(ВТ_ТаблицаДанных.Номенклатура.ВидНоменклатуры.ИспользоватьСерии, ЛОЖЬ)
	|	И ВТ_ТаблицаДанных.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И НЕ ВТ_ТаблицаДанных.СтатусУказанияСерий В (0, 11, 15, 21, 23, 25, 27, 28)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДанных.Склад,
	|	ВТ_ТаблицаДанных.Номенклатура,
	|	ВТ_ТаблицаДанных.Характеристика,
	|	ВТ_ТаблицаДанных.Назначение,
	|	ВТ_ТаблицаДанных.Назначение.ДвиженияПоСкладскимРегистрам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеРегистров.Склад КАК Склад,
	|	ДанныеРегистров.Номенклатура КАК Номенклатура,
	|	ДанныеРегистров.Характеристика КАК Характеристика,
	|	ДанныеРегистров.Серия КАК Серия,
	|	СУММА(ДанныеРегистров.СвободныйОстаток) КАК СвободныйОстаток
	|ПОМЕСТИТЬ ДанныеРегистров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Склад КАК Склад,
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ТоварыНаСкладахОстатки.Серия КАК Серия,
	|		ТоварыНаСкладахОстатки.ВНаличииОстаток КАК СвободныйОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				,
	|				(Номенклатура, Характеристика, Склад) В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура,
	|							Т.Характеристика,
	|							Т.Склад
	|						ИЗ
	|							ВТ_ТаблицаДляСерий КАК Т)
	|					И Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК ТоварыНаСкладахОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО ТоварыНаСкладахОстатки.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И ТоварыНаСкладахОстатки.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И ТоварыНаСкладахОстатки.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (ТоварыНаСкладахОстатки.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ ТоварыНаСкладахОстатки.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтгрузкеОстатки.Склад,
	|		ТоварыКОтгрузкеОстатки.Номенклатура,
	|		ТоварыКОтгрузкеОстатки.Характеристика,
	|		ТоварыКОтгрузкеОстатки.Серия,
	|		-ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток - ТоварыКОтгрузкеОстатки.ВРезервеОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|				,
	|				(Номенклатура, Характеристика, Склад) В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура,
	|							Т.Характеристика,
	|							Т.Склад
	|						ИЗ
	|							ВТ_ТаблицаДляСерий КАК Т)
	|					И Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК ТоварыКОтгрузкеОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО ТоварыКОтгрузкеОстатки.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И ТоварыКОтгрузкеОстатки.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И ТоварыКОтгрузкеОстатки.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (ТоварыКОтгрузкеОстатки.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ ТоварыКОтгрузкеОстатки.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНаСкладах.Склад,
	|		ТоварыНаСкладах.Номенклатура,
	|		ТоварыНаСкладах.Характеристика,
	|		ТоварыНаСкладах.Серия,
	|		ВЫБОР
	|			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТоварыНаСкладах.ВНаличии + ТоварыНаСкладах.КОтгрузке
	|			ИНАЧЕ ТоварыНаСкладах.ВНаличии - ТоварыНаСкладах.КОтгрузке
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО ТоварыНаСкладах.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И ТоварыНаСкладах.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И ТоварыНаСкладах.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (ТоварыНаСкладах.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ ТоварыНаСкладах.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	ГДЕ
	|		ТоварыНаСкладах.Регистратор = &Регистратор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыСерий.Склад,
	|		РезервыСерий.Номенклатура,
	|		РезервыСерий.Характеристика,
	|		РезервыСерий.Серия,
	|		ВЫБОР
	|			КОГДА РезервыСерий.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РезервыСерий.КОтгрузке + РезервыСерий.ВРезерве
	|			ИНАЧЕ -РезервыСерий.КОтгрузке - РезервыСерий.ВРезерве
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыКОтгрузке КАК РезервыСерий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО РезервыСерий.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И РезервыСерий.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И РезервыСерий.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (РезервыСерий.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ РезервыСерий.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	ГДЕ
	|		РезервыСерий.Регистратор = &Регистратор
	|		И РезервыСерий.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК ДанныеРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистров.Серия,
	|	ДанныеРегистров.Склад,
	|	ДанныеРегистров.Номенклатура,
	|	ДанныеРегистров.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистров.Склад КАК Склад,
	|	ДанныеРегистров.Номенклатура КАК Номенклатура,
	|	ДанныеРегистров.Характеристика КАК Характеристика,
	|	ДанныеРегистров.Серия КАК Серия,
	|	СУММА(ДанныеРегистров.СвободныйОстаток) КАК Доступно,
	|	ДанныеРегистров.Серия.Номер КАК Номер,
	|	ДанныеРегистров.Серия.ГоденДо КАК ГоденДо
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров
	|ГДЕ
	|	НЕ ДанныеРегистров.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И &УсловиеНаГодностьСерии
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистров.Склад,
	|	ДанныеРегистров.Номенклатура,
	|	ДанныеРегистров.Характеристика,
	|	ДанныеРегистров.Серия,
	|	ДанныеРегистров.Серия.Номер,
	|	ДанныеРегистров.Серия.ГоденДо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо,
	|	Номер";
	
	Запрос.УстановитьПараметр("ОбеспечениеМатериалов", Объект.ОбеспечениеМатериаламиИРаботами.Выгрузить());
	Запрос.УстановитьПараметр("Регистратор", Объект.Ссылка);
	
	ЕстьРеквизитРежимОтображенияСерийПоГодности = ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, "лимсРежимОтображенияСерийПоГодности");
	Если ЕстьРеквизитРежимОтображенияСерийПоГодности = Истина
		И ЭтотОбъект.лимсРежимОтображенияСерийПоГодности = "ГодныеУсловноГодные" Тогда
		ТипыСерий = Новый Массив;
		ТипыСерий.Добавить(Перечисления.лимсСтатусСерии.Годен);
		ТипыСерий.Добавить(Перечисления.лимсСтатусСерии.УсловноГоден);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаГодностьСерии", 
			"ДанныеРегистров.Серия.лимсСтатусГодности.Тип В (&ТипыСерий)");
		
		Запрос.УстановитьПараметр("ТипыСерий", ТипыСерий);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаГодностьСерии", "ИСТИНА");
	КонецЕсли;
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	СоответствиеСерий = Новый Соответствие;
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок", Истина);
	КэшированныеЗначения = Неопределено;
	
	ВсегоСтрок = Объект.ОбеспечениеМатериаламиИРаботами.Количество();
	Для Индекс = 1 По ВсегоСтрок Цикл
		
		СтрокаОбеспечения = Объект.ОбеспечениеМатериаламиИРаботами[Индекс - 1];
		ВсегоКоличество = СтрокаОбеспечения.Количество;
		
		ОтборСтрок = Новый Структура("Склад, Номенклатура, Характеристика");
		ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаОбеспечения);
		НайденныеСерии = Выгрузка.НайтиСтроки(ОтборСтрок);
		
		ЕстьНоваяСтрока = Ложь;
		
		Для Каждого СтрокаСерии Из НайденныеСерии Цикл
			
			Если ВсегоКоличество <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			ОсталосьПоСерии = СоответствиеСерий.Получить(СтрокаСерии);
			Если Не ОсталосьПоСерии = Неопределено И ОсталосьПоСерии = 0 Тогда
				Продолжить;
			Иначе
				ОсталосьПоСерии = СтрокаСерии.Доступно;
			КонецЕсли;
			
			Если СтрокаСерии.Доступно <=0 Тогда
				Продолжить;
			КонецЕсли;
						
			КоличествоДляУстановки = Мин(ВсегоКоличество, ОсталосьПоСерии);
			СтрокаОбеспечения.Серия = СтрокаСерии.Серия;
			СтрокаОбеспечения.Количество = КоличествоДляУстановки;
			
			ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоУпаковокВСтрокеТЧ(СтрокаОбеспечения, СтруктураДействий, КэшированныеЗначения);
			
			СоответствиеСерий.Вставить(СтрокаСерии.Серия, ОсталосьПоСерии - КоличествоДляУстановки);
			
			Если ВсегоКоличество > ОсталосьПоСерии Тогда
				НоваяСтрокаОбеспечения = Объект.ОбеспечениеМатериаламиИРаботами.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаОбеспечения, СтрокаОбеспечения, , "Серия, Количество, КодСтроки, КоличествоУпаковок, ИдентификаторСтроки, КлючНоменклатура");
				НоваяСтрокаОбеспечения.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
				СтрокаОбеспечения = НоваяСтрокаОбеспечения;
				СтрокаОбеспечения.Количество = ВсегоКоличество - ОсталосьПоСерии;
				ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоУпаковокВСтрокеТЧ(СтрокаОбеспечения, СтруктураДействий, КэшированныеЗначения);
				
				ЕстьНоваяСтрока = Истина;
			КонецЕсли;
			
			ВсегоКоличество = ВсегоКоличество - КоличествоДляУстановки;
			
		КонецЦикла;
		
		Если ЕстьНоваяСтрока И Не ЗначениеЗаполнено(СтрокаОбеспечения.Серия) Тогда
			
			СлужебныеПараметры = ДоступныеОстаткиПараметрыВстраивания.СлужебныеПараметры;
			СлужебныеПараметры.ВариантОбеспеченияДоИзменения = СтрокаОбеспечения.ВариантОбеспечения;
			СтрокаОбеспечения.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.КОбеспечению");
			ВариантВыбора = ОбеспечениеВДокументахКлиентСервер.ЗначениеВыбораВариантаОбеспечения();
			ВариантВыбора.ВариантОбеспечения = СтрокаОбеспечения.ВариантОбеспечения;
			СлужебныеПараметры.ВариантОбеспеченияВыбранноеЗначение = ВариантВыбора;
			ОбеспечениеМатериаламиИРаботамиВариантОбеспеченияПриИзмененииНаСервере(Неопределено);
			
		КонецЕсли;
		
	КонецЦикла;
				
КонецПроцедуры

&НаСервере
&После("ПослеИзмененияСтатусаНаСервере")
Процедура П3_ПослеИзмененияСтатусаНаСервере(ИзмененныеРеквизиты)
	
	Если Не Объект.Статус = СтатусЗавершен() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДвижениеПродукцииИМатериаловСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеПродукцииИМатериаловСерии.Характеристика КАК Характеристика,
	|	ДвижениеПродукцииИМатериаловСерии.Серия КАК Серия,
	|	СУММА(ДвижениеПродукцииИМатериаловСерии.Количество) КАК Количество
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.Серии КАК ДвижениеПродукцииИМатериаловСерии
	|ГДЕ
	|	ДвижениеПродукцииИМатериаловСерии.Ссылка.Распоряжение = &ЭтапПроизводства
	|	И ДвижениеПродукцииИМатериаловСерии.Ссылка.Проведен
	|	И ДвижениеПродукцииИМатериаловСерии.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДвиженияПродукцииИМатериалов.Принято)
	|	И НЕ ДвижениеПродукцииИМатериаловСерии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеПродукцииИМатериаловСерии.Номенклатура,
	|	ДвижениеПродукцииИМатериаловСерии.Характеристика,
	|	ДвижениеПродукцииИМатериаловСерии.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвижениеПродукцииИМатериаловТовары.Номенклатура,
	|	ДвижениеПродукцииИМатериаловТовары.Характеристика,
	|	ДвижениеПродукцииИМатериаловТовары.Серия,
	|	СУММА(ДвижениеПродукцииИМатериаловТовары.Количество)
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов.Товары КАК ДвижениеПродукцииИМатериаловТовары
	|ГДЕ
	|	ДвижениеПродукцииИМатериаловТовары.Ссылка.Распоряжение = &ЭтапПроизводства
	|	И ДвижениеПродукцииИМатериаловТовары.Ссылка.Проведен
	|	И ДвижениеПродукцииИМатериаловТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДвиженияПродукцииИМатериалов.Принято)
	|	И НЕ ДвижениеПродукцииИМатериаловТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеПродукцииИМатериаловТовары.Номенклатура,
	|	ДвижениеПродукцииИМатериаловТовары.Характеристика,
	|	ДвижениеПродукцииИМатериаловТовары.Серия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура,
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Характеристика,
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Серия,
	|	СУММА(РасходныйОрдерНаТоварыОтгружаемыеТовары.Количество)
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК РасходныйОрдерНаТоварыОтгружаемыеТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
	|		ПО РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка
	|			И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение = &ЭтапПроизводства)
	|			И РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Номенклатура
	|			И РасходныйОрдерНаТоварыОтгружаемыеТовары.Характеристика = РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Характеристика
	|ГДЕ
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка.Проведен
	|	И РасходныйОрдерНаТоварыОтгружаемыеТовары.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.Отгружен)
	|	И НЕ РасходныйОрдерНаТоварыОтгружаемыеТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Номенклатура,
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Характеристика,
	|	РасходныйОрдерНаТоварыОтгружаемыеТовары.Серия";
	
	Запрос.УстановитьПараметр("ЭтапПроизводства", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыгрузкаСерий = РезультатЗапроса.Выгрузить();

	ТаблицаРасходаЭтапа = Объект.РасходМатериаловИРабот.Выгрузить(Новый Структура("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка()));
	КолонкиСвертки = "Номенклатура,Характеристика,Упаковка,ДатаРасхода,СтатьяКалькуляции,Подразделение,ХарактеристикиИспользуются,ТипНоменклатуры,Артикул";
	ТаблицаРасходаЭтапа.Свернуть(КолонкиСвертки, "КоличествоУпаковок, Количество");
	
	ТаблицаНоменклатуры = ТаблицаРасходаЭтапа.Скопировать(, "Номенклатура, Характеристика");
	ТаблицаНоменклатуры.Свернуть("Номенклатура, Характеристика");
	
	ИзначальнаяТаблицаССериями = Объект.РасходМатериаловИРабот.Выгрузить();
	Объект.РасходМатериаловИРабот.Очистить();
	
	Для Каждого СтрокаТаблицыНоменклатуры Из ТаблицаНоменклатуры Цикл
		
		ОтборТаблицы = Новый Структура("Номенклатура, Характеристика");
		ЗаполнитьЗначенияСвойств(ОтборТаблицы, СтрокаТаблицыНоменклатуры);
		
		НайденныеСтрокиЭтапа = ТаблицаРасходаЭтапа.Скопировать(ОтборТаблицы);
		
		НайденныеСтрокиРасходов = ВыгрузкаСерий.Скопировать(ОтборТаблицы);
		
		Для Каждого СтрокаЭтапа Из НайденныеСтрокиЭтапа Цикл
			Остаток = СтрокаЭтапа.Количество;
			Для Каждого СтрокаРасходов Из НайденныеСтрокиРасходов Цикл
				Если СтрокаЭтапа.Номенклатура = СтрокаРасходов.Номенклатура Тогда

					КоличествоДляРаспределения = Мин(Остаток, СтрокаРасходов.Количество);
					Если КоличествоДляРаспределения > 0 Тогда

						НоваяСтрока = Объект.РасходМатериаловИРабот.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЭтапа);
						НоваяСтрока.Серия = СтрокаРасходов.Серия;
						НоваяСтрока.Количество = КоличествоДляРаспределения;
						НоваяСтрока.КоличествоУпаковок = КоличествоДляРаспределения;

						Остаток = Остаток - КоличествоДляРаспределения;
						СтрокаРасходов.Количество = СтрокаРасходов.Количество - КоличествоДляРаспределения;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если Остаток > 0 Тогда
				НоваяСтрока = Объект.РасходМатериаловИРабот.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЭтапа);
				НоваяСтрока.Количество = Остаток;
				НоваяСтрока.КоличествоУпаковок = Остаток;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	КолонкиНеЗаполнять = "ИдентификаторСтроки,НомерСтроки";
	Для Каждого СтрокаССериями Из ИзначальнаяТаблицаССериями Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаССериями.Серия) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Объект.РасходМатериаловИРабот.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаССериями, , КолонкиНеЗаполнять);
		НоваяСтрока.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		
	КонецЦикла;
	
	Если Объект.РасходМатериаловИРабот.Количество() > 0 Тогда
		ПараметрыУказанияСерий = Документы.ЭтапПроизводства2_2.ПараметрыУказанияСерий(Объект);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий.РасходМатериаловИРабот);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
&Перед("КоличествоУпаковокФактПриИзмененииНаСервере")
Процедура П3_КоличествоУпаковокФактПриИзмененииНаСервере()
	
	СоздатьСтрокуСвободногоОстаткаВыпускаНаСервере(Объект.Статус);
		
КонецПроцедуры

&НаСервере
Процедура СоздатьСтрокуСвободногоОстаткаВыпускаНаСервере(СтатусДокументаИсходный)
	
	Если Объект.ВыходныеИзделия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СтатусДокументаИсходный = Перечисления.СтатусыЭтаповПроизводства2_2.Начат Тогда
		Возврат;
	КонецЕсли;
		
	КоличествоУпаковокВыпускИтого = Объект.ВыходныеИзделия.Итог("КоличествоУпаковок");
	КоличествоСверхЗапланировано = Объект.КоличествоУпаковокФакт - КоличествоУпаковокВыпускИтого;
	
	Если КоличествоСверхЗапланировано <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.П3_СлужебныйПересчетВыпуска < 0 Тогда
		ПересчитатьВыпускОбратноНаСервере();
	ИначеЕсли Объект.П3_СлужебныйПересчетВыпуска > 0 Тогда
		ПересчитатьВыпускОбратноДляПоложительногоЗначения();
	КонецЕсли;
	
	ИсходнаяСтрокаВыходныеИзделия = Объект.ВыходныеИзделия[0]; 
	Для Каждого СтрокаВыходогоИзделия Из Объект.ВыходныеИзделия Цикл
		Если Не ЗначениеЗаполнено(СтрокаВыходогоИзделия.Назначение) Тогда
			ИсходнаяСтрокаВыходныеИзделия = СтрокаВыходогоИзделия;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	Если Объект.ВыходныеИзделияСерии.Количество() > 0 Тогда
		Серия = Объект.ВыходныеИзделияСерии[0].Серия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИсходнаяСтрокаВыходныеИзделия.Назначение) Тогда
		ИсходнаяСтрокаВыходныеИзделия = Объект.ВыходныеИзделия.Добавить();
		ЗаполнитьЗначенияСвойств(ИсходнаяСтрокаВыходныеИзделия, Объект.ВыходныеИзделия[0], , "КодСтроки, Назначение, КоличествоУпаковок, Количество, ИдентификаторСтроки");
		ИсходнаяСтрокаВыходныеИзделия.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		ИсходнаяСтрокаВыходныеИзделия.КодСтроки = Объект.ВыходныеИзделия.Количество();
		ИсходнаяСтрокаВыходныеИзделия.КлючНоменклатура = Новый УникальныйИдентификатор;
	КонецЕсли;
		
	ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок = ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок + КоличествоСверхЗапланировано;
	
	КоэффициентФакт = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ИсходнаяСтрокаВыходныеИзделия.Упаковка, 
																			   ИсходнаяСтрокаВыходныеИзделия.Номенклатура);
	
	ИсходнаяСтрокаВыходныеИзделия.Количество = ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок * КоэффициентФакт;
	
	Объект.П3_СлужебныйПересчетВыпуска = 1;
	
	КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
	
	ПересчитатьВыходныеИзделияСерии(Серия);
	
	ОбщиеПересчетВыходныхИзделий();
	
КонецПроцедуры

// Османов Н.М. 25.04.2025 
&НаСервере
Процедура ПересчитатьВыпускОбратноНаСервере(ВыводитьСообщения = Истина)
	
	КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
	
	Если КоличествоСтрокВыходныеИзделия = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтатьяРасходов = ОбщегоНазначенияДоработкиСервер.ЗначениеКонстанты("СтатьяРасходаПотериОтБрака");
	ОтборПоБраку = Новый Структура("СтатьяРасходов", СтатьяРасходов);
	ТаблицаБрака = Объект.ВыходныеИзделия.Выгрузить(ОтборПоБраку);
	
	ОбщегоНазначенияДоработкиСервер.СвернутьТаблицуЗначений(ТаблицаБрака);
	
	Если ТаблицаБрака.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОтборПоНеБраку = Новый Структура("СтатьяРасходов", ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка());
	ТаблицаОсновная = Объект.ВыходныеИзделия.Выгрузить(ОтборПоНеБраку);
	Если ТаблицаОсновная.Количество() = 0 Тогда
		Если ВыводитьСообщения Тогда
			ТекстСообщения = НСтр("ru='Не удаётся восстановить историю, так как не осталось изначальных строк'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	ОбщегоНазначенияДоработкиСервер.СвернутьТаблицуЗначений(ТаблицаОсновная);
	
	Получатель = ТаблицаОсновная[0].Получатель;
	
	КоличествоВсего = Объект.ВыходныеИзделия.Итог("КоличествоУпаковок");
	
	ИсходнаяСтрокаВыходныеИзделия = Объект.ВыходныеИзделия[0];
	КоэффициентФакт = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ИсходнаяСтрокаВыходныеИзделия.Упаковка, 
																			   ИсходнаяСтрокаВыходныеИзделия.Номенклатура);
	НоваяТаблица = ТаблицаОсновная.СкопироватьКолонки();
	
	Для Каждого СтрокаБрака Из ТаблицаБрака Цикл
		
		Если ЗначениеЗаполнено(СтрокаБрака.П3_НазначениеДоПересчета) Тогда
			
			НайденнаяСтрока = ТаблицаОсновная.Найти(СтрокаБрака.П3_НазначениеДоПересчета, "Назначение");
			Если НайденнаяСтрока = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = НоваяТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
			НоваяСтрока.КоличествоУпаковок  = НоваяСтрока.КоличествоУпаковок  + СтрокаБрака.КоличествоУпаковок;
			НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентФакт;
			
			ТаблицаОсновная.Удалить(НайденнаяСтрока);
			
		Иначе
			
			КоличествоУпаковок = СтрокаБрака.КоличествоУпаковок;
			НайденнаяСтрока = ТаблицаОсновная.Найти(СтрокаБрака.П3_НазначениеДоПересчета, "Назначение");
			
			НоваяСтрока = НоваяТаблица.Добавить();
			
			Если НайденнаяСтрока = Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаБрака, , "Получатель,СтатьяРасходов,АналитикаРасходов");
			Иначе
				КоличествоУпаковок = КоличествоУпаковок + НайденнаяСтрока.КоличествоУпаковок;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока, , "Получатель,СтатьяРасходов,АналитикаРасходов");
				ТаблицаОсновная.Удалить(НайденнаяСтрока);				
			КонецЕсли;
			
			НоваяСтрока.Получатель = Получатель;
			НоваяСтрока.НаправлениеВыпуска = Перечисления.ХозяйственныеОперации.ВыпускПродукцииНаСклад;
			Если НоваяСтрока.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
				НоваяСтрока.НаправлениеВыпуска = Перечисления.ХозяйственныеОперации.ВыпускПродукцииВПодразделение;
			КонецЕсли;
			НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
			НоваяСтрока.СписатьНаРасходы = Ложь;
			НоваяСтрока.КоличествоУпаковок  = КоличествоУпаковок;
			НоваяСтрока.Количество = КоличествоУпаковок * КоэффициентФакт;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаОсновнойТаблицы Из ТаблицаОсновная Цикл
		
		НоваяСтрока = НоваяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОсновнойТаблицы);
	
	КонецЦикла;
	
	Для Индекс = 1 По НоваяТаблица.Количество() Цикл
		
		НоваяСтрока = НоваяТаблица[Индекс - 1];
		Если Не ЗначениеЗаполнено(НоваяСтрока.ИдентификаторСтроки) Тогда
			НоваяСтрока.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		НоваяСтрока.П3_НазначениеДоПересчета = Справочники.Назначения.ПустаяСсылка();
		НоваяСтрока.КодСтроки = Индекс;
		
	КонецЦикла;
	
	Объект.ВыходныеИзделия.Загрузить(НоваяТаблица);
	КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
	
	Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	Если Объект.ВыходныеИзделияСерии.Количество() > 0 Тогда
		Серия = Объект.ВыходныеИзделияСерии[0].Серия;
	КонецЕсли;
	
	Объект.П3_СлужебныйПересчетВыпуска = 0;
	
	ПересчитатьВыходныеИзделияСерии(Серия);
	
	ОбщиеПересчетВыходныхИзделий();
	
КонецПроцедуры

// Османов Н.М. 25.04.2025 
&НаСервере
Процедура ПересчитатьВыпускОбратноДляПоложительногоЗначения()
	
	Если Объект.П3_СлужебныйПересчетВыпуска <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ВыходныеИзделия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
		
	Разница = Объект.КоличествоУпаковокФакт - Объект.КоличествоУпаковокПлан;
	Если Разница <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИсходнаяСтрокаВыходныеИзделия = Объект.ВыходныеИзделия[0]; 
	Для Каждого СтрокаВыходогоИзделия Из Объект.ВыходныеИзделия Цикл
		Если Не ЗначениеЗаполнено(СтрокаВыходогоИзделия.Назначение) Тогда
			ИсходнаяСтрокаВыходныеИзделия = СтрокаВыходогоИзделия;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ИсходнаяСтрокаВыходныеИзделия.Назначение) Тогда
		Возврат;
	КонецЕсли;
	
	Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	Если Объект.ВыходныеИзделияСерии.Количество() > 0 Тогда
		Серия = Объект.ВыходныеИзделияСерии[0].Серия;
	КонецЕсли;
	
	ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок = ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок - Разница;
	
	КоэффициентФакт = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ИсходнаяСтрокаВыходныеИзделия.Упаковка, 
																			   ИсходнаяСтрокаВыходныеИзделия.Номенклатура);
	
	ИсходнаяСтрокаВыходныеИзделия.Количество = ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок * КоэффициентФакт;
	
	Объект.П3_СлужебныйПересчетВыпуска = 0;
	
	КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
	
	ПересчитатьВыходныеИзделияСерии(Серия);
	
	ОбщиеПересчетВыходныхИзделий();
	
КонецПроцедуры

// Османов Н.М. 25.04.2025 
&НаСервере
Процедура ОтразитьТехпотериВыпускаНаСервере()
	
	КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
	
	Если КоличествоСтрокВыходныеИзделия = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИтогПоКоличествуУпаковок = Объект.ВыходныеИзделия.Итог("КоличествоУпаковок");
	КоличествоТехпотери = ИтогПоКоличествуУпаковок - Объект.КоличествоУпаковокФакт;
	
	Если КоличествоТехпотери <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.П3_СлужебныйПересчетВыпуска < 0 Тогда
		ПересчитатьВыпускОбратноНаСервере();
	ИначеЕсли Объект.П3_СлужебныйПересчетВыпуска > 0 Тогда
		ПересчитатьВыпускОбратноДляПоложительногоЗначения();
	КонецЕсли;
	
	Объект.КоличествоУпаковокОтменено = Объект.КоличествоУпаковокПлан - Объект.КоличествоУпаковокФакт;
	
	ИсходнаяСтрокаВыходныеИзделия = Объект.ВыходныеИзделия[0]; 
	
	КоэффициентФакт = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ИсходнаяСтрокаВыходныеИзделия.Упаковка, 
																			   ИсходнаяСтрокаВыходныеИзделия.Номенклатура);
																			   
	ВыходныеИзделияБезНазначения = Объект.ВыходныеИзделия.Выгрузить(Новый Структура("Назначение",
																	ПредопределенноеЗначение("Справочник.Назначения.ПустаяСсылка")));
																	
	ЕстьНазначения = Не ВыходныеИзделияБезНазначения.Количество() = КоличествоСтрокВыходныеИзделия;
	ОдинаковоеКоличество = (ВыходныеИзделияБезНазначения.Количество() = 0) 
							Или (ВыходныеИзделияБезНазначения.Количество() = КоличествоСтрокВыходныеИзделия);
	Если ОдинаковоеКоличество И КоличествоСтрокВыходныеИзделия > 1 Тогда

		ИтогПоКоличеству = Объект.ВыходныеИзделия.Итог("Количество");
		ИтогПоСумме = Объект.ВыходныеИзделия.Итог("Сумма");
		
		Для Индекс = 2 По КоличествоСтрокВыходныеИзделия Цикл
			
			ИндексСтрокиДляУдаления = КоличествоСтрокВыходныеИзделия - Индекс + 1;
			Объект.ВыходныеИзделия.Удалить(ИндексСтрокиДляУдаления);
			
		КонецЦикла;
		
		ИсходнаяСтрокаВыходныеИзделия.Количество = ИтогПоКоличеству;
		ИсходнаяСтрокаВыходныеИзделия.КоличествоУпаковок = ИтогПоКоличествуУпаковок;
		ИсходнаяСтрокаВыходныеИзделия.Сумма = ИтогПоСумме;
		ИсходнаяСтрокаВыходныеИзделия.Произведено = Истина;
		
	КонецЕсли;
	
	Если ЕстьНазначения Тогда
		
		КоличествоОсталосьСписать = КоличествоТехпотери;
		
		КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
		
		Для Индекс = 1 По 2 Цикл // 1 - строки без назначения, потом 2 - строки с назначением

			Для ИндексДокумента = 1 По КоличествоСтрокВыходныеИзделия Цикл
				
				СтрокаВыходныеИзделия = Объект.ВыходныеИзделия[ИндексДокумента - 1];
				СтрокаВыходныеИзделия.Произведено = Истина;
			
				Если КоличествоОсталосьСписать <=0 Тогда
					Прервать;
				КонецЕсли;
				
				Если Индекс = 1 И ЗначениеЗаполнено(СтрокаВыходныеИзделия.Назначение) Тогда
					Продолжить;
				КонецЕсли;
				
				Если Индекс = 2 И Не ЗначениеЗаполнено(СтрокаВыходныеИзделия.Назначение) Тогда
					Продолжить;
				КонецЕсли;
				
				КоличествоДляСписания = Мин(СтрокаВыходныеИзделия.КоличествоУпаковок, КоличествоОсталосьСписать);
				СтрокаВыходныеИзделия.КоличествоУпаковок = СтрокаВыходныеИзделия.КоличествоУпаковок - КоличествоОсталосьСписать;
				СтрокаВыходныеИзделия.Количество = СтрокаВыходныеИзделия.КоличествоУпаковок * КоэффициентФакт;
				Если КоличествоДляСписания > 0 Тогда
					ДобавитьНовуюСтрокуТехпотерь(СтрокаВыходныеИзделия, КоличествоДляСписания, КоэффициентФакт);
				КонецЕсли;
				
				КоличествоОсталосьСписать = КоличествоОсталосьСписать - КоличествоДляСписания;
				
			КонецЦикла;		
			
		КонецЦикла;
		
		КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
		
		Для Индекс = 1 По КоличествоСтрокВыходныеИзделия Цикл
						
			ИндексСтрокиДляУдаления = КоличествоСтрокВыходныеИзделия - Индекс;
			
			Если Объект.ВыходныеИзделия[ИндексСтрокиДляУдаления].КоличествоУпаковок = 0 Тогда
				Объект.ВыходныеИзделия.Удалить(ИндексСтрокиДляУдаления);
			КонецЕсли;			
			
		КонецЦикла;
		
		КолонкиНеСуммировать = "СтатусУказанияСерий,СтатусУказанияСерийОтправитель,СтатусУказанияСерийПолучатель,Цена";
		
		ТаблицаВыходныхИзделий = Объект.ВыходныеИзделия.Выгрузить();
		ОбщегоНазначенияДоработкиСервер.СвернутьТаблицуЗначений(ТаблицаВыходныхИзделий, КолонкиНеСуммировать);
		Объект.ВыходныеИзделия.Загрузить(ТаблицаВыходныхИзделий);
		
	Иначе
		
		СтрокаВыходныеИзделия = Объект.ВыходныеИзделия[0];
		СтрокаВыходныеИзделия.КоличествоУпаковок = СтрокаВыходныеИзделия.КоличествоУпаковок - КоличествоТехпотери;
		СтрокаВыходныеИзделия.Количество = СтрокаВыходныеИзделия.КоличествоУпаковок * КоэффициентФакт;
		СтрокаВыходныеИзделия.Произведено = Истина;
		
		ДобавитьНовуюСтрокуТехпотерь(СтрокаВыходныеИзделия, КоличествоТехпотери, КоэффициентФакт);
		
	КонецЕсли;
	
	Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	Если Объект.ВыходныеИзделияСерии.Количество() > 0 Тогда
		Серия = Объект.ВыходныеИзделияСерии[0].Серия;
	КонецЕсли;
	
	Объект.П3_СлужебныйПересчетВыпуска = -1;
	
	КоличествоСтрокВыходныеИзделия = Объект.ВыходныеИзделия.Количество();
	
	ПересчитатьВыходныеИзделияСерии(Серия);
	
	ОбщиеПересчетВыходныхИзделий();
	
КонецПроцедуры

// Османов Н.М. 16.06.2025 
&НаСервере
Процедура ПересчитатьВыходныеИзделияСерии(Серия)

	Объект.ВыходныеИзделияСерии.Очистить();
	
	Если Объект.ВыходныеИзделия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаВыходныеИзделия Из Объект.ВыходныеИзделия Цикл
		
		НоваяСтрока = Объект.ВыходныеИзделияСерии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаВыходныеИзделия);
		НоваяСтрока.Серия = Серия;
		
	КонецЦикла;
	
	ТаблицаССериями = Объект.ВыходныеИзделияСерии.Выгрузить();
	ОбщегоНазначенияДоработкиСервер.СвернутьТаблицуЗначений(ТаблицаССериями);
	Объект.ВыходныеИзделияСерии.Загрузить(ТаблицаССериями);

КонецПроцедуры

&НаСервере
Процедура ОбщиеПересчетВыходныхИзделий()

	// очищаем ВидыЗапасовИзделия (чтобы они заполнились типовым механизмом заново при проведении) 
	Объект.ВидыЗапасовИзделия.Очистить();
	
	Для Индекс = 0 По Объект.ВыходныеИзделия.Количество() - 1 Цикл
		СтрокаВыходногоИзделия = Объект.ВыходныеИзделия[Индекс];
		СтрокаВыходногоИзделия.КодСтроки = Индекс + 1;
	КонецЦикла;

КонецПроцедуры

// Османов Н.М. 25.04.2025 
&НаСервере
Процедура ДобавитьНовуюСтрокуТехпотерь(ИсходнаяСтрока, КоличествоТехпотери, КоэффициентФакт)
	
	НоваяСтрокаВыходныеИзделия = Объект.ВыходныеИзделия.Добавить();	
	ЗаполнитьЗначенияСвойств(НоваяСтрокаВыходныеИзделия, ИсходнаяСтрока);
	НоваяСтрокаВыходныеИзделия.П3_НазначениеДоПересчета = НоваяСтрокаВыходныеИзделия.Назначение;
	НоваяСтрокаВыходныеИзделия.Назначение = Справочники.Назначения.ПустаяСсылка();
	НоваяСтрокаВыходныеИзделия.КоличествоУпаковок = КоличествоТехпотери;      
	НоваяСтрокаВыходныеИзделия.КлючНоменклатура = Новый УникальныйИдентификатор;
	
	НоваяСтрокаВыходныеИзделия.Количество = КоличествоТехпотери * КоэффициентФакт;
	НоваяСтрокаВыходныеИзделия.КодСтроки = Объект.ВыходныеИзделия.Количество(); 
	СтруктураПредприятияТПСПроизводство = ОбщегоНазначенияДоработкиСервер.ЗначениеКонстанты("СтруктураПредприятияТПСПроизводство");
	НоваяСтрокаВыходныеИзделия.Получатель = СтруктураПредприятияТПСПроизводство;
	НоваяСтрокаВыходныеИзделия.АналитикаРасходов = СтруктураПредприятияТПСПроизводство;
	НоваяСтрокаВыходныеИзделия.СтатьяРасходов = ОбщегоНазначенияДоработкиСервер.ЗначениеКонстанты("СтатьяРасходаПотериОтБрака");
	НоваяСтрокаВыходныеИзделия.НаправлениеВыпуска =  Перечисления.ХозяйственныеОперации.СписаниеТоваровПоТребованию;
	НоваяСтрокаВыходныеИзделия.СписатьНаРасходы = Истина;
	НоваяСтрокаВыходныеИзделия.Произведено = Истина;

КонецПроцедуры

&НаСервере
&После("УстановитьУсловноеОформлениеВыходныеИзделия")
Процедура П3_УстановитьУсловноеОформлениеВыходныеИзделия()
	
	Если Не Пользователи.РолиДоступны("П3_РедактироватьВыпускЭтапа") Тогда
		Элементы.ГруппаПродукция.ТолькоПросмотр = Истина;
		Элементы.ВыходныеИзделия.КоманднаяПанель.ТолькоПросмотр = Истина;
		Для Каждого КомандаТаблицы Из Элементы.ВыходныеИзделия.КоманднаяПанель.ПодчиненныеЭлементы Цикл
			КомандаТаблицы.Доступность = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
&ИзменениеИКонтроль("СтатусПриИзменении")
Процедура П3_СтатусПриИзменении(Элемент)

	#Вставка
	Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Завершен") Тогда
	
		ТекстВопроса = НСтр("ru = 'Добавить замывку'");
		СуществующаяЗамывка = УжеЕстьЗамывка();
		Если СуществующаяЗамывка.ЕстьЗамывка Тогда
			Если СуществующаяЗамывка.ЕстьЗамывкаВОбеспечении И СуществующаяЗамывка.ЕстьЗамывкаВПобочке Тогда
				ОбработатьИзменениеСтатусаНаСервере();
				Возврат;
			КонецЕсли;
			
			Если СуществующаяЗамывка.ЕстьЗамывкаВОбеспечении Тогда
				ТекстВопроса = НСтр("ru = 'Добавить замывку в Побочный и промежуточный выход?'");
			Иначе
				ТекстВопроса = НСтр("ru = 'Добавить замывку в Обеспечение?'");
			КонецЕсли;
		КонецЕсли;
		
		Если СуществующаяЗамывка.ЭтоПолуфабрикат Тогда		
			ПоказатьВопрос(Новый ОписаниеОповещения("П3_СтатусПриИзмененииЗавершение", ЭтотОбъект), 
					   	   ТекстВопроса, 
					   	   РежимДиалогаВопрос.ДаНет);
		Иначе
			ОбработатьИзменениеСтатусаНаСервере();
		КонецЕсли;
	Иначе
		ОбработатьИзменениеСтатусаНаСервере();
	КонецЕсли;
	#КонецВставки
	
	#Удаление
	ОбработатьИзменениеСтатусаНаСервере();
	#КонецУдаления

КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
Процедура П3_СтатусПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОбработатьИзменениеСтатусаНаСервере();
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	ДополнительныеПараметры.Вставить("ИмяПоследующейПроцедуры", "ОбработатьИзменениеСтатусаНаСервере");
	
	ДобавитьЗамывкуНаКлиенте(ДополнительныеПараметры);

КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
&ИзменениеИКонтроль("КоличествоУпаковокФактПриИзменении")
Процедура П3_КоличествоУпаковокФактПриИзменении(Элемент)

	#Вставка
	Если Объект.КоличествоУпаковокФакт >= Объект.КоличествоУпаковокПлан И Объект.КоличествоУпаковокОтменено = 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Добавить замывку'");
		СуществующаяЗамывка = УжеЕстьЗамывка();
		Если СуществующаяЗамывка.ЕстьЗамывка Тогда
			Если СуществующаяЗамывка.ЕстьЗамывкаВОбеспечении И СуществующаяЗамывка.ЕстьЗамывкаВПобочке Тогда
				КоличествоУпаковокФактПриИзмененииНаСервере();
				П3_КоличествоУпаковокФактПриИзмененииПосле(Элемент);
				Возврат;
			КонецЕсли;
			
			Если СуществующаяЗамывка.ЕстьЗамывкаВОбеспечении Тогда
				ТекстВопроса = НСтр("ru = 'Добавить замывку в Побочный и промежуточный выход?'");
			Иначе
				ТекстВопроса = НСтр("ru = 'Добавить замывку в Обеспечение?'");
			КонецЕсли;
		КонецЕсли;
		
		Если СуществующаяЗамывка.ЭтоПолуфабрикат Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("П3_КоличествоУпаковокФактПриИзмененииЗавершение", ЭтотОбъект), 
				   	   	   ТекстВопроса, 
				   	   	   РежимДиалогаВопрос.ДаНет);
		Иначе
			КоличествоУпаковокФактПриИзмененииНаСервере();
			П3_КоличествоУпаковокФактПриИзмененииПосле(Элемент);
		КонецЕсли;
		
	Иначе
		КоличествоУпаковокФактПриИзмененииНаСервере();
		П3_КоличествоУпаковокФактПриИзмененииПосле(Элемент);
	КонецЕсли;
	#КонецВставки
	
	#Удаление
	КоличествоУпаковокФактПриИзмененииНаСервере();
	#КонецУдаления

КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
Процедура П3_КоличествоУпаковокФактПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		КоличествоУпаковокФактПриИзмененииНаСервере();
		П3_КоличествоУпаковокФактПриИзмененииПосле(Неопределено);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура;
	КонецЕсли;
	ДополнительныеПараметры.Вставить("ИмяПоследующейПроцедуры", "КоличествоУпаковокФактПриИзмененииНаСервере");
	
	ДобавитьЗамывкуНаКлиенте(ДополнительныеПараметры);
	
КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
Процедура ДобавитьЗамывкуНаКлиенте(ДополнительныеПараметры) Экспорт
		
	НоменклатураЗамывки = НоменклатураЗамывкиМассив();
	ДополнительныеПараметры.Вставить("НоменклатураЗамывки", НоменклатураЗамывки);
	Если НоменклатураЗамывки.Количество() > 1 Тогда
		
		СписокВопроса = Новый СписокЗначений;
		Для Каждого СтрокаНоменклатуры Из НоменклатураЗамывки Цикл
			СписокВопроса.Добавить(СтрокаНоменклатуры.Номер, 
								   СтрШаблон("Склад: %1 | Номенклатура: %2",
								   			 СтрокаНоменклатуры.Склад,
								   			 СтрокаНоменклатуры.Номенклатура));
		КонецЦикла;
		
		ТекстСообщения = НСтр("ru = 'Выберите склад и номенклатуру замывки'");
		СписокВопроса.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("ДобавитьЗамывкуНаКлиентеФрагмент", 
																	ЭтотОбъект,
																	ДополнительныеПараметры), 
											ТекстСообщения);
	Иначе
		ДобавитьЗамывкуНаКлиентеФрагмент(Неопределено, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
Процедура ДобавитьЗамывкуНаКлиентеФрагмент(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.НоменклатураЗамывки.Количество() > 0 И Не ВыбранныйЭлемент = Неопределено Тогда
		
		Для Каждого СтрокаПараметра Из ДополнительныеПараметры.НоменклатураЗамывки Цикл
			Если СтрокаПараметра.Номер = ВыбранныйЭлемент.Значение Тогда
				ДобавитьЗамывку(СтрокаПараметра);
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		ДобавитьЗамывку(ВыбранныйЭлемент);
		
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИмяПоследующейПроцедуры = "КоличествоУпаковокФактПриИзмененииНаСервере" Тогда
		КоличествоУпаковокФактПриИзмененииНаСервере();
		П3_КоличествоУпаковокФактПриИзмененииПосле(Неопределено);
	ИначеЕсли ДополнительныеПараметры.ИмяПоследующейПроцедуры = "ОбработатьИзменениеСтатусаНаСервере" Тогда
		ОбработатьИзменениеСтатусаНаСервере();
	ИначеЕсли ДополнительныеПараметры.ИмяПоследующейПроцедуры = "СкорректироватьСтрокиЗаказа" Тогда
		СкорректироватьСтрокиЗаказа(ДополнительныеПараметры.ВыбранноеЗначение);
	КонецЕсли;

КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаСервере
Функция НоменклатураЗамывкиМассив()
	
	Возврат Документы.ЭтапПроизводства2_2.НоменклатураЗамывки(Объект.Дата, Объект.РабочийЦентр);	
	
КонецФункции

// Османов Н.М. 14.10.2025
&НаСервере
Функция УжеЕстьЗамывка()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Возврат Документы.ЭтапПроизводства2_2.УжеЕстьНоменклатураЗамывки(ДокументОбъект);
	
КонецФункции

// Османов Н.М. 01.10.2025  
&НаСервере
Процедура ДобавитьЗамывку(СтруктураОтбора)

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	
	Результат = Документы.ЭтапПроизводства2_2.ДобавитьЗамывку(ДокументОбъект, СтруктураОтбора);
	Если Результат.НомерСтрокиОбеспечения = 0 Или Результат.НомерСтрокиПобочки = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Прочитать();
	
	//СтрокаОбъекта = ДокументОбъект.ОбеспечениеМатериаламиИРаботами[Результат.НомерСтрокиОбеспечения - 1];
	НоваяСтрокаФормы = Объект.ОбеспечениеМатериаламиИРаботами[Результат.НомерСтрокиОбеспечения - 1];
	//ЗаполнитьЗначенияСвойств(НоваяСтрокаФормы, СтрокаОбъекта);
	НоваяСтрокаФормы.ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрокаФормы.Номенклатура, "ТипНоменклатуры");
	
	ЗаполнитьРасходМатериаловИРаботПоДаннымОбеспеченияНаСервере(1);
	
	СтрокаОбъекта = ДокументОбъект.ПобочныеИзделия[Результат.НомерСтрокиПобочки - 1];
	НоваяСтрокаФормы = Объект.ПобочныеИзделия.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрокаФормы, СтрокаОбъекта);
	НоваяСтрокаФормы.НаправлениеВыпуска = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад");
	НоваяСтрокаФормы.ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрокаФормы.Номенклатура, "ТипНоменклатуры");
	НоваяСтрокаФормы.ТипСтатьи = 1;
	НоваяСтрокаФормы.НужноОкруглять = Истина;
	НоваяСтрокаФормы.ВладелецИзделияДоступен = Истина;
	
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Завершен");
	Модифицированность = Истина;
	
КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
Асинх Процедура П3_ОбработкаВыбораЗамена(ВыбранноеЗначение, ИсточникВыбора)
		
	ТекстВопроса = НСтр("ru = 'Добавить замывку'");
	СуществующаяЗамывка = УжеЕстьЗамывка();
	Если СуществующаяЗамывка.ЕстьЗамывка Тогда
		Если СуществующаяЗамывка.ЕстьЗамывкаВОбеспечении И СуществующаяЗамывка.ЕстьЗамывкаВПобочке Тогда
			СкорректироватьСтрокиЗаказа(ВыбранноеЗначение);
			Возврат;
		КонецЕсли;
		
		Если СуществующаяЗамывка.ЕстьЗамывкаВОбеспечении Тогда
			ТекстВопроса = НСтр("ru = 'Добавить замывку в Побочный и промежуточный выход?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Добавить замывку в Обеспечение?'");
		КонецЕсли;
	КонецЕсли;
	
	Если СуществующаяЗамывка.ЭтоПолуфабрикат Тогда
		
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			СкорректироватьСтрокиЗаказа(ВыбранноеЗначение);
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура("ИмяПоследующейПроцедуры", "СкорректироватьСтрокиЗаказа");
		ДополнительныеПараметры.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
		ДобавитьЗамывкуНаКлиенте(ДополнительныеПараметры);
		
	Иначе
		СкорректироватьСтрокиЗаказа(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

// Османов Н.М. 01.10.2025  
&НаКлиенте
&ИзменениеИКонтроль("ОбработкаВыбора")
Процедура П3_ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени(
	"Документ.ЭтапПроизводства2_2.Форма.ФормаДокумента.Событие.ОбработкаВыбора");

	Если ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ВводДолиСтоимости" Тогда

		ОбработкаВводаДолиСтоимости(ТекущийЭлемент.ТекущиеДанные, ВыбранноеЗначение);

	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.СостояниеОбеспеченияЗаказов.Форма.Форма" Тогда

		АдресТаблицыДвижений = ЗаполнитьОбеспечениеИзФормыСостоянияОбеспечения(
		ВыбранноеЗначение.КодДействия,
		ВыбранноеЗначение.АдресАналитик,
		ВыбранноеЗначение.ПараметрыЗаполнения);

		// Возврат управления в форму обработки состояния обеспечения.
		ВыполнитьОбработкуОповещения(ВыбранноеЗначение.ВыполняемоеОповещение, АдресТаблицыДвижений);

	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.РазрешениеНаЗаменуМатериалов.Форма.ПодборАналогов" Тогда

		ЗаменитьНаАналоги(ВыбранноеЗначение.АдресВХранилище);

	ИначеЕсли НоменклатураКлиент.ЭтоУказаниеСерий(ИсточникВыбора) Тогда

		НоменклатураКлиент.ОбработатьУказаниеСерии(ЭтотОбъект, ПараметрыУказанияСерий[ВыбранноеЗначение.ИмяТЧ], ВыбранноеЗначение);

		ТоварыСерияПересчитатьЦены(, ВыбранноеЗначение.ИмяТЧ);

		Если ВыбранноеЗначение.ИмяТЧ = "ВыходныеИзделия" 
			И ПроизводствоКлиент.НеобходимоРассчитатьПроцентРаспределенияЗатратНаВыходныеИзделия(
			Элементы.ВыходныеИзделия, 
			ПараметрыРаспределенияЗатрат, 
			РаботаСТабличнымиЧастямиКлиентСервер.КэшСтроки(Элементы.ВыходныеИзделия, ЭтотОбъект)) Тогда

			РассчитатьПроцентРаспределенияЗатратНаВыходныеИзделия();

		КонецЕсли;

	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.ПомощникЗакрытияЗаказов.Форма.ФормаЗакрытия" Тогда

		#Вставка
		П3_ОбработкаВыбораЗамена(ВыбранноеЗначение, ИсточникВыбора);
		#КонецВставки
		#Удаление
		СкорректироватьСтрокиЗаказа(ВыбранноеЗначение);
		#КонецУдаления

	ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ПодборМатериаловПоСпецификации" Тогда

		НачатьОбработкуРезультатаПодбора(ВыбранноеЗначение, "ПодборМатериаловПоСпецификации");

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
