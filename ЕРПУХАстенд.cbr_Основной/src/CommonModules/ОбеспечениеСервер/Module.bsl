#Область РасчетСтатистикиПотребленияЯдро

//++ ШмелевР 27.01.2026 ЗКП.2
&ИзменениеИКонтроль("СтатистикаПотребленияЯдро")
Процедура cbr_СтатистикаПотребленияЯдро(Запрос, ИмяТаблицыТовары, ИмяТаблицыРезультат)

	// Этап №1.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** ИмяТаблицыТовары - ВременнаяТаблицаЗапроса - Вход. Имя таблицы задано следующим параметром.
	//    *** РазличныеТоварыИСклады - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//    *** ТопологияСкладов - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Центр - СправочникСсылка.Склады - склад являющийся центром распределительной сети,
	//                     для исходного товара и склада.
	// ИмяТаблицыТовары - Строка - Имя таблицы, первой таблицы менеджера временных таблиц. 
	ДобавитьВременныеТаблицыТопологииСкладов(Запрос, ИмяТаблицыТовары);

	// Этап №2.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** РазличныеТоварыИСклады - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №1.
	//    *** СезонныеКоэффициенты - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Коэффициент - Число - сезонный коэффициент для каждого товара и склада переданнх на вход.
	ДобавитьВременнуюТаблицуСезонныеКоэффициенты(Запрос);

	// Этап №3.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** РазличныеТоварыИСклады - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №1.
	//    *** ГрафикРаботыПоКалендарю - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Календарь - СправочникСсылка.Календари
	//        **** Дата - Дата - дата по календарю складав периоде расчета статистики, для перерданных складов.
	ДобавитьВременнуюТаблицуГрафикРаботыПоКалендарю(Запрос);

	// Этап №4.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** РазличныеТоварыИСклады - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №1.
	//    *** ТопологияСкладов - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №1.
	//    *** ПризнакиНаличияОстатковПоДатам - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Дата - Дата
	//        **** ЕстьКонечныйОстаток - Булево - если на дату есть конечный остаток
	//        **** ЕстьОстатки - Булево - если на дату есть начальный или конечный остаток
	//    *** РасходПоДнямДанныеИБ - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Дата - Дата
	//        **** Количество - Число - расход в эту дату
	//    *** ВозвратыДанныеИБ - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Дата - Дата
	//        **** Количество - Число - возврат в эту дату
	ДобавитьВременныеТаблицыОстатковРасходаИВозвратов(Запрос);

	// Этап №5.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** РасходПоДнямДанныеИБ - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №4.
	//    *** ВозвратыДанныеИБ - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №4.
	//    *** СезонныеКоэффициенты - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №4.
	//    *** ГрафикРаботыПоКалендарю - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №2.
	//    *** ПризнакиНаличияОстатковПоДатам - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №4.
	//    *** РасходПоДням - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Дата - Дата
	//        **** Количество - Число - расход в эту дату
	//        **** Количество - Число - расход в эту дату, если в эту дату был начальный и конечный остаток.
	//    *** Расходы - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Количество - Число - расход за весь период расчета
	//        **** КоличествоВДниПолныхПродаж - Число - расход  за весь период расчета, только в даты, когда был начальный и конечный остаток.
	//        **** КоличествоКвадрат - Число - расход квадрата за весь период расчета
	//        **** КоличествоВДниПолныхПродажКвадрат - Число - расход квадрата за весь период расчета, только в даты, когда был начальный и конечный остаток.
	//    *** Возвраты - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Количество - Число - возврат за весь период расчета
	УчестьКоэффициентыСезонностиРабочиеДниИДниНеполныхПродаж(Запрос);

	// Этап №6.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** ГрафикРаботыПоКалендарю - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №2.
	//    *** ПризнакиНаличияОстатковПоДатам - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №4.
	//    *** РасходПоДням - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №5.
	//    *** ПризнакиУчетаДней - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Дата - Дата
	//        **** Учитывать - Булево - нужно учитывать дату как дату продаж
	//        **** УчитыватьВДняхПолныхПродаж - Число - нужно учитывать дату как дату продаж, когда был и начальный и конечный остаток
	ДобавитьВременнуюТаблицуПризнакиУчетаДней(Запрос);

	// Этап №7.
	// Параметры:
	// Запрос - Запрос:
	//  * МенеджерВременныхТаблиц - МенеджерВременныхТаблиц:
	//   ** Таблицы - ВременныеТаблицыЗапроса:
	//    *** ПризнакиУчетаДней - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №6.
	//    *** Расходы - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №5.
	//    *** Возвраты - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №5.
	//    *** ГрафикРаботыПоКалендарю - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №2.
	//    *** ТопологияСкладов - ВременнаяТаблицаЗапроса - Вход. рассчитана на этапе №1.
	//    *** СреднедневноеПотребление - ВременнаяТаблицаЗапроса - Выход. Таблица с полями:
	//        **** Склад - СправочникСсылка.Склады
	//        **** Номенклатура - СправочникСсылка.Номенклатура
	//        **** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры
	//        **** Количество - Число
	//        **** Расход - Число
	//        **** Дисперсия - Число
	//        **** ПотреблениеСтабильно - Число
	ДобавитьВременнуюТаблицуСреднедневноеПотребление(Запрос);

	ТекстыЗапроса = Новый Массив();
	Текст =
	"ВЫБРАТЬ
	|	Товары.Номенклатура                                                КАК Номенклатура,
	|	Товары.Характеристика                                              КАК Характеристика,
	|	Товары.Склад                                                       КАК Склад,
	|	&ПериодКлассификацииДатаНачала                                     КАК ДатаНачалаПериода,
	|	&ПериодКлассификацииДатаОкончания                                  КАК ДатаОкончанияПериода,
	|	ЕСТЬNULL(СреднедневноеПотреблениеТовара.Расход, 0)                 КАК Потребление,
	|	ЕСТЬNULL(СреднедневноеПотреблениеТовара.Количество, 0)             КАК СреднедневноеПотребление,
	|	ЕСТЬNULL(СреднедневноеПотреблениеТовара.Дисперсия, 0)              КАК ДисперсияПотребления,
	|	ЕСТЬNULL(СреднедневноеПотреблениеТовара.ПотреблениеСтабильно, ЛОЖЬ)КАК ПотреблениеСтабильно
	|ПОМЕСТИТЬ РезультатПереопределяемый
	|ИЗ
	|	ТоварыПереопределяемый КАК Товары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СреднедневноеПотребление КАК СреднедневноеПотреблениеТовара
	|		ПО СреднедневноеПотреблениеТовара.Номенклатура   = Товары.Номенклатура
	|		 И СреднедневноеПотреблениеТовара.Характеристика = Товары.Характеристика
	|		 И СреднедневноеПотреблениеТовара.Склад          = Товары.Склад
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад";

	Текст = СтрЗаменить(Текст, "ТоварыПереопределяемый", ИмяТаблицыТовары);
	Текст = СтрЗаменить(Текст, "РезультатПереопределяемый", ИмяТаблицыРезультат);
	ТекстыЗапроса.Добавить(Текст);

	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ РазличныеТоварыИСклады");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ТопологияСкладов");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ГрафикРаботыПоКалендарю");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ПризнакиНаличияОстатковПоДатам");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ СезонныеКоэффициенты");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ РасходПоДнямДанныеИБ");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ВозвратыДанныеИБ");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ РасходПоДням");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ Расходы");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ Возвраты");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ПризнакиУчетаДней");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ СреднедневноеПотребление");
	ТекстыЗапроса.Добавить("УНИЧТОЖИТЬ ВтГрафик");

	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	#Вставка
	//++ ШмелевР 27.01.2026 ЗКП.2
	Если Запрос.Параметры.Свойство("МестоЗапуска") И Запрос.Параметры.МестоЗапуска = "ОбеспечениеПотребностейНоваяСхема" Тогда 
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПериодКлассификацииДатаНачала", "&НачалоИсторииПотребления");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПериодКлассификацииДатаОкончания", "&ОкончаниеИсторииПотребления");
	КонецЕсли;
	//-- ШмелевР 27.01.2026 ЗКП.2
	#КонецВставки
	Запрос.Выполнить();

КонецПроцедуры //-- ШмелевР 27.01.2026 ЗКП.2

#КонецОбласти