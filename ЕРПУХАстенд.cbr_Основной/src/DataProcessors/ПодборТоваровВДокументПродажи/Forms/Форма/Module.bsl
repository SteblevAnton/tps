#Область СлужебныеПроцедурыИФункции

//++ cba AlopinAS 27.01.26
&ИзменениеИКонтроль("ДобавитьВКорзинуНаКлиенте")
&НаСервере
Функция cbr_ДобавитьВКорзинуНаКлиенте(ПараметрыТовара, НовыеСтроки)

	ПустаяКорзина = Объект.Корзина.Количество() = 0;
	РеквизитыОтбора = "Артикул, НоменклатураНабора, ХарактеристикаНабора, Номенклатура, Характеристика,
		|ХарактеристикиИспользуются, Упаковка, Цена, ВидЦены, Склад, ПроцентРучнойСкидки, ТипНоменклатуры,
		|СрокПоставки, ВариантОбеспечения, Обособленно, Серия";
	Если ИспользоватьДатыОтгрузки Тогда
		РеквизитыОтбора = РеквизитыОтбора + ", ДатаОтгрузки";
	КонецЕсли;
	Отбор = Новый Структура(РеквизитыОтбора);

	ИдентификаторыДляУстановкиСтатусаСерий = Новый Массив;
	ОбрабатываемыеСтроки = Новый Массив;
	ОбрабатываемыеСтрокиСПогрешностью = Новый Массив;
	ПогрешностьСтрок = Новый Соответствие();
	ОбрабатываемыеСтрокиСЦеной = Новый Массив;

	Для Каждого НоваяСтрока Из НовыеСтроки Цикл

		ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
		#Вставка
		
		//++ cba AlopinAS 27.01.26
		Отбор.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.НеТребуется");
		//-- cba AlopinAS 27.01.26
		
		#КонецВставки
		Если НоваяСтрока.Цена = 0 Тогда
			Отбор.ВидЦены = ПредопределенноеЗначение("Справочник.ВидыЦен.ПустаяСсылка");
		КонецЕсли;

		Если Не ЗначениеЗаполнено(Отбор.ТипНоменклатуры) Тогда
			Отбор.ТипНоменклатуры = ПараметрыТовара.ТипНоменклатуры;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(Отбор.ВариантОбеспечения)
				И НЕ Отбор.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Набор") Тогда
			ЗаполнитьВариантОбеспеченияИФлагНаСервере(Отбор);
		КонецЕсли;

		Если ПустаяКорзина Тогда
			РезультатПоиска = Новый Массив;
		Иначе
			РезультатПоиска = Объект.Корзина.НайтиСтроки(Отбор);
		КонецЕсли;

		ЦелевойМассив = ОбрабатываемыеСтроки;

		Если РезультатПоиска.Количество() = 0 Или НоваяСтрока.Погрешность <> 0 Тогда

			ТекущаяСтрока = Объект.Корзина.Добавить();
			ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Отбор);
			ТекущаяСтрока.ИндексНабора = ?(ЗначениеЗаполнено(ТекущаяСтрока.НоменклатураНабора), 1, 0);

			ЦелевойМассив = ОбрабатываемыеСтрокиСПогрешностью;
			ПогрешностьСтрок.Вставить(ТекущаяСтрока, НоваяСтрока.Погрешность);

			Если Не ПараметрыТовара.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Набор")
				И (РежимПодбораБезСуммовыхПараметров
				Или Не ЗапрашиватьЦену
				Или ЦенообразованиеВызовСервера.НеобходимПересчетЦеныПриИзменении(ТекущаяСтрока.Номенклатура, "Серия", Неопределено)) Тогда

				ЦелевойМассив = ОбрабатываемыеСтрокиСЦеной;

			КонецЕсли;

		Иначе
			ТекущаяСтрока = РезультатПоиска[0];
		КонецЕсли;

		ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковок + НоваяСтрока.КоличествоУпаковок;

		ЦелевойМассив.Добавить(ТекущаяСтрока);

		Если Не ПоказыватьПодобранныеТовары Тогда

			ТекстОповещения = Символы.ПС + НСтр("ru = 'Товар ""[Товар]"" стоимостью [Цена] [Валюта] в количестве [КоличествоУпаковок] [ЕдиницаИзмерения] добавлен в корзину';
												|en = 'The ""[Товар]"" goods of value [Цена] [Валюта] are added to the cart in quantity of [КоличествоУпаковок] [ЕдиницаИзмерения]'");

			ВставляемыеЗначения = Новый Структура("Товар, Цена, Валюта, КоличествоУпаковок, ЕдиницаИзмерения");

			ВставляемыеЗначения.Товар              = Строка(ТекущаяСтрока.Номенклатура) + ?(ЗначениеЗаполнено(НоваяСтрока.Характеристика)," (" + НоваяСтрока.Характеристика + ")","");
			ВставляемыеЗначения.Цена               = Формат(ТекущаяСтрока.Цена, "ЧДЦ=2; ЧН=");
			ВставляемыеЗначения.Валюта             = Валюта;
			ВставляемыеЗначения.ЕдиницаИзмерения   = ?(ЗначениеЗаполнено(НоваяСтрока.Упаковка), НоваяСтрока.Упаковка, НСтр("ru = 'ед.';
																															|en = 'unit'"));
			ВставляемыеЗначения.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковок;

			ТекстОповещения = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ТекстОповещения, ВставляемыеЗначения);

		КонецЕсли;

		ТекущаяСтрока.ЗаказатьНаСклад = Истина;
		Если ЗначениеЗаполнено(ПараметрыУказанияСерий) Тогда
			Если Не ПроверитьУстановитьСтатусСерий(ТекущаяСтрока, ПараметрыУказанияСерий) Тогда
				ИдентификаторыДляУстановкиСтатусаСерий.Добавить(ТекущаяСтрока.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	ДополнитьСтруктуруДействиямиПересчетаСумм(СтруктураДействий);

	СтруктураДействийСПогрешностью = ОбщегоНазначения.СкопироватьРекурсивно(СтруктураДействий, Ложь);
	СтруктураДействийСПогрешностью.Вставить("ПересчитатьСуммуСУчетомПогрешностиОкругления",
		Новый Структура("ПогрешностиПоСтрокам", ПогрешностьСтрок));

	СтруктураДействийСПогрешностью.Вставить("ЗаполнитьПризнакНаличияНоменклатурыПродаваемойСовместно",
		ВариантАнализаНоменклатурыПродаваемойСовместно);

	СтруктураДействийСЦеной = Новый Структура();
	Если ОбрабатываемыеСтрокиСЦеной.Количество() Тогда
		СтруктураДействийСЦеной = ОбщегоНазначения.СкопироватьРекурсивно(СтруктураДействийСПогрешностью, Ложь);
		ДополнитьСтруктуруДействиямиРасчетаЦены(ЭтотОбъект, СтруктураДействийСЦеной, ОбрабатываемыеСтрокиСЦеной[0]);
	КонецЕсли;

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		Объект.Корзина,
		Неопределено);

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтрокиСПогрешностью,
		СтруктураДействийСПогрешностью,
		Объект.Корзина,
		Неопределено);

	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтрокиСЦеной,
		СтруктураДействийСЦеной,
		Объект.Корзина,
		Неопределено);

	Если ИдентификаторыДляУстановкиСтатусаСерий.Количество() > 0 Тогда
		ЗаполнитьСтатусыУказанияСерийПоИдентификатору(ИдентификаторыДляУстановкиСтатусаСерий);
	КонецЕсли;

	Если НовыеСтроки.Количество() > 0 Тогда
		Элементы.Корзина.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
	КонецЕсли;

	Возврат ТекстОповещения;

КонецФункции
//-- cba AlopinAS 27.01.26

#КонецОбласти
