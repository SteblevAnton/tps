#Область ОбработчикиСобытийФормы

&НаСервере
Процедура cbr_ПередЗаписьюНаСервереПосле(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//++ БаженоваЕО 27.02.2026 ПРД.21
	ТекущийОбъект.cbr_МаксимальнаяРучнаяСкидка = 0;
	Для Каждого СтрТЧ Из ТекущийОбъект.cbr_ЦеныПоТоварам Цикл
		ТекущийОбъект.cbr_МаксимальнаяРучнаяСкидка = Макс(ТекущийОбъект.cbr_МаксимальнаяРучнаяСкидка, СтрТЧ.ПрцентРучнаяСкидка);
	КонецЦикла;

	ТекущийОбъект.cbr_ПроцентТУ = 1;
	ТекущийОбъект.cbr_МаксимальнаяРучная = ТекущийОбъект.cbr_МаксимальнаяРучнаяСкидка + ТекущийОбъект.cbr_ПроцентТУ + 1;

	ТекущийОбъект.cbr_ПроцентАВ = 1;
	Для Каждого Стр Из ТекущийОбъект.cbr_АгентскиеВознаграждения Цикл
		ТекущийОбъект.cbr_ПроцентАВ = ТекущийОбъект.cbr_ПроцентАВ + Стр.СтавкаАВ;
	КонецЦикла;
	//-- БаженоваЕО 27.02.2026
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//++ Тусеев Н.Ю. 04.02.2026 ПРД12
&НаКлиенте
Процедура cbr_АгентскоеВознаграждениеПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, 
	"СтраницаАгентскоеВознаграждение", "Видимость", 
	Объект.cbr_АгентскоеВознаграждение);
	
КонецПроцедуры //++ Тусеев Н.Ю. 04.02.2026 ПРД12

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыcbr_АгентскиеВознаграждения
&НаКлиенте
Процедура cbr_АгентскоеВознаграждениеДоговорАгентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	//++ cbr-larkovgeorg 12.02.2026 ПРД12
	
	ТекСтрока = Элементы.cbr_АгентскиеВознаграждения.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекСтрока.Агент) Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура("Отбор, АгентскоеВознаграждение", Новый Структура("Организация, Контрагент",
			Объект.Организация, ТекСтрока.Агент), Истина);
		ОповещениеЗакрытия = Новый ОписаниеОповещения("cbr_АгентскоеВознаграждениеДоговорАгентаНачалоВыбораЗавершение",
			ЭтотОбъект);
		ОткрытьФорму("Документ.ВерсияСоглашенияКоммерческийДоговор.Форма.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, , ,
			, ОповещениеЗакрытия);
	КонецЕсли;
	
КонецПроцедуры //-- cbr-larkovgeorg 12.02.2026 ПРД12

&НаКлиенте
Процедура cbr_АгентскоеВознаграждениеДоговорАгентаНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	//++ cbr-larkovgeorg 12.02.2026 ПРД12
	Если ЗначениеЗаполнено(Результат) Тогда 
	
		ТекСтрока = Элементы.cbr_АгентскиеВознаграждения.ТекущиеДанные;
		ТекСтрока.ДоговорАгента = Результат;
		
	КонецЕсли
	
КонецПроцедуры //-- cbr-larkovgeorg 12.02.2026 ПРД12
#КонецОбласти
#Область СлужебныеПроцедурыИФункции

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Процедура ЗаполнитьЦенуНоменклатуры(ИдентификаторСтроки)
	
	Результат = 0;
	ТекущаяСтрокаТЧ = Объект.cbr_ЦеныПоТоварам.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ОсновнаяЕдиницаИзмеренияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТЧ.Номенклатура, "ЕдиницаИзмерения");
	
	Если ТекущаяСтрокаТЧ.ЕдиницаИзмерения = ОсновнаяЕдиницаИзмеренияНоменклатуры Тогда
		ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	Иначе
		ЕдиницаИзмерения = ТекущаяСтрокаТЧ.ЕдиницаИзмерения;
	КонецЕсли;
	
	Характеристика = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТЧ.Характеристика, "ХарактеристикаНоменклатурыДляЦенообразования");
	
	Если Характеристика = Неопределено Тогда
		Характеристика = Справочники.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ 
		|	ЦеныНоменклатуры25СрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры25.СрезПоследних(&Дата, Номенклатура = &Номенклатура
		|	И ХарактеристикаЦО = &Характеристика
		|	И УпаковкаЦО = &Упаковка
		|	И ВидЦены = &ВидЦены) КАК ЦеныНоменклатуры25СрезПоследних";
	
	Запрос.УстановитьПараметр("ВидЦены", ТекущаяСтрокаТЧ.ВидЦены);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("Упаковка", ЕдиницаИзмерения);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Номенклатура", ТекущаяСтрокаТЧ.Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Результат = РезультатЗапроса.Выгрузить()[0].Цена;
	КонецЕсли;
	
	ТекущаяСтрокаТЧ.Цена = Результат;
	
	Если ТекущаяСтрокаТЧ.ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка() Тогда
		ТекущаяСтрокаТЧ.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТЧ.Номенклатура, "ЕдиницаИзмерения");
	КонецЕсли;
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Функция ПроверитьИспользованиеХарактеристик(Номенклатура)
	
	ХарактеристикиИспользуются = Справочники.Номенклатура.ПроверитьИспользованиеХарактеристикИПолучитьВладельцаДляВыбора(Номенклатура, );
	
	Возврат ХарактеристикиИспользуются;
	
КонецФункции
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаСервере
Процедура ЦеныПоТоварамНоменклатураПриИзмененииНаСервере(ИдентификаторСтроки)
	
 	ТекущаяСтрокаТЧ = Объект.cbr_ЦеныПоТоварам.НайтиПоИдентификатору(ИдентификаторСтроки);
	ТекущаяСтрокаТЧ.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрокаТЧ.Номенклатура, "ЕдиницаИзмерения");
	ТекущаяСтрокаТЧ.ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ВидЦен");
 	ЗаполнитьЦенуНоменклатуры(ИдентификаторСтроки);
 	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ПересчитатьЗависимыеПоказатели(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	Если Элемент.Имя = "ЦеныПоТоварамКоличество" Тогда
		ТекущаяСтрока.СуммаРучнаяСкидка = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена * ТекущаяСтрока.ПрцентРучнаяСкидка / 100;
		Если ЗначениеЗаполнено(ТекущаяСтрока.Сумма) Тогда
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.ЦенаСоСкидкой;
		Иначе
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена - ТекущаяСтрока.СуммаРучнаяСкидка;
		КонецЕсли;
	ИначеЕсли Элемент.Имя = "ЦеныПоТоварамЦена" Тогда
		ТекущаяСтрока.ЦенаСоСкидкой = ТекущаяСтрока.Цена * (1 - (ТекущаяСтрока.ПрцентРучнаяСкидка / 100));
		ТекущаяСтрока.СуммаРучнаяСкидка = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена * ТекущаяСтрока.ПрцентРучнаяСкидка / 100;
		Если ЗначениеЗаполнено(ТекущаяСтрока.Сумма) Тогда
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.ЦенаСоСкидкой;
		Иначе
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена - ТекущаяСтрока.СуммаРучнаяСкидка;
		КонецЕсли;
			
	ИначеЕсли Элемент.Имя = "ЦеныПоТоварамПрцентРучнаяСкидка" Тогда
		ТекущаяСтрока.ЦенаСоСкидкой = ТекущаяСтрока.Цена * (1 - (ТекущаяСтрока.ПрцентРучнаяСкидка / 100));
		ТекущаяСтрока.СуммаРучнаяСкидка = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена * ТекущаяСтрока.ПрцентРучнаяСкидка / 100;
		Если ЗначениеЗаполнено(ТекущаяСтрока.Сумма) Тогда
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.ЦенаСоСкидкой;
		Иначе
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена - ТекущаяСтрока.СуммаРучнаяСкидка;
		КонецЕсли;
			
	ИначеЕсли Элемент.Имя = "ЦеныПоТоварамЦенаСоСкидкой" Тогда
		ТекущаяСтрока.ПрцентРучнаяСкидка = (100 - ТекущаяСтрока.ЦенаСоСкидкой / ТекущаяСтрока.Цена * 100);
		ТекущаяСтрока.СуммаРучнаяСкидка = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена * ТекущаяСтрока.ПрцентРучнаяСкидка / 100;
		Если ЗначениеЗаполнено(ТекущаяСтрока.Сумма) Тогда
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.ЦенаСоСкидкой;
		Иначе
			ТекущаяСтрока.Сумма = ТекущаяСтрока.Количество * ТекущаяСтрока.Цена - ТекущаяСтрока.СуммаРучнаяСкидка;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЦеныПоТоварам

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ХарактеристикиИспользуются = ПроверитьИспользованиеХарактеристик(ТекущаяСтрока.Номенклатура);

	ТекущаяСтрока.ХарактеристикиИспользуются = ХарактеристикиИспользуются;
	
	ИдентификаторСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
	ЦеныПоТоварамНоменклатураПриИзмененииНаСервере(ИдентификаторСтроки);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамХарактеристикаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ИдентификаторСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
	ЗаполнитьЦенуНоменклатуры(ИдентификаторСтроки);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамЕдиницаИзмеренияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ИдентификаторСтроки = ТекущаяСтрока.ПолучитьИдентификатор();
	ЗаполнитьЦенуНоменклатуры(ИдентификаторСтроки);
		
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамВидЦеныПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ИдентификаторСтроки = ТекущаяСтрока.ПолучитьИдентификатор();	
	ЗаполнитьЦенуНоменклатуры(ИдентификаторСтроки);
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамКоличествоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ПересчитатьЗависимыеПоказатели(Элемент);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамЦенаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ПересчитатьЗависимыеПоказатели(Элемент);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамПрцентРучнаяСкидкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ПересчитатьЗависимыеПоказатели(Элемент);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

//++AlopinAS 28.01.2026 ПРД.20
&НаКлиенте
Процедура ЦеныПоТоварамЦенаСоСкидкойПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЦеныПоТоварам.ТекущиеДанные;
	ПересчитатьЗависимыеПоказатели(Элемент);
	
КонецПроцедуры
//--AlopinAS 28.01.2026 ПРД.20

#КонецОбласти