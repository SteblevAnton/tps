//////////////////////////////////////////////////////////
// РАБОТА С ФОРМАМИ

#Область РеквизитыФормы

//////////////////////////////////////////////////////////
// РАБОТА С РЕКВИЗИТАМИ ФОРМЫ

// Процедура - Добавить реквизит формы
//
// Параметры:
//  Форма				 - УправляемаяФорма - Форма, на которую добавляем реквизит
//  Имя					 - Строка - Имя реквизита
//  Тип					 - ОписаниеТипов - Тип реквизита 
//  Путь				 - Строка - Путь к создаваемому реквизиту. Не включает имя реквизита. 
//  Заголовок			 - Строка - Содержит отображаемый текст реквизита
//  СохраняемыеДанные	 - Булево - Реквизит содержит сохраняемые данные
//
Процедура ДобавитьРеквизитФормы(Форма, Имя, Знач Тип="", Путь="", Заголовок = "", СохраняемыеДанные = Ложь) Экспорт
	
	Если Тип = "" Тогда
		Тип = Новый ОписаниеТипов("Булево");
	КонецЕсли;
	
	МассивРеквизитов = Новый Массив;
    Реквизит = Новый РеквизитФормы(Имя, Тип, Путь, Заголовок, СохраняемыеДанные);
    МассивРеквизитов.Добавить(Реквизит);
    Форма.ИзменитьРеквизиты(МассивРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ЭлементыФормы

//////////////////////////////////////////////////////////
// РАБОТА С ЭЛЕМЕНТАМИ ФОРМЫ

// Функция - Добавить группу формы
//
// Параметры:
//  Форма								 - УправляемаяФорма - Форма, на которую добавляем страницу
//  Имя									 - Строка - Имя элемента формы
//  Заголовок							 - Строка - Заголовок страницы
//  ЭлементВладелец						 - ЭлементФормы - Группа на которую добавляем страницу
//  ЭлементСтраницаПередКоторымВставляем - ЭлементФормы - Страница перед которой добавляем страницу
//  Свертываемая						 - Булево - Сертываемая группа
// 
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьСтраницыФормы(Форма, Имя, Заголовок="", ЭлементВладелец, ЭлементСтраницаПередКоторымВставляем=Неопределено, ОтображениеСтраниц=Неопределено) Экспорт
	
	//Добавим или вставим страницу
	Если ЭлементСтраницаПередКоторымВставляем = Неопределено Тогда
		НоваяГруппа = Форма.Элементы.Добавить(Имя,Тип("ГруппаФормы"),ЭлементВладелец);
	Иначе
		НоваяГруппа = Форма.Элементы.Вставить(Имя,Тип("ГруппаФормы"),ЭлементВладелец,ЭлементСтраницаПередКоторымВставляем);
	КонецЕсли;
	
	НоваяГруппа.Вид = ВидГруппыФормы.Страницы;
	НоваяГруппа.Видимость = Истина;
	НоваяГруппа.Доступность = Истина;
	НоваяГруппа.Заголовок = Заголовок;
	
	Если ОтображениеСтраниц = Неопределено Тогда
		НоваяГруппа.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	Иначе
		НоваяГруппа.ОтображениеСтраниц = ОтображениеСтраниц;
	КонецЕсли;
	
	Возврат НоваяГруппа;
	
КонецФункции

// Функция - Добавить страницу формы
//
// Параметры:
//  Форма								 - УправляемаяФорма - Форма, на которую добавляем страницу
//  Имя									 - Строка - Имя элемента формы
//  Заголовок							 - Строка - Заголовок страницы
//  ЭлементВладелец						 - ЭлементФормы - Группа на которую добавляем страницу
//  ЭлементСтраницаПередКоторымВставляем - ЭлементФормы - Страница перед которой добавляем страницу
// 
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьСтраницуФормы(Форма, Имя, Заголовок, ЭлементВладелец, ЭлементСтраницаПередКоторымВставляем=Неопределено) Экспорт
	
	//Добавим или вставим страницу
	Если ЭлементСтраницаПередКоторымВставляем = Неопределено Тогда
		НоваяСтраница = Форма.Элементы.Добавить(Имя,Тип("ГруппаФормы"),ЭлементВладелец);
	Иначе
		НоваяСтраница = Форма.Элементы.Вставить(Имя,Тип("ГруппаФормы"),ЭлементВладелец,ЭлементСтраницаПередКоторымВставляем);
	КонецЕсли;
	
	НоваяСтраница.Вид = ВидГруппыФормы.Страница;
	НоваяСтраница.Видимость = Истина;
	НоваяСтраница.Заголовок = Заголовок;
	
	Возврат НоваяСтраница;
	
КонецФункции

// Функция - Добавить группу формы
//
// Параметры:
//  Форма								 - УправляемаяФорма - Форма, на которую добавляем страницу
//  Имя									 - Строка - Имя элемента формы
//  Заголовок							 - Строка - Заголовок страницы
//  ЭлементВладелец						 - ЭлементФормы - Группа на которую добавляем страницу
//  ЭлементСтраницаПередКоторымВставляем - ЭлементФормы - Страница перед которой добавляем страницу
//  Свертываемая						 - Булево - Сертываемая группа
//  ВидГруппы							 - ВидГруппыФормы - вид группы формы
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьГруппуФормы(Форма, Имя, Заголовок="", ЭлементВладелец, ЭлементСтраницаПередКоторымВставляем=Неопределено, Свертываемая=Ложь, ВидГруппы=Неопределено) Экспорт
	
	//Добавим или вставим страницу
	Если ЭлементСтраницаПередКоторымВставляем = Неопределено Тогда
		НоваяГруппа = Форма.Элементы.Добавить(Имя,Тип("ГруппаФормы"),ЭлементВладелец);
	Иначе
		НоваяГруппа = Форма.Элементы.Вставить(Имя,Тип("ГруппаФормы"),ЭлементВладелец,ЭлементСтраницаПередКоторымВставляем);
	КонецЕсли;
	//++ nv:cvelodubovb 21.05.2025 
	Если ВидГруппы = Неопределено Тогда
		НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	Иначе
		НоваяГруппа.Вид = ВидГруппы;
	КонецЕсли;
	//-- nv:cvelodubovb 21.05.2025
	
	НоваяГруппа.Видимость = Истина;
	НоваяГруппа.Доступность = Истина;
	НоваяГруппа.Заголовок = Заголовок;
	
	Если Заголовок = "" Тогда
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
	КонецЕсли;
	
	Если Свертываемая Тогда
		НоваяГруппа.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
		НоваяГруппа.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;		
	КонецЕсли;
	
	Возврат НоваяГруппа;
	
КонецФункции

// Функция - Добавить поле формы
//
// Параметры:
//  Форма						 - УправляемаяФорма - Форма, на которую добавляем страницу 
//  Имя							 - Строка - Имя элемента формы
//										- Если не задано, то формируется из пути к данным
//  ПутьКДанным					 - Строка - Путь к данным элемента
//  ЭлементВладелец				 - ЭлементФормы - Группа, на которую добавляем элемент 
//  ЭлементПередКоторымВставляем - ЭлементФормы - Элемент перед которым добавляем элемент
//	Действия 					 - Строка - Строка, состоящие из добавляемых действий, разделенных запятой
//                               - Массив - Массив со строковым представлением действий
//
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьТаблицуФормы(Форма, 
	Знач Имя = Неопределено, ПутьКДанным = Неопределено, 
	ЭлементВладелец = Неопределено, ЭлементПередКоторымВставляем = Неопределено,
	Действия = Неопределено) Экспорт
	
	// Если имя не задано, то формируем имя из пути к данным
	Если Имя = Неопределено Тогда
		Имя = ИмяЭлементаФормы(ПутьКДанным);
	КонецЕсли;
	
	//Добавим или вставим страницу
	Если ЭлементПередКоторымВставляем = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(Имя,Тип("ТаблицаФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец));
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(Имя,Тип("ТаблицаФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец),ЭлементПередКоторымВставляем);
	КонецЕсли;
	
	НовыйЭлемент.Видимость = Истина;
	
	Если ПутьКДанным <> Неопределено Тогда
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
	КонецЕсли;
	
	// Добавляем действия, если необходимо
	Если Действия <> Неопределено Тогда
		ДобавитьДействияЭлементаФормы(НовыйЭлемент,Действия);
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

// Функция - Добавить группу колонок таблицы
//
// Параметры:
//  Форма								 - УправляемаяФорма - Форма, на которую добавляем страницу
//  Имя									 - Строка - Имя элемента формы
//  Заголовок							 - Строка - Заголовок страницы
//  ЭлементВладелец						 - ЭлементФормы - Группа на которую добавляем страницу
//  ЭлементСтраницаПередКоторымВставляем - ЭлементФормы - Страница перед которой добавляем страницу
//  Свертываемая						 - Булево - Сертываемая группа
// 
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьГруппуКолонокТаблицыФормы(Форма, Имя, 
	Заголовок = Неопределено, 
	ЭлементВладелец = Неопределено, ЭлементСтраницаПередКоторымВставляем = Неопределено) Экспорт
	
	//Добавим или вставим страницу
	Если ЭлементСтраницаПередКоторымВставляем = Неопределено Тогда
		НоваяГруппа = Форма.Элементы.Добавить(Имя,Тип("ГруппаФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец));
	Иначе
		НоваяГруппа = Форма.Элементы.Вставить(Имя,Тип("ГруппаФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец),ЭлементСтраницаПередКоторымВставляем);
	КонецЕсли;
	
	НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
	НоваяГруппа.Видимость = Истина;
	НоваяГруппа.Доступность = Истина;
	НоваяГруппа.Группировка = ГруппировкаКолонок.Вертикальная;
	
	Если Заголовок = Неопределено Тогда
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
		НоваяГруппа.ОтображатьВШапке = Ложь;
	Иначе
		НоваяГруппа.Заголовок = Заголовок;
		НоваяГруппа.ОтображатьЗаголовок = Истина;
		НоваяГруппа.ОтображатьВШапке = Истина;
	КонецЕсли;
	
	Возврат НоваяГруппа;
	
КонецФункции

// Добавление поля формы
//
// Параметры:
//  Форма						 - УправляемаяФорма - Форма, на которую добавляем страницу 
//  Имя							 - Строка - Имя элемента формы
//										- Если не задано, то формируется из пути к данным
//  ПутьКДанным					 - Строка - Путь к данным элемента
//  Заголовок					 - Строка - Заголовок элемента
//  ВидПоля						 - ВидПоляФормы - Вид поля формы
//  ЭлементВладелец				 - ЭлементФормы - Группа, на которую добавляем элемент 
//  ЭлементПередКоторымВставляем - ЭлементФормы - Элемент перед которым добавляем элемент
//	Действия 					 - Строка - Строка, состоящие из добавляемых действий, разделенных запятой
//                               - Массив - Массив со строковым представлением действий
// 
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьПолеФормы(Форма, 
	Знач Имя = Неопределено, ПутьКДанным = Неопределено, Заголовок = Неопределено, ВидПоля=Неопределено, 
	ЭлементВладелец = Неопределено, ЭлементПередКоторымВставляем = Неопределено,
	Действия = Неопределено) Экспорт
	
	// Если имя не задано, то формируем имя из пути к данным
	Если Имя = Неопределено Тогда
		Имя = ИмяЭлементаФормы(ПутьКДанным);
	КонецЕсли;
	
	// Если элемент с таким именем уже есть, то ничего не делаем
	Если Форма.Элементы.Найти(Имя) <> Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЭлементПередКоторымВставляем = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(Имя,Тип("ПолеФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец));
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(Имя,Тип("ПолеФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец),ЭлементПередКоторымВставляем);
	КонецЕсли;
	
	Если ВидПоля = Неопределено Тогда
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	Иначе
		НовыйЭлемент.Вид = ВидПоля;
	КонецЕсли;	
	
	НовыйЭлемент.Видимость = Истина;
	
	Если ПутьКДанным <> Неопределено Тогда
		НовыйЭлемент.ПутьКДанным = ПутьКДанным;
	КонецЕсли;	
	
	Если Заголовок <> Неопределено Тогда
		НовыйЭлемент.Заголовок = Заголовок;
	КонецЕсли;
	
	Если ВидПоля = ВидПоляФормы.ПолеФлажка Тогда
		НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;	
	КонецЕсли;
	
	// Добавляем действия, если необходимо
	Если Действия <> Неопределено Тогда
		ДобавитьДействияЭлементаФормы(НовыйЭлемент,Действия);
	КонецЕсли;
		
	Возврат НовыйЭлемент;
	
КонецФункции

// Функция - Добавить декорацию формы
//
// Параметры:
//  Форма								 - УправляемаяФорма - Форма, на которую добавляем страницу
//  Имя									 - Строка - Имя элемента формы
//  Заголовок							 - Строка - Заголовок страницы
//  ЭлементВладелец						 - ЭлементФормы - Группа на которую добавляем страницу
//  ЭлементСтраницаПередКоторымВставляем - ЭлементФормы - Страница перед которой добавляем страницу
// 
// Возвращаемое значение:
//   ЭлементФормы - добавленный элемент
//
Функция ДобавитьДекорациюФормы(Форма, Имя, 
	Заголовок = Неопределено, 
	ЭлементВладелец = Неопределено, ЭлементСтраницаПередКоторымВставляем = Неопределено) Экспорт
	
	//Добавим или вставим страницу
	Если ЭлементСтраницаПередКоторымВставляем = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(Имя,Тип("ДекорацияФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец));
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(Имя,Тип("ДекорацияФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец),ЭлементСтраницаПередКоторымВставляем);
	КонецЕсли;
	
	НовыйЭлемент.Видимость = Истина;
	НовыйЭлемент.Доступность = Истина;
	
	Если Заголовок <> Неопределено Тогда
		НовыйЭлемент.Заголовок = Заголовок;
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

// Добавляем действия для элемента формы.
// Имя обработчика формируется по правилу "ЭлементФормы.Имя+Действие"
//
// Параметры:
//  ЭлементФормы  				- ЭлементФормы - Элмент формы, на которое добавляются действия
//	Действия 					- Строка - Строка, состоящие из добавляемых действий, разделенных запятой
//                              - Массив - Массив со строковым представлением действий
//
Процедура ДобавитьДействияЭлементаФормы(ЭлементФормы, Знач Действия) Экспорт

	Если ТипЗнч(Действия) = Тип("Строка") Тогда
		Действия = СтрРазделить(Действия,",",Ложь);
	КонецЕсли;
	
	Если ТипЗнч(Действия) = Тип("Массив") Тогда	
		Для Каждого Действие Из Действия Цикл
			ЭлементФормы.УстановитьДействие(Действие,ЭлементФормы.Имя+Действие);
		КонецЦикла;
	КонецЕсли;	

КонецПроцедуры // ДобавитьДействияЭлементаФормы()

// Добавляет кнопку на форму
//
// Параметры:
//  Форма		 	- УправляемаяФорма - Форма, на которую добавляется кнопка
//  Имя			 	- Строка - Имя кнопки. Является уникальным среди всех элементов формы.
//	ИмяКоманды		- Строка - Имя команды формы
//  Заголовок		- Строка - Заголовок кнопки, отображаемый в настройке формы
//	Вид				- ВидКнопкиФормы - Вид кнопки
//  ЭлементВладелец - ЭлементФормы - Владелец элемента формы
//	ЭлементПередКоторымВставляем - ЭлементФормы - Элемент, перед которым добавляется кнопка
//
// Возвращаемое значение:
//  КнопкаФормы   	- КнопкаФормы - Добавленная кнопка формы
//
Функция ДобавитьКнопкуФормы(Форма, Имя, 
	ИмяКоманды = Неопределено, Заголовок = Неопределено, Вид = Неопределено, 
	ЭлементВладелец = Неопределено, ЭлементПередКоторымВставляем = Неопределено) Экспорт

	Если Форма.Элементы.Найти(Имя) <> Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЭлементПередКоторымВставляем = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(Имя,Тип("КнопкаФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец));
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(Имя,Тип("КнопкаФормы"),?(ЭлементВладелец=Неопределено,Форма,ЭлементВладелец),ЭлементПередКоторымВставляем);
	КонецЕсли;
	
	Если ИмяКоманды <> Неопределено Тогда
		НовыйЭлемент.ИмяКоманды = ИмяКоманды;
	КонецЕсли;
	
	Если Заголовок <> Неопределено Тогда
		НовыйЭлемент.Заголовок = Заголовок;
	Иначе
		НовыйЭлемент.Заголовок = Синоним(Имя);
	КонецЕсли;
	
	Если Вид = Неопределено Тогда
		Если ЭлементВладелец <> Неопределено
			И ЭлементВладелец.Вид = ВидГруппыФормы.КоманднаяПанель Тогда
			НовыйЭлемент.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Иначе
			НовыйЭлемент.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
		КонецЕсли;
	Иначе
		НовыйЭлемент.Вид = Вид;	
	КонецЕсли;
	
	Возврат НовыйЭлемент;

КонецФункции // ДобавитьКнопкуФормы()

// Добавляет кнопку и команду на форму
//
// Параметры:
//  Форма		 	- УправляемаяФорма - Форма, на которую добавляется кнопка
//  Имя			 	- Строка - Имя кнопки и команды. Является уникальным среди всех элементов формы.
//  Заголовок		- Строка - Заголовок кнопки, отображаемый в настройке формы
//	Вид				- ВидКнопкиФормы - Вид кнопки
//  ЭлементВладелец - ЭлементФормы - Владелец элемента формы
//	ЭлементПередКоторымВставляем - ЭлементФормы - Элемент, перед которым добавляется кнопка
//	Картинка - Картинка - картинка команды
//	Отображение - ОтображениеКнопки - вид отображения связанной кнопки
//	Подсказка - Строка - подсказка команды
//	СочетаниеКлавиш - СочетаниеКлавиш - акселератор команды
//  ИзменяетСохраняемыеДанные - Булево - признак необходимости сохранения данных при вызове команды
//	ИспользованиеТекущейСтроки - ИспользованиеТекущейСтроки - указывает, что для выполнения команды требуется наличие текущей строки
//	ИспользуемаяТаблица - ТаблицаФормы - задается вместе со свойством ИспользованиеТекущейСтроки для указания таблицы, текущая строка которой используется
//
// Возвращаемое значение:
//   КнопкаФормы   	- КнопкаФормы - Добавленная кнопка формы
//
Функция ДобавитьКнопкуИКомандуФормы(Форма, Имя,
	Заголовок = Неопределено, Вид = Неопределено,
	ЭлементВладелец = Неопределено, ЭлементПередКоторымВставляем = Неопределено,
	Картинка = Неопределено, Отображение = Неопределено, Подсказка = Неопределено, СочетаниеКлавиш = Неопределено,  
	ИзменяетСохраняемыеДанные = Ложь, 	ИспользованиеТекущейСтроки = Неопределено, ИспользуемаяТаблица = Неопределено) Экспорт
	
	НоваяКоманда = ДобавитьКомандуФормы(Форма, Имя, Неопределено, Заголовок, Картинка, Отображение, Подсказка, СочетаниеКлавиш,
		ИзменяетСохраняемыеДанные, ИспользованиеТекущейСтроки, ИспользуемаяТаблица);
		
	Если НоваяКоманда = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
		
	НоваяКнопка = ДобавитьКнопкуФормы(Форма, Имя, НоваяКоманда.Имя, Заголовок, Вид, ЭлементВладелец, ЭлементПередКоторымВставляем); 
	
	Возврат НоваяКнопка;

КонецФункции // ДобавитьКнопкуИКомандуФормы()

// Добавляет динамический список на форму
//
// Параметры:
//  Форма				 - УправляемаяФорма - форма, на которую добавляется динамический список
//	Имя            		 - Строка - имя динамического списка на форме и реквизита
//  ТекстЗапроса		 - Строка - текст запроса, если он не указан, необходимо указать таблицу
//  МассивКолонок		 - Массив - массив создаваемых колонок, значения тип строка. Пример: "Д,ДоговораКонтрагента" значит будет создана колонка
//									с заголовком "Д" и колонкой списка "ДоговораКонтрагента". Если указать просто "ДоговораКонтрагента", 
//									заголовок будет идентичен заголовку по умолчанию типа "Договор контрагента".
//  СписокДействий		 - Структура - структура содержит действия (свойства) динамического списка, можно указать только те которые необходимы.
//									   пример: СписокДействий = Новый Структура("Выбор,ПриАктивизацииСтроки","СписокВыбор","СписокПриАктивизацииСтроки")
//  ТаблицаСписка		 - Строка - содержит имя основной таблицы, пример: "Документ.РеализацияТоваровУслуг"
//  ЭлементВладелец		 - Строка - имя элемента на который будет размещен список, если пустое, тогда добавляется на форму
//  ВставитьПеред		 - Строка - имя элемента перед которым будет размещен список, если пустое, будет просто добавлен в конец
//  СвояКоманднаяПанель	 - Булево - если Истина, скрывает стандартную панель и создает свою пустую для будушего наполнения ИмяСписка+"КоманднаяПанель2"
//  ПараметрыЗапроса	 - Структура - содержит перечень параметров, если они используются в запросе.
//
Процедура ДобавитьДинамическийСписок(Форма, Имя = "Список", ТекстЗапроса = "", МассивКолонок, СписокДействий = Неопределено,
										ТаблицаСписка = "", ЭлементВладелец = "", ВставитьПеред = "", СвояКоманднаяПанель = Ложь, ПараметрыЗапроса = Неопределено) Экспорт
	
	Если ТекстЗапроса = "" И ТаблицаСписка = "" Тогда Сообщить("Ошибка формирования динамического списка, укажите запрос или таблицу"); 
		Возврат; 
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	//Создаем свою командную панель, тот случай когда я не нашел способа снять галочку "Автозаполнение"
	Если СвояКоманднаяПанель Тогда
		Если ВставитьПеред = "" Тогда
			ГруппаДинамическогоСписка  = Элементы.Добавить("Группа" + Имя + "CоСвоейКоманднойПанелью",Тип("ГруппаФормы"),?(ЭлементВладелец = "",Форма,Форма.Элементы[ЭлементВладелец]));
		Иначе
			ГруппаДинамическогоСписка  = Элементы.Вставить("Группа" + Имя + "CоСвоейКоманднойПанелью",Тип("ГруппаФормы"),?(ЭлементВладелец = "",Форма,Форма.Элементы[ЭлементВладелец]),Форма.Элементы[ВставитьПеред]);			
		КонецЕсли;
		ГруппаДинамическогоСписка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		ГруппаДинамическогоСписка.ОтображатьЗаголовок = Ложь;
		ГруппаДинамическогоСписка.Отображение = ОтображениеОбычнойГруппы.Нет;
		ГруппаДинамическогоСписка.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		КоманднаяПанельСписка  = Элементы.Добавить(Имя + "КоманднаяПанель2",Тип("ГруппаФормы"),ГруппаДинамическогоСписка);
		КоманднаяПанельСписка.Вид = ВидГруппыФормы.КоманднаяПанель;			
	КонецЕсли;
									
	//Создаем реквизит формы
	ТипыРеквизита = Новый Массив;                             
	ТипыРеквизита.Добавить(Тип("ДинамическийСписок"));
	ОписаниеТиповДляРеквизита = Новый ОписаниеТипов(ТипыРеквизита); 
	ДинамическийСписок = Новый РеквизитФормы(Имя, ОписаниеТиповДляРеквизита,,"",ЛОЖЬ); //Имя реквизита
	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(ДинамическийСписок);        
	
	//Задаем свойства реквизиту
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);        
	РеквизитДинамическийСписок = Форма[Имя]; //Имя реквизита
	Если ТекстЗапроса = "" Тогда
		РеквизитДинамическийСписок.ПроизвольныйЗапрос = ЛОЖЬ;
		РеквизитДинамическийСписок.ОсновнаяТаблица = ТаблицаСписка;
	Иначе
		РеквизитДинамическийСписок.ПроизвольныйЗапрос = ИСТИНА;
		РеквизитДинамическийСписок.ТекстЗапроса = ТекстЗапроса;
		Если ТаблицаСписка <> "" Тогда РеквизитДинамическийСписок.ОсновнаяТаблица = ТаблицаСписка; КонецЕсли;		
	КонецЕсли;
	
	//Заполняем параметры если они были указаны
	Если ПараметрыЗапроса <> Неопределено Тогда
		Для Каждого Параметра из ПараметрыЗапроса Цикл
			РеквизитДинамическийСписок.Параметры.УстановитьЗначениеПараметра(Параметра.Ключ,Параметра.Значение);	
		КонецЦикла;		 
	КонецЕсли;
	
	//Размещаем реквизит на форме
	Если ЛОЖЬ
		ИЛИ ВставитьПеред = "" 
		ИЛИ СвояКоманднаяПанель //Если своя командная панель тогда нет логики уже в параметре ВставитьПеред
		Тогда
		ТаблицаФормы = Элементы.Добавить(Имя,Тип("ТаблицаФормы"),?(СвояКоманднаяПанель,ГруппаДинамическогоСписка,?(ЭлементВладелец = "",Форма,Форма.Элементы[ЭлементВладелец])));
	Иначе
		ТаблицаФормы = Элементы.Вставить(Имя,Тип("ТаблицаФормы"),?(ЭлементВладелец = "",Форма,Форма.Элементы[ЭлементВладелец]),Форма.Элементы[ВставитьПеред]);	
	КонецЕсли;		
	ТаблицаФормы.ПутьКДанным = Имя; //Имя реквизита 
	
	//Если своя панель тогда скрываем стандартную
	Если СвояКоманднаяПанель Тогда
		Элементы[Имя].ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	КонецЕсли;

	//Создание колонок на основание МассивКолонок
	Для Каждого Элемента Из МассивКолонок Цикл
		ПараметрыКолонки = СтрЗаменить(Элемента,",",Символы.ПС);
		Если СтрЧислоСтрок(ПараметрыКолонки) > 1 Тогда
			Заголовок = СтрПолучитьСтроку(ПараметрыКолонки, 1);
			ИмяКолонки = СтрПолучитьСтроку(ПараметрыКолонки, 2);
		Иначе
			Заголовок = "";
			ИмяКолонки = ПараметрыКолонки; 
		КонецЕсли;	
		НоваяКолонкаТаблицы = Элементы.Добавить(Имя + ИмяКолонки, Тип("ПолеФормы"), ТаблицаФормы);
		Если Заголовок <> "" Тогда НоваяКолонкаТаблицы.Заголовок = Заголовок; КонецЕсли;
		НоваяКолонкаТаблицы.ПутьКДанным = Имя + "." + ИмяКолонки; 	
	КонецЦикла;
	
	//Задаем действия (События) на основание СписокДействий, если он определен
	Если СписокДействий <> Неопределено Тогда 	
		ПереченьСвойств = "ПриИзменении
						|Выбор
						|ПриАктивизацииСтроки
						|ВыборЗначения
						|ПриАктивизацииПоля
						|ПриАктивизацииЯчейки
						|ПередНачаломДобавления
						|ПередНачаломИзменения
						|ПередУдалением
						|ПриНачалеРедактирования
						|ПередОкончаниемРедактирования
						|ПриОкончанииРедактирования
						|ОбработкаВыбора
						|ПередРазворачиванием
						|ПередСворачиванием
						|ПослеУдаления
						|ПриСменеТекущегоРодителя
						|ОбработкаЗаписиНового
						|ПриСохраненииПользовательскихНастроекНаСервере
						|ПередЗагрузкойПользовательскихНастроекНаСервере
						|ПриЗагрузкеПользовательскихНастроекНаСервере
						|ПриОбновленииСоставаПользовательскихНастроекНаСервере
						|ОбработкаЗапросаОбновления
						|ПриПолученииДанныхНаСервере
						|НачалоПеретаскивания
						|ПроверкаПеретаскивания
						|ОкончаниеПеретаскивания
						|Перетаскивание";
		Для Счетчик = 1 по 28 Цикл
			ИмяСвойства = СтрПолучитьСтроку(ПереченьСвойств, Счетчик);
			Элементы[Имя].УстановитьДействие(ИмяСвойства,?(СписокДействий.Свойство(ИмяСвойства),СписокДействий[ИмяСвойства],""));
		КонецЦикла;
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

//////////////////////////////////////////////////////////
// РАБОТА С КОМАНДАМИ ФОРМЫ

// Добавляет команду на форму
//
// Параметры:
//  Форма		 - УправляемаяФорма - Форма, на которую добавляется команда
//  Имя			 - Строка - имя команды
//  Действие	 - Строка - имя процедуры обработчика команды
// 	Заголовок	 - Строка - отображаемый текст команды
//	Картинка - Картинка - картинка команды
//	Отображение - ОтображениеКнопки - вид отображения связанной кнопки
//	Подсказка - Строка - подсказка команды
//	СочетаниеКлавиш - СочетаниеКлавиш - акселератор команды 
//  ИзменяетСохраняемыеДанные - Булево - признак необходимости сохранения данных при вызове команды
//	ИспользованиеТекущейСтроки - ИспользованиеТекущейСтроки - указывает, что для выполнения команды требуется наличие текущей строки
//	ИспользуемаяТаблица - ТаблицаФормы - задается вместе со свойством ИспользованиеТекущейСтроки для указания таблицы, текущая строка которой используется
//
// Возвращаемое значение:
//   КомандаФормы   - Созданная команда
//
Функция ДобавитьКомандуФормы(Форма, Имя, 
	Действие = Неопределено, Заголовок = Неопределено,
	Картинка = Неопределено, Отображение = Неопределено, Подсказка = Неопределено, СочетаниеКлавиш = Неопределено,  
	ИзменяетСохраняемыеДанные = Ложь, 	ИспользованиеТекущейСтроки = Неопределено, ИспользуемаяТаблица = Неопределено) Экспорт
	
	Если Форма.Команды.Найти(Имя) <> Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяКоманда = Форма.Команды.Добавить(Имя);
	
	Если Действие = Неопределено Тогда
		НоваяКоманда.Действие = Имя;
	Иначе
		НоваяКоманда.Действие = Действие;
	КонецЕсли;
	
	Если Заголовок <> Неопределено Тогда
		НоваяКоманда.Заголовок = Заголовок;
	Иначе
		НоваяКоманда.Заголовок = Синоним(Имя);
	КонецЕсли;
	
	Если Картинка <> Неопределено Тогда
		НоваяКоманда.Картинка = Картинка;
	КонецЕсли;
	
	Если Отображение <> Неопределено Тогда
		НоваяКоманда.Отображение = Отображение;
	КонецЕсли;

	Если Подсказка <> Неопределено Тогда
		НоваяКоманда.Подсказка = Подсказка;
	КонецЕсли;

	Если СочетаниеКлавиш <> Неопределено Тогда
		НоваяКоманда.СочетаниеКлавиш = СочетаниеКлавиш;
	КонецЕсли;

	НоваяКоманда.ИзменяетСохраняемыеДанные = ИзменяетСохраняемыеДанные;

	Если ИспользованиеТекущейСтроки <> Неопределено Тогда
		НоваяКоманда.ИспользованиеТекущейСтроки = ИспользованиеТекущейСтроки;
	КонецЕсли;

	Если ИспользуемаяТаблица <> Неопределено Тогда
		НоваяКоманда.ИспользуемаяТаблица = ИспользуемаяТаблица;
	КонецЕсли;

	Возврат НоваяКоманда;

КонецФункции // ДобавитьКомандуФормы()

#КонецОбласти

#Область УсловноеОформление

//////////////////////////////////////////////////////////
// РАБОТА С УСЛОВНЫМ ОФОРМЛЕНИЕМ

// Процедура - Установить условное оформление элемента
//
// Параметры:
//  Форма		 - УправляемаяФорма - Форма, на которую добавляем страницу  
//  МассивИмен	 - Массив или Строка - Массив с именами элементов формы 
//  ПутьКДанным	 - Строка - Путь к данным отбора (проверка на Ложь)
//	ВидСравнения - Вид сравнения отбора компоновки данных
//	Значение 	 - Значение отбора компоновки данных
//	СтруктураПараметров - Структура с параметрами условного оформления
//
Процедура УстановитьУсловноеОформлениеЭлемента(Форма, МассивИмен, ПутьКДанным, ВидСравнения, Значение, СтурктураПараметров) Экспорт
	
	НовыйЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ТипЗнч(МассивИмен) = Тип("Строка") Тогда	
		ПолеОформления = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(МассивИмен);
	ИначеЕсли ТипЗнч(МассивИмен) = Тип("Массив") Тогда
		Для Каждого Имя ИЗ МассивИмен Цикл
			ПолеОформления = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ПолеОформления.Использование = Истина;
			ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Имя);
		КонецЦикла;
	КонецЕсли;

	ОтборЭлемента = НовыйЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравнения;
	ОтборЭлемента.ПравоеЗначение = Значение;
	
	Для Каждого Элемент ИЗ СтурктураПараметров Цикл
		НовыйЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;

КонецПроцедуры

// Процедура - Установить условное оформление недоступного элемента
//
// Параметры:
//  Форма		 - УправляемаяФорма - Форма, на которую добавляем страницу  
//  МассивИмен	 - Массив или Строка - Массив с именами элементов формы 
//  ПутьКДанным	 - Строка - Путь к данным отбора (проверка на Ложь)
//
Процедура УстановитьУсловноеОформлениеНедоступногоЭлемента(Форма, МассивИмен, ПутьКДанным, ВидСравнения=Неопределено) Экспорт
	
	НовыйЭлементУсловногоОформления = Форма.УсловноеОформление.Элементы.Добавить();
	
	Если ТипЗнч(МассивИмен) = Тип("Строка") Тогда	
		ПолеОформления = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(МассивИмен);
	ИначеЕсли ТипЗнч(МассивИмен) = Тип("Массив") Тогда
		Для Каждого Имя ИЗ МассивИмен Цикл
			ПолеОформления = НовыйЭлементУсловногоОформления.Поля.Элементы.Добавить();
			ПолеОформления.Использование = Истина;
			ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Имя);
		КонецЦикла;
	КонецЕсли;

	ОтборЭлемента = НовыйЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	Если ВидСравнения = Неопределено Тогда
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Иначе
		ОтборЭлемента.ВидСравнения = ВидСравнения;
	КонецЕсли;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	НовыйЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", Новый Цвет(255,219,219));
	НовыйЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает синоним реквизита 
//	https://infostart.ru/public/196681/
//
// Параметры:
//  Имя  	- Строка - Имя реквизита для преобразования
//
// Возвращаемое значение:
//  Синоним - Строка - Преобразованная строка
//
Функция Синоним(Знач Имя) Экспорт
	
	НовоеИмя = "";
	Имя = СтрЗаменить(Имя, "СНДС", "_сНДС"); 
	Длина = СтрДлина(Имя);
	Для Индекс = 1 По Длина Цикл
		
		Символ = Сред(Имя, Индекс, 1);
		
		Если Символ = НРег(Символ) И Найти("1234567890", Символ) = 0 Тогда 
			НовоеИмя = НовоеИмя + Символ;
			Продолжить;
		КонецЕсли;
		
		Если Символ = ВРег(Символ) Тогда
			
			Если Индекс > 1 Тогда
				НовоеИмя = НовоеИмя + " "
			КонецЕсли;
			
			Аббревиатура = (Сред(Имя, Индекс, 3) = ВРег(Сред(Имя, Индекс, 3))) И (Длина - Индекс > 1);
			Предлог = (Сред(Имя, Индекс, 2) = ВРег(Сред(Имя, Индекс, 2))) И (Длина > Индекс);
			
			Если Аббревиатура Тогда
				ДлинаАббревиатуры = 3;
				
				Пока Сред(Имя, Индекс, ДлинаАббревиатуры) = ВРег(Сред(Имя, Индекс, ДлинаАббревиатуры)) 
					И  Индекс + ДлинаАббревиатуры <= Длина + 1 Цикл
					
					ДлинаАббревиатуры = ДлинаАббревиатуры + 1
				КонецЦикла;
				
				ДлинаАббревиатуры = ДлинаАббревиатуры - 1;
				
				Если (Индекс + ДлинаАббревиатуры - 1) = Длина Тогда
					НовоеИмя = НовоеИмя + Сред(Имя, Индекс, ДлинаАббревиатуры);
				Иначе
					ДлинаАббревиатуры = ДлинаАббревиатуры - 1;
					НовоеИмя = НовоеИмя + Сред(Имя, Индекс, ДлинаАббревиатуры) + " " + НРег(Сред(Имя, Индекс + ДлинаАббревиатуры, 1));
				КонецЕсли;
				
				Индекс = Индекс + ДлинаАббревиатуры;
				Продолжить;
			КонецЕсли;			
			
			Если Предлог Тогда
				НовоеИмя = НовоеИмя + НРег(Символ) + " " + НРег(Сред(Имя, Индекс + 1, 1)) + " ";
				Индекс = Индекс + 1;
				Продолжить;
			КонецЕсли;			
			
			НовоеИмя = НовоеИмя + НРег(Символ); 			
			
		КонецЕсли; 
		
	КонецЦикла;
	
	НовоеИмя = СтрЗаменить(НовоеИмя, "_", " ");
	НовоеИмя = ВРег(Лев(НовоеИмя, 1)) + Прав(НовоеИмя, СтрДлина(НовоеИмя) - 1);
	Возврат НовоеИмя;
 
КонецФункции  

// Возвращает имя элемента формы по пути
//
// Параметры:
//  ПутьКДанным  - Строка - Путь к данным элемента формы
//
// Возвращаемое значение:
//   ИмяЭлемента   - Строка - Имя элемента формы
//
Функция ИмяЭлементаФормы(ПутьКДанным)

	Имя = СтрЗаменить(ПутьКДанным,".","");
	Если Лев(Имя,6) = "Объект" Тогда
		Имя = Сред(Имя,7);
	КонецЕсли;
	
	Возврат Имя;

КонецФункции // ИмяЭлементаФормы()

// Проверяет наличие реквизита у формы
//
// Параметры:
//  Форма		  - УправляемаяФорма - Форма, на которую добавляем страницу  
//  ИмяРеквизита  - Строка - Имя реквизита формы
//
// Возвращаемое значение:
//   НаличиеЭлемента   - Булево - Есть элемент или его нет
//
Функция ЕстьРеквизитФормы(Форма, ИмяРеквизита) Экспорт

	СтруктураПоиска = Новый Структура;
 	СтруктураПоиска.Вставить(ИмяРеквизита, Неопределено);
 	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Форма);
	
	Возврат СтруктураПоиска[ИмяРеквизита] <> Неопределено;

КонецФункции

#КонецОбласти