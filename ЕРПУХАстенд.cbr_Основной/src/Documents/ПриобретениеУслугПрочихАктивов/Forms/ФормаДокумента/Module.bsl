#Область СлужебныеПроцедурыИФункции

&После("ПриОткрытии")
&НаКлиенте
Процедура cbr_ПриОткрытии(Отказ)
		
	//++АшаеваАН 03.02.26 СКЛ.9
	Если ЗначениеЗаполнено(Объект.cbr_ЗаданиеНаПеревозку) И Не ЗначениеЗаполнено(Объект.Организация) Тогда
		cbr_ВывестиПредупреждениеПоЗаказуНаПеревозку();
		Элементы.Организация.РежимВыбораИзСписка = Истина;
		Элементы.Организация.СписокВыбора.ЗагрузитьЗначения(ПолучитьСписокОрганизацийПоЗаданиеНаПеревозку());
	КонецЕсли;

	cbr_ГруппаПараметрыЗаданиеНаПеревозкуУправлениеВидимостью();
	//--АшаеваАН 03.02.26 СКЛ.9

КонецПроцедуры

//++АшаеваАН 03.02.26 СКЛ.9
&НаКлиенте
Асинх Процедура cbr_ВывестиПредупреждениеПоЗаказуНаПеревозку()

	ВопросАсинх(НСтр("ru = 'В документе Задании на перевозку есть распоряжения с разными организациями. 
					 |Выберите организацию для заполнения таблицы.'"), РежимДиалогаВопрос.Ок);

КонецПроцедуры  //--АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
&НаКлиенте
Асинх Процедура cbr_ВывестиПредупреждениеОИзмененииТЧРасходы()

	ВопросАсинх(НСтр("ru = 'Табличная часть ""Расходы и прочие активы"" была перезаполнена.'"), РежимДиалогаВопрос.Ок);

КонецПроцедуры  //--АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
&НаСервере
Функция ПолучитьСписокОрганизацийПоЗаданиеНаПеревозку()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Объект.cbr_ЗаданиеНаПеревозку);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение.Организация КАК Организация
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
	|ГДЕ
	|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение.Организация";

	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");

КонецФункции  //--АшаеваАН 03.02.26 СКЛ.9

&После("ОрганизацияПриИзменении")
&НаКлиенте
Процедура cbr_ОрганизацияПриИзменении(Элемент)
	
	//++АшаеваАН 03.02.26 СКЛ.9
	Если ЗначениеЗаполнено(Объект.cbr_ЗаданиеНаПеревозку) И ЗначениеЗаполнено(Объект.Организация) Тогда
		cbr_ОрганизацияПриИзмененииНаСервере();
		cbr_ВывестиПредупреждениеОИзмененииТЧРасходы();
	КонецЕсли;
	//--АшаеваАН 03.02.26 СКЛ.9

КонецПроцедуры

//++АшаеваАН 03.02.26 СКЛ.9
&НаСервере
Процедура cbr_ОрганизацияПриИзмененииНаСервере()

	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.cbr_ЗаполнитьРасходы(Объект.cbr_ЗаданиеНаПеревозку);
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");

	ПараметрыВыбораСтатейИАналитик = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыВыбораСтатейИАналитик(
		Объект.ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ПриЧтенииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);

КонецПроцедуры //++АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
&НаКлиенте
Процедура cbr_ГруппаПараметрыЗаданиеНаПеревозкуУправлениеВидимостью()

	Если ЗначениеЗаполнено(Объект.cbr_ЗаданиеНаПеревозку) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "cbr_ГруппаПараметрыЗаданиеНаПеревозку",
			"Видимость", Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "cbr_ГруппаПараметрыЗаданиеНаПеревозку",
			"Видимость", Ложь);
	КонецЕсли;

КонецПроцедуры //++АшаеваАН 03.02.26 СКЛ.9

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

//++АшаеваАН 03.02.26 СКЛ.9
&НаКлиенте
Процедура cbr_ЗаданиеНаПеревозкуПриИзменении(Элемент)

	Если Не ЗначениеЗаполнено(Объект.cbr_ЗаданиеНаПеревозку) Тогда
		Объект.cbr_Транспорт = Неопределено;
		Объект.cbr_Марка = Неопределено;
		Объект.cbr_НомерМашины = Неопределено;
		Объект.cbr_НомерПрицепа = Неопределено;
		Объект.cbr_НомерЗаявкиПоДаннымПеревозчика = Неопределено;
		Объект.cbr_НомерПеревозкиAtrucks = Неопределено;
	КонецЕсли;

	cbr_ГруппаПараметрыЗаданиеНаПеревозкуУправлениеВидимостью();

КонецПроцедуры //++АшаеваАН 03.02.26 СКЛ.9

#КонецОбласти