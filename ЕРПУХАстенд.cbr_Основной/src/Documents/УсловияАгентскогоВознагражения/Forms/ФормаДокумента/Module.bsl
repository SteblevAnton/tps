
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	УсловияАгентскогоВознаграждения.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрСведений.УсловияАгентскогоВознаграждения КАК УсловияАгентскогоВознаграждения
	|ГДЕ
	|	УсловияАгентскогоВознаграждения.Основание = &ЗаказКлиента";
	Запрос.УстановитьПараметр("ЗаказКлиента", Объект.Основание);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И Не РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = НСтр("ru='По данному заказу уже существует документ Условия агентского вознаграждения.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	#Область УХ_УниверсальныеПроцессыСогласование
	ДействияСогласованиеУХСервер.НарисоватьПанельСогласованияИОпределитьСостояниеОбъекта(ЭтотОбъект);
	#КонецОбласти
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	ЗаполнитьНачальныхАгентов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	МассивАгентов = Новый Массив;
	Для Каждого СтрокаАгентов Из Объект.Агенты Цикл
		МассивАгентов.Добавить(СтрокаАгентов.Агент);
	КонецЦикла;
	КоличествоДо = МассивАгентов.Количество();
	МассивАгентов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивАгентов);
	КоличествоПосле = МассивАгентов.Количество();
	
	Если КоличествоДо > КоличествоПосле Тогда
		
		ТекстСообщения = НСтр("ru='Существуют дубли по агенту. Запись отменена'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	#Область УХ_УниверсальныеПроцессыСогласование
	Если ИмяСобытия = "ОбъектСогласован" Тогда
		ОпределитьСостояниеОбъекта();
	ИначеЕсли ИмяСобытия = "ОбъектОтклонен" Тогда
		ОпределитьСостояниеОбъекта();
	ИначеЕсли ИмяСобытия = "МаршрутИнициализирован" Тогда
		ОпределитьСостояниеОбъекта();
	ИначеЕсли ИмяСобытия = "СостояниеЗаявкиПриИзменении" Тогда
		ОпределитьСостояниеОбъекта();
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура ПровестиИЗакрыть(Команда)
	
	ОбщегоНазначенияУТКлиент.ПровестиИЗакрыть(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Записать(ЭтаФорма, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокумент(Команда)
	
	ОбщегоНазначенияУТКлиент.Провести(ЭтаФорма, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПартнерКлиентаПриИзменении(Элемент)
	
	ПартнерыИКонтрагентыВызовСервера.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Объект.ПартнерКлиента, Объект.Клиент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Объект.ПартнерКлиента = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.Клиент, "Партнер");
	
КонецПроцедуры

#КонецОбласти

#Область ОбластьВстраиванияКоманды

&НаКлиенте
Процедура ПринятьКСогласованию_Подключаемый() Экспорт
	ДействияСогласованиеУХКлиент.ПринятьКСогласованию(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ИсторияСогласования_Подключаемый() Экспорт
	ДействияСогласованиеУХКлиент.ИсторияСогласования(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура СогласоватьДокумент_Подключаемый() Экспорт
	ДействияСогласованиеУХКлиент.СогласоватьДокумент(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьСогласование_Подключаемый() Экспорт
	ДействияСогласованиеУХКлиент.ОтменитьСогласование(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура МаршрутСогласования_Подключаемый() Экспорт
	ДействияСогласованиеУХКлиент.МаршрутСогласования(ЭтотОбъект, Объект.Ссылка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАгенты

&НаКлиенте
Процедура АгентыПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьОтборНоменклатуры(ТекущиеДанные.Агент);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентыПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбщаяПроцедураУдалитьСтроки(ТекущиеДанные.Агент);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	
КонецПроцедуры

&НаКлиенте
Процедура АгентыПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Агент <> ТекущиеДанные.КонтрагентНачальный Тогда
		
		МассивАгентов = Новый Массив;
		Для Каждого СтрокаАгентов Из Объект.Агенты Цикл
			МассивАгентов.Добавить(СтрокаАгентов.Агент);
		КонецЦикла;
		КоличествоДо = МассивАгентов.Количество();
		МассивАгентов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивАгентов);
		КоличествоПосле = МассивАгентов.Количество();
		
		Если КоличествоДо > КоличествоПосле Тогда
			
			ТекстСообщения = НСтр("ru='Данный агент уже выбран в табличной части'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
			
		Иначе
			
			МассивНайденныхСтрок = Объект.Номенклатура.НайтиСтроки(Новый Структура("Агент", ТекущиеДанные.КонтрагентНачальный));
			Для Каждого СтрокаМассива ИЗ МассивНайденныхСтрок Цикл
				СтрокаМассива.Агент = ТекущиеДанные.Агент;
			КонецЦикла;
			
			ТекущиеДанные.КонтрагентНачальный = ТекущиеДанные.Агент;
			
			УстановитьОтборНоменклатуры(ТекущиеДанные.Агент);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТекущиеДанные.БазоваяСтавка < ТекущиеДанные.СтавкаАгента Тогда
		
		ТекстСообщения = НСтр("ru='Ставка АВ не может быть больше базовой ставки!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		ТекущиеДанные.СтавкаАгента = ТекущиеДанные.БазоваяСтавка;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура АгентыПартнерАгентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ПартнерАгент) Тогда
		ТекущиеДанные.Агент = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
		Возврат;
	КонецЕсли;
	
	ПартнерыИКонтрагентыВызовСервера.ЗаполнитьКонтрагентаПартнераПоУмолчанию(ТекущиеДанные.ПартнерАгент, ТекущиеДанные.Агент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура АгентыДоговорПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//ТекущиеДанные.БазоваяСтавка = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ТекущиеДанные.ДоговорАгента, "БазоваяСтавкаАВ");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНоменклатура

&НаКлиенте
Процедура НоменклатураПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекущиеДанные = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		ТекущиеДанныеАгентов = Элементы.Агенты.ТекущиеДанные;
		Если ТекущиеДанныеАгентов = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Элемент.ТекущиеДанные.Агент = ТекущиеДанныеАгентов.Агент;
		Элемент.ТекущиеДанные.СтавкаАгента = ТекущиеДанныеАгентов.СтавкаАгента;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	ТекущиеДанные = Элементы.Номенклатура.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеАгентов = Элементы.Агенты.ТекущиеДанные;
	Если ТекущиеДанныеАгентов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанныеАгентов.БазоваяСтавка < ТекущиеДанные.СтавкаАгента Тогда
		
		ТекстСообщения = НСтр("ru='Ставка АВ не может быть больше базовой ставки!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		ТекущиеДанные.СтавкаАгента = ТекущиеДанныеАгентов.БазоваяСтавка;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтборНоменклатуры(Агент)
	
	Элементы.Номенклатура.ОтборСтрок = Новый ФиксированнаяСтруктура("Агент", Агент); 
	
КонецПроцедуры

&НаКлиенте 
Процедура ОбщаяПроцедураУдалитьСтроки(Агент)
	
	МассивНайденныхСтрок = Объект.Номенклатура.НайтиСтроки(Новый Структура("Агент", Агент));
	Для Каждого СтрокаМассива ИЗ МассивНайденныхСтрок Цикл
		Объект.Номенклатура.Удалить(СтрокаМассива);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьСостояниеОбъекта(ОбновитьОтветственныхВход = Ложь)
	ДействияСогласованиеУХСервер.ОпределитьСостояниеЗаявки(ЭтаФорма, ОбновитьОтветственныхВход);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачальныхАгентов()
	
	Для Каждого СтрокаТаблицы ИЗ Объект.Агенты Цикл
		СтрокаТаблицы.КонтрагентНачальный = СтрокаТаблицы.Агент;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает значение реквизита СостояниеЗаявки на форме ФормаВход.
// Т.к. данный реквизит генерируется кодом, обращение к нему напрямую из
// кода недоступно.
&НаКлиентеНаСервереБезКонтекста
Функция РеквизитСостояниеЗаявки(ФормаВход)
	Возврат ФормаВход["СостояниеЗаявки"];
КонецФункции

// Возвращает значение реквизита СтатусОбъекта на форме ФормаВход.
// Т.к. данный реквизит генерируется кодом, обращение к нему напрямую из
// кода недоступно.
&НаКлиентеНаСервереБезКонтекста
Функция РеквизитСтатусОбъекта(ФормаВход)
	Возврат ФормаВход["СтатусОбъекта"];
КонецФункции

// Возвращает значение реквизита Согласующий на форме ФормаВход.
// Т.к. данный реквизит генерируется кодом, обращение к нему напрямую из
// кода недоступно.
&НаКлиентеНаСервереБезКонтекста
Функция РеквизитСогласующий(ФормаВход)
	Возврат ФормаВход["Согласующий"];
КонецФункции

&НаСервере
Процедура ЗаполнитьПоЗаказуНаСервере()
	
	ОбъектДокумента = РеквизитФормыВЗначение("Объект");
	ОбъектДокумента.Заполнить(Объект.Основание);
	ЗначениеВРеквизитФормы(ОбъектДокумента, "Объект");
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗаполнитьПоЗаказу(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Основание) Тогда
		ТекстСообщения = НСтр("ru='Не указано основание для заполнения!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Данные будут полностью перезаполнены. Изменения не сохранятся. Продолжить?'");
	РезультатОтвет = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , НСтр("ru = 'Внимание!'"));
	
	Если РезультатОтвет = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоЗаказуНаСервере();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти



















