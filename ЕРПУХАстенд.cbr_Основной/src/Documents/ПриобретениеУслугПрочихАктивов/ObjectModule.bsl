#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура cbr_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	//++ НЕ УТ
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПереработчику2_5")
		  Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетПереработчика2_5") Тогда
		ЗаполнитьДокументНаОснованииДанныхПереработчика2_5(ДанныеЗаполнения);
	//++ Устарело_Переработка24
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПереработчику")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
		ЗаполнитьДокументНаОснованииДанныхПереработчика(ДанныеЗаполнения);
	//-- Устарело_Переработка24
	КонецЕсли;
	//-- НЕ УТ

	#Вставка
	//++АшаеваАН 03.02.26 СКЛ.9
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаданиеНаПеревозку") Тогда
		cbr_ЗаполнитьДокументНаОснованииДанныхЗаданияНаПеревозку(ДанныеЗаполнения);
	КонецЕсли;
	//--АшаеваАН 03.02.26 СКЛ.9
	#КонецВставки
	ПараметрыЗаполнения = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыЗаполненияНалогообложенияНДСЗакупки(ЭтотОбъект);
	УчетНДСУП.ЗаполнитьНалогообложениеНДСЗакупки(НалогообложениеНДС, ПараметрыЗаполнения);

	ПараметрыЗаполнения = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыЗаполненияВидаДеятельностиНДС(ЭтотОбъект);
	УчетНДСУП.ЗаполнитьВидДеятельностиНДС(ЗакупкаПодДеятельность, ПараметрыЗаполнения);

	ПараметрыВыбораСтатейИАналитик = Документы.ПриобретениеУслугПрочихАктивов.ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ОбработкаЗаполнения(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);

	ВзаиморасчетыСервер.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения);

	ПриобретениеУслугПрочихАктивовЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

	ИнициализироватьДокумент(ДанныеЗаполнения);
	ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);

КонецПроцедуры

#КонецОбласти

#Область Прочее

//++АшаеваАН 03.02.26 СКЛ.9
Процедура cbr_ЗаполнитьДокументНаОснованииДанныхЗаданияНаПеревозку(ДокументОснование)

	Если Не ЗначениеЗаполнено(cbr_ЗаданиеНаПеревозку) Тогда

		Дата = ТекущаяДатаСеанса();

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаданиеНаПеревозку.Перевозчик КАК Партнер,
		|	ЗаданиеНаПеревозку.Ответственный КАК Менеджер,
		|	ЗаданиеНаПеревозку.АвтомобильТип КАК cbr_Транспорт,
		|	ЗаданиеНаПеревозку.АвтомобильМарка КАК cbr_Марка,
		|	ЗаданиеНаПеревозку.АвтомобильГосударственныйНомер КАК cbr_НомерМашины,
		|	ЗаданиеНаПеревозку.ГосударственныйНомерПрицепа КАК cbr_НомерПрицепа,
		|	ЗаданиеНаПеревозку.cbr_НомерЗаявкиПоДаннымПеревозчика КАК cbr_НомерЗаявкиПоДаннымПеревозчика,
		|	ЗаданиеНаПеревозку.cbr_НомерПеревозкиAtrucks КАК cbr_НомерПеревозкиAtrucks,
		|	&Ссылка КАК cbr_ЗаданиеНаПеревозку,
		|	ВложенныйЗапрос.КолРазОрганизация КАК КолРазОрганизация
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаданиеНаПеревозкуРаспоряжения.Распоряжение.Организация) КАК КолРазОрганизация
		|	ИЗ
		|		Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
		|	ГДЕ
		|		ЗаданиеНаПеревозкуРаспоряжения.Ссылка = &Ссылка
		|		И ЗаданиеНаПеревозкуРаспоряжения.Ссылка.Проведен) КАК ВложенныйЗапрос
		|ГДЕ
		|	ЗаданиеНаПеревозку.Ссылка = &Ссылка";

		Результат = Запрос.Выполнить();
		Шапка = Результат.Выбрать();
		Шапка.Следующий();

		Если Не Шапка.КолРазОрганизация > 1 Тогда
			cbr_ЗаполнитьРасходы(ДокументОснование);
		КонецЕсли;
    
	    // Заполнение шапки.
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Шапка);
		Подразделение = Справочники.СтруктураПредприятия.ТПСОтделЛогистикиИВЭД;

		Если ЗначениеЗаполнено(Партнер) И ЗначениеЗаполнено(Организация) Тогда
			ЗаполнитьУсловияЗакупокПоУмолчанию();
		Иначе
			Валюта = Константы.БазоваяВалютаПоУмолчанию.Получить();
			ВалютаВзаиморасчетов = Константы.БазоваяВалютаПоУмолчанию.Получить();
		КонецЕсли;

	Иначе
		cbr_ЗаполнитьРасходы(ДокументОснование);
	КонецЕсли;

КонецПроцедуры //--АшаеваАН 03.02.26 СКЛ.9

//++АшаеваАН 03.02.26 СКЛ.9
Процедура cbr_ЗаполнитьРасходы(ДокументОснование) Экспорт

	Расходы.Очистить();
	ОбрабатываемыеСтроки = Новый Массив;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Распоряжения.Распоряжение							КАК Распоряжение,
	|	Распоряжения.Распоряжение.Организация				КАК Организация,
	|	Распоряжения.Распоряжение.ДополнительнаяИнформация	КАК ДополнительнаяИнформация,
	|	Распоряжения.Вес									КАК Вес,
	|	Распоряжения.Распоряжение.НаправлениеДеятельности	КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК Распоряжения
	|ГДЕ
	|	Распоряжения.Ссылка = &Ссылка
	|   И Распоряжения.Распоряжение.Проведен
	| 	И &УсловиеОрганизация";

	Если ЗначениеЗаполнено(Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеОрганизация",
			"Распоряжения.Распоряжение.Организация = &Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	Иначе
		Запрос.УстановитьПараметр("УсловиеОрганизация", Истина);
	КонецЕсли;

	ТЗ = Запрос.Выполнить().Выгрузить();
	Вес = ТЗ.Итог("Вес");

	Подразделение = Справочники.СтруктураПредприятия.ТПСОтделЛогистикиИВЭД;
	СтавкаНДС = СтавкаНДС();

	Если Не ЗначениеЗаполнено(Организация) Тогда
		Если ТЗ.Количество() Тогда
			Организация = ТЗ[0].Организация;
			ОтветственныеЛицаСервер.ПриИзмененииСвязанныхРеквизитовДокумента(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;

	Для Каждого Строка Из ТЗ Цикл

		cbr_ФактическаяСтоимостьПеревозки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОснование,
			"cbr_ФактическаяСтоимостьПеревозки");

		НоваяСтрока = Расходы.Добавить();
		НоваяСтрока.Содержание = "Транспортно-экспедиционные услуги («Адрес отправления» - «Адрес получения», "
			+ Строка.ДополнительнаяИнформация + ")";
		НоваяСтрока.Цена = Строка.Вес / Вес * cbr_ФактическаяСтоимостьПеревозки;
		НоваяСтрока.Сумма = НоваяСтрока.Цена;
		НоваяСтрока.СтавкаНДС = СтавкаНДС;
		Если ТипЗнч(Строка.Распоряжение) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
			Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Распоряжение, "Организация")
				= Справочники.Организации.НайтиПоРеквизиту("ИНН", "5042063026") Тогда
				НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.cbr_26ТранспортныеУслугиДоставкаПокупателюТранспортнойКомпании;
			Иначе
				НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.cbr_44ТранспортныеУслугиДоставкаПокупателюТранспортнойКомпании;
			КонецЕсли;
		ИначеЕсли ТипЗнч(Строка.Распоряжение) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.cbr_ТранспортныеУслугиДоставкаПокупателюТранспортнойКомпании;
		КонецЕсли;
		НоваяСтрока.Подразделение = Подразделение;
		НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
		НоваяСтрока.АналитикаРасходов = Строка.Распоряжение;

		ОбрабатываемыеСтроки.Добавить(НоваяСтрока);

	КонецЦикла;

	Если ОбрабатываемыеСтроки.Количество() Тогда

		СтруктураДействий = Новый Структура;
		СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(
			ЭтотОбъект);
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);

		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки, СтруктураДействий, Расходы, Неопределено);

	КонецЕсли;

КонецПроцедуры //--АшаеваАН 03.02.26 СКЛ.9 

//++АшаеваАН 03.02.26 СКЛ.9 
Функция СтавкаНДС()

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	СтавкиНДС.Ссылка КАК Ссылка
				   |ИЗ
				   |	Справочник.СтавкиНДС КАК СтавкиНДС
				   |ГДЕ
				   |	СтавкиНДС.ПеречислениеСтавкаНДС = &ПеречислениеСтавкаНДС
				   |	И СтавкиНДС.Основная";

	Запрос.УстановитьПараметр("ПеречислениеСтавкаНДС", Перечисления.СтавкиНДС.НДС20);
	Выборка = Запрос.Выполнить().Выбрать();

	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;

	Возврат Справочники.СтавкиНДС.БезНДС;

КонецФункции  //--АшаеваАН 03.02.26 СКЛ.9 

#КонецОбласти

#КонецЕсли