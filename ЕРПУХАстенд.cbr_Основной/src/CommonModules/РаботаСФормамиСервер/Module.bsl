////////////////////////////////////////////////////////////////////////////////
// РаботаСФормамиСервер: дополнение к типовому механизму программного создания 
//						 элементов на управляемой форме
//  
////////////////////////////////////////////////////////////////////////////////
#Область ПрограммныйИнтерфейс

Функция СоздатьТаблицуНаФорме(Элементы, Имя, Синоним, ПутьКДанным, Родитель, МетаданныеТаблицы, ВыводитьНомер = Истина, СписокКолонок = "") Экспорт

	// НАЧАЛО Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	РезультатФункции = Новый Структура("Таблица, Колонки");
	
	ТаблицаДанных = ФормыУХ.СоздатьТаблицуФормы(Элементы,
												Имя,
												Синоним,
												ПутьКДанным,
												Родитель);
												
	Если ВыводитьНомер Тогда
		КолонкаНомер = ФормыУХ.СоздатьПолеФормы(Элементы,
												"НомерСтроки",
												"№",
												ПутьКДанным + ".НомерСтроки",
												ВидПоляФормы.ПолеНадписи,
												ТаблицаДанных);
	КонецЕсли;										
	
	МассивКолонокДляВывода = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СписокКолонок, ",", Истина, Истина);
	
	СтруктураКолонок = Новый Структура;
	Для Каждого КолонкаТаблицы Из МетаданныеТаблицы.Реквизиты Цикл
		
		Если ЗначениеЗаполнено(СписокКолонок)
			И МассивКолонокДляВывода.Найти(КолонкаТаблицы.Имя) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяКолонка = ФормыУХ.СоздатьПолеФормы(Элементы,
												Имя + КолонкаТаблицы.Имя,
												КолонкаТаблицы.Синоним,
												ПутьКДанным + "." + КолонкаТаблицы.Имя,
												ВидПоляФормы.ПолеВвода,
												ТаблицаДанных);
												
		СтруктураКолонок.Вставить(КолонкаТаблицы.Имя, НоваяКолонка);												
		
	КонецЦикла;	
	
	РезультатФункции.Таблица = ТаблицаДанных;
	РезультатФункции.Колонки = СтруктураКолонок;
	
	Возврат РезультатФункции;
	// КОНЕЦ Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	
КонецФункции

Процедура СоздатьИПереместитьВВертикальнуюГруппуКолонок(Элементы, Таблица, НазваниеГруппы, КолонкиДляПеремещения, ПередЭлементом = Неопределено) Экспорт
	
	НоваяГруппа = Элементы.Добавить(НазваниеГруппы, Тип("ГруппаФормы"), Таблица);
	НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
	НоваяГруппа.Группировка = ГруппировкаКолонок.Вертикальная;
	Если ПередЭлементом = Неопределено Тогда
		Элементы.Переместить(НоваяГруппа, Таблица);
	Иначе
		Элементы.Переместить(НоваяГруппа, Таблица, ПередЭлементом);
	КонецЕсли;
	
	Для Каждого КолонкаГруппы Из КолонкиДляПеремещения Цикл
		Элементы.Переместить(КолонкаГруппы, НоваяГруппа);
	КонецЦикла;
	
КонецПроцедуры

// Процедура - возвращает массив реквизитов для Таблицы значений
// 	далее на форме необходимо будет вызвать процедуру ИзменитьРеквизиты у формы с передачей
// 	этого массива
//
// Параметры:
//  ОписаниеТаблицы	 - Структура
//				Наименование - Строка - название таблицы значений
//				Колонки - Массив структур (Имя, Синоним, ТипДанных - Описание типов)
Функция МассивРеквизитовТаблицыЗначений(ОписаниеТаблицы) Экспорт
	
	РезультатФункции = Новый Массив;
	
	РеквизитТаблица = Новый РеквизитФормы(ОписаниеТаблицы.Наименование, Новый ОписаниеТипов("ТаблицаЗначений"));
	РезультатФункции.Добавить(РеквизитТаблица);
	
	Для Каждого СтрокаКолонок Из ОписаниеТаблицы.Колонки Цикл
		РеквизитКолонка = Новый РеквизитФормы(СтрокаКолонок.Имя, 
											  СтрокаКолонок.ТипДанных,
											  ОписаниеТаблицы.Наименование, 
											  СтрокаКолонок.Синоним);
											  
		РезультатФункции.Добавить(РеквизитКолонка);
	КонецЦикла;
	
	Возврат РезультатФункции;
	
КонецФункции

#КонецОбласти









