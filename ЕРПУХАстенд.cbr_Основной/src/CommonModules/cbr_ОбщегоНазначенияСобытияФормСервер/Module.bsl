Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Обработка.ОбеспечениеПотребностей.Форма.Форма" Тогда 
		cbr_РаботаСФормами.ДобавитьПолеФормы(Форма,
		 "НеПоддерживаетсяОстаток",
		 "Объект.ЗаказыКОбеспечению.cbr_НеПоддерживаетсяОстаток",
		 НСтр("ru='Не поддерживается остаток'"),
		 ВидПоляФормы.ПолеФлажка,
		 Форма.Элементы.ЗаказыКОбеспечению,
		 Форма.Элементы.СпособОбеспеченияЗаказы);	
	//++ Тусеев Н.Ю. 27.01.2026 ПРД2
	ИначеЕсли ИмяФормы = "Справочник.Контрагенты.Форма.Форма" Тогда
		 ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, "ГруппаКодFOBitrix", , 
		 ВидГруппыФормы.ОбычнаяГруппа, 
		 Форма.Элементы.СтраницаОсновное, Форма.Элементы.ГруппаПартнерНаименование, 
		 Новый Структура("ОтображатьЗаголовок", Ложь));
		 
		 ФормыУХ.СоздатьПолеФормы(Форма.Элементы, "cbr_КодFO", "Код FO", 
		 "Объект.cbr_КодFO", ВидПоляФормы.ПолеВвода, Форма.Элементы.ГруппаКодFOBitrix);
		 
		 ФормыУХ.СоздатьПолеФормы(Форма.Элементы, "cbr_КодBitrix", "Код Bitrix", 
		 "Объект.cbr_КодBitrix", ВидПоляФормы.ПолеВвода, Форма.Элементы.ГруппаКодFOBitrix);
		 
	 ИначеЕсли ИмяФормы = "Справочник.Партнеры.Форма.ФормаЭлементаРеквизитыКонтрагента" Тогда
		 ФормыУХ.СоздатьГруппуФормы(Форма.Элементы, "ГруппаКодFOBitrix", , 
		 ВидГруппыФормы.ОбычнаяГруппа, 
		 Форма.Элементы.ГруппаОбщаяИнформация, Форма.Элементы.ГруппаЮрФизЛицоДанныеФизЛица, 
		 Новый Структура("ОтображатьЗаголовок", Ложь));
		 
		 ДобавляемыеРеквизиты = Новый Массив;	
		 ФормыУХ.СоздатьДобавитьРеквизитФормы(Форма, ДобавляемыеРеквизиты, 
		 "cbr_КодFO", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(25))); 
		 
		 ФормыУХ.СоздатьДобавитьРеквизитФормы(Форма, ДобавляемыеРеквизиты, 
		 "cbr_КодBitrix", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(25)));
		 
		 Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
		 
		 ФормыУХ.СоздатьПолеФормы(Форма.Элементы, "cbr_КодFO", "Код FO", 
		 "cbr_КодFO", ВидПоляФормы.ПолеВвода, Форма.Элементы.ГруппаКодFOBitrix);
		 
		 ФормыУХ.СоздатьПолеФормы(Форма.Элементы, "cbr_КодBitrix", "Код Bitrix", 
		 "cbr_КодBitrix", ВидПоляФормы.ПолеВвода, Форма.Элементы.ГруппаКодFOBitrix);
		 
		 Контрагент = НайтиСсылкуПоРеквизиту("Справочники", "Контрагенты", "Партнер", Форма.Объект.Ссылка);
		 Если ЗначениеЗаполнено(Контрагент) Тогда
			 ДанныеКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Контрагент, "cbr_КодFO, cbr_КодBitrix"); 
			 ЗаполнитьЗначенияСвойств(Форма, ДанныеКонтрагента);
		 КонецЕсли;
	//-- Тусеев Н.Ю. 27.01.2026 ПРД2	
	КонецЕсли;
КонецПроцедуры

//++ Тусеев Н.Ю. 27.01.2026 ПРД2
Функция НайтиСсылкуПоРеквизиту(ТипМетаданных, ОбъектМетаданныхИмя, ИмяРеквизита, ЗначениеРеквизита)
	
	ТипОбъектаМетаданных = Метаданные[ТипМетаданных];
	ОбъектМетаданных =  ТипОбъектаМетаданных[ОбъектМетаданныхИмя];	
	Запрос = Новый Запрос();
	ТекстЗапроса = СтрШаблон("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1 Ссылка ИЗ %1 ГДЕ %2 = &П", 
	ОбъектМетаданных.ПолноеИмя(), ИмяРеквизита);
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("П", ЗначениеРеквизита);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка[0];
	КонецЕсли;
	
КонецФункции
//-- Тусеев Н.Ю. 27.01.2026 ПРД2