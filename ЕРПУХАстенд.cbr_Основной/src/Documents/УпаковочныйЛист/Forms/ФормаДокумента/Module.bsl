#Область ОбработчикиСобытийФормы
&После("ПриСозданииНаСервере")
&НаСервере
Процедура cbr_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//++ cbr-larkovgeorg 10.02.2026 СКЛ.12

	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);		
	
	//-- cbr-larkovgeorg 10.02.2026 СКЛ.12
КонецПроцедуры

&После("ОбработкаПроверкиЗаполненияНаСервере")
&НаСервере
Процедура cbr_ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	//++ cbr-larkovgeorg 10.02.2026 СКЛ.12
	Для Каждого Строка Из Объект.Товары Цикл
		Если ЗначениеЗаполнено(Строка.cbr_НомерПаллета) И Не ЗначениеЗаполнено(Строка.cbr_Уровень) Тогда

			Отказ = Истина;
			Сообщение = СтрШаблон("В строке %1 не заполнено колонка «Уровень»!", Строка.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(Сообщение);

		КонецЕсли;
	КонецЦикла;
	//-- cbr-larkovgeorg 10.02.2026 СКЛ.12
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура cbr_РасставитьПродукциюПоПаллетам(Команда)
	
	//++ cbr-larkovgeorg 10.02.2026 СКЛ.12
	Если Элементы.Товары.ВыделенныеСтроки.Количество() > 0 Тогда

		Паллеты = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(ПредопределенноеЗначение(
			"Справочник.cbr_ПредопределенныеЗначенияКонстант.НаборУпаковокПаллеты"), "Значение");

		ПараметрыОткрытия = Новый Структура("Отбор", Новый Структура("Владелец", Паллеты));
		ОповещениеЗакрытия = Новый ОписаниеОповещения("cbr_ВыборПаллетаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.УпаковкиЕдиницыИзмерения.Форма.ФормаВыбораЕдиницыИзмерения", ПараметрыОткрытия, , , , ,
			ОповещениеЗакрытия);

	Иначе

		ОбщегоНазначенияКлиент.СообщитьПользователю("Выделите строки для расстановки на паллеты!");

	КонецЕсли;
	//-- cbr-larkovgeorg 10.02.2026 СКЛ.12	

КонецПроцедуры

&НаКлиенте
Процедура cbr_НомерПаллетаПриИзменении(Элемент)

	//++ cbr-larkovgeorg 10.02.2026 СКЛ.12
	Объект.cbr_КоличествоПаллет = cbr_МаксимальныйНомерПаллетаВДокументе();

КонецПроцедуры //-- cbr-larkovgeorg 10.02.2026 СКЛ.12

&НаСервере
Функция cbr_МаксимальныйНомерПаллетаВДокументе()
	//++ cbr-larkovgeorg 15.02.2026 СКЛ.12
	НомераПаллетов = Объект.Товары.Выгрузить( , "cbr_НомерПаллета");
	НомераПаллетов.Сортировать("cbr_НомерПаллета УБЫВ");
	МаксимальныйНомерПаллета = НомераПаллетов[0].cbr_НомерПаллета;
	Возврат МаксимальныйНомерПаллета
КонецФункции //-- cbr-larkovgeorg 15.02.2026 СКЛ.12

&НаКлиенте
Процедура cbr_ВыборПаллетаЗавершение(Результат, Параметры) Экспорт
	Если Результат <> Неопределено Тогда
		ИндексыВыделенныхСтрок = Элементы.Товары.ВыделенныеСтроки;
		cbr_РасставитьПродукциюПоПаллетамНаСервере(Результат, ИндексыВыделенныхСтрок);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура cbr_РасставитьПродукциюПоПаллетамНаСервере(ВыбранныйПаллет, ВыбранныеСтроки)

	//Пункт 1. Формируем список продукции по выбранным строкам, у которых ед. изм <> Паллеты и сортируем по свойству упаковки и весу
	ТаблицаДокумента = Объект.Товары.Выгрузить();
	
	Упаковки = ТаблицаДокумента.ВыгрузитьКолонку("Упаковка");
	Наборы = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Упаковки, "Владелец, cbr_СвойствоУпаковки, Вес, Высота, Глубина, Ширина");
	ЗначениеПаллетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Справочники.cbr_ПредопределенныеЗначенияКонстант.НаборУпаковокПаллеты, "Значение");
	РеквизитыПаллета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВыбранныйПаллет, "Высота, Глубина, Ширина, Вес");

	ИтоговаяТаблица = Новый ТаблицаЗначений;
	ИтоговаяТаблица = ТаблицаДокумента.СкопироватьКолонки();
	ИтоговаяТаблица.Колонки.Добавить("cbr_СвойствоУпаковки", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("Вес", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("Высота", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("Глубина", Новый ОписаниеТипов("Число"));
	ИтоговаяТаблица.Колонки.Добавить("Ширина", Новый ОписаниеТипов("Число"));
	
	ОставленныеСтрокиТЧ = ТаблицаДокумента.Скопировать();
	ОставленныеСтрокиТЧ.Очистить();

	Для Каждого ИндексСтроки Из ВыбранныеСтроки Цикл
		СтруктураНабора = Наборы.Получить(ТаблицаДокумента[ИндексСтроки].Упаковка);
		Если ТаблицаДокумента[ИндексСтроки].Упаковка <> Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка()
			И СтруктураНабора.Владелец <> ЗначениеПаллетов И ЗначениеЗаполнено(СтруктураНабора.cbr_СвойствоУпаковки)
			И ЗначениеЗаполнено(СтруктураНабора.Вес) Тогда
			СтрокаТЗ = ИтоговаяТаблица.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТЗ, ТаблицаДокумента[ИндексСтроки]);
			ЗаполнитьЗначенияСвойств(СтрокаТЗ, СтруктураНабора);
		Иначе
			ОставленнаяСтрока = ОставленныеСтрокиТЧ.Добавить();
			ЗаполнитьЗначенияСвойств(ОставленнаяСтрока, ТаблицаДокумента[ИндексСтроки]);
		КонецЕсли;
	КонецЦикла;

	ИтоговаяТаблица.Сортировать("cbr_СвойствоУпаковки Возр, Вес Убыв");
	
	//Пункт 2. Получаем Максимальный номер паллета  и делаем +1
	МаксимальныйНомерПаллета = cbr_МаксимальныйНомерПаллетаВДокументе();
	МаксимальныйНомерПаллета = МаксимальныйНомерПаллета + 1;
	
	//2.1 Заполняем первый уровень
	ОстатокПлощадиПаллета = РеквизитыПаллета.Глубина * РеквизитыПаллета.Ширина;
	ВесПервогоУровня = 0;
	
	//2.1.1 Добавляем упаковки, начиная с первой по сформированному списку, пока не будет заполнена вся площадь паллета. (Площадь паллета > Площадь упаковок);
	Для Каждого СтрокаТаблицы из ИтоговаяТаблица Цикл
		ПлощадьУпаковки = СтрокаТаблицы.Глубина * СтрокаТаблицы.Ширина;
		ВесУпаковки = СтрокаТаблицы.Вес;
		
		ПлощадьУпаковок = ПлощадьУпаковки * СтрокаТаблицы.КоличествоУпаковок;
		ВесУпаковок = ВесУпаковки * СтрокаТаблицы.КоличествоУпаковок;
		
		ПлощадьУпаковокПоложенная = ПлощадьУпаковок; 
		Если СтрокаТаблицы.cbr_СвойствоУпаковки = 1 И ПлощадьУпаковки < ОстатокПлощадиПаллета Тогда
			Пока ПлощадьУпаковокПоложенная > ОстатокПлощадиПаллета Цикл
				ПлощадьУпаковокПоложенная = ПлощадьУпаковокПоложенная - ПлощадьУпаковки;
				ВесУпаковок = ВесУпаковок - ВесУпаковки;
				СтрокаТаблицы.КоличествоУпаковок = СтрокаТаблицы.КоличествоУпаковок - 1;
			КонецЦикла;

			СтрокаТаблицы.cbr_НомерПаллета = МаксимальныйНомерПаллета;
			СтрокаТаблицы.cbr_Уровень = 1;
			ОстатокПлощадиПаллета = ОстатокПлощадиПаллета - ПлощадьУпаковокПоложенная;
			ВесПервогоУровня = ВесПервогоУровня + ВесУпаковок;
			
			Если ПлощадьУпаковок > ПлощадьУпаковокПоложенная Тогда
				КоличествоОсталось = ПлощадьУпаковокПоложенная / ПлощадьУпаковки;
				
				СтрокаОстатка = ИтоговаяТаблица.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаОстатка, СтрокаТаблицы);
				СтрокаОстатка.cbr_НомерПаллета = 0;
				СтрокаОстатка.cbr_Уровень = 0;
				СтрокаОстатка.КоличествоУпаковок  = КоличествоОсталось;
				Прервать;
				
			КонецЕсли;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	//2.1.2 Проверяем вес, из веса (грузоподъемности) паллета вычитаем общий вес уровня (Грузоподъемность паллета > Вес упаковок на первом уровне);
	ОстатокВесаПаллета = РеквизитыПаллета.Вес;
	Если ВесПервогоУровня > ОстатокВесаПаллета Тогда
		Счетчик = ИтоговаяТаблица.Количество();
		Пока Счетчик > 0 И ВесПервогоУровня > ОстатокВесаПаллета Цикл
			Счетчик = Счетчик - 1;
			Если ИтоговаяТаблица[Счетчик].cbr_Уровень = 1 И Счетчик = 0 Тогда
				КоличествоОсталось = ИтоговаяТаблица[Счетчик].КоличествоУпаковок;
				Пока ИтоговаяТаблица[Счетчик].КоличествоУпаковок > 0 И ВесПервогоУровня > ОстатокВесаПаллета Цикл
					ВесПервогоУровня = ВесПервогоУровня - ИтоговаяТаблица[Счетчик].Вес;
					ИтоговаяТаблица[Счетчик].КоличествоУпаковок = ИтоговаяТаблица[Счетчик].КоличествоУпаковок - 1;
					ОстатокПлощадиПаллета = ОстатокПлощадиПаллета + (ИтоговаяТаблица[Счетчик].КоличествоУпаковок * ИтоговаяТаблица[Счетчик].Глубина * ИтоговаяТаблица[Счетчик].Ширина);
				КонецЦикла;
				
				Если ИтоговаяТаблица[Счетчик].КоличествоУпаковок <> 0 Тогда
					КоличествоОсталось = КоличествоОсталось - ИтоговаяТаблица[Счетчик].КоличествоУпаковок;
					
					СтрокаОстатка = ИтоговаяТаблица.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОстатка, ИтоговаяТаблица[Счетчик]);
					СтрокаОстатка.cbr_НомерПаллета = 0;
					СтрокаОстатка.cbr_Уровень = 0;
					СтрокаОстатка.КоличествоУпаковок = КоличествоОсталось;
				
				КонецЕсли;
			ИначеЕсли ИтоговаяТаблица[Счетчик].cbr_Уровень = 1 Тогда
				
				ИтоговаяТаблица[Счетчик].cbr_Уровень = 0;	
				ИтоговаяТаблица[Счетчик].cbr_НомерПаллета = 0;
				ВесПервогоУровня = ВесПервогоУровня - (ИтоговаяТаблица[Счетчик].КоличествоУпаковок * ИтоговаяТаблица[Счетчик].Вес);
				ОстатокПлощадиПаллета = ОстатокПлощадиПаллета + (ИтоговаяТаблица[Счетчик].КоличествоУпаковок * ИтоговаяТаблица[Счетчик].Глубина * ИтоговаяТаблица[Счетчик].Ширина);
				
				
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	
	
	//2.2 Добавляем упаковки, начиная с первой по сформированному списку, с условием, что «Свойство упаковки» <> 1, пока не будет заполнена вся площадь паллета. (Площадь паллета > Площадь упаковок);
	ТекущийУровень = 2;
	ВесУровня = 0;
	Для Каждого СтрокаТовараУровня Из ИтоговаяТаблица Цикл

		Если СтрокаТовараУровня.cbr_Уровень = 0 И СтрокаТовараУровня.cbr_НомерПаллета = 0
			И СтрокаТовараУровня.cbr_СвойствоУпаковки <> 1 Тогда
			
			ОстатокПлощадиПаллета = РеквизитыПаллета.Вес;
			ПлощадьУпаковки = СтрокаТовараУровня.Глубина * СтрокаТовараУровня.Ширина;
			ВесУпаковки = СтрокаТовараУровня.Вес;

			ПлощадьУпаковок = ПлощадьУпаковки * СтрокаТовараУровня.КоличествоУпаковок;
			ВесУпаковок = ВесУпаковки * СтрокаТовараУровня.КоличествоУпаковок;

			ПлощадьУпаковокПоложенная = ПлощадьУпаковок;

			Если ПлощадьУпаковки < ОстатокПлощадиПаллета Тогда
				Пока ПлощадьУпаковокПоложенная > ОстатокПлощадиПаллета Цикл
					ПлощадьУпаковокПоложенная = ПлощадьУпаковокПоложенная - ПлощадьУпаковки;
					ВесУпаковок = ВесУпаковок - ВесУпаковки;
					СтрокаТаблицы.КоличествоУпаковок = СтрокаТовараУровня.КоличествоУпаковок - 1;
				КонецЦикла;

				СтрокаТовараУровня.cbr_НомерПаллета = МаксимальныйНомерПаллета;
				СтрокаТовараУровня.cbr_Уровень = ТекущийУровень;
				ОстатокПлощадиПаллета = ОстатокПлощадиПаллета - ПлощадьУпаковокПоложенная;
				ВесУровня = ВесУровня + ВесУпаковок;

				Если ПлощадьУпаковок > ПлощадьУпаковокПоложенная Тогда
					КоличествоОсталось = ПлощадьУпаковокПоложенная / ПлощадьУпаковки;

					СтрокаОстатка = ИтоговаяТаблица.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаОстатка, СтрокаТовараУровня);
					СтрокаОстатка.cbr_НомерПаллета = 0;
					СтрокаОстатка.cbr_Уровень = 0;
					СтрокаОстатка.КоличествоУпаковок  = КоличествоОсталось;
					ТекущийУровень = ТекущийУровень + 1;
					ВесУровня = 0;
				КонецЕсли;
			Иначе
				Продолжить;
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
	
	Объект.Товары.Очистить();
	ИтоговаяТаблица.Свернуть("Номенклатура, Характеристика, Упаковка, cbr_Уровень, cbr_НомерПаллета", "КоличествоУпаковок");
	Объект.Товары.Загрузить(ИтоговаяТаблица);
	
	Для каждого ТоварОставшийся из ОставленныеСтрокиТЧ цикл
		Строка = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, ТоварОставшийся);
	КонецЦикла;
	
	Объект.Товары.Сортировать("cbr_НомерПаллета Возр, cbr_Уровень Возр");
	Счетчик = Объект.Товары.Количество();
	Пока Счетчик > 0 цикл
		Счетчик = Счетчик - 1;
		Если Объект.Товары[Счетчик].cbr_НомерПаллета = 0 или Объект.Товары[Счетчик].cbr_Уровень = 0 Тогда
			Объект.Товары.Сдвинуть(Счетчик, Объект.Товары.Количество()- Счетчик - 1);	
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры
#КонецОбласти