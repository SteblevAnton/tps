
#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//++AlopinAS 16.02.2026 ПРД20
Процедура ОбработататьРегЗаданиеУдалениеЗаписейИзРегистраСоглашенияСКлиентами() Экспорт
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	cbr_ОстаткиНоменклатурыПоСоглашениюОстатки.Соглашение КАК Соглашение,
	|	cbr_ОстаткиНоменклатурыПоСоглашениюОстатки.Номенклатура КАК Номенклатура,
	|	cbr_ОстаткиНоменклатурыПоСоглашениюОстатки.Характеристика КАК Характеристика,
	|	cbr_ОстаткиНоменклатурыПоСоглашениюОстатки.ДатаНачала КАК ДатаНачала,
	|	cbr_ОстаткиНоменклатурыПоСоглашениюОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВТРегистр
	|ИЗ
	|	РегистрНакопления.cbr_ОстаткиНоменклатурыПоСоглашению.Остатки КАК cbr_ОстаткиНоменклатурыПоСоглашениюОстатки
	|ГДЕ
	|	cbr_ОстаткиНоменклатурыПоСоглашениюОстатки.КоличествоОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Ссылка КАК Ссылка,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Номенклатура КАК Номенклатура,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.Характеристика КАК Характеристика,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаНачала КАК ДатаНачала,
	|	СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТСоглашения
	|ИЗ
	|	Справочник.СоглашенияСКлиентами.cbr_ЦеныПоТоварам КАК СоглашенияСКлиентамиcbr_ЦеныПоТоварам
	|ГДЕ
	|	РАЗНОСТЬДАТ(СоглашенияСКлиентамиcbr_ЦеныПоТоварам.ДатаОкончания, &ТекущаяДата, ДЕНЬ) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТРегистр.Соглашение КАК Соглашение,
	|	ВТРегистр.Номенклатура КАК Номенклатура,
	|	ВТРегистр.Характеристика КАК Характеристика,
	|	ВТРегистр.ДатаНачала КАК ДатаНачала,
	|	ВТРегистр.КоличествоОстаток КАК КоличествоОстаток,
	|	ВТСоглашения.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	ВТСоглашения КАК ВТСоглашения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистр КАК ВТРегистр
	|		ПО ВТРегистр.Соглашение = ВТСоглашения.Ссылка
	|		И ВТРегистр.Номенклатура = ВТСоглашения.Номенклатура
	|		И ВТРегистр.Характеристика = ВТСоглашения.Характеристика
	|		И ВТРегистр.ДатаНачала = ВТСоглашения.ДатаНачала
	|ИТОГИ
	|	МАКСИМУМ(Соглашение)
	|ПО
	|	ДатаНачала,
	|	ДатаОкончания";

	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());

	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДатаНачала = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	МассивДокументовДляПроведения = Новый Массив();
	
	Пока ВыборкаДатаНачала.Следующий() Цикл

		ВыборкаДатаОкончания = ВыборкаДатаНачала.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаДатаОкончания.Следующий() Цикл
			
			НовыйДокумент = Документы.cbr_ОперацияСоглашения.СоздатьДокумент();
			НовыйДокумент.Дата = ВыборкаДатаОкончания.ДатаНачала;
			НовыйДокумент.Соглашение = ВыборкаДатаОкончания.Соглашение;
			НовыйДокумент.Действие = ПредопределенноеЗначение("Перечисление.cbr_ВидыДвиженияСоглашенияСКлиентами.Расход");
			
			НовыйДокумент.ДатаОкончания = ВыборкаДатаОкончания.ДатаОкончания;
			
			ДанныеДляФормирования = ВыборкаДатаОкончания.Выбрать();

			Пока ДанныеДляФормирования.Следующий() Цикл
				Новаястрока = НовыйДокумент.СписокНоменклатура.Добавить();
				ЗаполнитьЗначенияСвойств(Новаястрока, ДанныеДляФормирования);
				
				Новаястрока.Количество = ДанныеДляФормирования.КоличествоОстаток;
			КонецЦикла;
			МассивДокументовДляПроведения.Добавить(НовыйДокумент);
		КонецЦикла;
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка 
		Для Каждого Документ Из МассивДокументовДляПроведения Цикл
			Документ.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru = 'Ошибка создания документа операции по соглашению! Подробности смотри в журнале регистрации.'");
		ЗаписьЖурналаРегистрации("ОшибкаСозданияОперацииСоглашения", УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.cbr_ОперацияСоглашения, , ТекстСообщения,
			РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
		
КонецПроцедуры
//++AlopinAS 16.02.2026 ПРД20
#КонецОбласти
