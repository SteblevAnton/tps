
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура cbr_ПриСозданииНаСервереПеред(Отказ, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 18.12.2024 ЗАДАЧА Спецификация только чтение
	Отказ = РаботаСПроизводствомСервер.УстановленЗапретОткрытияСпецификации(Объект);
	Если Отказ Тогда
		ТекстСообщения = НСтр("ru='Установлен запрет на просмотр ресурсной спецификации'");
		ТекстСообщения = СтрШаблон(ТекстСообщения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 18.12.2024 ЗАДАЧА Спецификация только чтение
	
КонецПроцедуры

&НаСервере
Процедура cbr_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	ПодготовитьФорму();
	// КОНЕЦ Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве	
	
КонецПроцедуры

&НаКлиенте
Процедура cbr_ПриОткрытииПосле(Отказ)
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	ИтогПодвалаВремяДействий = Объект.Действия.Итог("Время");
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры



#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура cbr_АктивностьПриИзмененииПодключаемый(Команда) Экспорт
	
	// НАЧАЛО Османов Н.М. 05.03.2025 
	Если Не Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует") Тогда
		Возврат;
	КонецЕсли;
	
	УжеАктивнаяСпецификация = АктивнаяСпецификация();
	
	Если Не Объект.П3_Активность Тогда
		Если Не ЗначениеЗаполнено(УжеАктивнаяСпецификация) Тогда
			ТекстСообщения = НСтр("ru='Внимание! При снятом флажке ""Активная"" не остаётся ни одной действующей активной ресурсной спецификации!'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	Если Не ЗначениеЗаполнено(УжеАктивнаяСпецификация) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Уже существует действующая ресурсная спецификация со статусом ""Активная"": %1. В ней будет снят флажок активности. Продолжить?'");
	ТекстВопроса = СтрШаблон(ТекстВопроса, УжеАктивнаяСпецификация);
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Объект.П3_Активность = Ложь;
		Возврат;
	КонецЕсли;
	
	ИзменитьАктивностьРесурснойСпецификации(УжеАктивнаяСпецификация);
	// КОНЕЦ Османов Н.М. 05.03.2025 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДействия

&НаКлиенте
Процедура ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура("ТаблицаВыбора", ТаблицаОбеспеченияВХранилище());
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборСтрокиДляПодстановки", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ВыборНоменклатуры", ПараметрыОткрытия, , , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Если Не Поле.Имя = "ТаблицаДействийНоменклатура"
		И Не Поле.Имя = "ТаблицаДействийХарактеристика" 
		И Не Поле.Имя = "ТаблицаДействийУпаковка" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура("ТаблицаВыбора", ТаблицаОбеспеченияВХранилище());
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыборСтрокиДляПодстановки", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ВыборНоменклатуры", ПараметрыОткрытия, , , , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПриИзменении(Элемент)
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	ИтогПодвалаВремяДействий = Объект.Действия.Итог("Время");
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборСтрокиДляПодстановки(Результат, ДополнительныеПараметры) Экспорт
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ТаблицаДействий.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, Результат);
	
	ТаблицаПоОтбору = Объект.МатериалыИУслуги.НайтиСтроки(Результат);
	
	ИтогоКоличество = 0;
	Для Каждого СтрокаТаблицыПоОтбору Из ТаблицаПоОтбору Цикл
		ИтогоКоличество = ИтогоКоличество + СтрокаТаблицыПоОтбору.КоличествоУпаковок;
	КонецЦикла;
	
	Если ИтогоКоличество > 0 Тогда
		ИтогоКоличествоВДействиях = 0;
		ТаблицаПоОтбору = Объект.Действия.НайтиСтроки(Результат);
		Для Каждого СтрокаДействий Из ТаблицаПоОтбору Цикл
			ИтогоКоличествоВДействиях = ИтогоКоличествоВДействиях + СтрокаДействий.КоличествоУпаковок;
		КонецЦикла;
	КонецЕсли;
	
	Если ИтогоКоличество <> ИтогоКоличествоВДействиях Тогда
		Разница = ИтогоКоличествоВДействиях - ИтогоКоличество;
		ТекущаяСтрока.КоличествоУпаковок = ТекущаяСтрока.КоличествоУпаковок - Разница;
		Модифицированность = Истина;
	КонецЕсли;
		
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецПроцедуры
	
&НаСервере
Процедура ПодготовитьФорму()

	// НАЧАЛО Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	Если ФормыУХ.ЭлементыФормыУХУжеСозданы(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ГруппаДействия = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаДействия",
												"Действия",
												ВидГруппыФормы.Страница,
												Элементы.Страницы,
												Элементы.ГруппаПроизводственныйПроцесс);
								
	ДанныеПоТаблице = РаботаСФормамиСервер.СоздатьТаблицуНаФорме(Элементы,
																"ТаблицаДействий",
																"Действия",
																"Объект.Действия",
																Элементы.ГруппаДействия,
																Метаданные.Справочники.РесурсныеСпецификации.ТабличныеЧасти.Действия,
																,
																"");

	ГруппаДействия.ПутьКДаннымЗаголовка = "Объект.Действия.КоличествоСтрок";
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДанныеПоТаблице.Таблица.Подвал = Истина;
	ИтогПодвалаВремяДействий = Новый РеквизитФормы("ИтогПодвалаВремяДействий", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0, ДопустимыйЗнак.Неотрицательный));
	ДобавляемыеРеквизиты.Добавить(ИтогПодвалаВремяДействий);
		
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ДанныеПоТаблице.Колонки.Время.ПутьКДаннымПодвала = "ИтогПодвалаВремяДействий";
	
	ДанныеПоТаблице.Таблица.УстановитьДействие("Выбор", "ДействияВыбор");
	ДанныеПоТаблице.Таблица.УстановитьДействие("ПриИзменении", "ДействияПриИзменении");

	ПараметрыГруппыДействияУпаковкаЕдиницИзмерения = Новый Структура("Группировка", ГруппировкаКолонок.ВЯчейке);
	ГруппаДействияУпаковкаЕдиницИзмерения = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаДействияУпаковкаЕдиницИзмерения",
												"Действия единицы измерения",
												ВидГруппыФормы.ГруппаКолонок,
												Элементы.ТаблицаДействий,
												Элементы.ТаблицаДействийКоличествоУпаковок,
												ПараметрыГруппыДействияУпаковкаЕдиницИзмерения);
												
	Элементы.Переместить(ДанныеПоТаблице.Колонки.Упаковка, ГруппаДействияУпаковкаЕдиницИзмерения);
												
	КолонкаНоменклатураЕдиницаИзмерения = ФормыУХ.СоздатьПолеФормы(Элементы,
																"ТаблицаДействийНоменклатураЕдиницаИзмерения",
																"Ед. изм.",
																"Объект.Действия.Номенклатура.ЕдиницаИзмерения",
																ВидПоляФормы.ПолеНадписи,
																ГруппаДействияУпаковкаЕдиницИзмерения);
												
	ДанныеПоТаблице.Колонки.Номенклатура.УстановитьДействие("НачалоВыбора", "ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора");
	ДанныеПоТаблице.Колонки.Характеристика.УстановитьДействие("НачалоВыбора", "ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора");
	ДанныеПоТаблице.Колонки.Упаковка.УстановитьДействие("НачалоВыбора", "ДействияНоменклатураХарактеристикаУпаковкаНачалоВыбора");
	
	ДанныеПоТаблице.Колонки.Номенклатура.РедактированиеТекста = Ложь;
	ДанныеПоТаблице.Колонки.Номенклатура.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	ДанныеПоТаблице.Колонки.Характеристика.РедактированиеТекста = Ложь;
	ДанныеПоТаблице.Колонки.Характеристика.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	ДанныеПоТаблице.Колонки.Упаковка.РедактированиеТекста = Ложь;
	ДанныеПоТаблице.Колонки.Упаковка.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	
	// НАЧАЛО Османов Н.М. 05.03.2025 
	ЭлементПолеАктивность = ФормыУХ.СоздатьПолеФормы(Элементы,
													"ЭлементПолеАктивность",
													,
													"Объект.П3_Активность",
													ВидПоляФормы.ПолеФлажка,
													Элементы.ГруппаСтатус);
													
	ЭлементПолеАктивность.УстановитьДействие("ПриИзменении", "cbr_АктивностьПриИзмененииПодключаемый");
	// КОНЕЦ Османов Н.М. 05.03.2025 

	ФормыУХ.ЭлементыФормыУХДобавлены(ЭтотОбъект);
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, "ТаблицаДействий", "ТолькоПросмотр", НЕ ДоступностьРеквизитов);
	
	ДополнительныеПараметры = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ДополнительныеПараметры.ИмяПоляЕдиницаИзмерения = "ТаблицаДействийНоменклатураЕдиницаИзмерения";
	ДополнительныеПараметры.ПутьКПолюУпаковка = "Объект.Действия.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ДополнительныеПараметры);
	//НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, 
	//															   "ТаблицаДействийНоменклатураЕдиницаИзмерения", 
	//                                                               "Объект.Действия.Упаковка"); 
	// КОНЕЦ Османов Н.М. 21.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

&НаСервере
&Вместо("ПолучитьБлокируемыеРеквизитыОбъекта")
Функция П3_ПолучитьБлокируемыеРеквизитыОбъекта()
	
	// НАЧАЛО Османов Н.М. 23.11.2023 ЧТЗ №Действия в производстве
	Результат = ПродолжитьВызов();
	Результат.Добавить("ТаблицаДействий");
	Возврат Результат;
	// КОНЕЦ Османов Н.М. 23.11.2023 ЧТЗ №Действия в производстве
	
КонецФункции

&НаСервере
Функция ТаблицаОбеспеченияВХранилище()
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Обеспечение.Номенклатура КАК Номенклатура,
		|	Обеспечение.Характеристика КАК Характеристика,
		|	Обеспечение.Упаковка КАК Упаковка,
		|	Обеспечение.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ОбеспечениеПредварительно
		|ИЗ
		|	&Обеспечение КАК Обеспечение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Действия.Номенклатура КАК Номенклатура,
		|	Действия.Характеристика КАК Характеристика,
		|	Действия.Упаковка КАК Упаковка,
		|	Действия.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ДействияПредварительно
		|ИЗ
		|	&Действия КАК Действия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОбеспечениеПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ОбеспечениеПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Обеспечение
		|ИЗ
		|	ВТ_ОбеспечениеПредварительно КАК ВТ_ОбеспечениеПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ОбеспечениеПредварительно.Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДействияПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ДействияПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Действия
		|ИЗ
		|	ВТ_ДействияПредварительно КАК ВТ_ДействияПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДействияПредварительно.Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Обеспечение.Номенклатура КАК Номенклатура,
		|	ВТ_Обеспечение.Характеристика КАК Характеристика,
		|	ВТ_Обеспечение.Упаковка КАК Упаковка,
		|	ЕСТЬNULL(ВТ_Обеспечение.КоличествоУпаковок, 0) КАК КоличествоОбеспечение,
		|	ЕСТЬNULL(ВТ_Действия.КоличествоУпаковок, 0) КАК КоличествоДействия,
		|	ЕСТЬNULL(ВТ_Обеспечение.КоличествоУпаковок, 0) - ЕСТЬNULL(ВТ_Действия.КоличествоУпаковок, 0) КАК Разница
		|ИЗ
		|	ВТ_Обеспечение КАК ВТ_Обеспечение
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Действия КАК ВТ_Действия
		|		ПО ВТ_Обеспечение.Номенклатура = ВТ_Действия.Номенклатура
		|			И ВТ_Обеспечение.Характеристика = ВТ_Действия.Характеристика
		|			И ВТ_Обеспечение.Упаковка = ВТ_Действия.Упаковка";
	
	Запрос.УстановитьПараметр("Обеспечение", Объект.МатериалыИУслуги.Выгрузить());
	Запрос.УстановитьПараметр("Действия", Объект.Действия.Выгрузить());
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаДанных);
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
КонецФункции

&НаСервере
Функция АктивнаяСпецификация()

	// НАЧАЛО Османов Н.М. 05.03.2025 
	Возврат Справочники.РесурсныеСпецификации.АктивнаяСпецификация(Объект);
	// КОНЕЦ Османов Н.М. 05.03.2025 
	
КонецФункции

&НаСервереБезКонтекста
Процедура ИзменитьАктивностьРесурснойСпецификации(РесурснаяСпецификация)

	// НАЧАЛО Османов Н.М. 05.03.2025 
	ОбъектСпецификации = РесурснаяСпецификация.ПолучитьОбъект();
	ОбъектСпецификации.П3_Активность = Ложь;
	ОбъектСпецификации.Записать();
	// КОНЕЦ Османов Н.М. 05.03.2025 

КонецПроцедуры

&НаСервере
&После("ОсновноеИзделиеНоменклатураПриИзмененииНаСервере")
Процедура cbr_ОсновноеИзделиеНоменклатураПриИзмененииНаСервере(КэшированныеЗначения)
	
	// НАЧАЛО Османов Н.М. 19.08.2025 
	ИзменитьНаименование();
	// КОНЕЦ Османов Н.М. 19.08.2025
	
КонецПроцедуры

&НаСервере
&После("ОсновноеИзделиеХарактеристикаПриИзмененииНаСервере")
Процедура cbr_ОсновноеИзделиеХарактеристикаПриИзмененииНаСервере()
	
	// НАЧАЛО Османов Н.М. 29.05.2025 
	Если ЗначениеЗаполнено(Объект.ОсновноеИзделиеХарактеристика) Тогда
		ОсновноеИзделиеУпаковка = УправлениеСвойствами.ЗначениеСвойства(Объект.ОсновноеИзделиеХарактеристика, "УпаковкаРеквизитХарактеристики");
		Объект.ОсновноеИзделиеУпаковка = ОсновноеИзделиеУпаковка;
		
		Отбор = Новый Структура("Номенклатура, Характеристика", ОсновноеИзделиеНоменклатура, ОсновноеИзделиеХарактеристика);
		НайденныеСтроки = Объект.ВыходныеИзделия.НайтиСтроки(Отбор);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			НайденнаяСтрока.Упаковка = ОсновноеИзделиеУпаковка;
		КонецЦикла;
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 29.05.2025 
	
	// НАЧАЛО Османов Н.М. 19.08.2025 
	ИзменитьНаименование();
	// КОНЕЦ Османов Н.М. 19.08.2025 
		
КонецПроцедуры

&НаСервере
Процедура ИзменитьНаименование()

	// НАЧАЛО Османов Н.М. 19.08.2025 
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОсновноеИзделиеНоменклатура, "ВидНоменклатуры");
	ЭтоПолуфабрикат = УправлениеСвойствами.ЗначениеСвойства(ВидНоменклатуры, "ЭтоПолуфабрикатВидНоменклатуры");
	
	Если ЭтоПолуфабрикат = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Наименование = СокрЛП(СтрШаблон("%1 %2", Объект.ОсновноеИзделиеНоменклатура, Объект.ОсновноеИзделиеХарактеристика));
	
	ТекстСообщения = НСтр("ru='ВНИМАНИЕ!! Изменено наименование ресурсной спецификации'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	// КОНЕЦ Османов Н.М. 19.08.2025 

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ПроизводственныйПроцессДобавитьНовыйЭтап")
Функция cbr_ПроизводственныйПроцессДобавитьНовыйЭтап(ЭтапОбъект, КэшЭлементов, Коллекция, Параметры)

	#Вставка
	// НАЧАЛО Османов Н.М. 19.08.2025 
	ЭтапОбъект.Наименование = Объект.Наименование;
	// КОНЕЦ Османов Н.М. 19.08.2025 
	#КонецВставки
	
	НоваяСтрока = Коллекция.Добавить();
	НоваяСтрока.Наименование    = ЭтапОбъект.Наименование;
	НоваяСтрока.Номер           = ЭтапОбъект.НомерЭтапа;
	НоваяСтрока.НомерСледующего = ЭтапОбъект.НомерСледующегоЭтапа;
	НоваяСтрока.Ссылка          = ЭтапОбъект.ПолучитьСсылкуНового();
	НоваяСтрока.ВидЭлемента     = РедакторПроизводственногоПроцессаКлиентСервер.ВидЭлементаПоСсылке(НоваяСтрока.Ссылка);

	РедакторПроизводственногоПроцессаКлиентСервер.УстановитьКартинкуЭлемента(НоваяСтрока);

	КэшЭлемента  = ПроизводственныйПроцессКэшЭлементаКонструктор(НоваяСтрока.ВидЭлемента);
	КэшЭлемента.Объект     = ЭтапОбъект;
	КэшЭлемента.Записывать = Истина;
	КэшЭлементов.Вставить(НоваяСтрока.Ссылка, КэшЭлемента);

	Если Параметры.Свойство("СкорректироватьНумерацию") Тогда
		ПроизводственныйПроцессСкорректироватьНумерациюПослеДобавленияЭлемента(КэшЭлементов, Коллекция, ЭтапОбъект.НомерЭтапа);
	КонецЕсли;

	ПроизводственныйПроцессСкорректироватьСчетчикЭлементов(НоваяСтрока.ВидЭлемента, 1);

	Возврат НоваяСтрока.ПолучитьИдентификатор();

КонецФункции

#КонецОбласти




