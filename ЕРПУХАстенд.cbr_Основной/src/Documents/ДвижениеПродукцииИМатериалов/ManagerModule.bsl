#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СоздатьПередачуМатериаловНаОснованииРасходногоОрдера(РасходныйОрдер) Экспорт

	РезультатФункции = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Статус КАК Статус,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение ССЫЛКА Документ.ЭтапПроизводства2_2 КАК РаспоряжениеЭтоЭтап,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Склад КАК Склад
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
		|ГДЕ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка = &РасходныйОрдер
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Статус,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение ССЫЛКА Документ.ЭтапПроизводства2_2,
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Склад";
	
	Запрос.УстановитьПараметр("РасходныйОрдер", РасходныйОрдер);
		
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЭтапов = РезультатЗапроса.Выбрать();
	
	Если ВыборкаЭтапов.Количество() > 1 Тогда
		ТекстСообщения = НСтр("ru='Вы расходном ордере ""%1"" указано больше одного этапа производства'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РасходныйОрдер);
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Если Не ВыборкаЭтапов.Следующий() Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	Если Не ВыборкаЭтапов.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен") Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ЭтапПроизводства = ВыборкаЭтапов.Распоряжение;
	Распоряжения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтапПроизводства);
	ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство");
	ДанныеДляПроверки = Документы.ДвижениеПродукцииИМатериалов.ДанныеДляПроверкиВводаНаОсновании(Распоряжения, ХозяйственнаяОперация);
	ДанныеДляПроверки.Отправитель = ВыборкаЭтапов.Склад;
	
	Если Не ЗначениеЗаполнено(ДанныеДляПроверки.РаспоряженияКОформлению) Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ДанныеДляПроверки);
	РеквизитыШапки = Документы.ДвижениеПродукцииИМатериалов.ДанныеЗаполненияНакладной(ДанныеДляПроверки.РаспоряженияКОформлению, 
																					  ДанныеДляПроверки);
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения, 
																			   РеквизитыШапки,
																			   ДанныеДляПроверки.РаспоряженияКОформлению);
																			   
	ДокументПередачаМатериалов = Документы.ДвижениеПродукцииИМатериалов.СоздатьДокумент();
	ДокументПередачаМатериалов.Дата = ТекущаяДатаСеанса();
	ДокументПередачаМатериалов.Заполнить(ПараметрыЗаполнения);
	
	ДокументПередачаМатериалов.Записать(РежимЗаписиДокумента.Запись);
	РезультатФункции = ДокументПередачаМатериалов.Ссылка;
	
	ОшибкаПроведения = Ложь;
	
	Если ДокументПередачаМатериалов.Товары.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='В документе %1 не заполнена номенклатура!'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатФункции);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		ОшибкаПроведения = Истина;
	КонецЕсли;
	
	Если Не ОшибкаПроведения Тогда
		
		Попытка
			
			ДокументПередачаМатериалов.Записать(РежимЗаписиДокумента.Проведение);
			
		Исключение
			
			Инфо = ИнформацияОбОшибке();
			ОшибкаПроведения = Истина;		
			
		КонецПопытки;
		
	КонецЕсли;
	
	Если ОшибкаПроведения Тогда
		ТекстСообщения = НСтр("ru='Не удалось провести документ %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, РезультатФункции);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции // СоздатьПередачуМатериаловНаОснованииРасходногоОрдера()

Функция СоздатьПередачуМатериаловНаОснованииЭтапа(ЭтапПроизводства) Экспорт

	РезультатФункции = Неопределено;
	
	Распоряжения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтапПроизводства);
	ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство");
	ДанныеДляПроверки = Документы.ДвижениеПродукцииИМатериалов.ДанныеДляПроверкиВводаНаОсновании(Распоряжения, ХозяйственнаяОперация);
	
	Если Не ЗначениеЗаполнено(ДанныеДляПроверки.РаспоряженияКОформлению) Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ДанныеДляПроверки);
	РеквизитыШапки = Документы.ДвижениеПродукцииИМатериалов.ДанныеЗаполненияНакладной(ДанныеДляПроверки.РаспоряженияКОформлению, 
																					  ДанныеДляПроверки);
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, РеквизитыШапки, "Отправитель");
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения, 
																			   РеквизитыШапки,
																			   ДанныеДляПроверки.РаспоряженияКОформлению);

	ДокументПередачаМатериалов = Документы.ДвижениеПродукцииИМатериалов.СоздатьДокумент();
	ДокументПередачаМатериалов.Дата = ТекущаяДатаСеанса();
	ДокументПередачаМатериалов.Заполнить(ПараметрыЗаполнения);

	Если ДокументПередачаМатериалов.Товары.Количество() = 0 Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ДокументПередачаМатериалов.Записать(РежимЗаписиДокумента.Запись);
	РезультатФункции = ДокументПередачаМатериалов.Ссылка;
		
	ОшибкаПроведения = Ложь;
	Попытка
		
		ДокументПередачаМатериалов.Записать(РежимЗаписиДокумента.Проведение);
	
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ОшибкаПроведения = Истина;		
		
	КонецПопытки;
	
	Если ОшибкаПроведения Тогда
		ТекстСообщения = НСтр("ru='Не удалось создать документ Передача материалов в производство. Чтобы провести Этап производства создайте Передачу материалов вручную и после этого проведите документ'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции // СоздатьПередачуМатериаловНаОснованииЭтапа()

Функция СоздатьПередачуМатериаловНаОснованииЭтапаСЗаполнениеТоваров(ЭтапПроизводства, Товары) Экспорт

	РезультатФункции = Неопределено;
	
	Распоряжения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЭтапПроизводства);
	ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаМатериаловВПроизводство");
	ДанныеДляПроверки = Документы.ДвижениеПродукцииИМатериалов.ДанныеДляПроверкиВводаНаОсновании(Распоряжения, ХозяйственнаяОперация);
	
	Если Не ЗначениеЗаполнено(ДанныеДляПроверки.РаспоряженияКОформлению) Тогда
		Возврат РезультатФункции;
	КонецЕсли;
	
	ПараметрыЗаполнения = Документы.ДвижениеПродукцииИМатериалов.ПараметрыЗаполненияДокумента();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ДанныеДляПроверки);
	РеквизитыШапки = Документы.ДвижениеПродукцииИМатериалов.ДанныеЗаполненияНакладной(ДанныеДляПроверки.РаспоряженияКОформлению, 
																					  ДанныеДляПроверки);
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, РеквизитыШапки, "Отправитель");
	Документы.ДвижениеПродукцииИМатериалов.ИнициализироватьПараметрыЗаполнения(ПараметрыЗаполнения, 
																			   РеквизитыШапки,
																			   ДанныеДляПроверки.РаспоряженияКОформлению);

	ДокументПередачаМатериалов = Документы.ДвижениеПродукцииИМатериалов.СоздатьДокумент();
	ДокументПередачаМатериалов.Дата = ТекущаяДатаСеанса();
	ДокументПередачаМатериалов.Заполнить(ПараметрыЗаполнения);

	ДокументПередачаМатериалов.Товары.Очистить(); 
	
	Для Каждого СтрокаТоваров Из Товары Цикл
		НоваяСтрока = ДокументПередачаМатериалов.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
	КонецЦикла;
	
	ПараметрыУказанияСерий = Документы.ДвижениеПродукцииИМатериалов.ПараметрыУказанияСерий(ДокументПередачаМатериалов);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументПередачаМатериалов, ПараметрыУказанияСерий);
	
	ДокументПередачаМатериалов.Записать(РежимЗаписиДокумента.Запись);
	РезультатФункции = ДокументПередачаМатериалов.Ссылка;
		
	ОшибкаПроведения = Ложь;
	Попытка
		
		ДокументПередачаМатериалов.Записать(РежимЗаписиДокумента.Проведение);
	
	Исключение
		
		Инфо = ИнформацияОбОшибке();
		ОшибкаПроведения = Истина;		
		
	КонецПопытки;
	
	Если ОшибкаПроведения Тогда
		ТекстСообщения = НСтр("ru='Не удалось создать документ Передача материалов в производство для Замывки.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат РезультатФункции;
	
КонецФункции // СоздатьПередачуМатериаловНаОснованииЭтапаСЗаполнениеТоваров()

#КонецОбласти

#КонецЕсли