#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область СлужебныйПрограммныйИнтерфейс
&ИзменениеИКонтроль("ЗаполнитьСерииДляСтрокиТовары")
Процедура cbr_ЗаполнитьСерииДляСтрокиТовары(Форма, КлючевыеПараметры, ИндексСтроки, ПараметрыУказанияСерий) Экспорт

	ПодборСерий = КлючевыеПараметры.ПодборСерий;
	Объект = Форма.Объект; // ОбработкаОбъект.ЗапросКоличестваИСерий
	Строка = Объект.Товары[ИндексСтроки];

	Если Не ПодборСерий
			Или Строка.СтатусУказанияСерий = 0
			#Удаление
			Или Строка.НомерПервойСтрокиСерий <> 0
			#КонецУдаления
			#Вставка
			//++АшаеваАН 29.01.26 ПРД.5
			Или ?(КлючевыеПараметры.Свойство("ПерезаполнитьСерии"), НЕ КлючевыеПараметры.ПерезаполнитьСерии, Строка.НомерПервойСтрокиСерий <> 0)
			//--АшаеваАН 29.01.26 ПРД.5
			#КонецВставки
			Или Строка.Обособленно
				И Не ЗначениеЗаполнено(Строка.Назначение) Тогда
		Возврат;
	КонецЕсли;

	СтруктураОтбора = Новый Структура("Номенклатура,Характеристика,Склад,Обособленно");
	ЗаполнитьЗначенияСвойств(СтруктураОтбора, Строка);

	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Номенклатура, "ВидНоменклатуры");

	ВариантПолученияДанных = Обработки.ПодборСерийВДокументы.ВариантПолучениеДанныхИзРегистровПоПараметрамФормы(
		ПараметрыУказанияСерий,
		Неопределено,
		Строка.Склад,
		ВидНоменклатуры);

	Если ЭтоАдресВременногоХранилища(Форма.Параметры.АдресТаблицыПодобраноРанее) Тогда

		Если КлючевыеПараметры.ПодборТоваров Тогда
			ПодобраноРанее = ПолучитьИзВременногоХранилища(Форма.Параметры.АдресТаблицыПодобраноРанее).Скопировать(СтруктураОтбора);
		Иначе
			ПодобраноРанее = ПолучитьИзВременногоХранилища(Форма.Параметры.АдресТаблицыПодобраноРанее);
		КонецЕсли;

	Иначе

		ПодобраноРанее = Новый ТаблицаЗначений();
		ПодобраноРанее.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
		ПодобраноРанее.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
		ПодобраноРанее.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ПодобраноРанее.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ПодобраноРанее.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
		ПодобраноРанее.Колонки.Добавить("Назначение", Новый ОписаниеТипов("СправочникСсылка.Назначения"));
		ПодобраноРанее.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));

	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура",   Строка.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Строка.Характеристика);
	Запрос.УстановитьПараметр("Назначение",     Строка.Назначение);
	Запрос.УстановитьПараметр("Склад",          Строка.Склад);
	Запрос.УстановитьПараметр("Подразделение",  Строка.Подразделение);
	Запрос.УстановитьПараметр("Помещение",      Справочники.СкладскиеПомещения.ПустаяСсылка());
	Запрос.УстановитьПараметр("Регистратор",    Объект.Регистратор);
	Запрос.УстановитьПараметр("Распоряжение",   Неопределено);
	Запрос.УстановитьПараметр("ПодобраноРанее", ПодобраноРанее);
	Запрос.УстановитьПараметр("ВсеСерии",       Истина);

	Если ПараметрыУказанияСерий.ОперацияДокумента = Перечисления.ХозяйственныеОперации.ВозвратТоваровКомитенту Тогда
		Для Каждого ДополнительныйПараметр Из ПараметрыУказанияСерий.ПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(ДополнительныйПараметр.Ключ, ДополнительныйПараметр.Значение);
		КонецЦикла;
	КонецЕсли;

	ТекстыЗапроса = Новый Массив();
	ТекстЗапроса = Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(
		ВариантПолученияДанных,
		Строка.Склад);

	ТекстыЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Серия      КАК Серия,
		|	Таблица.Количество КАК Количество
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	&ПодобраноРанее КАК Таблица
		|ГДЕ
		|	Таблица.Номенклатура = &Номенклатура
		|		И Таблица.Характеристика = &Характеристика
		|		И Таблица.Склад = &Склад
		|		И Таблица.Назначение = &Назначение
		|		И Таблица.Подразделение = &Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Серия             КАК Серия,
		|	СУММА(Таблица.Количество) КАК Количество
		|ПОМЕСТИТЬ ПодобраноРанее
		|ИЗ
		|	ТаблицаТоваров КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Серия
		|ИНДЕКСИРОВАТЬ ПО
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистров.Серия                                                    КАК Серия,
		|	ДанныеРегистров.СвободныйОстаток - ЕСТЬNULL(ПодобраноРанее.Количество,0) КАК Доступно,
		|	&ТекстЗапросаРеквизитыСерийВыборка
		|ИЗ
		|	ДанныеРегистров КАК ДанныеРегистров
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПодобраноРанее КАК ПодобраноРанее
		|		ПО ПодобраноРанее.Серия = ДанныеРегистров.Серия
		#Вставка
        //++АшаеваАН 29.01.26 ПРД.5
        |     ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.лимсИсторияСтатусаСерии.СрезПоследних КАК ИсторияСтатусаСерии
        |     ПО ПодобраноРанее.Серия = ИсторияСтатусаСерии.Серия
        |ГДЕ
        |     &УсловияСпецТребований
        //--АшаеваАН 29.01.26 ПРД.5
        #КонецВставки
		|УПОРЯДОЧИТЬ ПО
		|	ГоденДо,
		|	Номер";

	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Номенклатура, "ВидНоменклатуры");
	НастройкиИспользованияСерий = Справочники.ВидыНоменклатуры.НастройкиИспользованияСерий(
		ВидНоменклатуры,
		ПараметрыУказанияСерий,
		Новый Структура("Склад", Строка.Склад));
	Подстановка = Обработки.ПодборСерийВДокументы.ТекстЗапросаРеквизитыСерий("ДанныеРегистров", НастройкиИспользованияСерий, Ложь);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаРеквизитыСерийВыборка", Подстановка);

	ТекстыЗапроса.Добавить(ТекстЗапроса);

	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	#Вставка
    //++АшаеваАН 29.01.26 ПРД.5
    Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "cbr_ПоказыватьСерииСУчетомСпецТребований") Тогда
        
        Если Форма.cbr_ПоказыватьСерииСУчетомСпецТребований Тогда
            
            МассивУсловий = Новый Массив();
            
            Если ЗНАЧЕНИЕЗАПОЛНЕНО(Форма.cbr_НеБолееДатыПроизводства) Тогда
                
                МассивУсловий.Добавить("РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДанныеРегистров.Серия.ДатаПроизводства, ДЕНЬ), &ТекущаяДата, МЕСЯЦ) <= &cbr_НеБолееДатыПроизводства");
                Запрос.УстановитьПараметр("cbr_НеБолееДатыПроизводства", Форма.cbr_НеБолееДатыПроизводства);
        
            КонецЕсли;

            Если ЗНАЧЕНИЕЗАПОЛНЕНО(Форма.cbr_ДопустимыйОстатокГСХНеМенееПроцент) Тогда
                
                МассивУсловий.Добавить("
                |ВЫБОР
                |        КОГДА ДанныеРегистров.Серия.ГоденДо > ДанныеРегистров.Серия.ДатаПроизводства ТОГДА
                |            (РАЗНОСТЬДАТ(&ТекущаяДата, НАЧАЛОПЕРИОДА(ДанныеРегистров.Серия.ГоденДо, ДЕНЬ), ДЕНЬ) /
                |            РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ДанныеРегистров.Серия.ДатаПроизводства, ДЕНЬ), НАЧАЛОПЕРИОДА(ДанныеРегистров.Серия.ГоденДо, ДЕНЬ), ДЕНЬ) * 100)
                |            >= &cbr_ДопустимыйОстатокГСХНеМенееПроцент
                |КОНЕЦ");
                Запрос.УстановитьПараметр("cbr_ДопустимыйОстатокГСХНеМенееПроцент", Форма.cbr_ДопустимыйОстатокГСХНеМенееПроцент);
        
            КонецЕсли;
                        
            Если ЗНАЧЕНИЕЗАПОЛНЕНО(Форма.cbr_ДопустимыйОстатокГСХНеМенееМес) Тогда
                
                МассивУсловий.Добавить("РАЗНОСТЬДАТ(&ТекущаяДата, НАЧАЛОПЕРИОДА(ДанныеРегистров.Серия.ГоденДо, ДЕНЬ), МЕСЯЦ) >= &cbr_ДопустимыйОстатокГСХНеМенееМес");
                Запрос.УстановитьПараметр("cbr_ДопустимыйОстатокГСХНеМенееМес", Форма.cbr_ДопустимыйОстатокГСХНеМенееМес);
        
            КонецЕсли;
        
            Если МассивУсловий.Количество() > 0 Тогда    
        
             Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
        
             Разделитель = " И ";
             Условие = СтрСоединить(МассивУсловий, Разделитель);
                        
             Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияСпецТребований", Условие);
                
         КонецЕсли;
        
        КонецЕсли;
    КонецЕсли;
    
    Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловияСпецТребований", "ИСТИНА");
    
    Если КлючевыеПараметры.Свойство("ПерезаполнитьСерии") И КлючевыеПараметры.ПерезаполнитьСерии Тогда
        Объект.Серии.Очистить();
    КонецЕсли;    
    //--АшаеваАН 29.01.26 ПРД.5
    #КонецВставки
	ТаблицаОстатков = Запрос.Выполнить().Выгрузить();

	Если ТаблицаОстатков.Количество() = 0 Тогда
		НомерПервойСтрокиСерий = -1;
	Иначе
		НомерПервойСтрокиСерий = Объект.Серии.Количество() + 1;
	КонецЕсли;

	Для Каждого СтрокаСерий Из ТаблицаОстатков Цикл

		НоваяСтрокаСерий = Объект.Серии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСерий, Строка, "Номенклатура,Характеристика,Склад,Назначение");
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСерий, СтрокаСерий);
		НоваяСтрокаСерий.ПроверятьДоступность = НоваяСтрокаСерий.Доступно > 0;

	КонецЦикла;

	УправлениеСвойствамиУТ.ЗаполнитьКолонкиДополнительныхРеквизитов(
		Форма,
		"Объект.Серии",
		"Серия");

	НайденныеСтроки = Объект.Товары.НайтиСтроки(СтруктураОтбора);
	Для Каждого Строка Из НайденныеСтроки Цикл
		Строка.НомерПервойСтрокиСерий = НомерПервойСтрокиСерий;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти


#КонецЕсли
