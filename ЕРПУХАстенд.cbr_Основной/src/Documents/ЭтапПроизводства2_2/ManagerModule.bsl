#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
// Османов Н.М. 01.10.2025  
Функция ДобавитьЗамывку(ДокументОбъект, СтруктураОтбора = Неопределено) Экспорт

	РезультатФункции = Новый Структура("НомерСтрокиОбеспечения, НомерСтрокиПобочки", 0, 0);
	
	ПостроительЗапроса = Новый ПостроительЗапроса;
	ПостроительЗапроса.Текст = 
		"ВЫБРАТЬ
		|	НормыЗамывкиСрезПоследних.СкладОбеспечения КАК СкладОбеспечения,
		|	НормыЗамывкиСрезПоследних.НоменклатураОбеспечения КАК НоменклатураОбеспечения,
		|	НормыЗамывкиСрезПоследних.СкладПобочногоВыпуска КАК СкладПобочногоВыпуска,
		|	НормыЗамывкиСрезПоследних.НоменклатураПобочногоВыпуска КАК НоменклатураПобочногоВыпуска,
		|	НормыЗамывкиСрезПоследних.Количество КАК Количество,
		|	НормыЗамывкиСрезПоследних.Цена КАК Цена,
		|	НормыЗамывкиСрезПоследних.КоэффициентВыпуска КАК КоэффициентВыпуска,
		|	НормыЗамывкиСрезПоследних.ВремяНаЗамывку КАК ВремяНаЗамывку
		|ИЗ
		|	РегистрСведений.НормыЗамывки.СрезПоследних(
		|			&ДатаЭтапа,
		|			РабочийЦентр = &РабочийЦентр
		|				{(СкладОбеспечения), (НоменклатураОбеспечения)}) КАК НормыЗамывкиСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатьиКалькуляции.Ссылка КАК СтатьяКалькуляции
		|ИЗ
		|	Справочник.СтатьиКалькуляции КАК СтатьиКалькуляции
		|ГДЕ
		|	ВРЕГ(СтатьиКалькуляции.Наименование) = &НаименованиеЗамывка";
	
	ПостроительЗапроса.Параметры.Вставить("ДатаЭтапа", ДокументОбъект.Дата);
	ПостроительЗапроса.Параметры.Вставить("РабочийЦентр", ДокументОбъект.РабочийЦентр);
	ПостроительЗапроса.Параметры.Вставить("НаименованиеЗамывка", "ЗАМЫВКА");
	
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") Тогда
		НовыйОтбор = ПостроительЗапроса.Отбор.Добавить("СкладОбеспечения");
		НовыйОтбор.ВидСравнения = ВидСравнения.Равно;
		НовыйОтбор.Значение = СтруктураОтбора.Склад;
		НовыйОтбор.Использование = Истина;
		
		НовыйОтбор = ПостроительЗапроса.Отбор.Добавить("НоменклатураОбеспечения");
		НовыйОтбор.ВидСравнения = ВидСравнения.Равно;
		НовыйОтбор.Значение = СтруктураОтбора.Номенклатура;
		НовыйОтбор.Использование = Истина;
	КонецЕсли;

	Запрос = ПостроительЗапроса.ПолучитьЗапрос();
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		ТекстСообщения = НСтр("ru='Не найдено ни одной нормы замывки'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат РезультатФункции;
	КонецЕсли;
	
	ВыборкаЗамывка = РезультатЗапроса[1].Выбрать();
	
	Если Не ВыборкаЗамывка.Следующий() Тогда
		ТекстСообщения = НСтр("ru='Не найдена статья калькуляции ""Замывка""'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат РезультатФункции;
	КонецЕсли;
		
	ЗамывкаВоВкладках = УжеЕстьНоменклатураЗамывки(ДокументОбъект);
	
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять"));
	КэшированныеЗначения = Неопределено;
		
	Если Не ЗамывкаВоВкладках.ЕстьЗамывкаВОбеспечении Тогда
		
		НоваяЗамывка = ДокументОбъект.ОбеспечениеМатериаламиИРаботами.Добавить();
		НоваяЗамывка.Номенклатура = Выборка.НоменклатураОбеспечения;
		НоваяЗамывка.Склад = Выборка.СкладОбеспечения;
		НоваяЗамывка.ВариантОбеспечения = ПредопределенноеЗначение("Перечисление.ВариантыОбеспечения.Отгрузить");
		НоваяЗамывка.КоличествоУпаковок = Выборка.Количество;
		НоваяЗамывка.Подразделение = ДокументОбъект.Подразделение;
		НоваяЗамывка.ДатаОтгрузки = ТекущаяДатаСеанса();
		НоваяЗамывка.СтатьяКалькуляции = ВыборкаЗамывка.СтатьяКалькуляции;
		НоваяЗамывка.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоЕдиницВСтрокеТЧ(НоваяЗамывка, СтруктураДействий, КэшированныеЗначения);
		РезультатФункции.НомерСтрокиОбеспечения = НоваяЗамывка.НомерСтроки;
		
		ОтборДляТаблицы = Новый Структура("ИдентификаторСтроки", НоваяЗамывка.ИдентификаторСтроки);
		ТаблицаЗаполнения = ДокументОбъект.ОбеспечениеМатериаламиИРаботами.Выгрузить(ОтборДляТаблицы);
		ЗаполнитьСерииВСтрокахТаблицы(ДокументОбъект.Ссылка, ТаблицаЗаполнения);
		
		МассивСтрокДляУказанияСерий = Новый Массив;
		
		ПервыйПрогон = Истина;
		Для Каждого СтрокаСерий Из ТаблицаЗаполнения Цикл
			Если Не ЗначениеЗаполнено(СтрокаСерий.Серия) Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаЗаполнения = НоваяЗамывка;
			Если Не ПервыйПрогон Тогда
				СтрокаЗаполнения = ДокументОбъект.ОбеспечениеМатериаламиИРаботами.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаЗаполнения, НоваяЗамывка);
				СтрокаЗаполнения.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
			КонецЕсли;
			
			СтрокаЗаполнения.Серия = СтрокаСерий.Серия;
			СтрокаЗаполнения.Количество = СтрокаСерий.Количество;
			СтрокаЗаполнения.КоличествоУпаковок = СтрокаСерий.КоличествоУпаковок;
			СтрокаЗаполнения.СтатусУказанияСерий = СтрокаСерий.СтатусУказанияСерий;
			СтрокаЗаполнения.СтатусУказанияСерийОтправитель = СтрокаСерий.СтатусУказанияСерийОтправитель;
			
			МассивСтрокДляУказанияСерий.Добавить(СтрокаЗаполнения);
			
			ПервыйПрогон = Ложь;
			
		КонецЦикла;
		
		ПараметрыУказанияСерий = Документы.ЭтапПроизводства2_2.ПараметрыУказанияСерий(ДокументОбъект);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий.ОбеспечениеМатериаламиИРаботами, МассивСтрокДляУказанияСерий);
		
		ПредыдущийСтатус = ДокументОбъект.Статус;
		ДокументОбъект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Начат");
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Документы.ЭтапПроизводства2_2.ЗаполнитьРасходМатериаловИРаботПоДаннымОбеспечения(ДокументОбъект, 1);
		
		ДокументОбъект.Статус = ПредыдущийСтатус;
		
	КонецЕсли;
	
	Если Не ЗамывкаВоВкладках.ЕстьЗамывкаВПобочке Тогда
		
		НоваяПобочка = ДокументОбъект.ПобочныеИзделия.Добавить();
		НоваяПобочка.Номенклатура = Выборка.НоменклатураПобочногоВыпуска;
		НоваяПобочка.Получатель = Выборка.СкладПобочногоВыпуска;
		НоваяПобочка.КоличествоУпаковок = Выборка.Количество * Выборка.КоэффициентВыпуска;
		НоваяПобочка.Цена = Выборка.Цена;
		НоваяПобочка.Произведено = Истина;
		НоваяПобочка.Подразделение = ДокументОбъект.Подразделение;
		НоваяПобочка.СтатьяКалькуляции = ВыборкаЗамывка.СтатьяКалькуляции;
		НоваяПобочка.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		Если ДокументОбъект.ВыходныеИзделия.Количество() > 0 Тогда
			НоваяПобочка.ДатаПроизводства = ДокументОбъект.ВыходныеИзделия[0].ДатаПроизводства;
		КонецЕсли;
		
		СтруктураДействий.Вставить("ПересчитатьСумму");
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяПобочка, СтруктураДействий, КэшированныеЗначения);
		РезультатФункции.НомерСтрокиПобочки = НоваяПобочка.НомерСтроки;
		
	КонецЕсли;
	
	Возврат РезультатФункции;

КонецФункции

// Османов Н.М. 01.10.2025  
Функция НоменклатураЗамывки(Дата, РабочийЦентр) Экспорт

	РезультатФункции = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НормыЗамывкиСрезПоследних.СкладОбеспечения КАК Склад,
		|	НормыЗамывкиСрезПоследних.НоменклатураОбеспечения КАК Номенклатура
		|ИЗ
		|	РегистрСведений.НормыЗамывки.СрезПоследних(&ДатаЭтапа, РабочийЦентр = &РабочийЦентр) КАК НормыЗамывкиСрезПоследних
		|
		|СГРУППИРОВАТЬ ПО
		|	НормыЗамывкиСрезПоследних.СкладОбеспечения,
		|	НормыЗамывкиСрезПоследних.НоменклатураОбеспечения";
	
	Запрос.УстановитьПараметр("ДатаЭтапа", Дата);
	Запрос.УстановитьПараметр("РабочийЦентр", РабочийЦентр);
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПорядковыйНомер = 1;
	Пока Выборка.Следующий() Цикл
		
		СтруктураДанных = Новый Структура("Номер, Склад, Номенклатура", ПорядковыйНомер);
		ЗаполнитьЗначенияСвойств(СтруктураДанных, Выборка);

		РезультатФункции.Добавить(СтруктураДанных);
		ПорядковыйНомер = ПорядковыйНомер + 1;
		
	КонецЦикла;
	
	Возврат РезультатФункции;

КонецФункции

Функция УжеЕстьНоменклатураЗамывки(Документ) Экспорт

	РезультатФункции = Новый Структура("ЕстьЗамывка, ЕстьЗамывкаВОбеспечении, ЕстьЗамывкаВПобочке, ЭтоПолуфабрикат", 
										Ложь, 
										Ложь, 
										Ложь,
										Ложь);
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.НомерСтроки КАК НомерСтроки,
	               |	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатьяКалькуляции КАК СтатьяКалькуляции
	               |ПОМЕСТИТЬ ВТ_ТаблицаОбеспечения
	               |ИЗ
	               |	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
	               |ГДЕ
	               |	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЭтапПроизводства2_2ПобочныеИзделия.НомерСтроки КАК НомерСтроки,
	               |	ЭтапПроизводства2_2ПобочныеИзделия.СтатьяКалькуляции КАК СтатьяКалькуляции
	               |ПОМЕСТИТЬ ВТ_ТаблицаПобочки
	               |ИЗ
	               |	Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ЭтапПроизводства2_2ПобочныеИзделия
	               |ГДЕ
	               |	ЭтапПроизводства2_2ПобочныеИзделия.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ДополнительныеСведения.Значение КАК Значение
	               |ИЗ
	               |	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ЭтапПроизводства2_2ВыходныеИзделия
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |		ПО ЭтапПроизводства2_2ВыходныеИзделия.Номенклатура.ВидНоменклатуры = ДополнительныеСведения.Объект
	               |			И (ДополнительныеСведения.Свойство = &СвойствоЭтоПолуфабрикат)
	               |			И (ДополнительныеСведения.Значение = ИСТИНА)
	               |ГДЕ
	               |	ЭтапПроизводства2_2ВыходныеИзделия.Ссылка = &Ссылка
				   |;";
	
	Запрос.УстановитьПараметр("Ссылка", Документ);
	Запрос.УстановитьПараметр("НаименованиеЗамывка", "ЗАМЫВКА");
	Запрос.УстановитьПараметр("СвойствоЭтоПолуфабрикат", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ЭтоПолуфабрикатВидНоменклатуры"));
	
	Если ТипЗнч(Документ) = Тип("ДокументОбъект.ЭтапПроизводства2_2") Тогда
		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.НомерСтроки КАК НомерСтроки,
		               |	ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами.СтатьяКалькуляции КАК СтатьяКалькуляции
		               |ПОМЕСТИТЬ ВТ_ТаблицаОбеспечения
		               |ИЗ
		               |	&ТаблицаОбеспечения КАК ЭтапПроизводства2_2ОбеспечениеМатериаламиИРаботами
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	ЭтапПроизводства2_2ПобочныеИзделия.НомерСтроки КАК НомерСтроки,
		               |	ЭтапПроизводства2_2ПобочныеИзделия.СтатьяКалькуляции КАК СтатьяКалькуляции
		               |ПОМЕСТИТЬ ВТ_ТаблицаПобочки
		               |ИЗ
		               |	&ТаблицаПобочки КАК ЭтапПроизводства2_2ПобочныеИзделия
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ ПЕРВЫЕ 1
		               |	ДополнительныеСведения.Значение КАК Значение
		               |ИЗ
		               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		               |ГДЕ
		               |	ДополнительныеСведения.Объект = ВЫРАЗИТЬ(&НоменклатураВыпуска КАК Справочник.Номенклатура).ВидНоменклатуры
		               |	И ДополнительныеСведения.Свойство = &СвойствоЭтоПолуфабрикат
		               |	И ДополнительныеСведения.Значение = ИСТИНА
					   |;";
		
		Запрос.УстановитьПараметр("ТаблицаОбеспечения", Документ.ОбеспечениеМатериаламиИРаботами.Выгрузить());
		Запрос.УстановитьПараметр("ТаблицаПобочки", Документ.ПобочныеИзделия.Выгрузить());
		НоменклатураВыпуска = Справочники.Номенклатура.ПустаяСсылка();
		Если Документ.ВыходныеИзделия.Количество() > 0 Тогда
			НоменклатураВыпуска = Документ.ВыходныеИзделия[0].Номенклатура;
		КонецЕсли;
		Запрос.УстановитьПараметр("НоменклатураВыпуска", НоменклатураВыпуска);
		
	КонецЕсли;
	
	ОсновнойТекстЗапроса = ТекстЗапроса + "
				   |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТаблицаОбеспечения.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	ВТ_ТаблицаОбеспечения КАК ВТ_ТаблицаОбеспечения
				   |ГДЕ
	               |	ВТ_ТаблицаОбеспечения.СтатьяКалькуляции В
	               |			(ВЫБРАТЬ ПЕРВЫЕ 1
	               |				СтатьиКалькуляции.Ссылка КАК СтатьяКалькуляции
	               |			ИЗ
	               |				Справочник.СтатьиКалькуляции КАК СтатьиКалькуляции
	               |			ГДЕ
	               |				ВРЕГ(СтатьиКалькуляции.Наименование) = &НаименованиеЗамывка
	               |				И НЕ СтатьиКалькуляции.ПометкаУдаления)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТаблицаПобочки.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	ВТ_ТаблицаПобочки КАК ВТ_ТаблицаПобочки
				   |ГДЕ
	               |	ВТ_ТаблицаПобочки.СтатьяКалькуляции В
	               |			(ВЫБРАТЬ ПЕРВЫЕ 1
	               |				СтатьиКалькуляции.Ссылка КАК СтатьяКалькуляции
	               |			ИЗ
	               |				Справочник.СтатьиКалькуляции КАК СтатьиКалькуляции
	               |			ГДЕ
	               |				ВРЕГ(СтатьиКалькуляции.Наименование) = &НаименованиеЗамывка
	               |				И НЕ СтатьиКалькуляции.ПометкаУдаления)";
	
	Запрос.Текст = ОсновнойТекстЗапроса;
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Если Не РезультатыЗапроса[2].Пустой() Тогда
		РезультатФункции.ЭтоПолуфабрикат = Истина;
	КонецЕсли;
	
	Если Не РезультатыЗапроса[3].Пустой() Тогда
		РезультатФункции.ЕстьЗамывкаВОбеспечении = Истина;
	КонецЕсли;
	
	Если Не РезультатыЗапроса[4].Пустой() Тогда
		РезультатФункции.ЕстьЗамывкаВПобочке = Истина;
	КонецЕсли;
	
	РезультатФункции.ЕстьЗамывка = РезультатФункции.ЕстьЗамывкаВОбеспечении Или РезультатФункции.ЕстьЗамывкаВПобочке;
	
	Возврат РезультатФункции;
	
КонецФункции // НоменклатураЗамывкиУжеЕсть()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

&После("ДобавитьКомандыПечати")
Процедура cbr_ДобавитьКомандыПечати(КомандыПечати)
	
	// НАЧАЛО Османов Н.М. 20.12.2023 ПФ Технологической карты
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТехнологическаяКарта";
	КомандаПечати.Представление = НСтр("ru = 'Технологическая карта'");
	// КОНЕЦ Османов Н.М. 20.12.2023 ПФ Технологической карты
	
КонецПроцедуры

&После("Печать")
Процедура cbr_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)

	// НАЧАЛО Османов Н.М. 20.12.2023 ПФ Технологической карты
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ТехнологическаяКарта") Тогда
		
		ТабличныйДокумент = СформироватьПечатнуюФормуТехКарты(МассивОбъектов, ОбъектыПечати, ПараметрыПечати);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
															"ТехнологическаяКарта",
															НСтр("ru = 'Технологическая карта'"),
															ТабличныйДокумент);
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 20.12.2023 ПФ Технологической карты
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуТехКарты(МассивОбъектов, ОбъектыПечати, ПараметрыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЭтапПроизводства2_2_ТехнологическаяКарта";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЭтапПроизводства2_2.ПФ_MXL_ТехнологическаяКарта");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьТранспортнаяЭтикетка = Макет.ПолучитьОбласть("ТранспортнаяЭтикетка");
	ОбластьКоличествоПродукции = Макет.ПолучитьОбласть("КоличествоПродукции");
	ОбластьТаблицаСырьеШапка = Макет.ПолучитьОбласть("ТаблицаСырьеШапка");
	ОбластьТаблицаСырьеЗаписи = Макет.ПолучитьОбласть("ТаблицаСырьеЗаписи");
	ОбластьИтогоИДоливыШапка = Макет.ПолучитьОбласть("ИтогоИДоливыШапка");
	ОбластьТаблицаДоливы = Макет.ПолучитьОбласть("ТаблицаДоливы");
	ОбластьИтогоИШапкаОпераций = Макет.ПолучитьОбласть("ИтогоИШапкаОпераций");
	ОбластьТаблицаДействийСырья = Макет.ПолучитьОбласть("ТаблицаДействийСырья");
	ОбластьТаблицаДействий = Макет.ПолучитьОбласть("ТаблицаДействий");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	ПервыйДокумент = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапПроизводства2_2.Ссылка КАК Ссылка,
		|	ЭтапПроизводства2_2.Дата КАК Дата,
		|	ЭтапПроизводства2_2.Номер КАК Номер,
		|	ЭтапПроизводства2_2.Распоряжение КАК Распоряжение,
		|	ЭтапПроизводства2_2.Ответственный.ФизическоеЛицо КАК ОтветственныйФизЛицо,
		|	ЭтапПроизводства2_2.Спецификация КАК Спецификация,
		|	ЭтапПроизводства2_2.Спецификация.НачалоДействия КАК ДатаВыдачиРецетуры,
		|	ЭтапПроизводства2_2.ВыходныеИзделия.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Характеристика КАК Характеристика,
		|		КоличествоУпаковок КАК КоличествоУпаковок,
		|		Количество КАК Количество,
		|		Упаковка КАК Упаковка,
		|		Спецификация КАК Спецификация
		|	) КАК ВыходныеИзделия,
		|	ЭтапПроизводства2_2.ВыходныеИзделияСерии.(
		|		НомерСтроки КАК НомерСтроки,
		|		Серия КАК Серия,
		|		Номенклатура КАК Номенклатура,
		|		Характеристика КАК Характеристика,
		|		Количество КАК Количество
		|	) КАК ВыходныеИзделияСерии,
		|	ЭтапПроизводства2_2.ВидыРабочихЦентров.(
		|		НомерСтроки КАК НомерСтроки,
		|		ВидРабочегоЦентра КАК ВидРабочегоЦентра
		|	) КАК ВидыРабочихЦентров,
		|	ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами.(
		|		НомерСтроки КАК НомерСтроки,
		|		Номенклатура КАК Номенклатура,
		|		Характеристика КАК Характеристика,
		|		КоличествоУпаковок КАК КоличествоУпаковок,
		|		Количество КАК Количество,
		|		Упаковка КАК Упаковка,
		|		Серия КАК Серия,
		|		СтатусУказанияСерий КАК СтатусУказанияСерий
		|	) КАК ОбеспечениеМатериаламиИРаботами
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		|ГДЕ
		|	ЭтапПроизводства2_2.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДокумент = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДокумент.Следующий() Цикл
	
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
        ОбластьШапка.Параметры.Заполнить(ВыборкаДокумент);
		Если ЗначениеЗаполнено(ВыборкаДокумент.ОтветственныйФизЛицо) Тогда
			ОбластьШапка.Параметры.Подготовил = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ВыборкаДокумент.ОтветственныйФизЛицо, ВыборкаДокумент.Дата).ФИОФизлица;
		КонецЕсли;
		
		ВыборкаВыходныеИздения = ВыборкаДокумент.ВыходныеИзделия.Выбрать();
		Пока ВыборкаВыходныеИздения.Следующий() Цикл
			
			Если ВыборкаВыходныеИздения.НомерСтроки = 1 Тогда
				
				ОбластьШапка.Параметры.НомерПартии = ВыборкаВыходныеИздения.РасчетноеКоличество;
				НомераПартий = Новый Массив;
				ВыборкаСерии = ВыборкаДокумент.ВыходныеИзделияСерии.Выбрать();
				Пока ВыборкаСерии.Следующий() Цикл
					Если ВыборкаВыходныеИздения.Номенклатура = ВыборкаСерии.Номенклатура
						И ВыборкаВыходныеИздения.Характеристика = ВыборкаСерии.Характеристика Тогда
						НомераПартий.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаВыходныеИздения.Серия, "Номер"));						
					КонецЕсли;
				КонецЦикла;
				
				Если НомераПартий.Количество() > 0 Тогда
					ОбластьШапка.Параметры.НомерПартии = СтрСоединить(НомераПартий, ", ");
				КонецЕсли;
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Индекс = 1;
		Оборудование2 = Новый Массив;
		ВыборкаВидовРабочихЦентров = ВыборкаДокумент.ВидыРабочихЦентров.Выбрать();
		Пока ВыборкаВидовРабочихЦентров.Следующий() Цикл
			
			Если Индекс = 1 Тогда				
				ОбластьШапка.Параметры.Оборудование1 = ВыборкаВидовРабочихЦентров.ВидРабочегоЦентра;
				Индекс = 2;
			КонецЕсли;
			
			Если Индекс = 2 Тогда
				Оборудование2.Добавить(Строка(ВыборкаВидовРабочихЦентров.ВидРабочегоЦентра));
			КонецЕсли;		
			
		КонецЦикла;
		
		Если Оборудование2.Количество() > 0 Тогда
			ОбластьШапка.Параметры.Оборудование2 = СтрСоединить(Оборудование2, ", ");
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьШапка);
		
		ТабличныйДокумент.Вывести(ОбластьТранспортнаяЭтикетка);
		
		ОбластьКоличествоПродукции.Параметры.Заполнить(ВыборкаДокумент);
		ТабличныйДокумент.Вывести(ОбластьКоличествоПродукции);
		
		ВыборкаОбеспечения = ВыборкаДокумент.ОбеспечениеМатериаламиИРаботами.Выбрать();
		Пока ВыборкаОбеспечения.Следующий() Цикл
			
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьТаблицаСырьеШапка);
		ТабличныйДокумент.Вывести(ОбластьТаблицаСырьеЗаписи);
		ТабличныйДокумент.Вывести(ОбластьИтогоИДоливыШапка);
		ТабличныйДокумент.Вывести(ОбластьТаблицаДоливы);
		ТабличныйДокумент.Вывести(ОбластьИтогоИШапкаОпераций);
		ТабличныйДокумент.Вывести(ОбластьТаблицаДействийСырья);
		ТабличныйДокумент.Вывести(ОбластьТаблицаДействий);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			ВыборкаДокумент.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область Очередь

Функция СтатусыЭтаповДляОчереди() Экспорт
	
	РезультатФункции = Новый СписокЗначений;

	РезультатФункции.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Формируется"));
	РезультатФункции.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Сформирован"));
	РезультатФункции.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.КВыполнению"));
	
	Возврат РезультатФункции;
	
КонецФункции

// Возвращает новый номер в очереди этапов производства.
//
// Возвращаемое значение:
//   - Число - новый номер в очереди.
//
Функция НовыйНомерОчереди() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	(Т.Очередь + 1) КАК НовыйНомерОчереди
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК Т
		|
		|УПОРЯДОЧИТЬ ПО
		|	Т.Очередь УБЫВ");
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		
		НовыйНомерОчереди = 1;
		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		НовыйНомерОчереди = Выборка.НовыйНомерОчереди;
		
	КонецЕсли;
	
	Возврат НовыйНомерОчереди;
	
КонецФункции

// Определяет можно ли переместить документ в очереди на одну позицию
//
// Параметры:
//  ТекущийЭтап - ДокументСсылка.ЭтапПроизводства2_2 - текущий этап
//  Направление	 - число - -1, перемещает заказ вверх по очереди
//                         +1, перемещает заказ вниз по очереди.
// 
// Возвращаемое значение:
//   - Булево - Истина, если документ можно переместить в очереди на одну позицию.
//
Функция МожноПереместитьДокументВОчереди(ТекущийЭтап, Направление) Экспорт
	
	Если Направление = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК ТекущийЭтапПроизводства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
		|		ПО ЭтапПроизводства.Очередь < ТекущийЭтапПроизводства.Очередь
		|ГДЕ
		|	ТекущийЭтапПроизводства.Ссылка = &ТекущийЭтап
		|	И ЭтапПроизводства.Статус В (&СтатусыДляОчереди)
		|	И ЭтапПроизводства.Проведен";
	
	Если Направление > 0 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<", ">");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТекущийЭтап", ТекущийЭтап);
	Запрос.УстановитьПараметр("СтатусыДляОчереди", СтатусыЭтаповДляОчереди());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// Перемещает документ в очереди заказов на одну позицию
//
// Параметры:
//  ТекущийЭтап - ДокументСсылка.ЭтапПроизводства2_2 - текущий этап
//  Направление	 - число - -1, перемещает заказ вверх по очереди
//                         +1, перемещает заказ вниз по очереди.
// 
Процедура ПереместитьДокументВОчереди(ТекущийЭтап, Направление) Экспорт
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(ТекущийЭтап);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Не удалось заблокировать %Документ%. %ОписаниеОшибки%';
							|en = 'Failed to lock %Документ%. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ШаблонОшибки, "%Документ%", ТекущийЭтап);
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ТекущийЭтап);
		Возврат;
	КонецПопытки;
		
	Запрос = Новый Запрос;
	
	Если Направление > 0 Тогда
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА Т.ОчередьСреднее = Т.ОчередьМаксимум
			|			ТОГДА Т.ОчередьМаксимум + 1
			|		ИНАЧЕ Т.ОчередьСреднее
			|	КОНЕЦ КАК Очередь
			|
			|ИЗ
			|	(ВЫБРАТЬ ПЕРВЫЕ 1
			|		СРЕДНЕЕ(Т.Очередь)  КАК ОчередьСреднее,
			|		МАКСИМУМ(Т.Очередь) КАК ОчередьМаксимум
			|	ИЗ
			|		(
			|			ВЫБРАТЬ ПЕРВЫЕ 2
			|				ЭтапПроизводства.Очередь КАК Очередь
			|			ИЗ
			|				Документ.ЭтапПроизводства2_2 КАК ТекущийЭтапПроизводства
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
			|					ПО ЭтапПроизводства.Очередь > ТекущийЭтапПроизводства.Очередь
			|			ГДЕ
			|				ТекущийЭтапПроизводства.Ссылка = &ТекущийЭтап
			|				И ЭтапПроизводства.Статус В (&СтатусыЭтаповДляОчереди)
			|				И ЭтапПроизводства.Проведен
			|				И НЕ ЭтапПроизводства.Очередь = 0
			|
			|			УПОРЯДОЧИТЬ ПО
			|				Очередь
			|		) КАК Т
			|
			|	) КАК Т
			|
			|ГДЕ
			|	НЕ Т.ОчередьСреднее ЕСТЬ NULL";
		
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА Т.ОчередьСреднее = Т.ОчередьМинимум
			|			ТОГДА Т.ОчередьМинимум / 2
			|		ИНАЧЕ Т.ОчередьСреднее
			|	КОНЕЦ КАК Очередь
			|
			|ИЗ
			|	(ВЫБРАТЬ ПЕРВЫЕ 1
			|		СРЕДНЕЕ(Т.Очередь)  КАК ОчередьСреднее,
			|		МИНИМУМ(Т.Очередь) КАК ОчередьМинимум
			|	ИЗ
			|		(
			|			ВЫБРАТЬ ПЕРВЫЕ 2
			|				ЭтапПроизводства.Очередь КАК Очередь
			|			ИЗ
			|				Документ.ЭтапПроизводства2_2 КАК ТекущийЭтапПроизводства
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
			|					ПО ЭтапПроизводства.Очередь < ТекущийЭтапПроизводства.Очередь
			|			ГДЕ
			|				ТекущийЭтапПроизводства.Ссылка = &ТекущийЭтап
			|				И ЭтапПроизводства.Статус В (&СтатусыЭтаповДляОчереди)
			|				И ЭтапПроизводства.Проведен
			|				И НЕ ЭтапПроизводства.Очередь = 0
			|
			|			УПОРЯДОЧИТЬ ПО
			|				Очередь УБЫВ
			|		) КАК Т
			|
			|	) КАК Т
			|
			|ГДЕ
			|	НЕ Т.ОчередьСреднее ЕСТЬ NULL";
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТекущийЭтап", ТекущийЭтап);
	Запрос.УстановитьПараметр("СтатусыЭтаповДляОчереди", СтатусыЭтаповДляОчереди());
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		
		НачатьТранзакцию();
		
		Попытка
			
			ЭтапОбъект = ТекущийЭтап.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(ЭтапОбъект, Выборка, "Очередь");
			ЭтапОбъект.ОбменДанными.Загрузка = Истина;
			ЭтапОбъект.Записать(РежимЗаписиДокумента.Запись);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать %1. %2';
										|en = 'Cannot save %1. %2'"),
				ТекущийЭтап,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ТекущийЭтап);
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет можно ли вставить документ в очередь
//
// Параметры:
//  ТекущийЭтап - ДокументСсылка.ЭтапПроизводства2_2 - текущий этап
//  НоваяПозиция - ДокументСсылка.ЭтапПроизводства2_2 - новая позиция в очереди.
// 
// Возвращаемое значение:
//   - Булево - Истина, если документ можно вставить в указанную позицию очереди.
//
Функция МожноВставитьДокументВОчередь(ТекущийЭтап, НоваяПозиция) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ИСТИНА 
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК НоваяПозиция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ТекущийЭтап
		|		ПО  ТекущийЭтап.Ссылка = &ТекущийЭтап
		|			И НоваяПозиция.Ссылка = &НоваяПозиция
		|			И НоваяПозиция.Статус В (&СтатусыЭтаповДляОчереди)
		|			И ТекущийЭтап.Статус В (&СтатусыЭтаповДляОчереди)
		|			И НоваяПозиция.Проведен
		|			И ТекущийЭтап.Проведен");
	
	Запрос.УстановитьПараметр("ТекущийЭтап", ТекущийЭтап);
	Запрос.УстановитьПараметр("НоваяПозиция", НоваяПозиция);
	Запрос.УстановитьПараметр("СтатусыЭтаповДляОчереди", СтатусыЭтаповДляОчереди());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// Вставляет документ в очередь заказов
//
// Параметры:
//  ТекущийЭтап - ДокументСсылка.ЭтапПроизводства2_2 - текущий этап
//  НоваяПозиция - ДокументСсылка.ЭтапПроизводства2_2 - новая позиция в очереди.
//
Процедура ВставитьДокументВОчередь(ТекущийЭтап, НоваяПозиция) Экспорт
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(ТекущийЭтап);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Не удалось заблокировать %1. %2';
							|en = 'Failed to lock %1. %2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ТекущийЭтап, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ТекущийЭтап);
		Возврат;
	КонецПопытки;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
			|	ВЫБОР
			|		КОГДА Т.ОчередьСреднее = Т.ОчередьМинимум
			|			ТОГДА Т.ОчередьМинимум / 2
			|		ИНАЧЕ Т.ОчередьСреднее
			|	КОНЕЦ       КАК Очередь
			|
			|ИЗ
			|	(ВЫБРАТЬ
			|		МИНИМУМ(Т.Очередь)  КАК ОчередьМинимум,
			|		СРЕДНЕЕ(Т.Очередь)  КАК ОчередьСреднее
			|	ИЗ
			|		(
			|			ВЫБРАТЬ ПЕРВЫЕ 2
			|				ЭтапПроизводства.Очередь   КАК Очередь
			|			ИЗ
			|				Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводстваНоваяПозиция
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
			|					ПО ЭтапПроизводства.Очередь <= ЭтапПроизводстваНоваяПозиция.Очередь
			|			ГДЕ
			|				ЭтапПроизводстваНоваяПозиция.Ссылка = &НоваяПозиция
			|				И ЭтапПроизводства.Статус В (&СтатусыЭтаповДляОчереди)
			|				И ЭтапПроизводства.Проведен
			|
			|			УПОРЯДОЧИТЬ ПО
			|				Очередь УБЫВ
			|		) КАК Т
			|
			|	) КАК Т
			|
			|ГДЕ
			|	НЕ Т.ОчередьСреднее ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("НоваяПозиция", НоваяПозиция);
	Запрос.УстановитьПараметр("СтатусыЭтаповДляОчереди", СтатусыЭтаповДляОчереди());
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		
		НачатьТранзакцию();
		Попытка
			
			ЭтапОбъект = ТекущийЭтап.ПолучитьОбъект();
			ЗаполнитьЗначенияСвойств(ЭтапОбъект, Выборка, "Очередь");
			ЭтапОбъект.ОбменДанными.Загрузка = Истина;
			ЭтапОбъект.Записать(РежимЗаписиДокумента.Запись);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ШаблонОшибки = НСтр("ru = 'Не удалось записать %1. %2';
								|en = 'Failed to save %1. %2'");
			
			ТекстОшибки = СтрШаблон(ШаблонОшибки, ТекущийЭтап, ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ТекущийЭтап);

		КонецПопытки
		
	КонецЕсли;
	
КонецПроцедуры

// Рассчитывает номер очереди для вставки перед документами
//
// Параметры:
//  Этапы - Массив - Этапы производства.
//
// Возвращаемое значение:
//   - Число - номер очереди для вставки перед документами.
//
Функция НомерОчередиДляВставкиПередДокументами(Этапы) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Граница.Очередь КАК Очередь
		|ПОМЕСТИТЬ ВТГраница
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК Граница
		|ГДЕ
		|	Граница.Ссылка В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				Т.Ссылка КАК Ссылка
		|			ИЗ
		|				Документ.ЭтапПроизводства2_2 КАК Т
		|			ГДЕ
		|				Т.Ссылка В (&Этапы)
		|				И Т.Статус В (&СтатусыЭтаповДляОчереди)
		|				И Т.Проведен
		|			УПОРЯДОЧИТЬ ПО
		|				Т.Очередь)
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.ОчередьСреднее = Т.ОчередьМинимум ТОГДА
		|		Т.ОчередьМинимум / 2
		|	ИНАЧЕ
		|		Т.ОчередьСреднее
		|	КОНЕЦ КАК НомерОчереди
		|
		|ИЗ
		|	(ВЫБРАТЬ
		|		МИНИМУМ(Т.Очередь)  КАК ОчередьМинимум,
		|		СРЕДНЕЕ(Т.Очередь)  КАК ОчередьСреднее,
		|		МАКСИМУМ(Т.Очередь) КАК ОчередьМаксимум
		|	ИЗ
		|		(ВЫБРАТЬ ПЕРВЫЕ 2
		|			ЭтапПроизводства.Очередь КАК Очередь
		|
		|		ИЗ
		|			Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
		|
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГраница КАК Граница
		|
		|				ПО ЭтапПроизводства.Очередь <= Граница.Очередь
		|					И ЭтапПроизводства.Проведен
		|	
		|		УПОРЯДОЧИТЬ ПО
		|			Очередь УБЫВ
		|		) КАК Т
		|	) КАК Т
		|
		|ГДЕ
		|	НЕ ВЫБОР КОГДА Т.ОчередьСреднее = Т.ОчередьМаксимум ТОГДА
		|		Т.ОчередьСреднее + 1
		|	ИНАЧЕ
		|		Т.ОчередьСреднее
		|	КОНЕЦ ЕСТЬ NULL");
	
	Запрос.УстановитьПараметр("Этапы", Этапы);
	Запрос.УстановитьПараметр("СтатусыЭтаповДляОчереди", СтатусыЭтаповДляОчереди());
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	НомерОчереди = 0;
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		НомерОчереди = Выборка.НомерОчереди;
		
	КонецЕсли;
	
	Возврат НомерОчереди;

КонецФункции

&Вместо("ПараметрыПроверкиРеквизитов")
Функция cbr_ПараметрыПроверкиРеквизитов(ИмяПоля, ИмяПоляПроверки)
	
	// НАЧАЛО Османов Н.М. 29.01.2025 
	Если ИмяПоля = "ВариантОбеспечения" И ИмяПоляПроверки = "НедоступнаОтгрузка" Тогда
		Возврат Новый Структура("ИмяПоля, ИмяПоляПроверки, ШаблонСообщения", "ГГШШММ1112230", "СкладНеЗаполнен", "");
	КонецЕсли;
	
	Результат = ПродолжитьВызов(ИмяПоля, ИмяПоляПроверки);
	Возврат Результат;
	// КОНЕЦ Османов Н.М. 29.01.2025 
	
КонецФункции

Процедура ЗаполнитьСерииВСтрокахТаблицы(Этап, ТаблицаОбеспечения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВТ_ПервичныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПервичныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПервичныеДанные.Серия КАК Серия,
	|	ВТ_ПервичныеДанные.ВариантОбеспечения КАК ВариантОбеспечения,
	|	ВТ_ПервичныеДанные.Склад КАК Склад,
	|	ВТ_ПервичныеДанные.Назначение КАК Назначение,
	|	ВТ_ПервичныеДанные.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ВТ_ПервичныеДанные.СтатьяКалькуляции КАК СтатьяКалькуляции
	|ПОМЕСТИТЬ ВТ_ТаблицаДанных
	|ИЗ
	|	&ОбеспечениеМатериалов КАК ВТ_ПервичныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаДанных.Характеристика КАК Характеристика,
	|	ВТ_ТаблицаДанных.Склад КАК Склад,
	|	ВТ_ТаблицаДанных.Назначение КАК Назначение,
	|	ВТ_ТаблицаДанных.Назначение.ДвиженияПоСкладскимРегистрам КАК ДвиженияПоСкладскимРегистрам
	|ПОМЕСТИТЬ ВТ_ТаблицаДляСерий
	|ИЗ
	|	ВТ_ТаблицаДанных КАК ВТ_ТаблицаДанных
	|ГДЕ
	|	ВТ_ТаблицаДанных.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И НЕ ВТ_ТаблицаДанных.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПолуфабрикатыПроизводимыеВПроцессе)
	|	И ВТ_ТаблицаДанных.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И ЕСТЬNULL(ВТ_ТаблицаДанных.Номенклатура.ВидНоменклатуры.ИспользоватьСерии, ЛОЖЬ)
	|	И ВТ_ТаблицаДанных.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И НЕ ВТ_ТаблицаДанных.СтатусУказанияСерий В (11, 15, 21, 23, 25, 27, 28)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаДанных.Склад,
	|	ВТ_ТаблицаДанных.Номенклатура,
	|	ВТ_ТаблицаДанных.Характеристика,
	|	ВТ_ТаблицаДанных.Назначение,
	|	ВТ_ТаблицаДанных.Назначение.ДвиженияПоСкладскимРегистрам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеРегистров.Склад КАК Склад,
	|	ДанныеРегистров.Номенклатура КАК Номенклатура,
	|	ДанныеРегистров.Характеристика КАК Характеристика,
	|	ДанныеРегистров.Серия КАК Серия,
	|	СУММА(ДанныеРегистров.СвободныйОстаток) КАК СвободныйОстаток
	|ПОМЕСТИТЬ ДанныеРегистров
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыНаСкладахОстатки.Склад КАК Склад,
	|		ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|		ТоварыНаСкладахОстатки.Характеристика КАК Характеристика,
	|		ТоварыНаСкладахОстатки.Серия КАК Серия,
	|		ТоварыНаСкладахОстатки.ВНаличииОстаток КАК СвободныйОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах.Остатки(
	|				,
	|				(Номенклатура, Характеристика, Склад) В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура,
	|							Т.Характеристика,
	|							Т.Склад
	|						ИЗ
	|							ВТ_ТаблицаДляСерий КАК Т)
	|					И Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК ТоварыНаСкладахОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО ТоварыНаСкладахОстатки.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И ТоварыНаСкладахОстатки.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И ТоварыНаСкладахОстатки.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (ТоварыНаСкладахОстатки.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ ТоварыНаСкладахОстатки.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтгрузкеОстатки.Склад,
	|		ТоварыКОтгрузкеОстатки.Номенклатура,
	|		ТоварыКОтгрузкеОстатки.Характеристика,
	|		ТоварыКОтгрузкеОстатки.Серия,
	|		-ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток - ТоварыКОтгрузкеОстатки.ВРезервеОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыКОтгрузке.Остатки(
	|				,
	|				(Номенклатура, Характеристика, Склад) В
	|						(ВЫБРАТЬ
	|							Т.Номенклатура,
	|							Т.Характеристика,
	|							Т.Склад
	|						ИЗ
	|							ВТ_ТаблицаДляСерий КАК Т)
	|					И Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК ТоварыКОтгрузкеОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО ТоварыКОтгрузкеОстатки.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И ТоварыКОтгрузкеОстатки.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И ТоварыКОтгрузкеОстатки.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (ТоварыКОтгрузкеОстатки.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ ТоварыКОтгрузкеОстатки.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНаСкладах.Склад,
	|		ТоварыНаСкладах.Номенклатура,
	|		ТоварыНаСкладах.Характеристика,
	|		ТоварыНаСкладах.Серия,
	|		ВЫБОР
	|			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТоварыНаСкладах.ВНаличии + ТоварыНаСкладах.КОтгрузке
	|			ИНАЧЕ ТоварыНаСкладах.ВНаличии - ТоварыНаСкладах.КОтгрузке
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО ТоварыНаСкладах.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И ТоварыНаСкладах.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И ТоварыНаСкладах.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (ТоварыНаСкладах.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ ТоварыНаСкладах.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	ГДЕ
	|		ТоварыНаСкладах.Регистратор = &Регистратор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыСерий.Склад,
	|		РезервыСерий.Номенклатура,
	|		РезервыСерий.Характеристика,
	|		РезервыСерий.Серия,
	|		ВЫБОР
	|			КОГДА РезервыСерий.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РезервыСерий.КОтгрузке + РезервыСерий.ВРезерве
	|			ИНАЧЕ -РезервыСерий.КОтгрузке - РезервыСерий.ВРезерве
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ТоварыКОтгрузке КАК РезервыСерий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаДляСерий КАК ВТ_ТаблицаДляСерий
	|			ПО РезервыСерий.Номенклатура = ВТ_ТаблицаДляСерий.Номенклатура
	|				И РезервыСерий.Характеристика = ВТ_ТаблицаДляСерий.Характеристика
	|				И РезервыСерий.Склад = ВТ_ТаблицаДляСерий.Склад
	|				И (РезервыСерий.Назначение = ВТ_ТаблицаДляСерий.Назначение
	|					ИЛИ РезервыСерий.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|						И НЕ ВТ_ТаблицаДляСерий.ДвиженияПоСкладскимРегистрам)
	|	ГДЕ
	|		РезервыСерий.Регистратор = &Регистратор
	|		И РезервыСерий.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК ДанныеРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистров.Серия,
	|	ДанныеРегистров.Склад,
	|	ДанныеРегистров.Номенклатура,
	|	ДанныеРегистров.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистров.Склад КАК Склад,
	|	ДанныеРегистров.Номенклатура КАК Номенклатура,
	|	ДанныеРегистров.Характеристика КАК Характеристика,
	|	ДанныеРегистров.Серия КАК Серия,
	|	СУММА(ДанныеРегистров.СвободныйОстаток) КАК Доступно,
	|	ДанныеРегистров.Серия.Номер КАК Номер,
	|	ДанныеРегистров.Серия.ГоденДо КАК ГоденДо
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров
	|ГДЕ
	|	НЕ ДанныеРегистров.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	И &УсловиеНаГодностьСерии
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистров.Склад,
	|	ДанныеРегистров.Номенклатура,
	|	ДанныеРегистров.Характеристика,
	|	ДанныеРегистров.Серия,
	|	ДанныеРегистров.Серия.Номер,
	|	ДанныеРегистров.Серия.ГоденДо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо,
	|	Номер";
	
	Запрос.УстановитьПараметр("ОбеспечениеМатериалов", ТаблицаОбеспечения);
	Запрос.УстановитьПараметр("Регистратор", Этап);
	
	ТипыСерий = Новый Массив;
	ТипыСерий.Добавить(Перечисления.лимсСтатусСерии.Годен);
	ТипыСерий.Добавить(Перечисления.лимсСтатусСерии.УсловноГоден);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаГодностьСерии", 
											 "ДанныеРегистров.Серия.лимсСтатусГодности.Тип В (&ТипыСерий)");
	
	Запрос.УстановитьПараметр("ТипыСерий", ТипыСерий);
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	СоответствиеСерий = Новый Соответствие;
	СтруктураДействий = Новый Структура("ПересчитатьКоличествоУпаковок", Истина);
	КэшированныеЗначения = Неопределено;
	
	ВсегоСтрок = ТаблицаОбеспечения.Количество();
	Для Индекс = 1 По ВсегоСтрок Цикл
		
		СтрокаОбеспечения = ТаблицаОбеспечения[Индекс - 1];
		ВсегоКоличество = СтрокаОбеспечения.Количество;
		
		ОтборСтрок = Новый Структура("Склад, Номенклатура, Характеристика");
		ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаОбеспечения);
		НайденныеСерии = Выгрузка.НайтиСтроки(ОтборСтрок);
		
		ЕстьНоваяСтрока = Ложь;
		
		Для Каждого СтрокаСерии Из НайденныеСерии Цикл
			
			Если ВсегоКоличество <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			ОсталосьПоСерии = СоответствиеСерий.Получить(СтрокаСерии);
			Если Не ОсталосьПоСерии = Неопределено И ОсталосьПоСерии = 0 Тогда
				Продолжить;
			Иначе
				ОсталосьПоСерии = СтрокаСерии.Доступно;
			КонецЕсли;
			
			Если СтрокаСерии.Доступно <=0 Тогда
				Продолжить;
			КонецЕсли;
						
			КоличествоДляУстановки = Мин(ВсегоКоличество, ОсталосьПоСерии);
			СтрокаОбеспечения.Серия = СтрокаСерии.Серия;
			СтрокаОбеспечения.Количество = КоличествоДляУстановки;
			
			ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоУпаковокВСтрокеТЧ(СтрокаОбеспечения, СтруктураДействий, КэшированныеЗначения);
			
			СоответствиеСерий.Вставить(СтрокаСерии.Серия, ОсталосьПоСерии - КоличествоДляУстановки);
			
			Если ВсегоКоличество > ОсталосьПоСерии Тогда
				НоваяСтрокаОбеспечения = ТаблицаОбеспечения.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаОбеспечения, СтрокаОбеспечения, , "Серия, Количество, КодСтроки, КоличествоУпаковок, ИдентификаторСтроки, КлючНоменклатура");
				НоваяСтрокаОбеспечения.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
				СтрокаОбеспечения = НоваяСтрокаОбеспечения;
				СтрокаОбеспечения.Количество = ВсегоКоличество - ОсталосьПоСерии;
				ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПересчитатьКоличествоУпаковокВСтрокеТЧ(СтрокаОбеспечения, СтруктураДействий, КэшированныеЗначения);
				
				ЕстьНоваяСтрока = Истина;
			КонецЕсли;
			
			ВсегоКоличество = ВсегоКоличество - КоличествоДляУстановки;
			
		КонецЦикла;
		
	КонецЦикла;
				
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли