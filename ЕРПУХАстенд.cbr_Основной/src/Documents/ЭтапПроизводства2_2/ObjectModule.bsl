#Область ПрограммныйИнтерфейс

Процедура УстановитьОтгрузкуИЗаполнитьСерииВОбеспечении() Экспорт
	
	Для Каждого СтрокаОбеспечения Из ОбеспечениеМатериаламиИРаботами Цикл
		СтрокаОбеспечения.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.Отгрузить;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&После("ОбработкаЗаполнения")
Процедура cbr_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 22.11.2023 ЧТЗ №Действия в производстве
	Если ЗначениеЗаполнено(ЭтотОбъект.Спецификация) Тогда
		ЗаполнитьДействияПоСпецификации(ЭтотОбъект)
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 22.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

&После("ПриЗаписи")
Процедура cbr_ПриЗаписи(Отказ)
	
	// НАЧАЛО Османов Н.М. 11.12.2023
	Если Отказ Тогда
		Возврат;		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Распоряжение) Тогда
		Если ПолучитьФункциональнуюОпцию("ДинамическаяСтруктураЗаказовНаПроизводство") Тогда
			РегистрыСведений.НормативныйГрафикЭтаповПроизводства.Рассчитать(Распоряжение);
		КонецЕсли;
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 11.12.2023
	
КонецПроцедуры

&После("ПередЗаписью")
Процедура cbr_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// НАЧАЛО Османов Н.М. 25.12.2023 Действия в производстве
	Если Не ОбменДанными.Загрузка Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Обеспечение.Номенклатура КАК Номенклатура,
		|	Обеспечение.Характеристика КАК Характеристика,
		|	Обеспечение.Упаковка КАК Упаковка,
		|	Обеспечение.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ОбеспечениеПредварительно
		|ИЗ
		|	&Обеспечение КАК Обеспечение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Действия.Номенклатура КАК Номенклатура,
		|	Действия.Характеристика КАК Характеристика,
		|	Действия.Упаковка КАК Упаковка,
		|	Действия.ВнесеноПлан КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_ДействияПредварительно
		|ИЗ
		|	&Действия КАК Действия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ОбеспечениеПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ОбеспечениеПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Обеспечение
		|ИЗ
		|	ВТ_ОбеспечениеПредварительно КАК ВТ_ОбеспечениеПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ОбеспечениеПредварительно.Номенклатура,
		|	ВТ_ОбеспечениеПредварительно.Характеристика,
		|	ВТ_ОбеспечениеПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДействияПредварительно.Номенклатура КАК Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика КАК Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка КАК Упаковка,
		|	СУММА(ВТ_ДействияПредварительно.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Действия
		|ИЗ
		|	ВТ_ДействияПредварительно КАК ВТ_ДействияПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_ДействияПредварительно.Номенклатура,
		|	ВТ_ДействияПредварительно.Характеристика,
		|	ВТ_ДействияПредварительно.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Действия.Номенклатура КАК Номенклатура,
		|	ВТ_Действия.Характеристика КАК Характеристика,
		|	ВТ_Действия.Упаковка КАК Упаковка,
		|	ВТ_Действия.КоличествоУпаковок КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ВТ_Разница
		|ИЗ
		|	ВТ_Действия КАК ВТ_Действия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТ_Обеспечение.Номенклатура,
		|	ВТ_Обеспечение.Характеристика,
		|	ВТ_Обеспечение.Упаковка,
		|	-ВТ_Обеспечение.КоличествоУпаковок
		|ИЗ
		|	ВТ_Обеспечение КАК ВТ_Обеспечение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Разница.Номенклатура КАК Номенклатура,
		|	ВТ_Разница.Характеристика КАК Характеристика,
		|	ВТ_Разница.Упаковка КАК Упаковка,
		|	СУММА(ВТ_Разница.КоличествоУпаковок) КАК КоличествоУпаковок
		|ИЗ
		|	ВТ_Разница КАК ВТ_Разница
		|ГДЕ
		|	НЕ ВТ_Разница.Номенклатура ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Разница.Номенклатура,
		|	ВТ_Разница.Характеристика,
		|	ВТ_Разница.Упаковка
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВТ_Разница.КоличествоУпаковок) <> 0";
		
		Запрос.УстановитьПараметр("Обеспечение", ОбеспечениеМатериаламиИРаботами.Выгрузить());
		Запрос.УстановитьПараметр("Действия", Действия.Выгрузить());
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КоличествоУпаковок > 0 Тогда
				
				ТекстСообщения = НСтр("ru='ВНИМАНИЕ! По номенклатуре: %1 и характеристике: %2 Внесено (План) на вкладке Действия превышает количество из вкладки Материалы и работы'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Номенклатура, Выборка.Характеристика);
				//ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
				
			Иначе
				
				ТекстСообщения = НСтр("ru='Внимание! По номенклатуре: %1 и характеристике: %2 недостаточно количества Внесено (План) (%3) на вкладке Действия'");
				ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Номенклатура, Выборка.Характеристика, Выборка.КоличествоУпаковок);
				//ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 25.12.2023 Действия в производстве
	
	// НАЧАЛО Османов Н.М. 27.09.2024
	Если Не Отказ Тогда
		
		СтатусыДляОчереди = Документы.ЭтапПроизводства2_2.СтатусыЭтаповДляОчереди();
		Если СтатусыДляОчереди.НайтиПоЗначению(Статус) = Неопределено Тогда
			
			Очередь = 0;
			
		Иначе
			
			Если Очередь = 0 Тогда
				Очередь = Документы.ЭтапПроизводства2_2.НовыйНомерОчереди();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 27.09.2024
	
	// НАЧАЛО Османов Н.М. 15.01.2025
	ПредыдущийСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
	
	Если Не Отказ 
		И Не ЭтоНовый()
		И Не РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		И Не ПредыдущийСтатус = Статус Тогда
		
		Если Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.КВыполнению")
			Или (Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Начат")
					И ПредыдущийСтатус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Сформирован"))
			Тогда
		
			ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(
										   НоменклатураСервер.ПараметрыУказанияСерий(
										   					  ЭтотОбъект, Документы.ЭтапПроизводства2_2));
			ИмяТЧСерии = ПараметрыУказанияСерий.ВыходныеИзделия.ИмяТЧСерии;
			
			ЕстьИзменения = Ложь;
			
			СтрокаССерией = Неопределено;
			Если ЭтотОбъект[ИмяТЧСерии].Количество() = 0 Тогда
			
				Если ИмяТЧСерии = "ВыходныеИзделияСерии"
					И ВыходныеИзделия.Количество() > 0
					Тогда
					
					СозданнаяСерия = Неопределено;
					Для Каждого СтрокаВыходногоИзделия Из ВыходныеИзделия Цикл
					
						ДанныеПоНоменклатуре = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаВыходногоИзделия.Номенклатура, 
																	  "ВидНоменклатуры,
																	  |ВидНоменклатуры.ИспользоватьСерии,
																	  |СрокГодности,
																	  |ЕдиницаИзмеренияСрокаГодности");
						ДанныеПоНоменклатуре.Вставить("Номенклатура", СтрокаВыходногоИзделия.Номенклатура);
						ДанныеПоНоменклатуре.Вставить("Характеристика", СтрокаВыходногоИзделия.Характеристика);
					
						Если ДанныеПоНоменклатуре.ВидНоменклатурыИспользоватьСерии = Истина Тогда						
							СтрокаССерией = ВыходныеИзделияСерии.Добавить();
							ЗаполнитьЗначенияСвойств(СтрокаССерией, СтрокаВыходногоИзделия);
							Если СозданнаяСерия = Неопределено Тогда
								СозданнаяСерия = СоздатьСерию(ДанныеПоНоменклатуре);
							КонецЕсли;
							СтрокаССерией.Серия = СозданнаяСерия;
							ЕстьИзменения = Истина;
						КонецЕсли;
					
					КонецЦикла; 
					
				ИначеЕсли Не ИмяТЧСерии = "ВыходныеИзделияСерии" 
					И ВыходныеИзделия.Количество() > 0 
					Тогда
					
					СозданнаяСерия = Неопределено;
					Для Каждого СтрокаССерией Из ЭтотОбъект[ИмяТЧСерии] Цикл
						
						ДанныеПоНоменклатуре = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаССерией.Номенклатура, 
																	  "ВидНоменклатуры,
																	  |ВидНоменклатуры.ИспользоватьСерии,
																	  |СрокГодности,
																	  |ЕдиницаИзмеренияСрокаГодности");
						ДанныеПоНоменклатуре.Вставить("Номенклатура", СтрокаССерией.Номенклатура);
						ДанныеПоНоменклатуре.Вставить("Характеристика", СтрокаССерией.Характеристика);
						
						Если Не ЗначениеЗаполнено(СтрокаССерией.Серия) 
							И ДанныеПоНоменклатуре.ВидНоменклатурыИспользоватьСерии = Истина
							Тогда
							
							Если СозданнаяСерия = Неопределено Тогда
								СозданнаяСерия = СоздатьСерию(ДанныеПоНоменклатуре);
							КонецЕсли;
							СтрокаССерией.Серия = СозданнаяСерия;
							ЕстьИзменения = Истина;
						Иначе
							Если СозданнаяСерия = Неопределено Тогда
								СозданнаяСерия = СтрокаССерией.Серия;
							КонецЕсли;	
						КонецЕсли;
						
					КонецЦикла;
					
				КонецЕсли;
			
			Иначе
				
				СозданнаяСерия = Неопределено;
				Для Каждого СтрокаССерией Из ЭтотОбъект[ИмяТЧСерии] Цикл
				
					ДанныеПоНоменклатуре = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаССерией.Номенклатура, 
																	  "ВидНоменклатуры,
																	  |ВидНоменклатуры.ИспользоватьСерии,
																	  |СрокГодности,
																	  |ЕдиницаИзмеренияСрокаГодности");
					ДанныеПоНоменклатуре.Вставить("Номенклатура", СтрокаССерией.Номенклатура);
					ДанныеПоНоменклатуре.Вставить("Характеристика", СтрокаССерией.Характеристика);
					Если Не ЗначениеЗаполнено(СтрокаССерией.Серия) 
						И ДанныеПоНоменклатуре.ВидНоменклатурыИспользоватьСерии = Истина
						Тогда
						Если СозданнаяСерия = Неопределено Тогда
							СозданнаяСерия = СоздатьСерию(ДанныеПоНоменклатуре);
						КонецЕсли;
						СтрокаССерией.Серия = СозданнаяСерия;	
						ЕстьИзменения = Истина;
					Иначе
						Если СозданнаяСерия = Неопределено Тогда
							СозданнаяСерия = СтрокаССерией.Серия;
						КонецЕсли;
					КонецЕсли;
				
				КонецЦикла;
			
			КонецЕсли;
			
			Если ЕстьИзменения Тогда
				ПараметрыУказанияСерий = Документы.ЭтапПроизводства2_2.ПараметрыУказанияСерий(ЭтотОбъект);
				НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий.ВыходныеИзделия);
			КонецЕсли;
			
	// НАЧАЛО Османов Н.М. 21.02.2025 
		ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Начат") Тогда
			
			ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(
										   НоменклатураСервер.ПараметрыУказанияСерий(
										   					  ЭтотОбъект, Документы.ЭтапПроизводства2_2));
			ИмяТЧСерии = ПараметрыУказанияСерий.ВыходныеИзделия.ИмяТЧСерии;
			Для Каждого СтрокаССерией Из ЭтотОбъект[ИмяТЧСерии] Цикл
				
				Если Не ЗначениеЗаполнено(СтрокаССерией.Серия) Тогда
					Продолжить;					
				КонецЕсли;
				
				ДанныеПоНоменклатуре = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаССерией.Номенклатура, 
																	  "ВидНоменклатуры,
																	  |СрокГодности,
																	  |ЕдиницаИзмеренияСрокаГодности");
				
				ОбъектСерия = СтрокаССерией.Серия.ПолучитьОбъект();
				ОбъектСерия.ДатаПроизводства = ФактическоеНачалоЭтапа;
				ОбъектСерия.ГоденДо = ОбщегоНазначенияУТКлиентСервер.ДобавитьКДатеВремя(ОбъектСерия.ДатаПроизводства,
																	 ДанныеПоНоменклатуре.СрокГодности,
																	 ДанныеПоНоменклатуре.ЕдиницаИзмеренияСрокаГодности);
																	 
				ОбъектСерия.Записать();
	
				
			КонецЦикла;
	// КОНЕЦ Османов Н.М. 21.02.2025 
			
		ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Завершен") Тогда
			
	//		ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(
	//									   НоменклатураСервер.ПараметрыУказанияСерий(
	//									   					  ЭтотОбъект, Документы.ЭтапПроизводства2_2));
	//		ИмяТЧСерии = ПараметрыУказанияСерий.ВыходныеИзделия.ИмяТЧСерии;
	//		Для Каждого СтрокаССерией Из ЭтотОбъект[ИмяТЧСерии] Цикл
	//			
	//			Если Не ЗначениеЗаполнено(СтрокаССерией.Серия) Тогда
	//				Продолжить;					
	//			КонецЕсли;
	//			
	//			ДанныеПоНоменклатуре = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаССерией.Номенклатура, 
	//																  "ВидНоменклатуры,
	//																  |СрокГодности,
	//																  |ЕдиницаИзмеренияСрокаГодности");
	//			
	//			ОбъектСерия = СтрокаССерией.Серия.ПолучитьОбъект();
	//			ОбъектСерия.ДатаПроизводства = ТекущаяДатаСеанса();
	//			ОбъектСерия.ГоденДо = ОбщегоНазначенияУТКлиентСервер.ДобавитьКДатеВремя(ОбъектСерия.ДатаПроизводства,
	//																 ДанныеПоНоменклатуре.СрокГодности,
	//																 ДанныеПоНоменклатуре.ЕдиницаИзмеренияСрокаГодности);
	//																 
	//			ОбъектСерия.Записать();
	//
	//			
	//		КонецЦикла;
				
		КонецЕсли;
	
	КонецЕсли;
	// КОНЕЦ Османов Н.М. 15.01.2025
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаполнитьДействияПоСпецификации(ДокументОбъект)
	
	// НАЧАЛО Османов Н.М. 22.11.2023 ЧТЗ №Действия в производстве
	//КоличествоУпаковокПлан - из этапа
	//ВыходныеИзделия.КоличествоУпаковок - первая строка из ресурсной спецификации
	Если Спецификация.ВыходныеИзделия.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Если Спецификация.ВыходныеИзделия[0].КоличествоУпаковок = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаМатериалов = ОбеспечениеМатериаламиИРаботами.Выгрузить( , "Номенклатура,Характеристика,Упаковка,КоличествоУпаковок");
	ТаблицаМатериалов.Свернуть("Номенклатура,Характеристика,Упаковка", "КоличествоУпаковок");
	
	ТаблицаДобавленныхДействий = ТаблицаМатериалов.Скопировать();
	ТаблицаДобавленныхДействий.Колонки.Добавить("КоличествоВДействиях", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Неотрицательный));
	
	КоэффициентПересчета = КоличествоУпаковокПлан / Спецификация.ВыходныеИзделия[0].КоличествоУпаковок;
	
	ОтборПоЭтапу = Новый Структура("Этап", Этап);
	ТаблицаДействий = Спецификация.Действия.Выгрузить(ОтборПоЭтапу);
	Для Каждого ДействиеСпецификации Из ТаблицаДействий Цикл
		
		НоваяСтрокаДействий = Действия.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаДействий, ДействиеСпецификации);
		НоваяСтрокаДействий.ВнесеноПлан = ДействиеСпецификации.КоличествоУпаковок * КоэффициентПересчета;

		ОтборПоДобавленнымДействиям = Новый Структура("Номенклатура,Характеристика,Упаковка");
		ЗаполнитьЗначенияСвойств(ОтборПоДобавленнымДействиям, ДействиеСпецификации);
		НайденнаяСтрокаДобавленныхДействий = ТаблицаДобавленныхДействий.НайтиСтроки(ОтборПоДобавленнымДействиям);
		Если НайденнаяСтрокаДобавленныхДействий.Количество() > 0 Тогда
			
			СтрокаДействия = НайденнаяСтрокаДобавленныхДействий[0];
			Если СтрокаДействия.КоличествоВДействиях + НоваяСтрокаДействий.ВнесеноПлан > СтрокаДействия.КоличествоУпаковок Тогда
				НоваяСтрокаДействий.ВнесеноПлан = СтрокаДействия.КоличествоУпаковок - СтрокаДействия.КоличествоВДействиях; 
			КонецЕсли;
			
			СтрокаДействия.КоличествоВДействиях = СтрокаДействия.КоличествоВДействиях + НоваяСтрокаДействий.ВнесеноПлан;
			
		КонецЕсли;			
		
		НоваяСтрокаДействий.Продолжительность = ДействиеСпецификации.Время;
		НоваяСтрокаДействий.ТемператураПлан = ДействиеСпецификации.Температура;
		НоваяСтрокаДействий.ДавлениеПлан = ДействиеСпецификации.Давление;
		НоваяСтрокаДействий.СкоростьВращенияПлан = ДействиеСпецификации.СкоростьВращения;
		
	КонецЦикла;
	
	Для Каждого СтрокаДобавленныхДействий Из ТаблицаДобавленныхДействий Цикл
		Если СтрокаДобавленныхДействий.КоличествоУпаковок > СтрокаДобавленныхДействий.КоличествоВДействиях Тогда 
			ОтборПоДействиям = Новый Структура("Номенклатура,Характеристика,Упаковка");
			ЗаполнитьЗначенияСвойств(ОтборПоДействиям, СтрокаДобавленныхДействий);
			НайденныеСтрокиДействий = Действия.НайтиСтроки(ОтборПоДействиям);
			Если НайденныеСтрокиДействий.Количество() > 0 Тогда
				СтрокаДействия = НайденныеСтрокиДействий[НайденныеСтрокиДействий.Количество() - 1];
				СтрокаДействия.ВнесеноПлан = СтрокаДействия.ВнесеноПлан + 
											СтрокаДобавленныхДействий.КоличествоУпаковок -
											СтрокаДобавленныхДействий.КоличествоВДействиях; 
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	// КОНЕЦ Османов Н.М. 22.11.2023 ЧТЗ №Действия в производстве
	
КонецПроцедуры

Функция СоздатьСерию(ДанныеПоНоменклатуре)
	
	НоваяСерия = Справочники.СерииНоменклатуры.СоздатьЭлемент();
	НоваяСерия.Номер = РегистрыСведений.НомераСерийПоВидам.СледущийНомерСерии(ДанныеПоНоменклатуре.ВидНоменклатуры);
	НоваяСерия.ВидНоменклатуры = ДанныеПоНоменклатуре.ВидНоменклатуры;
	НоваяСерия.ДатаПроизводства = ТекущаяДатаСеанса();
	НоваяСерия.ГоденДо = ОбщегоНазначенияУТКлиентСервер.ДобавитьКДатеВремя(НоваяСерия.ДатаПроизводства,
																		   ДанныеПоНоменклатуре.СрокГодности,
																		   ДанныеПоНоменклатуре.ЕдиницаИзмеренияСрокаГодности);
																		   
	НоваяСерия.ЗаполнитьСтатусГодностиВСерии(ДанныеПоНоменклатуре);
	
	НоваяСерия.Записать();
	
	Возврат НоваяСерия.Ссылка;
	
КонецФункции

// +-Алейников М.Н. 04.04.2025
&После("ОбработкаПроведения")
Процедура cbr_ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли;
	
	// НАЧАЛО Османов Н.М. 05.09.2025 
	НовыеДокументыРасходныхОрдеров = Документы.РасходныйОрдерНаТовары.СоздатьРасходныеОрдераИзЭтапа(ЭтотОбъект);
	Для Каждого ДокументРасходныйОрдер Из НовыеДокументыРасходныхОрдеров Цикл
		Документы.ОтборРазмещениеТоваров.СоздатьОтборРазмещениеИзРасходногоОрдера(ДокументРасходныйОрдер);
	КонецЦикла;
	
	// КОНЕЦ Османов Н.М. 05.09.2025 
	
	Если Не Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен Тогда 
		Возврат;
	КонецЕсли;	
	
	Если тпсКонтролиЭтапов.ЕстьЗаявкаНаКонтрольПоЭтапуПроизводства(Ссылка) Тогда
		Возврат;
	КонецЕсли;	
	
	ВидНоменклатуры = Неопределено;
	
	Если Не ВыходныеИзделия.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = ВыходныеИзделия[0].Номенклатура;
	ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры");
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		Возврат;			
	КонецЕсли;
	
	ЭтоПолуфабрикат = УправлениеСвойствами.ЗначениеСвойства(ВидНоменклатуры, "ЭтоПолуфабрикатВидНоменклатуры");
	
	Если Не (ЭтоПолуфабрикат = Ложь Или ЭтоПолуфабрикат = Неопределено) Тогда
		Возврат;	
	КонецЕсли; 
	
	ЗаявкаНаКонтрольОбъект = Документы.лимсЗаявкаНаКонтроль.СоздатьДокумент();
	ЗаявкаНаКонтрольОбъект.Заполнить(Ссылка);
	ЗаявкаНаКонтрольОбъект.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры

#КонецОбласти