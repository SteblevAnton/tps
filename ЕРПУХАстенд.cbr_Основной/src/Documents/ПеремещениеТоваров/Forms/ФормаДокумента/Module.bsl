#Область СлужебныеПроцедурыИФункции
&После("СкладОтправительПриИзменении")
&НаКлиенте
Процедура cbr_СкладОтправительПриИзменении(Элемент)
	
	//++АшаеваАН 13.02.26 СКЛ.3
	Если ЗначениеЗаполнено(Объект.СкладОтправитель) 
		 И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, "cbr_СкладРаспределения") Тогда
			ЭтаФорма.cbr_СкладРаспределения = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.СкладОтправитель, "cbr_СкладРаспределения");
	КонецЕсли;
	//--АшаеваАН 13.02.26 СКЛ.3
	
КонецПроцедуры

&ИзменениеИКонтроль("ОтвязатьОтЗаказаСервер")
&НаСервере
Процедура cbr_ОтвязатьОтЗаказаСервер(КэшированныеЗначения, ВсеСтроки)

	Если ВсеСтроки Тогда
		КоллекцияСтрок = Объект.Товары;
	Иначе
		КоллекцияСтрок = Элементы.Товары.ВыделенныеСтроки;
	КонецЕсли;

	Для Каждого ИдентификаторСтроки Из КоллекцияСтрок Цикл

		Если ВсеСтроки Тогда
			СтрокаТаблицы = ИдентификаторСтроки;
			ИдентификаторСтроки = СтрокаТаблицы.ПолучитьИдентификатор();
		Иначе
			СтрокаТаблицы = Объект.Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
		КонецЕсли;

		НоменклатураКлиентСервер.ОбновитьКешированныеЗначенияДляУчетаСерий(СтрокаТаблицы, КэшированныеЗначения, ПараметрыУказанияСерий);

		СтрокаТаблицы.КодСтроки = 0;
		СтрокаТаблицы.РасхождениеЗаказ = 1;
		СтрокаТаблицы.Сделка = Справочники.СделкиСКлиентами.ПустаяСсылка();
		СтрокаТаблицы.НазначениеОтправителя = СтрокаТаблицы.Назначение;
		СтрокаТаблицы.ЗаказНаПеремещение = ЗаказВСтрокеСверхЗаказа(Объект.ПеремещениеПоЗаказам, Объект.ЗаказНаПеремещение);
		#Вставка
		//++АшаеваАН 13.02.26 СКЛ.3
		СтрокаТаблицы.cbr_ЗаказПоставщику = Неопределено;
		СтрокаТаблицы.cbr_ПТУ = Неопределено;
		//--АшаеваАН 13.02.26 СКЛ.3
		#КонецВставки

		// Переподчиним строки серий
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Объект,ПараметрыУказанияСерий,ИдентификаторСтроки, КэшированныеЗначения);

	КонецЦикла;

	ОбновитьЗависимыеРеквизитыФормыСерверПоЗаказу();

КонецПроцедуры
#КонецОбласти
