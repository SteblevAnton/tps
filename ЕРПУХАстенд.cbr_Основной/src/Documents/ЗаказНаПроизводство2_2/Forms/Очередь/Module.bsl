
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура П3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	ПодготовитьФорму();
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФорму()
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	Если ФормыУХ.ЭлементыФормыУХУжеСозданы(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
												
	ЭлементНомерЗаказа = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокКоличество",
												"Количество",
												"Список.Количество",
												,
												Элементы.Список,
												Элементы.Ответственный);
												
	ЭлементНомерЗаказа = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокУпаковка",
												"Упаковка",
												"Список.Упаковка",
												,
												Элементы.Список,
												Элементы.Ответственный);

	ЭлементНомерЗаказа = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокНомерЗаказа",
												"Номер Заказа",
												"Список.НомерЗаказа",
												,
												Элементы.Список,
												Элементы.Ответственный);
												
	ЭлементГосЗаказ = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокГосЗаказ",
												"Госзаказ",
												"Список.ГосЗаказ",
												ВидПоляФормы.ПолеФлажка,
												Элементы.Список,
												Элементы.Ответственный);
												
	ЭлементСпециальныеТребования = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокСпециальныеТребования",
												"Спец.требование",
												"Список.СпециальныеТребования",
												,
												Элементы.Список,
												Элементы.Ответственный);
												
	ЭлементДатаЗавершения = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокДатаЗавершения",
												"Дата РС",
												"Список.ДатаЗавершения",
												,
												Элементы.Список,
												Элементы.Ответственный);
												
	ЭлементКонтрагент = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокКонтрагент",
												"Контрагент",
												"Список.Контрагент",
												,
												Элементы.Список,
												Элементы.Ответственный);
												
	ЭлементНоменклатура = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокНоменклатура",
												"Номенклатура",
												"Список.Номенклатура",
												,
												Элементы.Список,
												Элементы.Ответственный);
												
	ПараметрыЭлемента = Новый Структура("АвтоОтметкаНезаполненного", Ложь);
	ЭлементХарактеристика = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СписокХарактеристика",
												"Характеристика",
												"Список.Характеристика",
												,
												Элементы.Список,
												Элементы.Ответственный, 
												ПараметрыЭлемента);												
												
	ГруппаОтбораПоСтатусам = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаОтбораПоСтатусам",
												"Или отобрать по статусам",
												ВидГруппыФормы.ОбычнаяГруппа,
												Элементы.ГруппаЗаказыДопПанель,
												Элементы.Декорация7);
												

	СписокВыбораСтатусов = Новый СписокЗначений;
	
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется, "Формируется");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.АктуализацияСпецификаций, "Актуализация спецификаций");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.СогласованиеСпецтребований, "Согласование спецтребований");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Разработка, "Разработка");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.КПроизводству, "К производству");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.ВРаботе, "В работе");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.ПередачаНаСклад, "Передача на склад");
	СписокВыбораСтатусов.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Закрыт, "Закрыт");

	ДобавляемыеРеквизиты = Новый Массив;
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОтборСпискаВыбораСтатусов", Новый ОписаниеТипов("СписокЗначений")));
	
	Для Каждого СписокЗначение Из СписокВыбораСтатусов Цикл
		СтрокаИмя = СтрЗаменить(СписокЗначение.Значение, " ", "");
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ОтборПоСтатусам" + СтрокаИмя, Новый ОписаниеТипов("Булево")));
	КонецЦикла;
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ЭтотОбъект["ОтборСпискаВыбораСтатусов"] = СписокВыбораСтатусов;
	
	Для Каждого СписокЗначение Из СписокВыбораСтатусов Цикл
		
		СтрокаИмя = СтрЗаменить(СписокЗначение.Значение, " ", "");
		
		ПараметрыГруппы = Новый Структура("ОтображатьЗаголовок, Отображение, Группровка", Ложь, ОтображениеОбычнойГруппы.Нет, ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда);
		ПодГруппаОтбораПоСтатусам = ФормыУХ.СоздатьГруппуФормы(Элементы, 
												"ГруппаОтбораПоСтатусам" + СтрокаИмя,
												"",
												ВидГруппыФормы.ОбычнаяГруппа,
												Элементы.ГруппаОтбораПоСтатусам,
												, 
												ПараметрыГруппы);

		СобытияПометки = Новый Структура("ПриИзменении", "ПриИзмененииОтбораСтатуса");
		ПолеПометка = ФормыУХ.СоздатьПолеФормы(Элементы,
												"ОтборПоСтатусам" + СтрокаИмя,
												СписокЗначение.Представление,
												"ОтборПоСтатусам" + СтрокаИмя,
												ВидПоляФормы.ПолеФлажка,
												ПодГруппаОтбораПоСтатусам,
												,
												,
												СобытияПометки);
												
										

	КонецЦикла;
	
	ФормыУХ.ЭлементыФормыУХДобавлены(ЭтотОбъект);
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством

КонецПроцедуры

&НаСервере
Функция ТекстЗапросаДинамическогоСписка()
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	Возврат "ВЫБРАТЬ
	|	ВТ_Таблица.Ссылка КАК ЗаказНаПроизводство,
	|	ВТ_Таблица.ДокументОснование КАК ДокументОснование,
	|	ЗаказНаПроизводство2_2Продукция.Номенклатура КАК Номенклатура,
	|	ЗаказНаПроизводство2_2Продукция.Характеристика КАК Характеристика,
	|	ЗаказНаПроизводство2_2Продукция.Спецификация КАК Спецификация,
	|	ЗаказНаПроизводство2_2Продукция.Спецификация.КонецДействия КАК ДатаЗавершения,
	|	ЗаказНаПроизводство2_2Продукция.Упаковка,
	|	ЗаказНаПроизводство2_2Продукция.КоличествоУпаковок
	|ПОМЕСТИТЬ ВТ_Продукция
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2 КАК ВТ_Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
	|		ПО ВТ_Таблица.Ссылка = ЗаказНаПроизводство2_2Продукция.Ссылка
	|         И (ЗаказНаПроизводство2_2Продукция.НомерСтроки = 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Таблица.Ссылка,
	|	ВТ_Таблица.ДокументОснование,
	|	ЗаказНаПроизводство2_2Продукция.Номенклатура,
	|	ЗаказНаПроизводство2_2Продукция.Характеристика,
	|	ЗаказНаПроизводство2_2Продукция.Спецификация,
	|	ЗаказНаПроизводство2_2Продукция.Спецификация.КонецДействия,
	|	ЗаказНаПроизводство2_2Продукция.Упаковка,
	|	ЗаказНаПроизводство2_2Продукция.КоличествоУпаковок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Номер КАК Номер,
	|	Таблица.Дата КАК Дата,
	|	Таблица.Статус КАК Статус,
	|	Таблица.Приоритет КАК Приоритет,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.НачатьНеРанее КАК НачалоПроизводства,
	|	Таблица.ДатаПотребности КАК ДатаПотребности,
	|	НАЧАЛОПЕРИОДА(Таблица.ДатаПотребности, ДЕНЬ) КАК ДатаСобытия,
	|	ВЫБОР
	|		КОГДА Таблица.ДатаПотребности <> ДАТАВРЕМЯ(1, 1, 1)
	|				И &ДатаАктуальности > Таблица.ДатаПотребности
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Просрочен,
	|	Таблица.ДинамическаяСтруктура КАК ДинамическаяСтруктура,
	|	ВЫБОР
	|		КОГДА Таблица.ДинамическаяСтруктура
	|					И (ИСТИНА В
	|							(ВЫБРАТЬ ПЕРВЫЕ 1
	|								ИСТИНА
	|							ИЗ
	|								РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации КАК Т
	|							ГДЕ
	|								Т.ЗаказНаПроизводство = Таблица.Ссылка)
	|						ИЛИ ИСТИНА В
	|							(ВЫБРАТЬ ПЕРВЫЕ 1
	|								ИСТИНА
	|							ИЗ
	|								РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации КАК Т
	|							ГДЕ
	|								Т.ЗаказНаПроизводство = Таблица.Ссылка)
	|						ИЛИ ИСТИНА В
	|							(ВЫБРАТЬ ПЕРВЫЕ 1
	|								ИСТИНА
	|							ИЗ
	|								РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
	|							ГДЕ
	|								Т.ЗаказНаПроизводство = Таблица.Ссылка)
	|						ИЛИ ИСТИНА В
	|							(ВЫБРАТЬ ПЕРВЫЕ 1
	|								ИСТИНА
	|							ИЗ
	|								РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
	|							ГДЕ
	|								Т.ЗаказНаПроизводство = Таблица.Ссылка))
	|				ИЛИ ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа КАК Т
	|					ГДЕ
	|						Т.ЗаказНаПроизводство = Таблица.Ссылка)
	|			ТОГДА 5
	|		КОГДА Таблица.ДинамическаяСтруктура
	|				И ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						РегистрСведений.ПротоколРасчетаСтруктурыЗаказа КАК Т
	|					ГДЕ
	|						Т.ЗаказНаПроизводство = Таблица.Ссылка)
	|				И Таблица.Проведен
	|			ТОГДА 6
	|		КОГДА НЕ Таблица.ДинамическаяСтруктура
	|				И СостоянияЗаказовНаПроизводство.СостояниеЭтапов = &СостояниеТребуетсяПроверитьЭтапы
	|			ТОГДА 2
	|		КОГДА НЕ Таблица.ДинамическаяСтруктура
	|				И СостоянияЗаказовНаПроизводство.СостояниеЭтапов = &СостояниеТребуетсяСформироватьЭтапы
	|			ТОГДА 1
	|		КОГДА &ПланируетсяГрафикПроизводства
	|				И ИСТИНА В
	|					(ВЫБРАТЬ ПЕРВЫЕ 1
	|						ИСТИНА
	|					ИЗ
	|						РегистрСведений.ЗаданияКРасчетуГрафикаПроизводства КАК Т
	|					ГДЕ
	|						Т.Распоряжение = Таблица.Ссылка)
	|			ТОГДА 3
	|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеЭтапов = &СостояниеТребуетсяПередатьЭтапыКВыполнению
	|			ТОГДА 4
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК СостояниеЭтаповИГрафикаПроизводства,
	|	ВЫБОР
	|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеОбеспечения = &СостояниеТребуетсяОбеспечение
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СостояниеОбеспечения,
	|	ВЫБОР
	|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеВыпуска = &СостояниеВыполнен
	|			ТОГДА 2
	|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеВыпуска = &СостояниеНарушенаДатаПотребности
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ КАК СостояниеВыпускаПродукции,
	|	ВЫБОР Таблица.Статус
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству)
	|			ТОГДА 1
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Формируется)
	|			ТОГДА 2
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.Закрыт)
	|			ТОГДА 3
	|		ИНАЧЕ 4
	|	КОНЕЦ КАК СтатусОчередь,
	|	-Таблица.Приоритет.РеквизитДопУпорядочивания КАК ПриоритетОчередь,
	|	Таблица.Подразделение.РеквизитДопУпорядочивания КАК ПодразделениеОчередь,
	|	Таблица.Очередь КАК Очередь,
	|	Таблица.Ответственный КАК Ответственный,
	|	Таблица.Комментарий КАК Комментарий,
	|	ВТ_Продукция.ДатаЗавершения КАК ДатаЗавершения,
	|	ВТ_Продукция.Номенклатура КАК Номенклатура,
	|	ВТ_Продукция.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТ_Продукция.ДокументОснование ССЫЛКА Документ.ЗаказКлиента ТОГДА
	|			ВЫРАЗИТЬ(ВТ_Продукция.ДокументОснование КАК Документ.ЗаказКлиента).Контрагент
	|		ИНАЧЕ ВТ_Продукция.ДокументОснование
	|	КОНЕЦ КАК Контрагент,
	|	ВТ_Продукция.ДокументОснование.Номер КАК НомерЗаказа,
	|	ВТ_Продукция.Упаковка КАК Упаковка,
	|	ВТ_Продукция.КоличествоУпаковок КАК Количество,
	|	Таблица.ГосЗаказ КАК ГосЗаказ,
	|	Таблица.СпециальныеТребования КАК СпециальныеТребования
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2 КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовНаПроизводство КАК СостоянияЗаказовНаПроизводство
	|		ПО Таблица.Ссылка = СостоянияЗаказовНаПроизводство.Заказ
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Продукция КАК ВТ_Продукция
	|		ПО (ВТ_Продукция.ЗаказНаПроизводство = Таблица.Ссылка)";
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииОтбораСтатуса(Элемент)
		
	СписокОтбора = Новый СписокЗначений;
	Для Каждого СписокЗначение Из ЭтотОбъект["ОтборСпискаВыбораСтатусов"] Цикл
		
		ИмяЭлемента = СтрЗаменить(СписокЗначение.Значение, " ", "");
		ПеречислениеЭлемента = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовНаПроизводство2_2." + ИмяЭлемента);
		
		Если ЭтотОбъект["ОтборПоСтатусам" + ИмяЭлемента] = Истина Тогда
			СписокОтбора.Добавить(ПеречислениеЭлемента);
		КонецЕсли;
	КонецЦикла;
	
	УстановитьОтбор = Истина;
	Если СписокОтбора.Количество() = 0 Тогда
		УстановитьОтбор = Ложь;
	КонецЕсли;

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, 
														"Статус",
														СписокОтбора,
														УстановитьОтбор,
														ВидСравненияКомпоновкиДанных.ВСписке);
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("УстановитьСвойстваДинамическогоСписка")
Процедура П3_УстановитьСвойстваДинамическогоСписка()

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	
		|	Таблица.Ссылка КАК Ссылка,
		|	Таблица.Номер КАК Номер,
		|	Таблица.Дата КАК Дата,
		|	Таблица.Статус КАК Статус,
		|	Таблица.Приоритет КАК Приоритет,
		|	Таблица.Подразделение КАК Подразделение,
		|	Таблица.НачатьНеРанее КАК НачалоПроизводства,
		|	Таблица.ДатаПотребности КАК ДатаПотребности,
		|	Таблица.ДинамическаяСтруктура КАК ДинамическаяСтруктура,
		|	
		|	&ТекстПоляДатаСобытия КАК ДатаСобытия,
		|	&ТекстПоляПросрочен КАК Просрочен,
		|	
		|	ВЫБОР
		|		
		|		КОГДА Таблица.ДинамическаяСтруктура
		|			И (ИСТИНА В 
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = Таблица.Ссылка)
		|				ИЛИ ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = Таблица.Ссылка)
		|				ИЛИ ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = Таблица.Ссылка)
		|				ИЛИ ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = Таблица.Ссылка))
		|				ИЛИ ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = Таблица.Ссылка)
		|			ТОГДА 5
		|		
		|		КОГДА Таблица.ДинамическаяСтруктура
		|			И (ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					РегистрСведений.ПротоколРасчетаСтруктурыЗаказа КАК Т
		|				ГДЕ
		|					Т.ЗаказНаПроизводство = Таблица.Ссылка))
		|			И Таблица.Проведен
		|			ТОГДА 6
		|		
		|		КОГДА НЕ Таблица.ДинамическаяСтруктура
		|			И СостоянияЗаказовНаПроизводство.СостояниеЭтапов = &СостояниеТребуетсяПроверитьЭтапы
		|			ТОГДА 2
		|		
		|		КОГДА НЕ Таблица.ДинамическаяСтруктура
		|			И СостоянияЗаказовНаПроизводство.СостояниеЭтапов = &СостояниеТребуетсяСформироватьЭтапы
		|			ТОГДА 1
		|		
		|		КОГДА &ПланируетсяГрафикПроизводства
		|			И (
		|				ИСТИНА В
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА
		|					ИЗ
		|						РегистрСведений.ЗаданияКРасчетуГрафикаПроизводства КАК Т
		|					ГДЕ
		|						Т.Распоряжение = Таблица.Ссылка)
		|				ИЛИ ИСТИНА В
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА
		|					ИЗ
		|						РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Т
		|					ГДЕ
		|						Т.ЗаказНаПроизводство = Таблица.Ссылка)
		|				)
		|			ТОГДА 3
		|		
		|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеЭтапов = &СостояниеТребуетсяПередатьЭтапыКВыполнению
		|			ТОГДА 4
		|		
		|		ИНАЧЕ -1
		|		
		|	КОНЕЦ КАК СостояниеЭтаповИГрафикаПроизводства,
		|
		|	ВЫБОР
		|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеОбеспечения = &СостояниеТребуетсяОбеспечение
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СостояниеОбеспечения,
		|
		|	ВЫБОР
		|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеВыпуска = &СостояниеВыполнен
		|			ТОГДА 2
		|		КОГДА СостоянияЗаказовНаПроизводство.СостояниеВыпуска = &СостояниеНарушенаДатаПотребности
		|			ТОГДА 1
		|		ИНАЧЕ -1
		|	КОНЕЦ КАК СостояниеВыпускаПродукции,
		|	
		|	-Таблица.Приоритет.РеквизитДопУпорядочивания КАК ПриоритетОчередь,
		|	Таблица.Подразделение.РеквизитДопУпорядочивания КАК ПодразделениеОчередь,
		|	Таблица.Очередь КАК Очередь,
		|
		|	Таблица.Ответственный КАК Ответственный,
		|	Таблица.Комментарий   КАК Комментарий
		|
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК Таблица
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовНаПроизводство КАК СостоянияЗаказовНаПроизводство
		|		ПО Таблица.Ссылка = СостоянияЗаказовНаПроизводство.Заказ
		|		 
		|ГДЕ 
		|	Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство2_2.КПроизводству)
		|	И Таблица.Проведен";
	
	Если Не ИспользуетсяДинамическаяСтруктура Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПоляДатаСобытия", "Таблица.ДатаПотребности");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&ТекстПоляДатаСобытия", 
			"ВЫБОР
			|	КОГДА СостоянияЗаказовНаПроизводство.СостояниеВыпуска = &СостояниеВыполнен ТОГДА
			|		ДАТАВРЕМЯ(1, 1, 1)
			|	ИНАЧЕ
			// заглушка на период обновления данных в ИБ
			|		ВЫБОР
			|			КОГДА ISNULL(СостоянияЗаказовНаПроизводство.ДатаСобытия, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1) ТОГДА 
			|				Таблица.ДатаПотребности
			|			ИНАЧЕ
			|				СостоянияЗаказовНаПроизводство.ДатаСобытия
			|		КОНЕЦ
			|КОНЕЦ");
	КонецЕсли; 
		
	Если Не ИспользуетсяДинамическаяСтруктура Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&ТекстПоляПросрочен", 
			"ВЫБОР 
			|	КОГДА Таблица.ДатаПотребности <> ДАТАВРЕМЯ(1, 1, 1)
			|		И &ДатаАктуальности > Таблица.ДатаПотребности
			|	ТОГДА ИСТИНА
			|	ИНАЧЕ ЛОЖЬ
			|КОНЕЦ");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			"&ТекстПоляПросрочен", 
			"ВЫБОР
			|	КОГДА НЕ ISNULL(СостоянияЗаказовНаПроизводство.ДатаСобытия, ДАТАВРЕМЯ(1,1,1)) = ДАТАВРЕМЯ(1,1,1) 
			|		И &ДатаАктуальности > СостоянияЗаказовНаПроизводство.ДатаСобытия
			|	ТОГДА ИСТИНА
			|	ИНАЧЕ ЛОЖЬ
			|КОНЕЦ");
	КонецЕсли;
		
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();

	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	#Вставка
	СвойстваСписка.ТекстЗапроса = ТекстЗапросаДинамическогоСписка();
	#КонецВставки
	СвойстваСписка.ОсновнаяТаблица = Список.ОсновнаяТаблица;
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;

	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	Список.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности", НачалоДня(ТекущаяДатаСеанса()));
	Список.Параметры.УстановитьЗначениеПараметра("ПланируетсяГрафикПроизводства", ПланируетсяГрафикПроизводства);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеТребуетсяПроверитьЭтапы", Перечисления.СостоянияЗаказовНаПроизводствоВОчереди.ТребуетсяПроверитьЭтапы);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеТребуетсяСформироватьЭтапы", Перечисления.СостоянияЗаказовНаПроизводствоВОчереди.ТребуетсяСформироватьЭтапы);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеТребуетсяПередатьЭтапыКВыполнению", Перечисления.СостоянияЗаказовНаПроизводствоВОчереди.ТребуетсяПередатьЭтапыКВыполнению);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеТребуетсяОбеспечение", Перечисления.СостоянияЗаказовНаПроизводствоВОчереди.ТребуетсяОбеспечение);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеНарушенаДатаПотребности", Перечисления.СостоянияЗаказовНаПроизводствоВОчереди.НарушенаДатаПотребности);
	Список.Параметры.УстановитьЗначениеПараметра("СостояниеВыполнен", Перечисления.СостоянияЗаказовНаПроизводствоВОчереди.Выполнен);

КонецПроцедуры

#КонецОбласти