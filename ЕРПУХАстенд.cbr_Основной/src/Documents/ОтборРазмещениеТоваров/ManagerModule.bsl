#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура СоздатьОтборРазмещениеИзРасходногоОрдера(РасходныйОрдер) Экспорт

	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РасходныйОрдер,
		"Статус, Склад, Помещение, ЗонаОтгрузки, ДатаОтгрузки");

	Если Не СкладыСервер.ИспользоватьАдресноеХранение(РеквизитыОснования.Склад, РеквизитыОснования.Помещение,
		РеквизитыОснования.ДатаОтгрузки) Тогда
		Возврат;
	КонецЕсли;

	ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Отбор");

	ОтборРазмещениеТоваров = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
	ОтборРазмещениеТоваров.Дата = ТекущаяДатаСеанса();
	ОтборРазмещениеТоваров.ВидОперации = ВидОперации;
	ОтборРазмещениеТоваров.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВРаботе");
	ОтборРазмещениеТоваров.Распоряжение = РасходныйОрдер;

	ЗаполнитьЗначенияСвойств(ОтборРазмещениеТоваров, РеквизитыОснования, "ЗонаОтгрузки, Склад, Помещение");

	ОтборРазмещениеТоваров.ЗаполнитьТоварыОтборПоРаспоряжению();

	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОтборРазмещениеТоваров,
		Документы.ОтборРазмещениеТоваров);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);

	ОтборРазмещениеТоваров.ОтобратьТовары("ТоварыОтбор");

	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);

	ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Запись);

	ТекстСообщения = НСтр("ru = 'Создан документ %1'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);

	Если cbr_ПроверитьЗаполнениеЯчеек(ОтборРазмещениеТоваров, Истина) Тогда
		Возврат;
	КонецЕсли;

	cbr_ПровестиДокумент(ОтборРазмещениеТоваров);

КонецПроцедуры

//++ СкребковЮМ 10.02.26 СКЛ.19
Процедура cbr_СоздатьОтборРазмещениеИзПриходногоОрдера(ПриходныйОрдер) Экспорт

	ОтборРазмещениеТоваров = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
	ОтборРазмещениеТоваров.Заполнить(ПриходныйОрдер);

	ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Запись);

	ТекстСообщения = НСтр("ru = 'Создан документ %1'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);

	Если cbr_ПроверитьЗаполнениеЯчеек(ОтборРазмещениеТоваров, Истина) Тогда
		Возврат;
	КонецЕсли;
	
	cbr_ПровестиДокумент(ОтборРазмещениеТоваров);
	
КонецПроцедуры //-- СкребковЮМ 10.02.26 СКЛ.19

//++ СкребковЮМ 16.02.26 СКЛ.13
Процедура cbr_СоздатьОтборРазмещениеНаПеремещение(ТоварыОтбор, ТоварыРазмещение, Склад) Экспорт

	ОтборРазмещениеТоваров = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
	ОтборРазмещениеТоваров.Дата = ТекущаяДатаСеанса();
	ОтборРазмещениеТоваров.ВидОперации = ПредопределенноеЗначение(
		"Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Перемещение");
	ОтборРазмещениеТоваров.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВРаботе");
	ОтборРазмещениеТоваров.Склад = Склад;

	Для Каждого Товар Из ТоварыОтбор Цикл
		ТоварОтбор = ОтборРазмещениеТоваров.ТоварыОтбор.Добавить();
		ЗаполнитьЗначенияСвойств(ТоварОтбор, Товар);
	КонецЦикла;

	Для Каждого Товар Из ТоварыРазмещение Цикл
		ТоварРазмещение = ОтборРазмещениеТоваров.ТоварыРазмещение.Добавить();
		ЗаполнитьЗначенияСвойств(ТоварРазмещение, Товар);
	КонецЦикла;

	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОтборРазмещениеТоваров,
		Документы.ОтборРазмещениеТоваров);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОтборРазмещениеТоваров, ПараметрыУказанияСерий.Отбор);

	ОтборРазмещениеТоваров.Записать(РежимЗаписиДокумента.Запись);

	ТекстСообщения = НСтр("ru = 'Создан документ %1'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОтборРазмещениеТоваров.Ссылка);

	Если cbr_ПроверитьЗаполнениеЯчеек(ОтборРазмещениеТоваров, Истина, Истина) Тогда
		Возврат;
	КонецЕсли;

	cbr_ПровестиДокумент(ОтборРазмещениеТоваров);

КонецПроцедуры //-- СкребковЮМ 16.02.26 СКЛ.13

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ СкребковЮМ 10.02.26 СКЛ.19
Функция cbr_ПроверитьЗаполнениеЯчеек(Объект, ПроверятьОтбор = Ложь, ПроверятьРазмещение = Ложь)

	Ошибка = Ложь;

	Если ПроверятьОтбор Тогда
		МассивЯчеек = Объект.ТоварыОтбор.ВыгрузитьКолонку("Ячейка");
		Для Каждого ЭлементМассива Из МассивЯчеек Цикл
			Если Не ЗначениеЗаполнено(ЭлементМассива) Тогда
				Ошибка = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ПроверятьРазмещение Тогда
		МассивЯчеек = Объект.ТоварыРазмещение.ВыгрузитьКолонку("Ячейка");
		Для Каждого ЭлементМассива Из МассивЯчеек Цикл
			Если Не ЗначениеЗаполнено(ЭлементМассива) Тогда
				Ошибка = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если Ошибка Тогда
		ТекстСообщения = НСтр("ru='Ошибка проведения документа %1. Не заполнены некоторые ячейки.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Объект.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка);
	КонецЕсли;

	Возврат Ошибка;

КонецФункции //-- СкребковЮМ 10.02.26 СКЛ.19

//++ СкребковЮМ 10.02.26 СКЛ.19
Процедура cbr_ПровестиДокумент(Объект)
			
	Попытка
		Объект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ТекстСообщения = НСтр("ru = 'Ошибка проведения документа %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Объект.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект.Ссылка);

		ЗаписьЖурналаРегистрации("ОшибкаВыполнения", УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ОтборРазмещениеТоваров, , ОбработкаОшибок.ПодробноеПредставлениеОшибки(
			ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры //-- СкребковЮМ 10.02.26 СКЛ.19

#КонецОбласти

#КонецЕсли