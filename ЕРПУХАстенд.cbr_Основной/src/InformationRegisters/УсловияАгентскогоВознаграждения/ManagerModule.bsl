#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Функция ПроцентыАгентскогоВознагражденияПоЗаказу(ЗаказКлиента) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатФункции = Новый Структура("ДокументУсловий, УсловияАВ", Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказКлиентаТовары.Ссылка КАК Ссылка,
	|	ЗаказКлиентаТовары.Ссылка.Организация КАК Организация,
	|	ЗаказКлиентаТовары.Ссылка.Контрагент КАК Контрагент,
	|	ЗаказКлиентаТовары.Ссылка.Дата КАК ДатаДокумента,
	|	ЗаказКлиентаТовары.НомерСтроки КАК НомерСтроки,
	|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
	|	ЗаказКлиентаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга) КАК ЭтоУслуга,
	|	ЗаказКлиентаТовары.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТоварыДокумента
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка = &ЗаказКлиента
	|	И НЕ ЗаказКлиентаТовары.Отменено
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(БазовыеУсловияАгентскогоВознагражденияСрезПоследних.БазоваяСтавка) КАК БазоваяСтавка
	|ПОМЕСТИТЬ ВТ_БазоваяСтавкаАВ
	|ИЗ
	|	РегистрСведений.БазовыеУсловияАгентскогоВознаграждения.СрезПоследних(
	|			,
	|			(Организация, Клиент) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.Контрагент
	|					ИЗ
	|						ТоварыДокумента КАК Т)
	|				И ДатаРеализации >= &ДатаДокумента) КАК БазовыеУсловияАгентскогоВознагражденияСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияАгентскогоВознаграждения.Агент КАК Агент,
	|	УсловияАгентскогоВознаграждения.Номенклатура КАК Номенклатура,
	|	УсловияАгентскогоВознаграждения.Характеристика КАК Характеристика,
	|	УсловияАгентскогоВознаграждения.СтавкаАгентаОсновная КАК СтавкаАгентаОсновная,
	|	УсловияАгентскогоВознаграждения.СтавкаАгента КАК СтавкаАгента
	|ПОМЕСТИТЬ ВТ_АгентНоменклатураПредварительно
	|ИЗ
	|	РегистрСведений.УсловияАгентскогоВознаграждения КАК УсловияАгентскогоВознаграждения
	|ГДЕ
	|	УсловияАгентскогоВознаграждения.Основание В
	|			(ВЫБРАТЬ
	|				Т.Ссылка
	|			ИЗ
	|				ТоварыДокумента КАК Т)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_АгентНоменклатураПредварительно.Номенклатура КАК Номенклатура,
	|	ВТ_АгентНоменклатураПредварительно.Характеристика КАК Характеристика,
	|	СУММА(ВТ_АгентНоменклатураПредварительно.СтавкаАгента) КАК СтавкаАгента
	|ПОМЕСТИТЬ ВТ_СтавкиПоНоменклатуре
	|ИЗ
	|	ВТ_АгентНоменклатураПредварительно КАК ВТ_АгентНоменклатураПредварительно
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_АгентНоменклатураПредварительно.Номенклатура,
	|	ВТ_АгентНоменклатураПредварительно.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыДокумента.Номенклатура КАК Номенклатура,
	|	ТоварыДокумента.Характеристика КАК Характеристика,
	|	СУММА(ВТ_БазоваяСтавкаАВ.БазоваяСтавка) КАК БазоваяСтавка
	|ПОМЕСТИТЬ ВТ_СтавкиБезНоменклатуры
	|ИЗ
	|	ТоварыДокумента КАК ТоварыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_БазоваяСтавкаАВ КАК ВТ_БазоваяСтавкаАВ
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ (ТоварыДокумента.Номенклатура, ТоварыДокумента.Характеристика) В
	|				(ВЫБРАТЬ
	|					Т.Номенклатура,
	|					Т.Характеристика
	|				ИЗ
	|					ВТ_СтавкиПоНоменклатуре КАК Т)
	|	И НЕ ТоварыДокумента.ЭтоУслуга
	|	И НЕ ВТ_БазоваяСтавкаАВ.БазоваяСтавка = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыДокумента.Номенклатура,
	|	ТоварыДокумента.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_СтавкиПоНоменклатуре.Номенклатура КАК Номенклатура,
	|	ВТ_СтавкиПоНоменклатуре.Характеристика КАК Характеристика,
	|	ВТ_СтавкиПоНоменклатуре.СтавкаАгента КАК СтавкаАгентов
	|ИЗ
	|	ВТ_СтавкиПоНоменклатуре КАК ВТ_СтавкиПоНоменклатуре
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_СтавкиБезНоменклатуры.Номенклатура,
	|	ВТ_СтавкиБезНоменклатуры.Характеристика,
	|	ВТ_СтавкиБезНоменклатуры.БазоваяСтавка
	|ИЗ
	|	ВТ_СтавкиБезНоменклатуры КАК ВТ_СтавкиБезНоменклатуры
	|;
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	УсловияАгентскогоВознаграждения.Регистратор КАК ДокументУсловий
	|ИЗ
	|	РегистрСведений.УсловияАгентскогоВознаграждения КАК УсловияАгентскогоВознаграждения
	|ГДЕ
	|	УсловияАгентскогоВознаграждения.Основание = &ЗаказКлиента";
	
	Запрос.УстановитьПараметр("ЗаказКлиента", ЗаказКлиента);
	Запрос.УстановитьПараметр("ДатаДокумента", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказКлиента, "Дата"));
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Не РезультатЗапроса[6].Пустой() Тогда
		ВыборкаУсловий = РезультатЗапроса[6].Выбрать();
		ВыборкаУсловий.Следующий();
		РезультатФункции.ДокументУсловий = ВыборкаУсловий.ДокументУсловий;
	КонецЕсли;
	РезультатФункции.УсловияАВ = РезультатЗапроса[5].Выгрузить();
	
	Возврат РезультатФункции;
			
КонецФункции

#КонецОбласти

#КонецЕсли