#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Не ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") 
		И Не ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
		Тогда
		
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	РеквизитыПартнера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Партнер, Партнер.Поставщик");
	Если Не РеквизитыПартнера.ПартнерПоставщик Тогда
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = НСтр("ru='Документ ""Базовые условия агентского вознаграждения"" можно создавать только для контрагентов-поставщиков'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПартнерАгента = РеквизитыПартнера.Партнер;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		Агент = ДанныеЗаполнения;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, Контрагент");
		Организация = РеквизитыОснования.Организация;
		Агент = РеквизитыОснования.Контрагент;
		Договор = ДанныеЗаполнения;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СтатусСогласования = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(
													Ссылка,
													ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
													
	Если Не СтатусСогласования = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Утверждена") Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	СтатусСогласования = УправлениеПроцессамиСогласованияУХ.ВернутьСтатусОбъекта(
													Ссылка,
													ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Черновик"));
													
	Если СтатусСогласования = ПредопределенноеЗначение("Перечисление.СостоянияСогласования.Утверждена") Тогда
		Отказ = Истина;
		ТекстСообщения = НСтр("ru='Документ уже согласован. Отмена проведения невозможна!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры  

Процедура ПриЗаписи(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
