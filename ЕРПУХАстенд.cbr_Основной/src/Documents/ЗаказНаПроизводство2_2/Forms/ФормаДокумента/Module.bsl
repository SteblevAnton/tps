
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура П3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	ПодготовитьФорму();
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФорму()
	
	// НАЧАЛО Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством
	Если ФормыУХ.ЭлементыФормыУХУжеСозданы(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	//ПараметрыГруппы = Новый Структура("ОтображатьЗаголовок, Группировка", Ложь, ГруппировкаПодчиненныхЭлементовФормы.Вертикальная);
	//ГруппаГосзаказСпецтребование = ФормыУХ.СоздатьГруппуФормы(Элементы, 
	//											"ГруппаГосзаказСпецтребование",
	//											"ГосзаказСпецтребование",
	//											ВидГруппыФормы.ОбычнаяГруппа,
	//											Элементы.СтраницаОсновное,
	//											,
	//											ПараметрыГруппы);
												
	ЭлементГосзаказ = ФормыУХ.СоздатьПолеФормы(Элементы,
												"Госзаказ",
												"Госзаказ",
												"Объект.ГосЗаказ",
												ВидПоляФормы.ПолеФлажка,
												Элементы.СтраницаОсновное,
												Элементы.Комментарий);
												
	ПараметрЭлемента = Новый Структура("МногострочныйРежим, РастягиватьПоГоризонтали, РастягиватьПоВертикали, ПоложениеЗаголовка, Высота", 
										Истина, Истина, Ложь, ПоложениеЗаголовкаЭлементаФормы.Лево, 4);
	ЭлементСпецТребования = ФормыУХ.СоздатьПолеФормы(Элементы,
												"СпециальныеТребования",
												"Спец.требования",
												"Объект.СпециальныеТребования",
												,
												Элементы.СтраницаОсновное,
												Элементы.Комментарий,
												ПараметрЭлемента);
	
	Элементы.Статус.СписокВыбора.Очистить();
	Элементы.Статус.РежимВыбораИзСписка = Истина;
	Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Формируется, "Формируется");
	Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.АктуализацияСпецификаций, "Актуализация спецификаций");
	// НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	// Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.СогласованиеСпецтребований, "Согласование спецтребований");
	// Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Разработка, "Разработка");
	// КОНЕЦ Алейников М.Н. 24.03.2025 Б121
	Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.КПроизводству, "К производству"); 
	// НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	// Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.ВРаботе, "В работе");
	// Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.ПередачаНаСклад, "Передача на склад");
	// КОНЕЦ Алейников М.Н. 24.03.2025 Б121
	Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Закрыт, "Закрыт");
	// НАЧАЛО Алейников М.Н. 24.03.2025 Б121
	// Элементы.Статус.СписокВыбора.Добавить(Перечисления.СтатусыЗаказовНаПроизводство2_2.Запущен, "Запущен");
	// КОНЕЦ Алейников М.Н. 24.03.2025 Б121 
	
	УсловияВидимостиСпецтребований = Документы.СпециальныеТребования.УсловияВидимостиСпецтребований();
	УсловияВидимостиСпецтребований.Логистические = Ложь;
	УсловияВидимостиСпецтребований.Прочие = Ложь;
	Документы.СпециальныеТребования.ВывестиТаблицуСпецТребований(ЭтотОбъект, Элементы.Содержание, УсловияВидимостиСпецтребований, Объект.ДокументОснование);
	
	// НАЧАЛО Алейников М.Н. 24.03.2025 Б121 
	ИмяКомандыПересчитатьКоличествоУпаковок = "П3_ПересчитатьКоличествоУпаковок";
	Команда = ФормыУХ.СоздатьКоманду(ЭтотОбъект, ИмяКомандыПересчитатьКоличествоУпаковок, "", ИмяКомандыПересчитатьКоличествоУпаковок);
	
	ФормыУХ.СоздатьКнопкуФормы(Элементы, 
							  "П3_ПересчитатьКоличествоУпаковок", 
							  НСтр("ru = 'Пересчитать количество в упаковках'"),
							  ИмяКомандыПересчитатьКоличествоУпаковок,
							  ВидКнопкиФормы.КнопкаКоманднойПанели,
							  Элементы.ПродукцияГруппаДеревоСпецификаций,
							  );
	// КОНЕЦ Алейников М.Н. 24.03.2025 Б121 
	
	// НАЧАЛО Алейников М.Н. 04.05.2025 Б1083
	РежимТолькоПросмотр = РежимТолькоПросмотр();
	Элементы.ПродукцияНоменклатураКод.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияНоменклатураТипНоменклатуры.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияНоменклатура.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияХарактеристика.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияАртикул.ТолькоПросмотр = РежимТолькоПросмотр;
    Элементы.ПродукцияНазначение.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияКоличествоУпаковок.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияУпаковкаЕдиницаИзмерения.ТолькоПросмотр = РежимТолькоПросмотр;
	Элементы.ПродукцияДатаПотребности.ТолькоПросмотр = РежимТолькоПросмотр;
	
	Элементы.ПродукцияРазбитьСтроку.Доступность = Не РежимТолькоПросмотр;

	Элементы.Продукция.УстановитьДействие("ПередУдалением", "Подключаемый_Продукция_ПередУдалением");
	Элементы.Продукция.УстановитьДействие("ПередНачаломДобавления", "Подключаемый_Продукция_ПередНачаломДобавления");
	// КОНЕЦ Алейников М.Н. 04.05.2025 Б1083
	
	ФормыУХ.ЭлементыФормыУХДобавлены(ЭтотОбъект);
	// КОНЕЦ Османов Н.М. 27.11.2023 ЧТЗ №2 Управление производством

КонецПроцедуры

#КонецОбласти

// +- Алейников М.Н. 04.05.2025 Б1083
&НаКлиенте
Процедура Подключаемый_Продукция_ПередУдалением(Элемент, Отказ)
	
	Отказ = РежимТолькоПросмотр();
	
	Если Отказ Тогда 
		ВызватьИсключение НСтр("ru = 'Запрещено удаление строки документа'");
	КонецЕсли;
	
КонецПроцедуры 

// +- Алейников М.Н. 04.05.2025 Б1083
&НаКлиенте
Процедура Подключаемый_Продукция_ПередНачаломДобавления(Элемент, Отказ)
	
	Отказ = РежимТолькоПросмотр();
	
	Если Отказ Тогда 
    	ВызватьИсключение НСтр("ru = 'Запрещено добавление строки документа'");
	КонецЕсли;
	
КонецПроцедуры

// +- Алейников М.Н. 04.05.2025 Б1083
&НаСервере
Функция РежимТолькоПросмотр()
	
	Результат = Не Пользователи.РолиДоступны("П3_ИзменениеЗаказНаПроизводствоПродукция")
	            И (ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ЗаказКлиента")
					Или ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ЗаказНаВнутреннееПотребление"));
	Возврат Результат; 
	
КонецФункции

// +- Алейников М.Н. 24.03.2025 Б121
&НаКлиенте
Процедура П3_ПересчитатьКоличествоУпаковок(Команда)
	
	П3_ПересчитатьКоличествоУпаковокНаСервере();
	
КонецПроцедуры

// +- Алейников М.Н. 24.03.2025 Б121
&НаСервере
Процедура П3_ПересчитатьКоличествоУпаковокНаСервере()
	
	СвойствоУпаковка = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "УпаковкаРеквизитХарактеристики");
	МассивСвойств = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвойствоУпаковка);
	
	Для Каждого СтрокаПродукции Из Объект.Продукция Цикл
		ТаблицаСвойствХарактеристик = УправлениеСвойствами.ЗначенияСвойств(СтрокаПродукции.Характеристика, Истина, , МассивСвойств);
		
		Если ТаблицаСвойствХарактеристик.Количество() Тогда
			ЗначениеСвойстваХарактеристика = ТаблицаСвойствХарактеристик[0].Значение;
			Если Не СтрокаПродукции.Упаковка = ЗначениеСвойстваХарактеристика Тогда
				КоэффициентУпаковки= Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки
				(ЗначениеСвойстваХарактеристика, СтрокаПродукции.Номенклатура);
				СтрокаПродукции.КоличествоУпаковок = СтрокаПродукции.КоличествоУпаковок / КоэффициентУпаковки;
				СтрокаПродукции.Упаковка = ЗначениеСвойстваХарактеристика;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры